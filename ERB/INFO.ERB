@SHOW_STATUS
#DIMS ボタン表示格納用
#DIM ループ用
#DIM 行数保存
#DIM 文字数カウント

VARSET LOCAL
;-------------------------------------------------
;キャラの態度
;-------------------------------------------------
CALL CHARA_ATTITUDE

;-------------------------------------------------
;時間経過、キャラの移動
;-------------------------------------------------
CALL CHARA_MOVEMENT
IF TCVAR:MASTER:馴れ合い強度 >= 1
	LOCAL:1 = IN_ROOM("MIN", CFLAG:MASTER:現在位置, "CFLAG", 6)
	LOCAL:2 = CFLAG:(LOCAL:1):6
	; IF CFLAG:MASTER:隠密
	; 	;隠密だと空気変わらない
	; ELSEIF LOCAL:2 <= 0 && LOCAL:1 > 0 && CFLAG:(LOCAL:1):現在位置 != CFLAG:(LOCAL:1):前ターン位置 && !CFLAG:(LOCAL:1):睡眠
	; 	PRINTFORMW %CALLNAME:(LOCAL:1)%が入ってきたことにより空気が変わってしまった…
	; 	FOR LOCAL, 0, CHARANUM
	; 		SIF CFLAG:LOCAL:同室 && CFLAG:LOCAL:6 >= 1 && !CFLAG:LOCAL:睡眠
	; 			CALL KOJO_EVENT(8, LOCAL, LOCAL:1)
	; 	NEXT
	; ENDIF
ENDIF
VARSET LOCAL

TFLAG:行動前時刻 = TIME + 1440 * DAY
CFLAG:MASTER:睡眠 = 0

;-------------------------------------------------
;TARGET選択
;-------------------------------------------------
;同室中のキャラをTARGETに代入
VARSET TARGET
VARSET 飲み会TARGET
;キャラを好感度レベルの降順に並び替え
CALL SORT_CFLAG(21)
IF TFLAG:調教モード == 3
	FOR LOCAL,1,CHARANUM
		SIF CFLAG:MASTER:現在マップ種別 != CFLAG:(RESULT:LOCAL):現在マップ種別
			CONTINUE
		SIF CFLAG:MASTER:現在位置 != CFLAG:(RESULT:LOCAL):現在位置
			CONTINUE
		IF CFLAG:(RESULT:LOCAL):飲み会
			LOCAL:1 ++
			飲み会TARGET:(LOCAL:1) = RESULT:LOCAL
		ELSE
			LOCAL:2 ++
			TARGET:(LOCAL:2) = RESULT:LOCAL
		ENDIF
	NEXT
ELSE
	FOR LOCAL,1,CHARANUM
		SIF CFLAG:MASTER:現在マップ種別 != CFLAG:(RESULT:LOCAL):現在マップ種別
			CONTINUE
		SIF CFLAG:MASTER:現在位置 != CFLAG:(RESULT:LOCAL):現在位置
			CONTINUE
		LOCAL:1 ++
		TARGET:(LOCAL:1) = RESULT:LOCAL
	NEXT
ENDIF

;TFLAG:TARGET変更用が設定されているときその番号のターゲットが存在するならTARGETに代入、いなければTFLAG:TARGET変更用をリセット
LOCAL:1 = 0
IF TFLAG:調教モード == 3
	TARGET = 飲み会TARGET:1
	FOR LOCAL,1,CHARANUM
		SIF TFLAG:TARGET変更用 < 1
			BREAK
		SIF 飲み会TARGET:LOCAL < 1
			BREAK
		IF 飲み会TARGET:LOCAL == TFLAG:TARGET変更用
			TARGET = 飲み会TARGET:LOCAL
			LOCAL:1 ++
		ENDIF
	NEXT
ELSE
	TARGET = TARGET:1
	FOR LOCAL,1,CHARANUM
		SIF TFLAG:TARGET変更用 < 1
			BREAK
		SIF TARGET:LOCAL < 1
			BREAK
		IF TARGET:LOCAL == TFLAG:TARGET変更用
			TARGET = TARGET:LOCAL
			LOCAL:1 ++
		ENDIF
	NEXT
ENDIF
SIF !LOCAL:1
	TFLAG:TARGET変更用 = 0

;MASTERと同室フラグ
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	IF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置 && CFLAG:MASTER:現在マップ種別 == CFLAG:LOCAL:現在マップ種別
		;遭遇口上（ターゲットになってなくても喋る）と情事発覚口上
		SIF !CFLAG:LOCAL:同室 && !CFLAG:LOCAL:睡眠
			CALL AFFAIR_DISCLOSURE(LOCAL, 0)
		CFLAG:LOCAL:同室 = 1
	;覗きは一旦オミット
	ELSE
	; 	;相手からこちらが見えていて、相手かこちらかが移動したかこちらが押し倒した
	; 	SIF CAN_MOVE_1(CFLAG:LOCAL:現在位置, CFLAG:MASTER:現在位置) == 2 && (CFLAG:MASTER:前ターン位置 != CFLAG:MASTER:現在位置 || CFLAG:LOCAL:前ターン位置 != CFLAG:LOCAL:現在位置 || TRAIN_COMNAME(PREVCOM) == "押し倒す") && !CFLAG:LOCAL:睡眠
	; 		CALL AFFAIR_DISCLOSURE(LOCAL, 1)
		CFLAG:LOCAL:同室 = 0
	ENDIF
NEXT
;馴れ合い強度決定
;TARGET側
IF TFLAG:調教モード == 3
	FOR LOCAL,1,CHARANUM
		SIF 飲み会TARGET:LOCAL < 1
			BREAK
		;合意か弱味がないと2以上にならない
		IF GETBIT(CFLAG:(飲み会TARGET:LOCAL):1,0) || CFLAG:(飲み会TARGET:LOCAL):10
			TCVAR:(飲み会TARGET:LOCAL):馴れ合い強度 = CFLAG:(飲み会TARGET:LOCAL):6
		;飲み会中は酩酊数値一定以上でも2以上になる
		ELSEIF BASE:(飲み会TARGET:LOCAL):酩酊 > 500
			TCVAR:(飲み会TARGET:LOCAL):馴れ合い強度 = CFLAG:(飲み会TARGET:LOCAL):6
		ELSE
			TCVAR:(飲み会TARGET:LOCAL):馴れ合い強度 = MIN(CFLAG:(飲み会TARGET:LOCAL):6 , 1)
		ENDIF
		;自室で遊ぶ場合は１以上保証
		IF CFLAG:(飲み会TARGET:LOCAL):一緒に遊ぶ && CFLAG:(飲み会TARGET:LOCAL):現在位置 == CFLAG:PLAYER:311
			TCVAR:(飲み会TARGET:LOCAL):馴れ合い強度 = MAX(TCVAR:(飲み会TARGET:LOCAL):馴れ合い強度 , 1)
		ENDIF
	NEXT
ELSE
	FOR LOCAL,1,CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		;合意か弱味がないと2以上にならない
		IF GETBIT(CFLAG:(TARGET:LOCAL):1,0) || CFLAG:(TARGET:LOCAL):10
			TCVAR:(TARGET:LOCAL):馴れ合い強度 = CFLAG:(TARGET:LOCAL):6
		ELSE
			TCVAR:(TARGET:LOCAL):馴れ合い強度 = MIN(CFLAG:(TARGET:LOCAL):6 , 1)
		ENDIF
		;自室で遊ぶ場合は１以上保証
		IF CFLAG:(TARGET:LOCAL):一緒に遊ぶ && CFLAG:(TARGET:LOCAL):現在位置 == CFLAG:PLAYER:311
			TCVAR:(TARGET:LOCAL):馴れ合い強度 = MAX(TCVAR:(TARGET:LOCAL):馴れ合い強度 , 1)
		ENDIF
	NEXT
ENDIF

TCVAR:MASTER:馴れ合い強度 = TCVAR:TARGET:馴れ合い強度

;ウフフ終了TEQUIP処理
FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	IF !CFLAG:LOCAL:うふふ || CFLAG:LOCAL:現在位置 != CFLAG:LOCAL:前ターン位置 || CFLAG:LOCAL:現在マップ種別 != CFLAG:LOCAL:前マップ種別
		IF CSTR:LOCAL:うふふ前の服 != ""
			CALL CLOTHES_CHANGE(LOCAL, CSTR:LOCAL:うふふ前の服)
;			CSTR:LOCAL:着せ替え服 = %CSTR:LOCAL:うふふ前の服%
			CSTR:LOCAL:うふふ前の服 = 
		ENDIF
;		CALL CLOTHES_RESET_TRAIN(LOCAL)
		FOR LOCAL:1, 10, 150
			TEQUIP:LOCAL:(LOCAL:1) = 0
		NEXT
		IF TFLAG:調教モード < 2
			BASE:LOCAL:射精 = 0
			TCVAR:LOCAL:イきそう = 0
		ENDIF
	ENDIF

	IF CFLAG:LOCAL:現在位置 != CFLAG:LOCAL:前ターン位置 || CFLAG:LOCAL:現在マップ種別 != CFLAG:LOCAL:前マップ種別 || CFLAG:MASTER:隠密 != CFLAG:LOCAL:隠密
		VARSET 体位モード格納:LOCAL:0
		VARSET モード主犯:LOCAL:0
	ENDIF
	
	IF CFLAG:LOCAL:うふふ
		;うふふ時は自動着替えは発動しない
		;ついでにうふふ突入前の服を保存する
		IF CSTR:LOCAL:うふふ前の服 == ""
			CSTR:LOCAL:うふふ前の服 = %CSTR:LOCAL:着せ替え服%
			SIF CSTR:LOCAL:うふふ前の服 == ""
				CSTR:LOCAL:うふふ前の服 = デフォルト
		ENDIF
		;うふふ中、全ての服がなくなっているのなら全裸に切り替える
		LOCAL:10 = 0
		FOR LOCAL:11, 150, 172
			SIF TEQUIP:LOCAL:(LOCAL:11)
				LOCAL:10 = 1
		NEXT
		IF LOCAL:10 == 0
			;全裸なら全裸に
			CALL CLOTHES_CHANGE(LOCAL, "全裸")
		ENDIF
		CONTINUE
	ENDIF
	IF CFLAG:LOCAL:飲み会
		;飲み会時は自動着替えは発動しない
		CONTINUE
	ENDIF

	CALL CLOTHES_CHECK(LOCAL)
	;移動を視野に別関数化
;	IF SWIMROOM(CFLAG:LOCAL:現在マップ種別, CFLAG:LOCAL:現在位置)
;		CALL CLOTHES_CHANGE(LOCAL, "水着")
;	ENDIF
;	
;	;水着エリアから出てきたら前の服に戻る
;	SIF !SWIMROOM(CFLAG:LOCAL:現在マップ種別, CFLAG:LOCAL:現在位置) && CSTR:LOCAL:着せ替え服 == "水着"
;		CALL CLOTHES_CHANGE(LOCAL, CSTR:LOCAL:脱ぐ前の服)
;	
;	;風呂場では全裸になる
;	SIF BATHROOM(CFLAG:LOCAL:現在マップ種別, CFLAG:LOCAL:現在位置) && !BATHROOM(CFLAG:LOCAL:現在マップ種別, CFLAG:LOCAL:前ターン位置)
;		CALL CLOTHES_CHANGE(LOCAL, "全裸")
;	
;	;風呂場から出てきたら浴衣になる
;;	SIF !BATHROOM(CFLAG:LOCAL:現在マップ種別, CFLAG:LOCAL:現在位置) && BATHROOM(CFLAG:LOCAL:現在マップ種別, CFLAG:LOCAL:前ターン位置)
;	SIF !BATHROOM(CFLAG:LOCAL:現在マップ種別, CFLAG:LOCAL:現在位置) && CSTR:LOCAL:着せ替え服 == "全裸"
;		CALL CLOTHES_CHANGE(LOCAL, "浴衣")
;
;	;自室では部屋着になる
;	SIF CFLAG:LOCAL:現在マップ種別 == 999 && CFLAG:LOCAL:現在位置 == FINDELEMENT(滞在者部屋割り配列, LOCAL)
;		CALL CLOTHES_CHANGE(LOCAL, "部屋着")
;
;	;自室から出たら普段着に戻る
;	IF CFLAG:LOCAL:現在マップ種別 != 999 && CSTR:LOCAL:着せ替え服 == "部屋着"
;		CSTR:LOCAL:着せ替え服 = 
;		CALL CLOTHES_RESET_TRAIN(LOCAL)
;	ENDIF

NEXT
;-------------------------------------------------
;遭遇口上（TARGETのみ）
;-------------------------------------------------
CALL KOJO_遭遇時EVENT
;-------------------------------------------------
;情報表示
;-------------------------------------------------
DRAWLINE
CALL サイド領域描画
;時計画像表示
CALL INFO_CLOCK_IMAGE(TIME)
IF TIME > 1440
	DAY ++
	TIME -= 1440
ENDIF
LOCALS = %月算出()%
PRINTFORM {RESULT}年目 %LOCALS% {DAY % 30 + 1}日(%GET_DAY()%){TIME / 60}時{TIME % 60}分 
IF DAY * 1440 + TIME - CFLAG:MASTER:314 >= 960
	PRINT [要睡眠]
ELSEIF DAY * 1440 + TIME - CFLAG:MASTER:314 >= 720
	PRINT [睡眠可]
ENDIF
PRINT  
;-------------------------------------------------
;状態情報（現在隠密のみ）
;-------------------------------------------------
SIF CFLAG:MASTER:隠密
	PRINTFORM （隠密中）
;-------------------------------------------------
;リゾートランク
;-------------------------------------------------
SIF CFLAG:PLAYER:現在マップ種別 == 0
	PRINTFORM 　　リゾートランク：%リゾートランク文字列:リゾートランク%
PRINTL
;-------------------------------------------------
;現在位置情報
;-------------------------------------------------
PRINTFORM %GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:MASTER:現在位置)%
;-------------------------------------------------
;雑務ゲージ
;-------------------------------------------------
IF (CFLAG:PLAYER:現在位置) != 20 && (CFLAG:PLAYER:現在位置) / 100 != 20 && CFLAG:PLAYER:現在マップ種別 == 0
	PRINTFORM 　　　雑務:{雑務ゲージ:(CFLAG:PLAYER:現在位置), 5}/10000　
	CALL PRINT_COLORBAR, 雑務ゲージ:(CFLAG:PLAYER:現在位置), 10000, 600, 60, BARCOLORSET("白"), RESULT:1
ENDIF
IF 開催予定祭り名 != "" &&  開催予定日取り == DAY
	PRINTL
	SETCOLOR カラーパレット("黄")
	PRINTFORM 「%SUBSTRING(開催予定祭り名, 9)%」開催中！
	RESETCOLOR
ENDIF
PRINTL 
;DRAWLINE
PRINTL ------------------------------------------------------------------------------------------------------------
;-------------------------------------------------
;TARGET情報とモード切り替え
;-------------------------------------------------
VARSET LOCAL

IF TARGET
	;MASTERの位置から見て他の部屋にだれかいるかどうか
	LOCAL:1 = 0
	FOR LOCAL,1,CHARANUM
		SIF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置 && CFLAG:LOCAL:現在マップ種別 == CFLAG:MASTER:現在マップ種別
			CONTINUE
		SIF CAN_MOVE_1(CFLAG:PLAYER:現在マップ種別, CFLAG:MASTER:現在位置, CFLAG:LOCAL:現在位置) == 2
			LOCAL:1 ++
	NEXT
	IF CFLAG:うふふ == 1
		TFLAG:調教モード = 2
	ELSEIF CFLAG:飲み会 == 1
		TFLAG:調教モード = 3
	ELSE
		TFLAG:調教モード = 1
	ENDIF
	文字数カウント = 8
	PRINT 同室者：
	FOR LOCAL,1,CHARANUM
	;同室一覧から即座に相手を変えられるようにする
		SIF TARGET:LOCAL < 1
			BREAK
		IF TARGET:LOCAL == TARGET
		ELSEIF TARGET:LOCAL
			ボタン表示格納用 = [%CALLNAME:(TARGET:LOCAL)%
			IF CFLAG:(TARGET:LOCAL):ゲージ起動分類 == 1
				SETCOLOR カラーパレット("えっちな色")
				ボタン表示格納用 += "(恋)"
			ENDIF
			IF CFLAG:(TARGET:LOCAL):発情期フラグ < 0
				SETCOLOR カラーパレット("えっちな色")
				ボタン表示格納用 += "(発)"
			ENDIF
			IF TALENT:(TARGET:LOCAL):妊娠
				IF PREGNANCY_PROGRESS(TARGET:LOCAL) == 5
					ボタン表示格納用 += "(臨)"
				ELSE
					ボタン表示格納用 += "(妊)"
				ENDIF
				SIF GETCOLOR() != カラーパレット("えっちな色")
					SETCOLOR カラーパレット("ピンク")
			ELSEIF あなた特殊能力:危険日感知
				IF RISKY_DAY(TARGET:LOCAL) == 2
					ボタン表示格納用 += "(危)"
					SETCOLOR カラーパレット("赤ピンク")
				ELSEIF RISKY_DAY(TARGET:LOCAL) == 1
					ボタン表示格納用 += "(危)"
					SIF GETCOLOR() != カラーパレット("えっちな色")
						SETCOLOR カラーパレット("薄ピンク")
				ENDIF
			ENDIF
			ボタン表示格納用 += "]"
			文字数カウント += STRLENS(ボタン表示格納用)
			IF 文字数カウント > 85
				PRINTL
				文字数カウント = 0
			ENDIF
			IF TFLAG:調教モード == 3
				PRINTPLAINFORM %ボタン表示格納用%
			ELSE
				PRINTBUTTON ボタン表示格納用, (TARGET:LOCAL)+2000
			ENDIF
			RESETCOLOR
		ENDIF
		PRINT  
	NEXT
	IF TFLAG:調教モード == 3
		PRINTL
		PRINT 飲み会参加者：
		FOR LOCAL,1,CHARANUM
		;同室一覧から即座に相手を変えられるようにする
			SIF 飲み会TARGET:LOCAL < 1
				BREAK
			IF 飲み会TARGET:LOCAL == TARGET
			ELSEIF 飲み会TARGET:LOCAL
				ボタン表示格納用 = [%CALLNAME:(飲み会TARGET:LOCAL)%
				IF CFLAG:(飲み会TARGET:LOCAL):ゲージ起動分類 == 1
					SETCOLOR カラーパレット("えっちな色")
					ボタン表示格納用 += "(恋)"
				ENDIF
				IF CFLAG:(飲み会TARGET:LOCAL):発情期フラグ < 0
					SETCOLOR カラーパレット("えっちな色")
					ボタン表示格納用 += "(発)"
				ENDIF
				IF TALENT:(飲み会TARGET:LOCAL):妊娠
					IF PREGNANCY_PROGRESS(飲み会TARGET:LOCAL) == 5
						ボタン表示格納用 += "(臨)"
					ELSE
						ボタン表示格納用 += "(妊)"
					ENDIF
					SIF GETCOLOR() != カラーパレット("えっちな色")
						SETCOLOR カラーパレット("ピンク")
				ELSEIF あなた特殊能力:危険日感知
					IF RISKY_DAY(飲み会TARGET:LOCAL) == 2 
						ボタン表示格納用 += "(危)"
						SETCOLOR カラーパレット("赤ピンク")
					ELSEIF RISKY_DAY(飲み会TARGET:LOCAL) == 1
						ボタン表示格納用 += "(危)"
						SIF GETCOLOR() != カラーパレット("えっちな色")
							SETCOLOR カラーパレット("薄ピンク")
					ENDIF
				ENDIF
				ボタン表示格納用 += "]"
				PRINTBUTTON ボタン表示格納用, (飲み会TARGET:LOCAL)+2000
				RESETCOLOR
			ENDIF
			PRINT  
		NEXT
	ENDIF
	PRINTL
	LOCAL:1 = 0
	FOR LOCAL,1,CHARANUM
		SIF TARGET:LOCAL <= 0
			CONTINUE
		SIF !CFLAG:(TARGET:LOCAL):同行
			CONTINUE
		LOCAL:1 ++
		SIF LOCAL:1 > 1
				PRINT 、
		PRINTFORM %CALLNAME:(TARGET:LOCAL)%
	NEXT
	SIF LOCAL:1
		PRINT 同行中
	PRINTL 
		
	;ここで立ち絵表示
	LOCAL = 助手は誰だ()
	IF GETBIT(画像サイズ切り替えOPTION記憶, 0)
		LOCALS '= @"<div rect='0, 0, 1125, 2475'><button value='889' title='表示方法変更:正方形(\@ LOCAL? 3P対応# 単独\@)'>%短冊グラ表示用文字列関数(TARGET)%"
		LOCALS += @"%"<br>" * 20%[表示方法変更:\@ LOCAL? 3P対応# 正方形\@]</button>"
		SIF LOCAL
			LOCALS += @"<button value='{LOCAL+2000}' title='%CALLNAME:LOCAL%をメインにする'>[%CALLNAME:LOCAL%]</button>"
		LOCALS += @"</div>"
		HTML_PRINT LOCALS, 1
	ELSEIF GETBIT(画像サイズ切り替えOPTION記憶, 4) && LOCAL
		LOCALS '= @"<div rect='0, 0, 1125, 2475'><button value='889' title='表示方法変更:正方形(単独)'>%顔グラ表示用文字列関数(TARGET)%</button>"
		LOCALS += @"<br><button value='{LOCAL+2000}' title='%CALLNAME:LOCAL%をメインにする'>%顔グラ表示用文字列関数(LOCAL, 1, 1012)%</button>"
		LOCALS += @"%"<br>" * 19%<button value='889' title='表示方法変更:正方形(単独)'>[表示方法変更:正方形]</button>"
		LOCALS += @"<button value='{LOCAL+2000}' title='%CALLNAME:LOCAL%をメインにする'>[%CALLNAME:LOCAL%]</button>"
		LOCALS += @"</div>"
		HTML_PRINT LOCALS, 1
	ELSE
		LOCALS '= @"<div rect='0, 0, 1125, 1350'><button value='889' title='表示方法変更:短冊'>%顔グラ表示用文字列関数(TARGET)%"
		LOCALS += @"%"<br>" * 10%[表示方法変更:短冊]</button>"
		SIF LOCAL
			LOCALS += @"<button value='{LOCAL+2000}' title='%CALLNAME:LOCAL%をメインにする'>[%CALLNAME:LOCAL%]</button>"
		LOCALS += @"</div>"
		HTML_PRINT LOCALS, 1
	ENDIF
	PRINT 　　　　　　　　　　　 
	IF CFLAG:TARGET:ゲージ起動分類 == 1 || CFLAG:TARGET:発情期フラグ < 0
		SETCOLOR カラーパレット("えっちな色")
	ELSEIF TALENT:TARGET:妊娠
		SETCOLOR カラーパレット("ピンク")
	ELSEIF あなた特殊能力:危険日感知
		IF RISKY_DAY(TARGET) == 2
			SETCOLOR カラーパレット("赤ピンク")
		ELSEIF RISKY_DAY(TARGET) == 1
			SETCOLOR カラーパレット("薄ピンク")
		ENDIF
	ENDIF
	行数保存 = LINECOUNT
	PRINTFORM %NAME:TARGET%
	SIF CFLAG:TARGET:ゲージ起動分類 == 1
		PRINT (恋慕対象)
	IF CFLAG:TARGET:発情期フラグ < 0
		PRINT (発情期
		PRINT_IMG "えっちハート"
		PRINT )
	ENDIF
	IF TALENT:TARGET:妊娠
		SETCOLOR カラーパレット("ピンク")
		IF PREGNANCY_PROGRESS(TARGET) == 5
			PRINT (臨月
		ELSE
			PRINT (妊娠中
		ENDIF
		PRINT_IMG "えっちハート"
		PRINT )
	ELSEIF あなた特殊能力:危険日感知
		IF RISKY_DAY(TARGET) == 2
			SETCOLOR カラーパレット("赤ピンク")
			PRINT (
			PRINT_IMG "えっちハート"
			PRINT 超危険日
			PRINT_IMG "えっちハート"
			PRINT )
		ELSEIF RISKY_DAY(TARGET) == 1
			SETCOLOR カラーパレット("薄ピンク")
			PRINT (危険日
			PRINT_IMG "えっちハート"
			PRINT )
		ELSEIF RISKY_DAY(TARGET) == -1
			SETCOLOR 0xccffff
			PRINT （安全日）
		ENDIF
	ENDIF
	RESETCOLOR
	SIF CFLAG:TARGET:隠密
		PRINT （隠密）
	;酩酊系
	IF CFLAG:TARGET:酔いすぎ == 1
		PRINT （泥酔）
	ELSEIF CFLAG:TARGET:酔いすぎ == 2
		PRINT （二日酔い）
	ELSEIF BASE:TARGET:酩酊 == 0
		
	ELSEIF BASE:TARGET:酩酊 < 500
		PRINT （ほろ酔い）
	ELSEIF BASE:TARGET:酩酊 < 1000
		PRINT （酩酊）
	ENDIF
	SIF CFLAG:TARGET:睡眠
		PRINT （睡眠）
	LOCAL = BASE:怒り / 200
	IF CFLAG:怒り 
		SETCOLOR 255,0,0
		PRINTL 　　怒
		RESETCOLOR
	ELSEIF LOCAL
		PRINTFORML 　　怒り:%"！" * LOCAL%
	ELSE
		PRINTL 
	ENDIF

	CALL SHOW_INFO_PALAM
	PRINT 　　　　　　　　　　　 
	IF CFLAG:TARGET:滞在期間 == 999
		PRINTFORML 残り滞在日数：常時滞在
	ELSE
		PRINTFORML 残り滞在日数：{CFLAG:TARGET:滞在期間, 2}日
	ENDIF
	PRINT 　　　　　　　　　　　 
	CALL PRINT_好感度レベル表示(TARGET)
	IF CFLAG:TARGET:ゲージ起動分類 == 1
		PRINTL 
		PRINT 　　　　　　　　　　　 
		CALL PRINT_恋慕レベル表示(TARGET)
	ELSE
		PRINTL
	ENDIF
	RESETCOLOR
	PRINTL
ELSE
	TFLAG:調教モード = 0
ENDIF
PRINT 　　　　　　　　　　　 
CALL SHOW_EQUIP_2

IF TARGET > 0
	LOCAL = LIMIT(BASE:ムード / 300,0,5)
	PRINT 　　　　　　　　　　　 
	PRINTFORM ムード:
	SIF BASE:ムード == MAXBASE:ムード
		SETCOLOR 255,51,153
	PRINTFORM %UNICODE(0x2665) * LOCAL,5,LEFT% 
	RESETCOLOR
	LOCAL = MAX(BASE:理性 / 200,1)
	PRINT 理性:
	SIF BASE:理性 < 100
		SETCOLOR 255,255,0
	PRINTFORM %"★" * LOCAL,10,LEFT% 
	RESETCOLOR
	PRINTL 
	; PRINT 　　　　　　　　　　　 
	; PRINTFORM 満足:
	; CALL PRINT_COLORBAR, BASE:満足, MAXBASE:満足, 600, 60, BARCOLORSET("白"), RESULT:1
	; 	PRINTFORM ({BASE:満足,4}/{MAXBASE:満足,4})
	; PRINTL 
	PRINT 　　　　　　　　　　　 
	CALL 通常時体力ゲージ表示(TARGET)
	PRINTL 
	PRINT 　　　　　　　　　　　 
	CALL 通常時性欲ゲージ表示(TARGET)
	PRINTL 

	IF BASE:酩酊 > 0
		LOCALS = 酩酊:
		PRINTFORM %LOCALS%
		IF BASE:酩酊 < 500
			CALL PRINT_COLORBAR, BASE:酩酊, MAXBASE:酩酊, 600, 100, BARCOLORSET("青"), RESULT:1
		ELSE
			CALL PRINT_COLORBAR, BASE:酩酊, MAXBASE:酩酊, 600, 100, BARCOLORSET("真っ赤"), RESULT:1
		ENDIF
		LOCALS = ({BASE:酩酊,4}/{MAXBASE:酩酊,4})
		PRINTFORM %LOCALS,14,LEFT%
		PRINTL 
	ELSEIF BASE:酩酊 < 0
		LOCALS = 二日酔い:
		PRINTFORM %LOCALS%
		CALL PRINT_COLORBAR, BASE:酩酊 * -1, MAXBASE:酩酊, 600, 100, BARCOLORSET("真っ赤"), RESULT:1
		LOCALS = ({BASE:酩酊 * -1,4}/{MAXBASE:酩酊,4})
		PRINTFORM %LOCALS,14,LEFT%
		PRINTL 
	ENDIF
	PRINT 　　　　　　　　　　　 
	
	IF TALENT:TARGET:2 & 2
		LOCALS = 勃起:
		PRINTFORM %LOCALS%
		CALL PRINT_COLORBAR, BASE:勃起, MAXBASE:勃起, 600, 100, BARCOLORSET("紫"), RESULT:1
		IF BASE:勃起 >= 1000
			LOCALS = ({BASE:勃起})
		ELSE
			LOCALS = ({BASE:勃起,4}/{MAXBASE:勃起,4})
		ENDIF
		PRINTFORM %LOCALS,14,LEFT%
	ENDIF
	PRINTL 
	PRINT 　　　　　　　　　　　 
	SETCOLOR 0xffd900
	PRINTBUTTON "[能力表示]", 803
	RESETCOLOR
	PRINTL 
	PRINTL 
	PRINTL 
	PRINTL 
	PRINTL 

	DRAWLINE
ENDIF



IF BASE:PLAYER:酩酊 > 0
	SIF GETBIT(画像サイズ切り替えOPTION記憶, 0) || (GETBIT(画像サイズ切り替えOPTION記憶, 4) && 助手は誰だ())
		PRINT 　　　　　　　　　　　 
	LOCALS = 酩酊(%CALLNAME:PLAYER%)
	PRINTFORM %LOCALS,20,LEFT%
	IF BASE:PLAYER:酩酊 < 500
		CALL PRINT_COLORBAR, BASE:PLAYER:酩酊, MAXBASE:PLAYER:酩酊, 600, 100, BARCOLORSET("青"), RESULT:1
	ELSE
		CALL PRINT_COLORBAR, BASE:PLAYER:酩酊, MAXBASE:PLAYER:酩酊, 600, 100, BARCOLORSET("真っ赤"), RESULT:1
	ENDIF
	LOCALS = ({BASE:PLAYER:酩酊,4}/{MAXBASE:PLAYER:酩酊,4})
	PRINTFORM %LOCALS,14,LEFT%
	PRINTL 
ELSEIF BASE:酩酊 < 0
	SIF GETBIT(画像サイズ切り替えOPTION記憶, 0) || (GETBIT(画像サイズ切り替えOPTION記憶, 4) && 助手は誰だ())
		PRINT 　　　　　　　　　　　 
	LOCALS = 二日酔い(%CALLNAME:TARGET%)
	PRINTFORM %LOCALS,20,LEFT%
	CALL PRINT_COLORBAR, BASE:酩酊 * -1, MAXBASE:酩酊, 600, 100, BARCOLORSET("真っ赤"), RESULT:1
	LOCALS = ({BASE:酩酊 * -1,4}/{MAXBASE:酩酊,4})
	PRINTFORM %LOCALS,14,LEFT%
	PRINTL 
ENDIF

SIF GETBIT(画像サイズ切り替えOPTION記憶, 0) || (GETBIT(画像サイズ切り替えOPTION記憶, 4) && 助手は誰だ())
	PRINT 　　　　　　　　　　　 
IF TALENT:PLAYER:2 & 2
	LOCALS = 勃起(%CALLNAME:PLAYER%)
	PRINTFORM %LOCALS,20,LEFT%
CALL PRINT_COLORBAR, BASE:PLAYER:勃起, MAXBASE:PLAYER:勃起, 600, 100, BARCOLORSET("紫"), RESULT:1
	IF BASE:PLAYER:勃起 >= 1000
		LOCALS = ({BASE:PLAYER:勃起})
	ELSE
		LOCALS = ({BASE:PLAYER:勃起,4}/{1000,4})
	ENDIF
	PRINTFORM %LOCALS,14,LEFT%　
	IF あなた射精我慢フラグ
		PRINT 射精我慢:ON 
	ELSE
		PRINT 射精我慢:OFF 
	ENDIF
	PRINTBUTTON "[切り替え]", 890
ENDIF

PRINTL
CALL SHOW_INFO_PALAM_PLAYER

PRINTL
PRINTL
行数保存 = LINECOUNT - 行数保存
IF 行数保存 < 21
	FOR LOCAL, 行数保存, 21
		PRINTL
	NEXT
ENDIF

B = 10000
SIF TALENT:MASTER:絶倫
	B /= 2
MAXBASE:MASTER:2 = B

SIF MAXBASE:2 == 0
	MAXBASE:2 = 10000

CALL MODE_BUTTON

@LIFE_BAR
PRINT 体力
A = BASE:0
SIF BASE:0 < 0
	A = 0
BAR A,MAXBASE:0,32
PRINTFORML ({A}/{MAXBASE:0})


@SAVEINFO
LOCALS = %月算出()%
PUTFORM {RESULT}年目 %LOCALS% {DAY % 30 + 1}日(%GET_DAY()%)

@SHOW_EQUIP_2
;状況の表示
;調教対象には使わない、特殊な着脱式の道具を追加した場合はここに追加をしてください
;（場所変更、マスターや助手が使用する道具等）
SIF TEQUIP:28
	PRINTFORM [ビデオ撮影中(テープ残量{TEQUIP:28}]
IF TEQUIP:野外プレイ == 2
	PRINT  [野外プレイ中]
ELSEIF TEQUIP:野外プレイ == 1
	PRINT  [館内露出プレイ中]
ENDIF
SIF TEQUIP:30 == 1
	PRINT  [羞恥プレイ中]
SIF TEQUIP:お風呂場プレイ
	PRINT  [お風呂場プレイ中]
SIF TEQUIP:33
	PRINT  [新妻プレイ中]
PRINTL  
	
	
@SHOW_INFO_PALAM
#DIMS HTML表示文字列
#DIMS ゲージ表示文
#DIMS 数値表示文

PRINTL
VARSET LOCAL
HTML表示文字列 = 
IF INFO格納フィルタ:0
	LOCALS = <div rect='3375,0,3750,3125'><font color='#666666'><button value='9100'> ▼PALAM_list[\@ INFO格納フィルタ:0 ? 閉 # 開 \@]</button>------</font><br>
ELSE
	LOCALS = <div rect='3375,0,3750,3125'><button value='9100'> ▼PALAM_list[\@ INFO格納フィルタ:0 ? 閉 # 開 \@]</button>------<br>
	ゲージ表示文 = <div rect='3375,0,3750,3125'><br>
	FOR LOCAL,0,10
		SIF !STRLENS(PALAMNAME:LOCAL)
			CONTINUE
		LOCALS += @" %PALAMNAME:LOCAL%Lv{GETPALAMLV(PALAM:LOCAL,15),2} "
		;ゲージ描画
		ゲージ表示文 += "　　　　　"
		IF PALAM:LOCAL && PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1) > 0
			ゲージ表示文 += @"<shape type='rect' param='0,70,{PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1)},30' color='#%カラーパレット_HTML("PALAMゲージ有")%'>"
		ENDIF
		IF PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1) - PALAM:LOCAL && (400 - (PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1))) > 0
			ゲージ表示文 += @"<shape type='rect' param='0,70,{400 - (PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1))},30' color='#%カラーパレット_HTML("PALAMゲージ無")%'>"
		ENDIF
		ゲージ表示文 += "<br>"
		LOCAL:1 = PALAM:LOCAL
		LOCAL:2 = 0
		;これだけ特殊
		IF LOCAL == 9 && LOCAL:1 >= 20000
			LOCALS += @"%"摩擦 0",8%<br>"
		ELSE
			;桁合わせ
			FOR LOCAL:10,0,10
				IF LOCAL:1 > 999999
					LOCAL:1 /= 1000
					LOCAL:2 ++
				ENDIF
			NEXT
			数値表示文 = {LOCAL:1}
			SELECTCASE LOCAL:2
				CASE 1
					数値表示文 += "k"
				CASE 2
					数値表示文 += "M"
				CASE 3
					数値表示文 += "G"
				CASE 4
					数値表示文 += "T"
				CASE 5
					数値表示文 += "P"
			ENDSELECT
			LOCALS += @"%数値表示文, 8%<br>"
			IF LOCAL == 4
				LOCALS += "<br>"
				ゲージ表示文 += "<br>"
			ENDIF
		ENDIF
	NEXT
	HTML表示文字列 += @"%ゲージ表示文%</div>%LOCALS%</div>"
	LOCALS = <div rect='4375,112,3750,3125'>
	ゲージ表示文 = <div rect='4375,112,3750,3125'>
	FOR LOCAL,10,100
		SIF !STRLENS(PALAMNAME:LOCAL)
			CONTINUE
		LOCALS += @" %PALAMNAME:LOCAL%Lv{GETPALAMLV(PALAM:LOCAL,15),2} "

		;ゲージ描画
		ゲージ表示文 += "　　　　　"
		IF PALAM:LOCAL && PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1) > 0
			ゲージ表示文 += @"<shape type='rect' param='0,70,{PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1)},30' color='#%カラーパレット_HTML("PALAMゲージ有")%'>"
		ENDIF
		IF PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1) - PALAM:LOCAL && (400 - (PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1))) > 0
			ゲージ表示文 += @"<shape type='rect' param='0,70,{400 - (PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1))},30' color='#%カラーパレット_HTML("PALAMゲージ無")%'>"
		ENDIF
		ゲージ表示文 += "<br>"

		LOCAL:1 = PALAM:LOCAL
		LOCAL:2 = 0
		;桁合わせ
		FOR LOCAL:10,0,10

			IF LOCAL:1 > 999999
				LOCAL:1 /= 1000
				LOCAL:2 ++
			ENDIF
		NEXT
		数値表示文 = {LOCAL:1}
		SELECTCASE LOCAL:2
			CASE 1
				LOCALS += "k"
			CASE 2
				LOCALS += "M"
			CASE 3
				LOCALS += "G"
			CASE 4
				LOCALS += "T"
			CASE 5
				LOCALS += "P"
		ENDSELECT
		LOCALS += @"%数値表示文, 8%<br>"
		IF LOCAL == 20
			LOCALS += "<br>"
			ゲージ表示文 += "<br>"
		ENDIF
	NEXT
ENDIF
LOCALS += "</div>"
HTML表示文字列 += @"%ゲージ表示文%</div>%LOCALS%"
HTML_PRINT HTML表示文字列, 1

@SHOW_INFO_PALAM_PLAYER
#DIMS HTML文字列
#DIMS ゲージ表示文
#DIMS ゲージ数値

VARSET LOCAL
SIF GETBIT(画像サイズ切り替えOPTION記憶, 0) || (GETBIT(画像サイズ切り替えOPTION記憶, 4) && 助手は誰だ())
	PRINT 　　　　　　　　　　　 
LOCALS = <div rect='0,0,5625,312'>
ゲージ表示文 = <div rect='50,0,5625,312'>
FOR LOCAL, 0, 5
	SIF !STRLENS(PALAMNAME:LOCAL)
		CONTINUE
	SIF TALENT:PLAYER:性別 == 2 && LOCAL == 1
		CONTINUE
	LOCALS += @"  %PALAMNAME:LOCAL%Lv{GETPALAMLV(PALAM:PLAYER:LOCAL,15),2} "
	;現在のPALAMLV
	ゲージ表示文 += "　　　　　"
	IF PALAM:LOCAL && PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1) > 0
		ゲージ表示文 += @"<shape type='rect' param='0,70,{PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1)},30' color='#%カラーパレット_HTML("PALAMゲージ有")%'>"
	ENDIF
	IF PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1) - PALAM:LOCAL && (400 - (PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1))) > 0
		ゲージ表示文 += @"<shape type='rect' param='0,70,{400 - (PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1))},30' color='#%カラーパレット_HTML("PALAMゲージ無")%'>"
	ENDIF
	ゲージ表示文 += " "

	LOCAL:1 = PALAM:PLAYER:LOCAL
	LOCAL:2 = 0
	;これだけ特殊
	IF LOCAL == 9 && LOCAL:1 >= 20000
		ゲージ数値 = %"摩擦 0",8% 
	ELSE
		;桁合わせ
		FOR LOCAL:10,0,10
			IF LOCAL:1 > 999999
				LOCAL:1 /= 1000
				LOCAL:2 ++
			ENDIF
		NEXT
		ゲージ数値 = {LOCAL:1}
		SELECTCASE LOCAL:2
			CASE 1
				ゲージ数値 += "k"
			CASE 2
				ゲージ数値 += "M"
			CASE 3
				ゲージ数値 += "G"
			CASE 4
				ゲージ数値 += "T"
			CASE 5
				ゲージ数値 += "P"
		ENDSELECT
	ENDIF
	LOCALS += @"%ゲージ数値, 8%"
	LOCAL:3 ++
	IF LOCAL:3 % PRINTCPERLINE() == 0
		LOCALS += "<br>"
		SIF GETBIT(画像サイズ切り替えOPTION記憶, 0) || (GETBIT(画像サイズ切り替えOPTION記憶, 4) && 助手は誰だ())
			LOCALS += "　　　　　　　　　　　 "
	ENDIF
NEXT
HTML文字列 = %ゲージ表示文%</div>%LOCALS%</div>
HTML_PRINT HTML文字列, 1

;-------------------------------------------------
;関数名:SORT_CFLAG
;CFLAG:ARGの昇順でRESULT:1にキャラ番号を代入する
;ARG CFLAGの番号
;-------------------------------------------------
@SORT_CFLAG(ARG)
VARSET LOCAL
VARSET RESULT
FOR LOCAL:10,1,CHARANUM
	LOCAL:1 = 0
	FOR LOCAL,1,CHARANUM
		SIF MATCH(RESULT,LOCAL)
			CONTINUE
		IF CFLAG:LOCAL:ARG >= LOCAL:1
			LOCAL:1 = CFLAG:LOCAL:ARG
			LOCAL:2 = LOCAL
		ENDIF
	NEXT
RESULT:(LOCAL:10) = LOCAL:2
NEXT

;キャラの態度
@CHARA_ATTITUDE
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	;二行に分けてるのは単純に見づらいから
	LOCAL:1 = TALENT:LOCAL:恋慕 * 10 + MARK:LOCAL:屈服刻印 * 3 + MARK:LOCAL:快楽刻印 * 3 + MIN(ABL:LOCAL:欲望 * 10,50) + GETPALAMLV(PALAM:LOCAL:欲情,5) * 5 + BASE:LOCAL:ムード / 50 + (1000 - BASE:LOCAL:理性) / 30 + BASE:LOCAL:酩酊 / 20
	LOCAL:1 += CFLAG:LOCAL:好感度レベル / 10 + CFLAG:LOCAL:恋慕レベル
	;既成事実
	SIF GETBIT (CFLAG:LOCAL:1 ,1)
		LOCAL:1 += 5
	;弱味
	SIF CFLAG:LOCAL:10
		LOCAL:1 += 5
	;弱味がなければ欲情補正
	SIF !CFLAG:LOCAL:10
		LOCAL:1 = LOCAL:1 * (5000 + MAX(PALAM:LOCAL:欲情,5000)) / 10000
	SELECTCASE LOCAL:1
		CASE IS >= 150
			CFLAG:LOCAL:6 = 3
		CASE IS >= 100 + TALENT:LOCAL:一線越えない * 20
			CFLAG:LOCAL:6 = 2
		CASE IS >= 50 + TALENT:LOCAL:一線越えない * 10
			CFLAG:LOCAL:6 = 1
		CASEELSE
			CFLAG:LOCAL:6 = 0
	ENDSELECT
	SIF CFLAG:LOCAL:怒り
		CFLAG:LOCAL:6 = 0
	SIF CFLAG:LOCAL:睡眠
		CFLAG:LOCAL:6 = 0
NEXT

;情事の発覚
@AFFAIR_DISCLOSURE(ARG, ARG:1)
;ARG 入室してきたキャラの番号　ARG:1 0なら同室、1なら覗き
VARSET LOCAL
LOCAL:1 = IN_ROOM("MAX", CFLAG:MASTER:現在位置, "CFLAG", GETNUM(CFLAG, "うふふ"))
IF CFLAG:MASTER:隠密
	;隠密だとそもそも遭遇しない
ELSEIF LOCAL:1 && CFLAG:(LOCAL:1):うふふ
	DRAWLINE
	PRINTFORMW %CALLNAME:ARG%に%CALLNAME:(LOCAL:1)%との情事を見られてしまった！
	SIF !GETBIT(CFLAG:ARG:10 , 1)
		PRINTFORMW %CALLNAME:ARG%に弱みを握られた
	IF CFLAG:ARG:6 < 2
		IF !ARG:1
			;見た人
			CALL KOJO_EVENT(1,ARG,LOCAL:1)
			;見られた人（最も地位の高いTARGETで代表）
			CALL KOJO_EVENT(2,LOCAL:1,ARG)
		ENDIF
		DRAWLINE
		LOCAL:2 = 1
		;謹慎処分とか弱味とかの処理を入れる予定
	ELSEIF !ARG:1
		PRINTFORMW %CALLNAME:ARG%は負けじと%CALLNAME:MASTER%に体を摺り寄せてきた
		CFLAG:ARG:うふふ = CFLAG:(LOCAL:1):うふふ
		;見た人
		CALL KOJO_EVENT(3,ARG,LOCAL:1)
		;見られた人（最も地位の高いTARGETで代表）
		CALL KOJO_EVENT(4,LOCAL:1,ARG)
		DRAWLINE
		RETURN 0
	ENDIF
	SETBIT CFLAG:ARG:10 ,1
ENDIF
IF LOCAL:2
	FOR LOCAL,0,CHARANUM
		CFLAG:LOCAL:うふふ = 0
		CALL 性欲ゲージ減少処理(LOCAL)
		TCVAR:LOCAL:初うふふフラグ = 0
	NEXT
	RETURN 1
ENDIF
;通常遭遇
;こちらからも見える位置にいる、もしくは同室
IF CFLAG:MASTER:隠密
	;隠密だとそもそも遭遇しない
ELSEIF !CFLAG:ARG:睡眠 && (!ARG:1 || CAN_MOVE_1(CFLAG:PLAYER:現在マップ種別, CFLAG:MASTER:現在位置, CFLAG:ARG:現在位置) == 2)
	;さっきから居るのは分かっていたか否か
	IF CFLAG:ARG:前ターン位置 == CFLAG:ARG:現在位置 && CFLAG:ARG:前マップ種別 == CFLAG:ARG:現在マップ種別
		PRINTFORMW %CALLNAME:ARG%が%GETPLACENAME(CFLAG:ARG:現在マップ種別, CFLAG:ARG:現在位置)%に居\@CAN_MOVE_1(CFLAG:PLAYER:現在マップ種別, CFLAG:MASTER:前ターン位置, CFLAG:ARG:現在位置) == 2 ? る # た\@
		LOCAL:3 = 1
	;さっきから居るのは分かってて相手が移動した場合は別の場所で表示が出る
	;これいらなくね？
	; ELSEIF CAN_MOVE_1(CFLAG:PLAYER:現在マップ種別, CFLAG:MASTER:前ターン位置, CFLAG:ARG:前ターン位置) < 2 && CFLAG:ARG:前ターン位置 != CFLAG:MASTER:前ターン位置
	; 	PRINTFORMW %CALLNAME:ARG%が%GETPLACENAME(CFLAG:ARG:現在マップ種別, CFLAG:ARG:現在位置)%に来た
	; 	LOCAL:3 = 2
	ENDIF
	IF !ARG:1 && LOCAL:3
		IF CFLAG:ARG:情事目撃 && CFLAG:(CFLAG:ARG:情事目撃):現在位置 != CFLAG:ARG:現在位置
			PRINTFORMW %CALLNAME:ARG%に先の%CALLNAME:(CFLAG:ARG:情事目撃)%との一件について問い詰められた…
			CALL KOJO_EVENT(0, ARG, 2)
			CFLAG:ARG:情事目撃 = 0
		ELSE
			CALL KOJO_EVENT(0, ARG, LOCAL:3 - 1)
		ENDIF
	ENDIF
ENDIF



@MODE_BUTTON
VARSET LOCAL
;うふふでなかったり、3Pじゃなけりゃそもそも表示されない
SIF !CFLAG:うふふ || !助手は誰だ()
	RETURN 0
LOCAL = FLAG:モード管理

SIF LOCAL
	SETCOLOR 0x666666
;短冊or2人同時表示時に助手切り替えボタンと被るのを防ぐ
SIF GETBIT(画像サイズ切り替えOPTION記憶, 4) || GETBIT(画像サイズ切り替えOPTION記憶, 0)
	PRINTFORM %"　" * 12%
PRINTBUTTON "[通常]", 9101
RESETCOLOR
PRINT  
SIF !GETBIT(LOCAL,0)
	SETCOLOR 0x666666
PRINTBUTTON "[同時]", 9102
RESETCOLOR
PRINT  
SIF !GETBIT(LOCAL,1)
	SETCOLOR 0x666666
PRINTBUTTON "[助手(MANUAL)]", 9103
RESETCOLOR
PRINT  
SIF !GETBIT(LOCAL,2)
	SETCOLOR 0x666666
PRINTBUTTON "[助手(AUTO)]", 9104
RESETCOLOR
PRINT   

IF GETBIT(LOCAL,2)

	IF !GETBIT(LOCAL,3)
		PRINTBUTTON "[NORMAL]", 9105
	ELSE
		PRINTBUTTON "[EXTREME]", 9105
	ENDIF
ENDIF
PRINTL 

;-------------------------------------------------
;	時計画像を表示
;
;	ARG		：時刻
;	iSize	：フォントに対するサイズ％数値　縦基準　縦横比維持　省略で500(5倍)
;	sAlign	：align属性　省略で左寄せ　left, center, rightの三種が指定できます
;-------------------------------------------------
@INFO_CLOCK_IMAGE(ARG, iSize=500,sAlign="right")
#DIM iSize
#DIMS sAlign
#DIMS sPrint_Text
#DIM iEnter_Con
#DIM iFont_Hei_mag
#DIM i_height
#DIM i_Width

iEnter_Con = 0
iFont_Hei_mag = GETCONFIG("一行の高さ") * iSize*10 / GETCONFIG("フォントサイズ")
iFont_Hei_mag = (iFont_Hei_mag/10) + (iFont_Hei_mag%10 >= 5?1#0)

LOCALS '= GET_CLOCK_IMAGE_NAME(ARG)
i_height = SPRITEHEIGHT(LOCALS)
i_width = SPRITEWIDTH(LOCALS)
IF i_height >= i_width
	;正方形～縦長の場合、縦幅を描画領域いっぱいにする
	i_height = iFont_Hei_mag
ELSE
	;横長の場合、縦幅を縮小する
	i_height = iFont_Hei_mag * i_height / i_width
ENDIF

sPrint_Text = <div size='{iFont_Hei_mag}, {iFont_Hei_mag}' xpos='{5500 - iFont_Hei_mag}'>
sPrint_Text += @"<p align='%sAlign%'><nobr>"
sPrint_Text += @"<img src='%LOCALS%' height='{i_height}'"
sPrint_Text += @"ypos='{iEnter_Con * iFont_Hei_mag / 10+1}'>"
sPrint_Text += @"</p></div>"

;PRINTFORM %sPrint_Text%
HTML_PRINT sPrint_Text, 1
;-------------------------------------------------
;	時計画像名を取得
;
;	ARG	：時刻
;-------------------------------------------------
@GET_CLOCK_IMAGE_NAME(ARG)
#FUNCTIONS
#DIM HOUR
#DIM MINUTE
#DIMS IMAGE_NAME
HOUR   = ARG / 60 % 12
MINUTE = ARG % 60 / 5 * 5
IMAGE_NAME = 時計_%TOSTR(HOUR,"00")%_%TOSTR(MINUTE,"00")%
RETURNF IMAGE_NAME


@通常時体力ゲージ表示(キャラ番号)
#DIM キャラ番号
#DIMS ゲージ表示文
#DIM ゲージ幅

LOCALS = 体力: 
ゲージ表示文 = <div rect='62,0,3125,250'>
;ゲージ幅決定（1000の時450、最大1800）
ゲージ幅 = MIN(350 + MAX(MAXBASE:キャラ番号:体力 - 700, 0) / 3, 1800)
IF BASE:キャラ番号:体力 > 0
	ゲージ表示文 += @"<shape type='rect' param='0,70,{BASE:キャラ番号:体力 * ゲージ幅 / MAXBASE:キャラ番号:体力},30' color='#50B050'>"
ENDIF
IF MAXBASE:キャラ番号:体力 - BASE:キャラ番号:体力 > 0
	ゲージ表示文 += @"<shape type='rect' param='0,70,{ゲージ幅 - (BASE:キャラ番号:体力 * ゲージ幅 / MAXBASE:キャラ番号:体力)},30' color='#205020'>"
ENDIF
ゲージ表示文 += "</div>"

LOCALS:1 = {BASE:キャラ番号:体力,4}/{MAXBASE:キャラ番号:体力,4}
LOCALS += @"%ゲージ表示文%<div rect='62,0,3125,250'>%LOCALS:1, ゲージ幅 / 50%</div>"
HTML_PRINT LOCALS, 1

@通常時性欲ゲージ表示(キャラ番号)
#DIM キャラ番号
#DIMS ゲージ表示文
#DIM ゲージ幅

LOCALS = 性欲: 
ゲージ表示文 = <div rect='62,0,3125,250'>
;ゲージ幅決定（1000の時450、最大1800）
ゲージ幅 = MIN(350 + MAX(MAXBASE:キャラ番号:性欲 - 700, 0) / 3, 1800)
IF BASE:キャラ番号:性欲 > 0
	ゲージ表示文 += @"<shape type='rect' param='0,70,{BASE:キャラ番号:性欲 * ゲージ幅 / MAXBASE:キャラ番号:性欲},30' color='#%カラーパレット_HTML("えっちなゲージ有")%'>"
ENDIF
IF MAXBASE:キャラ番号:性欲 - BASE:キャラ番号:性欲 > 0
	ゲージ表示文 += @"<shape type='rect' param='0,70,{ゲージ幅 - (BASE:キャラ番号:性欲 * ゲージ幅 / MAXBASE:キャラ番号:性欲)},30' color='#%カラーパレット_HTML("えっちなゲージ無")%'>"
ENDIF
ゲージ表示文 += "</div>"

LOCALS:1 = {BASE:キャラ番号:性欲,4}/{MAXBASE:キャラ番号:性欲,4}
LOCALS += @"%ゲージ表示文%<div rect='62,0,3125,250'>%LOCALS:1, ゲージ幅 / 50%</div>"
HTML_PRINT LOCALS, 1
