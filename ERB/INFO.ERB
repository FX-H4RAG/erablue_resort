@SHOW_STATUS
#DIMS ボタン表示格納用
#DIM ループ用
#DIM 行数保存
#DIM 文字数カウント
#DIM うふふ終了フラグ

VARSET LOCAL
;-------------------------------------------------
;キャラの態度
;-------------------------------------------------
CALL CHARA_ATTITUDE

;-------------------------------------------------
;時間経過、キャラの移動
;-------------------------------------------------
CALL CHARA_MOVEMENT
IF TCVAR:MASTER:馴れ合い強度 >= 1
	LOCAL:1 = IN_ROOM("MIN", CFLAG:MASTER:現在位置, "CFLAG", 6)
	LOCAL:2 = CFLAG:(LOCAL:1):態度保存
	; IF CFLAG:MASTER:隠密
	; 	;隠密だと空気変わらない
	; ELSEIF LOCAL:2 <= 0 && LOCAL:1 > 0 && CFLAG:(LOCAL:1):現在位置 != CFLAG:(LOCAL:1):前ターン位置 && !CFLAG:(LOCAL:1):睡眠
	; 	PRINTFORMW %CALLNAME:(LOCAL:1)%が入ってきたことにより空気が変わってしまった…
	; ENDIF
ENDIF
VARSET LOCAL

TFLAG:行動前時刻 = TIME + 1440 * DAY
CFLAG:MASTER:睡眠 = 0

;-------------------------------------------------
;TARGET選択
;-------------------------------------------------
CALL TARGET_SET()

;TFLAG:TARGET変更用が設定されているときその番号のターゲットが存在するならTARGETに代入、いなければTFLAG:TARGET変更用をリセット
LOCAL:1 = 0
IF TFLAG:調教モード == 3
	TARGET = 飲み会TARGET:1
	FOR LOCAL,1,CHARANUM
		SIF TFLAG:TARGET変更用 < 1
			BREAK
		SIF 飲み会TARGET:LOCAL < 1
			BREAK
		IF 飲み会TARGET:LOCAL == TFLAG:TARGET変更用
			TARGET = 飲み会TARGET:LOCAL
			LOCAL:1 ++
		ENDIF
	NEXT
ELSE
	TARGET = TARGET:1
	IF FLAG:モード管理 == 1
		IF TFLAG:あなたターゲット用
			TFLAG:TARGET変更用 = 0
			TARGET = 0
		ENDIF
	ELSE
		PLAYER = MASTER
	ENDIF
	FOR LOCAL,1,CHARANUM
		SIF TFLAG:TARGET変更用 < 1
			BREAK
		SIF TARGET:LOCAL < 1
			BREAK
		IF TARGET:LOCAL == TFLAG:TARGET変更用
			TARGET = TARGET:LOCAL
			LOCAL:1 ++
		ENDIF
	NEXT
	;もし隠密うふふ中なら相手にTARGETを固定
	IF CFLAG:PLAYER:隠密 && CFLAG:PLAYER:うふふ && (!CFLAG:TARGET:隠密 || !CFLAG:TARGET:うふふ)
		FOR LOCAL,1,CHARANUM
			SIF TARGET:LOCAL < 1
				BREAK
			IF CFLAG:(TARGET:LOCAL):隠密 && CFLAG:(TARGET:LOCAL):うふふ
				TARGET = TARGET:LOCAL
				BREAK
			ENDIF
		NEXT
	ENDIF
ENDIF
SIF !LOCAL:1
	TFLAG:TARGET変更用 = 0

;MASTERと同室フラグ
LOCALS '= ""
LOCALS:1 '= ""
;念の為好感度レベルでソートし直し
CALL SORT_CFLAG(21)
DRAWLINE
うふふ終了フラグ = 0
FOR LOCAL,1,CHARANUM
	SIF CFLAG:(CFLAG_SORTARRAY:LOCAL):滞在期間 < 0
		CONTINUE
	IF CFLAG:(CFLAG_SORTARRAY:LOCAL):現在位置 == CFLAG:MASTER:現在位置 && CFLAG:(CFLAG_SORTARRAY:LOCAL):現在マップ種別 == CFLAG:MASTER:現在マップ種別
		;遭遇口上（ターゲットになってなくても喋る）と情事発覚口上
		IF !CFLAG:(CFLAG_SORTARRAY:LOCAL):同室 && !CFLAG:(CFLAG_SORTARRAY:LOCAL):睡眠
			CALL AFFAIR_DISCLOSURE((CFLAG_SORTARRAY:LOCAL), うふふ終了フラグ, LOCALS, LOCALS:1)
			SIF RESULT
				うふふ終了フラグ = 1
		ELSEIF !TCVAR:(CFLAG_SORTARRAY:LOCAL):挨拶フラグ && !CFLAG:(CFLAG_SORTARRAY:LOCAL):睡眠 && PREVCOM == 841
			CALL AFFAIR_DISCLOSURE((CFLAG_SORTARRAY:LOCAL), うふふ終了フラグ, LOCALS, LOCALS:1)
		ENDIF
		CFLAG:(CFLAG_SORTARRAY:LOCAL):同室 = 1
	;覗きは一旦オミット
	ELSE
	; 	;相手からこちらが見えていて、相手かこちらかが移動したかこちらが押し倒した
	; 	SIF CAN_MOVE(CFLAG:LOCAL:現在位置, CFLAG:MASTER:現在位置) == 2 && (CFLAG:MASTER:前ターン位置 != CFLAG:MASTER:現在位置 || CFLAG:LOCAL:前ターン位置 != CFLAG:LOCAL:現在位置 || TRAIN_COMNAME(PREVCOM) == "押し倒す") && !CFLAG:LOCAL:睡眠
	; 		CALL AFFAIR_DISCLOSURE(LOCAL, 1)
		CFLAG:(CFLAG_SORTARRAY:LOCAL):同室 = 0
	ENDIF
NEXT
SIF うふふ終了フラグ
	CALL うふふ終了処理
SIF LOCALS != ""
	PRINTFORMW %LOCALS%が%GETPLACENAME(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置)%に居た
SIF LOCALS:1 != ""
	PRINTFORMW %LOCALS:1%が%GETPLACENAME(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置)%に来た
;馴れ合い強度決定
;TARGET側
IF TFLAG:調教モード == 3
	FOR LOCAL,1,CHARANUM
		SIF 飲み会TARGET:LOCAL < 1
			BREAK
		;合意がないと2以上にならない
		IF GETBIT(CFLAG:(飲み会TARGET:LOCAL):1,0)
			TCVAR:(飲み会TARGET:LOCAL):馴れ合い強度 = CFLAG:(飲み会TARGET:LOCAL):態度保存
		;飲み会中は酩酊数値一定以上でも2以上になる
		ELSEIF BASE:(飲み会TARGET:LOCAL):酩酊 > 500
			TCVAR:(飲み会TARGET:LOCAL):馴れ合い強度 = CFLAG:(飲み会TARGET:LOCAL):態度保存
		ELSE
			TCVAR:(飲み会TARGET:LOCAL):馴れ合い強度 = MIN(CFLAG:(飲み会TARGET:LOCAL):態度保存 , 1)
		ENDIF
		;自室で遊ぶ場合は１以上保証
		IF CFLAG:(飲み会TARGET:LOCAL):一緒に遊ぶ && CFLAG:(飲み会TARGET:LOCAL):現在位置 == CFLAG:MASTER:311
			TCVAR:(飲み会TARGET:LOCAL):馴れ合い強度 = MAX(TCVAR:(飲み会TARGET:LOCAL):馴れ合い強度 , 1)
		ENDIF
	NEXT
ELSE
	FOR LOCAL,1,CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		;合意がないと2以上にならない
		IF GETBIT(CFLAG:(TARGET:LOCAL):1,0)
			TCVAR:(TARGET:LOCAL):馴れ合い強度 = CFLAG:(TARGET:LOCAL):態度保存
		ELSE
			TCVAR:(TARGET:LOCAL):馴れ合い強度 = MIN(CFLAG:(TARGET:LOCAL):態度保存 , 1)
		ENDIF
		;自室で遊ぶ場合は１以上保証
		IF CFLAG:(TARGET:LOCAL):一緒に遊ぶ && CFLAG:(TARGET:LOCAL):現在位置 == CFLAG:MASTER:311
			TCVAR:(TARGET:LOCAL):馴れ合い強度 = MAX(TCVAR:(TARGET:LOCAL):馴れ合い強度 , 1)
		ENDIF
	NEXT
ENDIF

TCVAR:MASTER:馴れ合い強度 = TCVAR:TARGET:馴れ合い強度

;ウフフ終了TEQUIP処理
FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	IF !CFLAG:LOCAL:うふふ || CFLAG:LOCAL:現在位置 != CFLAG:LOCAL:前ターン位置 || CFLAG:LOCAL:現在マップ種別 != CFLAG:LOCAL:前マップ種別
		IF CSTR:LOCAL:うふふ前の服 != ""
			CALL CLOTHES_CHANGE(LOCAL, CSTR:LOCAL:うふふ前の服)
;			CSTR:LOCAL:着せ替え服 = %CSTR:LOCAL:うふふ前の服%
			CSTR:LOCAL:うふふ前の服 = 
		ENDIF
;		CALL CLOTHES_RESET_TRAIN(LOCAL)
		FOR LOCAL:1, 10, 100
			TEQUIP:LOCAL:(LOCAL:1) = 0
		NEXT
		IF TFLAG:調教モード < 2
			BASE:LOCAL:射精 = 0
			TCVAR:LOCAL:イきそう = 0
		ENDIF
	ENDIF

	IF CFLAG:LOCAL:現在位置 != CFLAG:LOCAL:前ターン位置 || CFLAG:LOCAL:現在マップ種別 != CFLAG:LOCAL:前マップ種別 || CFLAG:MASTER:隠密 != CFLAG:LOCAL:隠密
		CALL 移動時モード消去(LOCAL)
	ENDIF
	
	IF CFLAG:LOCAL:うふふ
		;うふふ時は自動着替えは発動しない
		;ついでにうふふ突入前の服を保存する
		IF CSTR:LOCAL:うふふ前の服 == ""
			CSTR:LOCAL:うふふ前の服 = %CSTR:LOCAL:着せ替え服%
			SIF CSTR:LOCAL:うふふ前の服 == ""
				CSTR:LOCAL:うふふ前の服 = デフォルト
		ENDIF
		;うふふ開始時間も記録
		IF CFLAG:LOCAL:うふふ開始時間記録 < 1 && !CFLAG:LOCAL:体力限界
			CFLAG:LOCAL:うふふ開始時間記録 = TIME + DAY * 1440
		ENDIF
		;うふふ中、全ての服がなくなっているのなら全裸に切り替える
		LOCAL:10 = 0
		FOR LOCAL:11, 150, 172
			SIF TEQUIP:LOCAL:(LOCAL:11)
				LOCAL:10 = 1
		NEXT
		IF LOCAL:10 == 0
			;全裸なら全裸に
			CALL CLOTHES_CHANGE(LOCAL, "全裸")
		ENDIF
		CONTINUE
	ENDIF
	IF CFLAG:LOCAL:飲み会
		;飲み会時は自動着替えは発動しない
		CONTINUE
	ENDIF

	CALL CLOTHES_CHECK(LOCAL)
NEXT
;-------------------------------------------------
;連れ込まれ処理
;-------------------------------------------------
IF 連れ込みパターン == ""
	CALL 物陰連れ込まれ処理
ENDIF
;-------------------------------------------------
;遭遇口上（TARGETのみ）
;-------------------------------------------------
CALL KOJO_遭遇時EVENT
;-------------------------------------------------
;情報表示
;-------------------------------------------------
DRAWLINE
IF サイド領域非表示オプション == 0
	CALL サイド領域描画
ENDIF
;時計画像表示
CALL INFO_CLOCK_IMAGE(TIME)
IF TIME > 1440
	DAY ++
	TIME -= 1440
ENDIF
LOCALS = %月算出()%
PRINTFORM {RESULT}年目 %LOCALS% {DAY % 30 + 1}日(%GET_DAY()%){TIME / 60}時{TIME % 60}分 
IF DAY * 1440 + TIME - CFLAG:MASTER:314 >= 960
	PRINT [要睡眠]
ELSEIF DAY * 1440 + TIME - CFLAG:MASTER:314 >= 720
	PRINT [睡眠可]
ENDIF
PRINT  
;-------------------------------------------------
;状態情報（現在隠密のみ）
;-------------------------------------------------
SIF CFLAG:MASTER:隠密
	PRINTFORM （隠密中）
;-------------------------------------------------
;リゾートランク
;-------------------------------------------------
SIF CFLAG:PLAYER:現在マップ種別 == 0
	PRINTFORM 　　リゾートランク：%リゾートランク文字列:リゾートランク%
PRINTL
;-------------------------------------------------
;現在位置情報
;-------------------------------------------------
PRINTFORM %GETPLACENAME(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置)%
;-------------------------------------------------
;雑務ゲージ
;-------------------------------------------------
IF (CFLAG:PLAYER:現在位置) != 20 && (CFLAG:PLAYER:現在位置) / 100 != 20 && CFLAG:PLAYER:現在マップ種別 == 0
	PRINTFORM 　　　雑務:{雑務ゲージ:(CFLAG:PLAYER:現在位置), 5}/10000　
	CALL PRINT_COLORBAR, 雑務ゲージ:(CFLAG:PLAYER:現在位置), 10000, 600, 60, BARCOLORSET("白"), RESULT:1
ENDIF
IF 開催予定祭り名 != "" &&  開催予定日取り == DAY
	PRINTL
	SETCOLOR カラーパレット("黄")
	PRINTFORM 「%SUBSTRING(開催予定祭り名, 9)%」開催中！
	RESETCOLOR
ENDIF
PRINTL 
IF フィールド展開:欲情フィールド
	SETCOLOR カラーパレット("えっちな色")
	PRINTFORML 展開中フィールド：欲情フィールド　　残り{フィールド展開:欲情フィールド, 2}日
	RESETCOLOR
ENDIF
IF フィールド展開:露出フィールド
	SETCOLOR カラーパレット("えっちな色")
	PRINTFORML 展開中フィールド：露出フィールド　　消費ZP 1日 / 5ZP
	RESETCOLOR
ENDIF
;DRAWLINE
PRINTL ------------------------------------------------------------------------------------------------------------
;-------------------------------------------------
;TARGET情報とモード切り替え
;-------------------------------------------------
VARSET LOCAL

IF TARGET || (FLAG:モード管理 == 1 && PLAYER != MASTER)
	;MASTERの位置から見て他の部屋にだれかいるかどうか
	LOCAL:1 = 0
	FOR LOCAL,1,CHARANUM
		SIF CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置 && CFLAG:LOCAL:現在マップ種別 == CFLAG:MASTER:現在マップ種別
			CONTINUE
		SIF CAN_MOVE(CFLAG:PLAYER:現在マップ種別, CFLAG:MASTER:現在位置, CFLAG:LOCAL:現在位置) == 2
			LOCAL:1 ++
	NEXT
	IF CFLAG:うふふ == 1
		TFLAG:調教モード = 2
	ELSEIF CFLAG:飲み会 == 1
		TFLAG:調教モード = 3
	ELSE
		TFLAG:調教モード = 1
	ENDIF
	文字数カウント = 8
	PRINT 同室者：
	FOR LOCAL,1,CHARANUM
	;同室一覧から即座に相手を変えられるようにする
		SIF TARGET:LOCAL < 1
			BREAK
		IF TARGET:LOCAL == PLAYER
			IF TARGET != MASTER && FLAG:モード管理 == 1
				PRINTBUTTON @"[%CALLNAME:MASTER%]", 2000
				PRINT  
			ENDIF
		ELSEIF TARGET:LOCAL == TARGET
			CONTINUE
		ELSE
			ボタン表示格納用 = [%CALLNAME:(TARGET:LOCAL)%
			IF CFLAG:(TARGET:LOCAL):体力限界
				RESETCOLOR
				ボタン表示格納用 += "(ダウン)"
			ELSE
				IF CFLAG:(TARGET:LOCAL):ゲージ起動分類 == 1
					SETCOLOR カラーパレット("えっちな色")
					ボタン表示格納用 += "(恋)"
				ENDIF
				IF CFLAG:(TARGET:LOCAL):発情期フラグ < 0
					SETCOLOR カラーパレット("えっちな色")
					ボタン表示格納用 += "(発)"
				ENDIF
				IF TALENT:(TARGET:LOCAL):妊娠
					IF PREGNANCY_PROGRESS(TARGET:LOCAL) == 5
						ボタン表示格納用 += "(臨)"
					ELSE
						ボタン表示格納用 += "(妊)"
					ENDIF
					SIF GETCOLOR() != カラーパレット("えっちな色")
						SETCOLOR カラーパレット("ピンク")
				ELSEIF あなた特殊能力:危険日感知 && GETBIT(TALENT:(TARGET:LOCAL):性別, 0) && TALENT:(TARGET:LOCAL):Ｖ感度 > -2
					IF RISKY_DAY(TARGET:LOCAL) == 2
						ボタン表示格納用 += "(危)"
						SETCOLOR カラーパレット("赤ピンク")
					ELSEIF RISKY_DAY(TARGET:LOCAL) == 1
						ボタン表示格納用 += "(危)"
						SIF GETCOLOR() != カラーパレット("えっちな色")
							SETCOLOR カラーパレット("薄ピンク")
					ENDIF
				ENDIF
			ENDIF
			ボタン表示格納用 += "]"
			文字数カウント += STRLENS(ボタン表示格納用)
			IF 文字数カウント > 85
				PRINTL
				文字数カウント = 0
			ENDIF
			IF TFLAG:調教モード == 3
				PRINTPLAINFORM %ボタン表示格納用%
			ELSE
				PRINTBUTTON ボタン表示格納用, (TARGET:LOCAL)+2000
			ENDIF
			RESETCOLOR
			PRINT  
		ENDIF
	NEXT
	IF TFLAG:調教モード == 3
		PRINTL
		PRINT 飲み会参加者：
		FOR LOCAL,1,CHARANUM
		;同室一覧から即座に相手を変えられるようにする
			SIF 飲み会TARGET:LOCAL < 1
				BREAK
			IF 飲み会TARGET:LOCAL == TARGET
			ELSEIF 飲み会TARGET:LOCAL
				ボタン表示格納用 = [%CALLNAME:(飲み会TARGET:LOCAL)%
				IF CFLAG:(TARGET:LOCAL):体力限界
					RESETCOLOR
					ボタン表示格納用 += "(ダウン)"
				ELSE
					IF CFLAG:(飲み会TARGET:LOCAL):ゲージ起動分類 == 1
						SETCOLOR カラーパレット("えっちな色")
						ボタン表示格納用 += "(恋)"
					ENDIF
					IF CFLAG:(飲み会TARGET:LOCAL):発情期フラグ < 0
						SETCOLOR カラーパレット("えっちな色")
						ボタン表示格納用 += "(発)"
					ENDIF
					IF TALENT:(飲み会TARGET:LOCAL):妊娠
						IF PREGNANCY_PROGRESS(飲み会TARGET:LOCAL) == 5
							ボタン表示格納用 += "(臨)"
						ELSE
							ボタン表示格納用 += "(妊)"
						ENDIF
						SIF GETCOLOR() != カラーパレット("えっちな色")
							SETCOLOR カラーパレット("ピンク")
					ELSEIF あなた特殊能力:危険日感知 && GETBIT(TALENT:(飲み会TARGET:LOCAL):性別, 0) && TALENT:(飲み会TARGET:LOCAL):Ｖ感度 > -2
						IF RISKY_DAY(飲み会TARGET:LOCAL) == 2 
							ボタン表示格納用 += "(危)"
							SETCOLOR カラーパレット("赤ピンク")
						ELSEIF RISKY_DAY(飲み会TARGET:LOCAL) == 1
							ボタン表示格納用 += "(危)"
							SIF GETCOLOR() != カラーパレット("えっちな色")
								SETCOLOR カラーパレット("薄ピンク")
						ENDIF
					ENDIF
				ENDIF
				ボタン表示格納用 += "]"
				PRINTBUTTON ボタン表示格納用, (飲み会TARGET:LOCAL)+2000
				RESETCOLOR
			ENDIF
			PRINT  
		NEXT
	ENDIF
	PRINTL
	LOCAL:1 = 0
	FOR LOCAL,1,CHARANUM
		SIF TARGET:LOCAL <= 0
			CONTINUE
		SIF !CFLAG:(TARGET:LOCAL):同行
			CONTINUE
		LOCAL:1 ++
		SIF LOCAL:1 > 1
				PRINT 、
		PRINTFORM %CALLNAME:(TARGET:LOCAL)%
	NEXT
	SIF LOCAL:1
		PRINT 同行中
	PRINTL 
		
	;ここで立ち絵表示
	LOCAL = (ASSI:1 ? ASSI:1# 二人目画像表示用())
	IF FLAG:モード管理 == 1 && TARGET == MASTER
		TARGET = PLAYER
	ENDIF
	IF GETBIT(画像サイズ切り替えOPTION記憶, 0)
		LOCALS:1 '= 短冊グラ表示用文字列関数(TARGET)
		LOCALS '= @"<div rect='0, 0, 1125, 2475'><button value='889' title='表示方法変更:正方形(\@ LOCAL? 3P対応# 単独\@)&#13;&#10;%RESULTS:2%'>%LOCALS:1%"
		LOCALS += @"%"<br>" * 20%[表示方法変更:\@ LOCAL? 3P対応# 正方形\@]</button>"
		SIF LOCAL
			LOCALS += @"<button value='{LOCAL+2000}' title='%CALLNAME:LOCAL%をメインにする'>[%CALLNAME:LOCAL%]</button>"
		LOCALS += @"</div>"
		HTML_PRINT LOCALS, 1
	ELSEIF GETBIT(画像サイズ切り替えOPTION記憶, 4) && LOCAL
		LOCALS:1 '= 顔グラ表示用文字列関数(TARGET)
		LOCALS '= @"<div rect='0, 0, 1125, 2475'><button value='889' title='表示方法変更:正方形(単独)&#13;&#10;%RESULTS:2%'>%LOCALS:1%</button>"
		LOCALS:2 '= 顔グラ表示用文字列関数(LOCAL, 1, 1012)
		LOCALS += @"<br><button value='{LOCAL+2000}' title='%CALLNAME:LOCAL%をメインにする&#13;&#10;%RESULTS:3%'>%LOCALS:2%</button>"
		LOCALS += @"%"<br>" * 19%<button value='889' title='表示方法変更:正方形(単独)'>[表示方法変更:正方形]</button>"
		LOCALS += @"<button value='{LOCAL+2000}' title='%CALLNAME:LOCAL%をメインにする'>[%CALLNAME:LOCAL%]</button>"
		LOCALS += @"</div>"
		HTML_PRINT LOCALS, 1
	ELSE
		LOCALS:1 '= 顔グラ表示用文字列関数(TARGET)
		LOCALS '= @"<div rect='0, 0, 1125, 1350'><button value='889' title='表示方法変更:短冊&#13;&#10;%RESULTS:2%'>%LOCALS:1%"
		LOCALS += @"%"<br>" * 10%[表示方法変更:短冊]</button>"
		SIF LOCAL
			LOCALS += @"<button value='{LOCAL+2000}' title='%CALLNAME:LOCAL%をメインにする'>[%CALLNAME:LOCAL%]</button>"
		LOCALS += @"</div>"
		HTML_PRINT LOCALS, 1
	ENDIF
	ASSI:1 = 0
	IF FLAG:モード管理 == 1 && TARGET == PLAYER
		TARGET = MASTER
	ENDIF
	PRINT 　　　　　　　　　　　 
	IF CFLAG:TARGET:ゲージ起動分類 == 1 || CFLAG:TARGET:発情期フラグ < 0
		SETCOLOR カラーパレット("えっちな色")
	ELSEIF TALENT:TARGET:妊娠
		SETCOLOR カラーパレット("ピンク")
	ELSEIF あなた特殊能力:危険日感知 && GETBIT(TALENT:TARGET:性別, 0) && TALENT:TARGET:Ｖ感度 > -2
		IF RISKY_DAY(TARGET) == 2
			SETCOLOR カラーパレット("赤ピンク")
		ELSEIF RISKY_DAY(TARGET) == 1
			SETCOLOR カラーパレット("薄ピンク")
		ENDIF
	ENDIF
	行数保存 = LINECOUNT
	PRINTFORM %NAME:TARGET%
	SIF CFLAG:TARGET:ゲージ起動分類 == 1
		PRINT (恋慕対象)
	IF CFLAG:TARGET:発情期フラグ < 0
		PRINT (発情期
		PRINT_IMG "えっちハート"
		PRINT )
	ENDIF
	IF TALENT:TARGET:妊娠
		SETCOLOR カラーパレット("ピンク")
		IF PREGNANCY_PROGRESS(TARGET) == 5
			PRINT (臨月
		ELSE
			PRINT (妊娠中
		ENDIF
		PRINT_IMG "えっちハート"
		PRINT )
	ELSEIF あなた特殊能力:危険日感知 && GETBIT(TALENT:TARGET:性別, 0) && TALENT:TARGET:Ｖ感度 > -2
		IF RISKY_DAY(TARGET) == 2
			SETCOLOR カラーパレット("赤ピンク")
			PRINT (
			PRINT_IMG "えっちハート"
			PRINT 超危険日
			PRINT_IMG "えっちハート"
			PRINT )
		ELSEIF RISKY_DAY(TARGET) == 1
			SETCOLOR カラーパレット("薄ピンク")
			PRINT (危険日
			PRINT_IMG "えっちハート"
			PRINT )
		ELSEIF RISKY_DAY(TARGET) == -1
			SETCOLOR 0xccffff
			PRINT （安全日）
		ENDIF
	ENDIF
	RESETCOLOR
	SIF CFLAG:TARGET:隠密
		PRINT （隠密）
	;酩酊系
	IF CFLAG:TARGET:酔いすぎ == 1
		PRINT （泥酔）
	ELSEIF CFLAG:TARGET:酔いすぎ == 2
		PRINT （二日酔い）
	ELSEIF BASE:TARGET:酩酊 == 0
		
	ELSEIF BASE:TARGET:酩酊 < 500
		PRINT （ほろ酔い）
	ELSEIF BASE:TARGET:酩酊 < 1000
		PRINT （酩酊）
	ENDIF
	SIF CFLAG:TARGET:睡眠
		PRINT （睡眠）
	IF CFLAG:TARGET:体力限界
		PRINT （ダウン）
	ENDIF
	LOCAL = BASE:怒り / 200
	IF CFLAG:怒り 
		SETCOLOR 255,0,0
		PRINTL 　　怒
		RESETCOLOR
	ELSEIF LOCAL
		PRINTFORML 　　怒り:%"！" * LOCAL%
	ELSE
		PRINTL 
	ENDIF

	CALL SHOW_INFO_PALAM
	PRINT 　　　　　　　　　　　 
	IF CFLAG:TARGET:滞在期間 == 999
		PRINTFORML 残り滞在日数：常時滞在
	ELSE
		PRINTFORML 残り滞在日数：{CFLAG:TARGET:滞在期間, 2}日
	ENDIF
	IF TARGET == MASTER
		PRINT 　　　　　　　　　　　 
		CALL PRINT_好感度レベル表示(PLAYER)
		PRINTL 
		IF CFLAG:PLAYER:ゲージ起動分類 == 1
			PRINT 　　　　　　　　　　　 
			CALL PRINT_恋慕レベル表示(PLAYER)
			PRINTL
		ENDIF
	ELSE
		PRINT 　　　　　　　　　　　 
		CALL PRINT_好感度レベル表示(TARGET)
		PRINTL 
		IF CFLAG:TARGET:ゲージ起動分類 == 1
			PRINT 　　　　　　　　　　　 
			CALL PRINT_恋慕レベル表示(TARGET)
			PRINTL
		ENDIF
	ENDIF
	RESETCOLOR
	PRINT 　　　　　　　 　　　　　服装:
	IF CSTR:TARGET:着せ替え服 != ""
		IF CSTR:TARGET:着せ替え服 == "浴衣"
			PRINT ユカタヴィラ
		ELSE
			PRINTFORM %CSTR:TARGET:着せ替え服%
		ENDIF
		SIF CSTR:TARGET:着せ替え服追加名 != ""
			PRINTFORM （%CSTR:TARGET:着せ替え服追加名%）
	ELSE
		PRINT 普段着
	ENDIF
	PRINTL
ELSE
	TFLAG:調教モード = 0
ENDIF
PRINT 　　　　　　　　　　　 
CALL SHOW_EQUIP_2

IF TARGET > 0
	IF SAVESTR:ゲームフェイズ管理 == "マッサージモード"
		;本来関数化して汎用化するべきなんだろうけどめんどいのでとりあえず仮分岐
	ELSE
		LOCAL = LIMIT(BASE:ムード / 300,0,5)
		PRINT 　　　　　　　　　　　 
		PRINTFORM ムード:
		SIF BASE:ムード == MAXBASE:ムード
			SETCOLOR 255,51,153
		PRINTFORM %UNICODE(0x2665) * LOCAL,5,LEFT% 
		RESETCOLOR
		LOCAL = MAX(BASE:理性 / 200,1)
		PRINT 理性:
		SIF BASE:理性 < 100
			SETCOLOR 255,255,0
		PRINTFORM %"★" * LOCAL,10,LEFT% 
		RESETCOLOR
	ENDIF
	PRINTL 
	; PRINT 　　　　　　　　　　　 
	; PRINTFORM 満足:
	; CALL PRINT_COLORBAR, BASE:満足, MAXBASE:満足, 600, 60, BARCOLORSET("白"), RESULT:1
	; 	PRINTFORM ({BASE:満足,4}/{MAXBASE:満足,4})
	; PRINTL 
	PRINT 　　　　　　　　　　　 
	CALL 通常時体力ゲージ表示(TARGET)
	PRINTL 
	PRINT 　　　　　　　　　　　 
	CALL 通常時性欲ゲージ表示(TARGET)
	PRINTL 

	PRINT 　　　　　　　　　　　 
	IF BASE:酩酊 > 0
		LOCALS = 酩酊:
		PRINTFORM %LOCALS%
		IF BASE:酩酊 < 500
			CALL PRINT_COLORBAR, BASE:酩酊, MAXBASE:酩酊, 600, 100, BARCOLORSET("青"), RESULT:1
		ELSE
			CALL PRINT_COLORBAR, BASE:酩酊, MAXBASE:酩酊, 600, 100, BARCOLORSET("真っ赤"), RESULT:1
		ENDIF
		LOCALS = ({BASE:酩酊,4}/{MAXBASE:酩酊,4})
		PRINTFORM %LOCALS,14,LEFT%
	ELSEIF BASE:酩酊 < 0
		LOCALS = 二日酔い:
		PRINTFORM %LOCALS%
		CALL PRINT_COLORBAR, BASE:酩酊 * -1, MAXBASE:酩酊, 600, 100, BARCOLORSET("真っ赤"), RESULT:1
		LOCALS = ({BASE:酩酊 * -1,4}/{MAXBASE:酩酊,4})
		PRINTFORM %LOCALS,14,LEFT%
	ENDIF
	PRINTL 
	PRINT 　　　　　　　　　　　 
	
	IF TALENT:TARGET:2 & 2
		LOCALS = 勃起:
		PRINTFORM %LOCALS%
		CALL PRINT_COLORBAR, BASE:勃起, MAXBASE:勃起, 600, 100, BARCOLORSET("紫"), RESULT:1
		IF BASE:勃起 >= 1000
			LOCALS = ({BASE:勃起})
		ELSE
			LOCALS = ({BASE:勃起,4}/{MAXBASE:勃起,4})
		ENDIF
		PRINTFORM %LOCALS,14,LEFT%
	ENDIF
	PRINTL 
	PRINT 　　　　　　　　　　　 
	SETCOLOR 0xffd900
	PRINTBUTTON "[能力表示]", 803
	RESETCOLOR
	PRINTL 
	PRINTL 
	PRINTL 
	IF CFLAG:TARGET:うふふ == 0
		TRYCCALLFORM 口上_INFO画面_一言コメント_{NO:TARGET}(TARGET)
		CATCH
			PRINTL 
		ENDCATCH
	ELSE
		PRINTL 
	ENDIF
ELSEIF FLAG:モード管理 == 1 && TARGET == MASTER
	PRINTL 
	PRINTL 
	PRINTL 

	IF BASE:酩酊 > 0
		LOCALS = 酩酊:
		PRINTFORM %LOCALS%
		IF BASE:酩酊 < 500
			CALL PRINT_COLORBAR, BASE:酩酊, MAXBASE:酩酊, 600, 100, BARCOLORSET("青"), RESULT:1
		ELSE
			CALL PRINT_COLORBAR, BASE:酩酊, MAXBASE:酩酊, 600, 100, BARCOLORSET("真っ赤"), RESULT:1
		ENDIF
		LOCALS = ({BASE:酩酊,4}/{MAXBASE:酩酊,4})
		PRINTFORM %LOCALS,14,LEFT%
		PRINTL 
	ELSEIF BASE:酩酊 < 0
		LOCALS = 二日酔い:
		PRINTFORM %LOCALS%
		CALL PRINT_COLORBAR, BASE:酩酊 * -1, MAXBASE:酩酊, 600, 100, BARCOLORSET("真っ赤"), RESULT:1
		LOCALS = ({BASE:酩酊 * -1,4}/{MAXBASE:酩酊,4})
		PRINTFORM %LOCALS,14,LEFT%
		PRINTL 
	ENDIF
	PRINT 　　　　　　　　　　　 
	
	IF TALENT:TARGET:2 & 2
		LOCALS = 勃起:
		PRINTFORM %LOCALS%
		CALL PRINT_COLORBAR, BASE:勃起, MAXBASE:勃起, 600, 100, BARCOLORSET("紫"), RESULT:1
		IF BASE:勃起 >= 1000
			LOCALS = ({BASE:勃起})
		ELSE
			LOCALS = ({BASE:勃起,4}/{MAXBASE:勃起,4})
		ENDIF
		PRINTFORM %LOCALS,14,LEFT%
	ENDIF
	PRINTL 
	PRINT 　　　　　　　　　　　 
	SETCOLOR 0xffd900
	PRINTBUTTON "[能力表示]", 804
	RESETCOLOR
	PRINTL 
	PRINTL 
	PRINTL 
	PRINTL 
	PRINTL 
ENDIF
DRAWLINE


TRYCCALLFORM SHOW_INFO_%SAVESTR:ゲームフェイズ管理%
CATCH
	CALL SHOW_INFO_PALAM_PLAYER
ENDCATCH

行数保存 = LINECOUNT - 行数保存
IF 行数保存 < 21
	FOR LOCAL, 行数保存, 21
		PRINTL
	NEXT
ENDIF

B = 10000
SIF TALENT:MASTER:絶倫
	B /= 2
MAXBASE:MASTER:2 = B

SIF MAXBASE:2 == 0
	MAXBASE:2 = 10000

CALL MODE_BUTTON

@LIFE_BAR
PRINT 体力
A = BASE:0
SIF BASE:0 < 0
	A = 0
BAR A,MAXBASE:0,32
PRINTFORML ({A}/{MAXBASE:0})


@SAVEINFO
LOCALS = %月算出()%
PUTFORM {RESULT}年目 %LOCALS% {DAY % 30 + 1}日(%GET_DAY()%)

@SHOW_EQUIP_2
;状況の表示
;調教対象には使わない、特殊な着脱式の道具を追加した場合はここに追加をしてください
;（場所変更、マスターや助手が使用する道具等）
SIF TEQUIP:28
	PRINTFORM [ビデオ撮影中(テープ残量{TEQUIP:28}]
IF TEQUIP:野外プレイ == 2
	PRINT  [野外プレイ中]
ELSEIF TEQUIP:野外プレイ == 1
	PRINT  [館内露出プレイ中]
ENDIF
SIF TEQUIP:30 == 1
	PRINT  [羞恥プレイ中]
SIF TEQUIP:お風呂場プレイ
	PRINT  [お風呂場プレイ中]
SIF TEQUIP:33
	PRINT  [新妻プレイ中]
PRINTL  
	
	
@SHOW_INFO_PALAM
#DIMS HTML表示文字列
#DIMS ゲージ表示文
#DIMS 数値表示文

PRINTL
VARSET LOCAL
HTML表示文字列 = 
LOCALS = 
ゲージ表示文 = 
IF INFO格納フィルタ:0
	LOCALS = <div rect='3375,0,3750,3125'><font color='#666666'><button value='9100'> ▼PALAM_list[\@ INFO格納フィルタ:0 ? 閉 # 開 \@]</button>------</font><br>
	ゲージ表示文 = <div rect='3375,0,3750,3125'>
ELSE
	LOCALS = <div rect='3375,0,3750,3125'><button value='9100'> ▼PALAM_list[\@ INFO格納フィルタ:0 ? 閉 # 開 \@]</button>------<br>
	ゲージ表示文 = <div rect='3375,0,3750,3125'><br>
	FOR LOCAL,0,10
		SIF !STRLENS(PALAMNAME:LOCAL)
			CONTINUE
		SIF GETBIT(TALENT:TARGET:性別, 0) == 0 && LOCAL == 1
			CONTINUE
		SIF LOCAL != 4 && TALENT:TARGET:(LOCAL + 部位感度) == -2
			CONTINUE
		LOCALS += @" %PALAMNAME:LOCAL%Lv{GETPALAMLV(PALAM:LOCAL,15),2} "
		;ゲージ描画
		ゲージ表示文 += "　　　　　"
		IF PALAM:LOCAL && PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1) > 0
			ゲージ表示文 += @"<shape type='rect' param='0,70,{MIN(400, PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1))},30' color='#%カラーパレット_HTML("PALAMゲージ有")%'>"
		ENDIF
		IF PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1) - PALAM:LOCAL && (400 - (PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1))) > 0
			ゲージ表示文 += @"<shape type='rect' param='0,70,{MAX(0, 400 - (PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1)))},30' color='#%カラーパレット_HTML("PALAMゲージ無")%'>"
		ENDIF
		ゲージ表示文 += "<br>"
		LOCAL:1 = PALAM:LOCAL
		LOCAL:2 = 0
		;これだけ特殊
		IF LOCAL == 9 && LOCAL:1 >= 20000
			LOCALS += @"%"摩擦 0",8%<br>"
		ELSE
			;桁合わせ
			FOR LOCAL:10,0,10
				IF LOCAL:1 > 999999
					LOCAL:1 /= 1000
					LOCAL:2 ++
				ENDIF
			NEXT
			数値表示文 = {LOCAL:1}
			SELECTCASE LOCAL:2
				CASE 1
					数値表示文 += "k"
				CASE 2
					数値表示文 += "M"
				CASE 3
					数値表示文 += "G"
				CASE 4
					数値表示文 += "T"
				CASE 5
					数値表示文 += "P"
			ENDSELECT
			LOCALS += @"%数値表示文, 8%<br>"
			IF LOCAL == 4
				LOCALS += "<br>"
				ゲージ表示文 += "<br>"
			ENDIF
		ENDIF
	NEXT
	HTML表示文字列 += @"%ゲージ表示文%</div>%LOCALS%</div>"
	LOCALS = <div rect='4375,112,3750,3125'>
	ゲージ表示文 = <div rect='4375,112,3750,3125'>
	FOR LOCAL,10,100
		SIF !STRLENS(PALAMNAME:LOCAL)
			CONTINUE
		LOCALS += @" %PALAMNAME:LOCAL%Lv{GETPALAMLV(PALAM:LOCAL,15),2} "

		;ゲージ描画
		ゲージ表示文 += "　　　　　"
		IF PALAM:LOCAL && PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1) > 0
			ゲージ表示文 += @"<shape type='rect' param='0,70,{PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1)},30' color='#%カラーパレット_HTML("PALAMゲージ有")%'>"
		ENDIF
		IF PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1) - PALAM:LOCAL && (400 - (PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1))) > 0
			ゲージ表示文 += @"<shape type='rect' param='0,70,{400 - (PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1))},30' color='#%カラーパレット_HTML("PALAMゲージ無")%'>"
		ENDIF
		ゲージ表示文 += "<br>"

		LOCAL:1 = PALAM:LOCAL
		LOCAL:2 = 0
		;桁合わせ
		FOR LOCAL:10,0,10

			IF LOCAL:1 > 999999
				LOCAL:1 /= 1000
				LOCAL:2 ++
			ENDIF
		NEXT
		数値表示文 = {LOCAL:1}
		SELECTCASE LOCAL:2
			CASE 1
				LOCALS += "k"
			CASE 2
				LOCALS += "M"
			CASE 3
				LOCALS += "G"
			CASE 4
				LOCALS += "T"
			CASE 5
				LOCALS += "P"
		ENDSELECT
		LOCALS += @"%数値表示文, 8%<br>"
		IF LOCAL == 20
			LOCALS += "<br>"
			ゲージ表示文 += "<br>"
		ENDIF
	NEXT
ENDIF
LOCALS += "</div>"
HTML表示文字列 += @"%ゲージ表示文%</div>%LOCALS%"
HTML_PRINT HTML表示文字列, 1

@SHOW_INFO_PALAM_PLAYER
#DIMS HTML文字列
#DIMS ゲージ表示文
#DIMS ゲージ数値
#DIM 横位置


IF BASE:PLAYER:酩酊 > 0
	SIF GETBIT(画像サイズ切り替えOPTION記憶, 0) || (GETBIT(画像サイズ切り替えOPTION記憶, 4) && 二人目画像表示用())
		PRINT 　　　　　　　　　　　 
	LOCALS = 酩酊(%CALLNAME:PLAYER%)
	PRINTFORM %LOCALS,20,LEFT%
	IF BASE:PLAYER:酩酊 < 500
		CALL PRINT_COLORBAR, BASE:PLAYER:酩酊, MAXBASE:PLAYER:酩酊, 600, 100, BARCOLORSET("青"), RESULT:1
	ELSE
		CALL PRINT_COLORBAR, BASE:PLAYER:酩酊, MAXBASE:PLAYER:酩酊, 600, 100, BARCOLORSET("真っ赤"), RESULT:1
	ENDIF
	LOCALS = ({BASE:PLAYER:酩酊,4}/{MAXBASE:PLAYER:酩酊,4})
	PRINTFORM %LOCALS,14,LEFT%
	PRINTL 
ELSEIF BASE:PLAYER:酩酊 < 0
	SIF GETBIT(画像サイズ切り替えOPTION記憶, 0) || (GETBIT(画像サイズ切り替えOPTION記憶, 4) && 二人目画像表示用())
		PRINT 　　　　　　　　　　　 
	LOCALS = 二日酔い(%CALLNAME:TARGET%)
	PRINTFORM %LOCALS,20,LEFT%
	CALL PRINT_COLORBAR, BASE:PLAYER:酩酊 * -1, MAXBASE:PLAYER:酩酊, 600, 100, BARCOLORSET("真っ赤"), RESULT:1
	LOCALS = ({BASE:PLAYER:酩酊 * -1,4}/{MAXBASE:酩酊,4})
	PRINTFORM %LOCALS,14,LEFT%
	PRINTL 
ENDIF

SIF GETBIT(画像サイズ切り替えOPTION記憶, 0) || (GETBIT(画像サイズ切り替えOPTION記憶, 4) && 二人目画像表示用())
	PRINT 　　　　　　　　　　　 
IF TALENT:PLAYER:2 & 2
	LOCALS = 勃起(%CALLNAME:PLAYER%)
	PRINTFORM %LOCALS,20,LEFT%
CALL PRINT_COLORBAR, BASE:PLAYER:勃起, MAXBASE:PLAYER:勃起, 600, 100, BARCOLORSET("紫"), RESULT:1
	IF BASE:PLAYER:勃起 >= 1000
		LOCALS = ({BASE:PLAYER:勃起})
	ELSE
		LOCALS = ({BASE:PLAYER:勃起,4}/{1000,4})
	ENDIF
	PRINTFORM %LOCALS,14,LEFT%　
	PRINTBUTTON "[射精先設定]", 891
	PRINT 　　
ENDIF
IF PLAYER == MASTER
	IF あなた射精我慢フラグ
		PRINT 絶頂我慢:
		PRINTBUTTON "[ON]", 890
	ELSE
		PRINT 絶頂我慢:
		PRINTBUTTON "[OFF]", 890
	ENDIF
ENDIF

PRINTL

VARSET LOCAL
横位置 = 0
SIF GETBIT(画像サイズ切り替えOPTION記憶, 0) || (GETBIT(画像サイズ切り替えOPTION記憶, 4) && 二人目画像表示用())
	横位置 += 1150
LOCALS = <div rect='{横位置},0,5625,312'>
ゲージ表示文 = <div rect='{横位置 + 50},0,5625,312'>
FOR LOCAL, 0, 5
	SIF !STRLENS(PALAMNAME:LOCAL)
		CONTINUE
	SIF GETBIT(TALENT:PLAYER:性別, 0) == 0 && LOCAL == 1
		CONTINUE
	SIF LOCAL != 4 && TALENT:PLAYER:(LOCAL + 部位感度) == -2
		CONTINUE
	LOCALS += @"  %PALAMNAME:LOCAL%Lv{GETPALAMLV(PALAM:PLAYER:LOCAL,15),2} "
	;現在のPALAMLV
	ゲージ表示文 += "　　　　　"
	IF PALAM:LOCAL && PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1) > 0
		ゲージ表示文 += @"<shape type='rect' param='0,70,{PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1)},30' color='#%カラーパレット_HTML("PALAMゲージ有")%'>"
	ENDIF
	IF PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1) - PALAM:LOCAL && (400 - (PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1))) > 0
		ゲージ表示文 += @"<shape type='rect' param='0,70,{400 - (PALAM:LOCAL * 400 / PALAMLV:(GETPALAMLV(PALAM:LOCAL,15) + 1))},30' color='#%カラーパレット_HTML("PALAMゲージ無")%'>"
	ENDIF
	ゲージ表示文 += " "

	LOCAL:1 = PALAM:PLAYER:LOCAL
	LOCAL:2 = 0
	;これだけ特殊
	IF LOCAL == 9 && LOCAL:1 >= 20000
		ゲージ数値 = %"摩擦 0",8% 
	ELSE
		;桁合わせ
		FOR LOCAL:10,0,10
			IF LOCAL:1 > 999999
				LOCAL:1 /= 1000
				LOCAL:2 ++
			ENDIF
		NEXT
		ゲージ数値 = {LOCAL:1}
		SELECTCASE LOCAL:2
			CASE 1
				ゲージ数値 += "k"
			CASE 2
				ゲージ数値 += "M"
			CASE 3
				ゲージ数値 += "G"
			CASE 4
				ゲージ数値 += "T"
			CASE 5
				ゲージ数値 += "P"
		ENDSELECT
	ENDIF
	LOCALS += @"%ゲージ数値, 8%"
	LOCAL:3 ++
	IF LOCAL:3 % PRINTCPERLINE() == 0
		LOCALS += "<br>"
		ゲージ表示文 += "<br>"
	ENDIF
NEXT
HTML文字列 = %ゲージ表示文%</div>%LOCALS%</div>
HTML_PRINT HTML文字列

PRINTL 
SIF GETBIT(画像サイズ切り替えOPTION記憶, 0) || (GETBIT(画像サイズ切り替えOPTION記憶, 4) && 二人目画像表示用())
	PRINT 　　　　　　　　　　　 
PRINT 連れ込み禁止機能:
IF 連れ込み制御OPTION
	PRINTBUTTON "[ON]", 892
ELSE
	PRINTBUTTON "[OFF]", 892
ENDIF
PRINTL 
SIF GETBIT(画像サイズ切り替えOPTION記憶, 0) || (GETBIT(画像サイズ切り替えOPTION記憶, 4) && 二人目画像表示用())
	PRINT 　　　　　　　　　　　 
IF BATHROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	SETBGCOLOR 0x101020
	SETCOLOR カラーパレット("黄")
	PRINTL ♨♨♨♨♨【入浴中】♨♨♨♨♨
	RESETCOLOR
ELSEIF SAUNAROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	SETBGCOLOR 0x201010
	SETCOLOR カラーパレット("真っ赤")
	PRINTL ♨♨♨♨♨【サウナ中】♨♨♨♨♨
	RESETCOLOR
ELSE
	RESETBGCOLOR
	PRINTL
ENDIF

;-------------------------------------------------
;関数名:SORT_CFLAG
;CFLAG:ARGの昇順でCFLAG_SORTARRAY:1にキャラ番号を代入する
;ARG CFLAGの番号
;-------------------------------------------------
@SORT_CFLAG(ARG)
#DIM SORT_NUM, 3000
VARSET SORT_NUM
VARSET CFLAG_SORTARRAY

FOR LOCAL, 1, CHARANUM
	SORT_NUM:(LOCAL - 1) = (CFLAG:LOCAL:ARG + 1) * -1
	CFLAG_SORTARRAY:(LOCAL - 1) = LOCAL
NEXT
ARRAYMSORT SORT_NUM, CFLAG_SORTARRAY
ARRAYSHIFT CFLAG_SORTARRAY, 1, 0

;-------------------------------------------------
;関数名:TARGET_SET
;同室中のキャラをTARGETに代入
;-------------------------------------------------
@TARGET_SET()
VARSET LOCAL
VARSET TARGET
VARSET 飲み会TARGET
IF TFLAG:調教モード == 3
	FOR LOCAL,1,CHARANUM
		SIF CFLAG:MASTER:現在マップ種別 != CFLAG:(CFLAG_SORTARRAY:LOCAL):現在マップ種別
			CONTINUE
		SIF CFLAG:MASTER:現在位置 != CFLAG:(CFLAG_SORTARRAY:LOCAL):現在位置
			CONTINUE
		IF CFLAG:(CFLAG_SORTARRAY:LOCAL):飲み会
			LOCAL:1 ++
			飲み会TARGET:(LOCAL:1) = CFLAG_SORTARRAY:LOCAL
		ELSE
			LOCAL:2 ++
			TARGET:(LOCAL:2) = CFLAG_SORTARRAY:LOCAL
		ENDIF
	NEXT
ELSE
	FOR LOCAL,1,CHARANUM
		SIF CFLAG:MASTER:現在マップ種別 != CFLAG:(CFLAG_SORTARRAY:LOCAL):現在マップ種別
			CONTINUE
		SIF CFLAG:MASTER:現在位置 != CFLAG:(CFLAG_SORTARRAY:LOCAL):現在位置
			CONTINUE
		LOCAL:1 ++
		TARGET:(LOCAL:1) = CFLAG_SORTARRAY:LOCAL
	NEXT
ENDIF


;キャラの態度
@CHARA_ATTITUDE
FOR LOCAL,1,CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	;二行に分けてるのは単純に見づらいから
	LOCAL:1 = TALENT:LOCAL:恋慕 * 10 + MARK:LOCAL:屈服刻印 * 3 + MARK:LOCAL:快楽刻印 * 3 + MIN(ABL:LOCAL:欲望 * 10,50) + GETPALAMLV(PALAM:LOCAL:欲情,5) * 5 + BASE:LOCAL:ムード / 50 + (1000 - BASE:LOCAL:理性) / 30 + BASE:LOCAL:酩酊 / 20
	LOCAL:1 += CFLAG:LOCAL:好感度レベル / 10 + 恋慕_恋心比較(LOCAL)
	;既成事実
	SIF GETBIT (CFLAG:LOCAL:1 ,1)
		LOCAL:1 += 5
	;欲情補正
	LOCAL:1 = LOCAL:1 * (5000 + MAX(PALAM:LOCAL:欲情,5000)) / 10000
	SELECTCASE LOCAL:1
		CASE IS >= 150
			CFLAG:LOCAL:態度保存 = 3
		CASE IS >= 100 + TALENT:LOCAL:一線越えない * 20
			CFLAG:LOCAL:態度保存 = 2
		CASE IS >= 50 + TALENT:LOCAL:一線越えない * 10
			CFLAG:LOCAL:態度保存 = 1
		CASEELSE
			CFLAG:LOCAL:態度保存 = 0
	ENDSELECT
	SIF CFLAG:LOCAL:怒り
		CFLAG:LOCAL:態度保存 = 0
	SIF CFLAG:LOCAL:睡眠
		CFLAG:LOCAL:態度保存 = 0
NEXT

;情事の発覚
@AFFAIR_DISCLOSURE(対象キャラ, うふふ終了フラグ, 在室キャラ, 在室キャラ:1)
#DIM 対象キャラ
#DIM うふふ終了フラグ
#DIMS REF 在室キャラ
#DIM L_TMP
#DIMS L_TMPS, 2

VARSET LOCAL
LOCAL:1 = IN_ROOM("MAX", CFLAG:MASTER:現在位置, "CFLAG", GETNUM(CFLAG, "うふふ"))
IF CFLAG:MASTER:隠密
	;隠密だとそもそも遭遇しない
ELSEIF LOCAL:1 && CFLAG:(LOCAL:1):うふふ && うふふ終了フラグ == 0
	DRAWLINE
	PRINTFORMW %CALLNAME:対象キャラ%に%CALLNAME:(LOCAL:1)%との情事を見られてしまった！
	IF 陥落チェック(対象キャラ)
		PRINTFORMW %CALLNAME:対象キャラ%は負けじと%CALLNAME:MASTER%に体を摺り寄せてきた
		CFLAG:対象キャラ:うふふ = CFLAG:(LOCAL:1):うふふ
		DRAWLINE
		RETURN 0
	ELSE
		CFLAG:対象キャラ:情事目撃 = LOCAL:1
		LOCAL:2 = 1
	ENDIF
ENDIF
IF LOCAL:2 || うふふ終了フラグ
	RETURN 1
ENDIF
;通常遭遇
;先んじて部屋にいたのか、あるいは後から来たのか
IF (CFLAG:対象キャラ:前ターン位置 == CFLAG:対象キャラ:現在位置 && CFLAG:対象キャラ:前マップ種別 == CFLAG:対象キャラ:現在マップ種別)
	IF REGEXPMATCH(在室キャラ, "たち$", L_TMP, L_TMPS) 
	ELSEIF STRLENSU(在室キャラ) > 30
		在室キャラ += "たち"
	ELSE
		SIF 在室キャラ != ""
			在室キャラ += "、"
		在室キャラ += CALLNAME:対象キャラ
	ENDIF
ELSEIF (CFLAG:対象キャラ:前ターン位置 != CFLAG:対象キャラ:現在位置 || CFLAG:対象キャラ:前マップ種別 != CFLAG:対象キャラ:現在マップ種別) && (CFLAG:MASTER:前ターン位置 == CFLAG:MASTER:現在位置 && CFLAG:MASTER:前マップ種別 == CFLAG:MASTER:現在マップ種別)
	IF REGEXPMATCH(在室キャラ:1, "たち$", L_TMP, L_TMPS) 
	ELSEIF STRLENSU(在室キャラ:1) > 30
		在室キャラ:1 += "たち"
	ELSE
		SIF 在室キャラ:1 != ""
			在室キャラ:1 += "、"
		在室キャラ:1 += CALLNAME:対象キャラ
	ENDIF
ENDIF
IF CFLAG:MASTER:隠密
	;隠密だと話しかけてこない
	LOCAL:3 = 0
ELSE
	LOCAL:3 = 1
ENDIF
IF LOCAL:3
	IF CFLAG:対象キャラ:情事目撃 && CFLAG:(CFLAG:対象キャラ:情事目撃):現在位置 != CFLAG:対象キャラ:現在位置
		PRINTFORMW %CALLNAME:対象キャラ%に先の%CALLNAME:(CFLAG:対象キャラ:情事目撃)%との一件について問い詰められた…
		TRYCALLFORM KOJO_EVENT_情事目撃_{NO:対象キャラ}(対象キャラ)
		CFLAG:対象キャラ:情事目撃 = 0
	ELSEIF !TCVAR:対象キャラ:挨拶フラグ
		L_TMP = LINECOUNT
		TRYCCALLFORM KOJO_EVENT_挨拶_{NO:対象キャラ}(対象キャラ)
			IF L_TMP == LINECOUNT && STRLENSU(在室キャラ) + STRLENSU(在室キャラ:1) < 31 && MAX(CFLAG:対象キャラ:好感度レベル, CFLAG:対象キャラ:恋慕レベル) > 関係閾値:1
				PRINTFORML あなたを見かけた%CALLNAME:対象キャラ%\@ MAX(CFLAG:対象キャラ:好感度レベル, CFLAG:対象キャラ:恋慕レベル) > 関係閾値:3? は、駆け寄ってきて# が\@挨拶してきた
			ENDIF
		CATCH
			;ある程度顔見知りじゃないと挨拶しない
			IF STRLENSU(在室キャラ) + STRLENSU(在室キャラ:1) < 31 && MAX(CFLAG:対象キャラ:好感度レベル, CFLAG:対象キャラ:恋慕レベル) > 関係閾値:1
				PRINTFORML あなたを見かけた%CALLNAME:対象キャラ%\@ MAX(CFLAG:対象キャラ:好感度レベル, CFLAG:対象キャラ:恋慕レベル) > 関係閾値:3? は、駆け寄ってきて# が\@挨拶してきた
			ENDIF
		ENDCATCH
		TCVAR:対象キャラ:挨拶フラグ = 1
	ENDIF
ENDIF



@MODE_BUTTON
VARSET LOCAL
;うふふ時のみ
SIF !CFLAG:うふふ
	RETURN 0
LOCAL = FLAG:モード管理

SIF LOCAL == 0
	SETCOLOR カラーパレット("黄")
;短冊or2人同時表示時に助手切り替えボタンと被るのを防ぐ
SIF GETBIT(画像サイズ切り替えOPTION記憶, 4) || GETBIT(画像サイズ切り替えOPTION記憶, 0)
	PRINTFORM %"　" * 12%
PRINTBUTTON "[通常]", 9101
RESETCOLOR
PRINT  
IF LOCAL == 1
	SETCOLOR カラーパレット("黄")
	PRINTBUTTON @"[指示(%CALLNAME:PLAYER%指示中)]", 9102
ELSE
	PRINTBUTTON "[指示]", 9102
ENDIF
RESETCOLOR
IF GET_TARGETNUM() > 1
	PRINT  
	IF LOCAL == 2
		SETCOLOR カラーパレット("黄")
		LOCALS = [同時(
		FOR LOCAL, 0, 同時モード_選択数
			SIF LOCAL > 0
				LOCALS += "、"
			LOCALS += @"%CALLNAME:(同時モード_選択キャラ:LOCAL)%"
		NEXT
		LOCALS += ")]"
		PRINTBUTTON @"%LOCALS%", 9103
		RESETCOLOR
	ELSE
		PRINTBUTTON "[同時]", 9103
	ENDIF
ENDIF
PRINT  
LOCALS = [AUTO設定:
SELECTCASE FLAG:オートコマンド切り替えオプション
	CASE 0
		LOCALS += "全許可"
	CASEELSE
		LOCAL:1 = 0
		IF GETBIT(FLAG:オートコマンド切り替えオプション, 0)
			LOCALS += "AUTO"
			LOCAL:1 += 1
		ENDIF
		IF GETBIT(FLAG:オートコマンド切り替えオプション, 1)
			SIF LOCAL:1
				LOCALS += "/"
			LOCALS += "一般"
			LOCAL:1 += 1
		ENDIF
		IF GETBIT(FLAG:オートコマンド切り替えオプション, 2)
			SIF LOCAL:1
				LOCALS += "/"
			LOCALS += "自慰"
			LOCAL:1 += 1
		ENDIF
		IF GETBIT(FLAG:オートコマンド切り替えオプション, 3)
			SIF LOCAL:1
				LOCALS += "/"
			LOCALS += "許可系"
			LOCAL:1 += 1
		ENDIF
		LOCALS += "禁止"
		SIF LOCAL:1 == 3
			LOCALS = [AUTO設定:全禁止
ENDSELECT


LOCALS += "]"
PRINTBUTTON @"%LOCALS%", 9104

PRINTL 

;-------------------------------------------------
;	時計画像を表示
;
;	ARG		：時刻
;	iSize	：フォントに対するサイズ％数値　縦基準　縦横比維持　省略で500(5倍)
;	sAlign	：align属性　省略で左寄せ　left, center, rightの三種が指定できます
;-------------------------------------------------
@INFO_CLOCK_IMAGE(ARG, iSize=500,sAlign="right")
#DIM iSize
#DIMS sAlign
#DIMS sPrint_Text
#DIM iEnter_Con
#DIM iFont_Hei_mag
#DIM i_height
#DIM i_Width

iEnter_Con = 0
iFont_Hei_mag = GETCONFIG("一行の高さ") * iSize*10 / GETCONFIG("フォントサイズ")
iFont_Hei_mag = (iFont_Hei_mag/10) + (iFont_Hei_mag%10 >= 5?1#0)

LOCALS '= GET_CLOCK_IMAGE_NAME(ARG)
i_height = SPRITEHEIGHT(LOCALS)
i_width = SPRITEWIDTH(LOCALS)
IF i_height >= i_width
	;正方形～縦長の場合、縦幅を描画領域いっぱいにする
	i_height = iFont_Hei_mag
ELSE
	;横長の場合、縦幅を縮小する
	i_height = iFont_Hei_mag * i_height / i_width
ENDIF

sPrint_Text = <div size='{iFont_Hei_mag}, {iFont_Hei_mag}' xpos='{5500 - iFont_Hei_mag}'>
sPrint_Text += @"<p align='%sAlign%'><nobr>"
sPrint_Text += @"<img src='%LOCALS%' height='{i_height}'"
sPrint_Text += @"ypos='{iEnter_Con * iFont_Hei_mag / 10+1}'>"
sPrint_Text += @"</p></div>"

;PRINTFORM %sPrint_Text%
HTML_PRINT sPrint_Text, 1
;-------------------------------------------------
;	時計画像名を取得
;
;	ARG	：時刻
;-------------------------------------------------
@GET_CLOCK_IMAGE_NAME(ARG)
#FUNCTIONS
#DIM HOUR
#DIM MINUTE
#DIMS IMAGE_NAME
HOUR   = ARG / 60 % 12
MINUTE = ARG % 60 / 5 * 5
IMAGE_NAME = 時計_%TOSTR(HOUR,"00")%_%TOSTR(MINUTE,"00")%
RETURNF IMAGE_NAME


@通常時体力ゲージ表示(キャラ番号)
#DIM キャラ番号
#DIMS ゲージ表示文
#DIM ゲージ幅

LOCALS = 体力: 
ゲージ表示文 = <div rect='62,0,3125,250'>
;ゲージ幅決定（1000の時450、最大1800）
ゲージ幅 = MIN(350 + MAX(MAXBASE:キャラ番号:体力 - 700, 0) / 3, 1800)
IF BASE:キャラ番号:体力 > 0
	ゲージ表示文 += @"<shape type='rect' param='0,70,{MAX(1, BASE:キャラ番号:体力 * ゲージ幅 / MAXBASE:キャラ番号:体力)},30' color='#50B050'>"
ENDIF
IF MAXBASE:キャラ番号:体力 - BASE:キャラ番号:体力 > 0
	ゲージ表示文 += @"<shape type='rect' param='0,70,{ゲージ幅 - (MAX(1, BASE:キャラ番号:体力 * ゲージ幅 / MAXBASE:キャラ番号:体力))},30' color='#205020'>"
ENDIF
ゲージ表示文 += "</div>"

LOCALS:1 = {BASE:キャラ番号:体力,4}/{MAXBASE:キャラ番号:体力,4}
LOCALS += @"%ゲージ表示文%<div rect='62,0,3125,250'>%LOCALS:1, ゲージ幅 / 50%</div>"
HTML_PRINT LOCALS, 1

@通常時性欲ゲージ表示(キャラ番号)
#DIM キャラ番号
#DIMS ゲージ表示文
#DIM ゲージ幅

LOCALS = 性欲: 
ゲージ表示文 = <div rect='62,0,3125,250'>
;ゲージ幅決定（1000の時450、最大1800）
ゲージ幅 = MIN(350 + MAX(MAXBASE:キャラ番号:性欲 - 700, 0) / 3, 1800)
IF BASE:キャラ番号:性欲 > 0
	ゲージ表示文 += @"<shape type='rect' param='0,70,{MAX(1, BASE:キャラ番号:性欲 * ゲージ幅 / MAXBASE:キャラ番号:性欲)},30' color='#%カラーパレット_HTML("えっちなゲージ有")%'>"
ENDIF
IF MAXBASE:キャラ番号:性欲 - BASE:キャラ番号:性欲 > 0
	ゲージ表示文 += @"<shape type='rect' param='0,70,{ゲージ幅 - MAX(1, BASE:キャラ番号:性欲 * ゲージ幅 / MAXBASE:キャラ番号:性欲)},30' color='#%カラーパレット_HTML("えっちなゲージ無")%'>"
ENDIF
ゲージ表示文 += "</div>"

LOCALS:1 = {BASE:キャラ番号:性欲,4}/{MAXBASE:キャラ番号:性欲,4}

IF CFLAG:キャラ番号:うふふ
	ゲージ表示文 += "<div rect='62,0,3125,250'>"
	;ゲージ幅決定（1000の時450、最大1800）
	ゲージ幅 = MIN(350 + MAX(MAXBASE:キャラ番号:満足 - 700, 0) / 3, 1800)
	IF BASE:キャラ番号:満足 > 0
		ゲージ表示文 += @"<shape type='rect' param='0,70,{MAX(1, BASE:キャラ番号:満足 * ゲージ幅 / MAXBASE:キャラ番号:満足)},30' color='#%カラーパレット_HTML("黄")%'>"
	ENDIF
	; IF MAXBASE:キャラ番号:満足 - BASE:キャラ番号:満足 > 0
	; 	ゲージ表示文 += @"<shape type='rect' param='0,70,{ゲージ幅 - MAX(1, BASE:キャラ番号:満足 * ゲージ幅 / MAXBASE:キャラ番号:満足)},30' color='#%カラーパレット_HTML("えっちなゲージ無")%'>"
	; ENDIF
	ゲージ表示文 += "</div>"
	ゲージ表示文 += @"<div rect='{MAX(1, BASE:キャラ番号:満足 * ゲージ幅 / MAXBASE:キャラ番号:満足) + 20},65,100,250'>▴</div>"
	LOCALS:2 = 満足度{BASE:キャラ番号:満足 * 100 / MAX(BASE:キャラ番号:性欲, 1), 3}\%
	IF 連れ込みパターン != ""
		LOCALS:2 = <img src='えっちハート'>欲求不満<img src='えっちハート'>：%連れ込みパターン%
	ENDIF
	ゲージ表示文 += @"<div rect='{LIMIT((BASE:キャラ番号:満足 * ゲージ幅 / MAXBASE:キャラ番号:満足) - 80, 1, 1350)},145,3000,250'>%LOCALS:2%</div>"

ENDIF

LOCALS += @"%ゲージ表示文%<div rect='62,0,3125,250'>%LOCALS:1, ゲージ幅 / 50%</div>"
HTML_PRINT LOCALS, 1
