@PRINT_STATE(ARG, ページめくりフラグ)
#DIM ページめくりフラグ
#DIM 能力表示

$STATE_START
;ツールチップ設定
TOOLTIP_CUSTOM 1
TOOLTIP_SETFONTSIZE 12
TOOLTIP_SETDURATION 100000

IF ARG == MASTER && (能力表示 == 2 || 能力表示 == 3)
	能力表示 = 0
ENDIF
;非戦闘キャラは戦闘能力画面封印
IF TALENT:ARG:非戦闘キャラ && 能力表示 == 2
	能力表示 = 0
ENDIF
;キャラ解説がないなら封印
IF EXISTFUNCTION(@"口上_ステータス画面_キャラ解説_{NO:ARG}") == 0 && 能力表示 == 3
	能力表示 = 0
ENDIF
;血縁が無いならタブ封印
IF EXIST_FAMILY(CFLAG:ARG:人物番号) == 0 && 能力表示 == 4
	能力表示 = 0
ENDIF


DRAWLINE

IF 能力表示 == 0
	CALL 通常能力表示画面(ARG)
ELSEIF 能力表示 == 1
	CALL 性的能力表示画面(ARG)
ELSEIF 能力表示 == 2
	CALL 戦闘能力表示画面(ARG)
ELSEIF 能力表示 == 3
	CALL キャラ解説表示画面(ARG)
ELSEIF 能力表示 == 4
	CALL 血縁関係表示画面(ARG)
ELSEIF 能力表示 == 5
	CALL キャラ履歴表示画面(ARG)
ENDIF


;CALL CHOICE_LOVE(ARG)
PRINTL
DRAWLINE
IF 能力表示 != 0
	PRINTBUTTON "[90]通常能力　　", 90
ELSE
	SETCOLOR カラーパレット("黄")
	PRINTPLAIN [90]通常能力　　
	RESETCOLOR
ENDIF
IF 能力表示 != 1
	PRINTBUTTON "[91]性的能力　　", 91
ELSE
	SETCOLOR カラーパレット("黄")
	PRINTPLAIN [91]性的能力　　
	RESETCOLOR
ENDIF
IF TALENT:ARG:非戦闘キャラ == 0 && ARG != MASTER 
	IF 能力表示 != 2
		PRINTBUTTON "[92]戦闘能力　　", 92
	ELSE
		SETCOLOR カラーパレット("黄")
		PRINTPLAIN [92]戦闘能力　　
		RESETCOLOR
	ENDIF
ENDIF
IF EXISTFUNCTION(@"口上_ステータス画面_キャラ解説_{NO:ARG}")
	IF 能力表示 != 3
		PRINTBUTTON "[93]キャラ解説　　", 93
	ELSE
		SETCOLOR カラーパレット("黄")
		PRINTPLAIN [93]キャラ解説　　
		RESETCOLOR
	ENDIF
ENDIF
IF EXIST_FAMILY(CFLAG:ARG:人物番号)
	IF 能力表示 != 4
		PRINTBUTTON "[94]血縁関係　　", 94
	ELSE
		SETCOLOR カラーパレット("黄")
		PRINTPLAIN [94]血縁関係　　
		RESETCOLOR
	ENDIF
ENDIF
IF 能力表示 != 5
	PRINTBUTTON "[95]キャラ履歴　　", 95
ELSE
	SETCOLOR カラーパレット("黄")
	PRINTPLAIN [95]キャラ履歴　　
	RESETCOLOR
ENDIF
PRINTL
DRAWLINE
IF ARG != MASTER
	IF GETBIT(ページめくりフラグ, 0)
		PRINTBUTTONLC "[100]前のキャラへ", 100
	ELSE
		PRINTLC  
	ENDIF
	IF GETBIT(ページめくりフラグ, 1)
		PRINTBUTTONLC "[101]次のキャラへ", 101
	ELSE
		PRINTLC  
	ENDIF
	IF TFLAG:調教モード == -1
		PRINTBUTTONLC "[130]画像フォルダ選択", 130
		IF ARG == 6
			;一人でresourceフォルダ2つ使うのビカラだけだよね？
			PRINTBUTTONLC "[131]ネズ耳切り替え", 131
		ENDIF
	ENDIF
	PRINTBUTTONLC "[140]汎用喘ぎ個別設定", 140

	PRINTL
	IF 初期設定中 == 0
		IF 能力表示 == 2
			IF CFLAG:ARG:滞在期間 >= 0
				PRINTL
				PRINTBUTTONLC "[120]指輪装備変更", 120
				PRINTBUTTONLC "[121]耳飾り装備変更", 121
				PRINTBUTTONLC "[122]ジョブの証装備変更", 122
				PRINTL
				PRINTBUTTONLC "[123]経験値増加アイテムを使う", 123
				PRINTL
				PRINTBUTTONLC "[124]カットイン個別設定変更", 124
				PRINTL
				PRINTL
			ENDIF
		ELSE
			LOCAL:2 = 恋慕枠判定()
			LOCAL:1 = -1
			FOR LOCAL, 0, LOCAL:2
				IF 恋慕対象フラグ:LOCAL == ARG
					LOCAL:1 = LOCAL
					BREAK
				ENDIF
			NEXT
			IF LOCAL:1 >=0
				PRINTBUTTON "[110]恋慕対象から外す", 110
				PRINTL
			ELSE
				PRINTBUTTON "[110]恋慕対象に選ぶ", 110
				PRINTL
			ENDIF
		ENDIF
	ENDIF
ENDIF
PRINTBUTTON "[999]戻る", 999
PRINTL
DO
BINPUT
SELECTCASE RESULT
	CASE 100
		能力表示現在ページ = 0
		RETURN 100
	CASE 101
		能力表示現在ページ = 0
		RETURN 101
	CASE 90
		能力表示 = 0
		能力表示現在ページ = 0
		RESTART
	CASE 91
		能力表示 = 1
		能力表示現在ページ = 0
		RESTART
	CASE 92
		能力表示 = 2
		能力表示現在ページ = 0
		RESTART
	CASE 93
		能力表示 = 3
		能力表示現在ページ = 0
		RESTART
	CASE 94
		能力表示 = 4
		能力表示現在ページ = 0
		RESTART
	CASE 95
		能力表示 = 5
		能力表示現在ページ = 0
		RESTART
	CASE 110
		CALL CHOICE_LOVE(ARG)
		RESTART
	CASE 120
		CALL 指輪付け替え処理画面(ARG)
		RESTART
	CASE 121
		CALL 耳飾り付け替え処理画面(ARG)
		RESTART
	CASE 122
		CALL 証付け替え処理画面(ARG)
		RESTART
	CASE 123
		CALL 経験値増加アイテム処理画面(ARG)
		RESTART
	CASE 124
		CALL 奥義カットイン個別設定画面(ARG)
		RESTART
	CASE 130
		IF TFLAG:調教モード == -1
			CALL 画像フォルダ選択(ARG)
			RESTART
		ENDIF
	CASE 131
		IF ARG == 6 && TFLAG:調教モード == -1
			CALL CHARACOM370_6()
			RESTART
		ENDIF
	CASE 132
		INVERTBIT 身体項目表示切り替えOPTION, 1
		RESTART
	CASE 140
		CALL 汎用喘ぎ個別設定(ARG)
		RESTART
	CASE 150
		INVERTBIT EXP表示切替, 0
		RESTART
	CASE 200
		INVERTBIT 全表示フラグ:0, 0
		RESTART
	CASE 900
		能力表示現在ページ --
		RESTART
	CASE 901
		能力表示現在ページ ++
		RESTART
	CASE 998
		CALL 弱点コマンド全表示(ARG)
		RESTART
	CASE 999
		能力表示 = 0
		VARSET 全表示フラグ
		RETURN 0
	CASE 1001
		IF GETBIT(履歴表示フィルタ, 0)
			CLEARBIT 履歴表示フィルタ, 0
		ELSE
			SETBIT 履歴表示フィルタ, 0
		ENDIF
		RESTART
	CASE 1002
		IF GETBIT(履歴表示フィルタ, 1)
			CLEARBIT 履歴表示フィルタ, 1
		ELSE
			SETBIT 履歴表示フィルタ, 1
		ENDIF
		RESTART
	CASE 1003
		IF GETBIT(履歴表示フィルタ, 2)
			CLEARBIT 履歴表示フィルタ, 2
		ELSE
			SETBIT 履歴表示フィルタ, 2
		ENDIF
		RESTART
	CASE 1004
		IF GETBIT(履歴表示フィルタ, 3)
			CLEARBIT 履歴表示フィルタ, 3
		ELSE
			SETBIT 履歴表示フィルタ, 3
		ENDIF
		RESTART
	CASE 1005
		IF GETBIT(履歴表示フィルタ, 4)
			CLEARBIT 履歴表示フィルタ, 4
		ELSE
			SETBIT 履歴表示フィルタ, 4
		ENDIF
		RESTART
	CASE 1006
		IF GETBIT(履歴表示フィルタ, 5)
			CLEARBIT 履歴表示フィルタ, 5
		ELSE
			SETBIT 履歴表示フィルタ, 5
		ENDIF
		RESTART
	CASE 1007
		IF GETBIT(履歴表示フィルタ, 6)
			CLEARBIT 履歴表示フィルタ, 6
		ELSE
			SETBIT 履歴表示フィルタ, 6
		ENDIF
		RESTART
	CASE 1008
		IF GETBIT(履歴表示フィルタ, 7)
			CLEARBIT 履歴表示フィルタ, 7
		ELSE
			SETBIT 履歴表示フィルタ, 7
		ENDIF
		RESTART
	CASE 1009
		SETBIT 履歴表示フィルタ, 0
		SETBIT 履歴表示フィルタ, 1
		SETBIT 履歴表示フィルタ, 2
		SETBIT 履歴表示フィルタ, 3
		SETBIT 履歴表示フィルタ, 4
		SETBIT 履歴表示フィルタ, 5
		SETBIT 履歴表示フィルタ, 6
		SETBIT 履歴表示フィルタ, 7
		RESTART
	CASE 1010
		履歴表示フィルタ = 0
		RESTART
	CASE 1011
		INVERTBIT 部分一致, 0
		RESTART
ENDSELECT
LOOP 1

@通常能力表示画面(ARG)
#DIM L_C, 2
#DIM 同行キャラ番, 100
#DIM 同行キャラ値, 100
#DIMS Arrays = "同行キャラ番", "同行キャラ値"
;ここで立ち絵表示
CALL 画像描画終了
IF GETBIT(画像サイズ切り替えOPTION記憶, 1)
	HTML_PRINT @"<div rect='4062, 0, 1125, 2250'>%短冊グラ表示用文字列関数(ARG)%</div>", 1
ELSE
	HTML_PRINT @"<div rect='4062, 0, 1125, 1125'>%顔グラ表示用文字列関数(ARG)%</div>", 1
ENDIF

VARSET 同行キャラ番
VARSET 同行キャラ値

LOCALS = □ 招待時同行キャラ □-----------------<br>
FOR LOCAL, 0, 100
	同行キャラ番:LOCAL = 同行候補キャラ番号:ARG:LOCAL
	同行キャラ値:LOCAL = 同行候補キャラ確率:ARG:LOCAL
NEXT
IF 同行キャラ番:15 > 0
	;同行候補キャラが15人以上いる場合、確率の高い順でソートする
	ARRAYMSORTEX 同行キャラ値, Arrays, 0
ENDIF
FOR LOCAL, 0, 15
	IF 同行キャラ番:LOCAL < 1
		BREAK
	ENDIF
	SIF 同行キャラ値:LOCAL < 0
		CONTINUE
	LOCALS += @"　%CALLNAME:(同行キャラ番:LOCAL), 20, LEFT%"
	LOCALS += @"：{MIN(同行キャラ値:LOCAL, 100)}"
	[IF_DEBUG]
		LOCALS += @"<font color='#666666'>.%TOSTR(GET_RELATION(ARG, 同行キャラ番:LOCAL)%100, "00")%</font>"
	[ENDIF]
	LOCALS += "％<br>"
NEXT
SIF 同行キャラ番:15 > 0
	LOCALS += @"　他、{(FINDELEMENT(同行キャラ番, 0) >  0 ? FINDELEMENT(同行キャラ番, 0)# 100) - 15}名<br>"


LOCALS += "<br>"
CALL PRINT_STATE_弱点コマンド(ARG)
LOCALS += @"%RESULTS%"
L_C:0 = 360 / GETCONFIG("フォントサイズ") + STRCOUNT(LOCALS, "<br>")

IF GETBIT(画像サイズ切り替えOPTION記憶, 1)
	HTML_PRINT @"<div rect = '4000, 2393, 2500, 4375'>%LOCALS%</div>", 1
ELSE
	HTML_PRINT @"<div rect = '4000, 1269, 2500, 4375'>%LOCALS%</div>", 1
ENDIF
L_C:1 = LINECOUNT
PRINTFORML ■%NAME:ARG%
IF CSTR:ARG:出演作品 != ""
	PRINTFORML 　出演作品：%CSTR:ARG:出演作品%
ENDIF
IF ARG != MASTER
	PRINT 　
	CALL 通常時体力ゲージ表示(ARG)
	PRINTL 
	PRINT 　
	CALL 通常時性欲ゲージ表示(ARG)
	PRINTL 
ENDIF
CALL PRINT_STATE_BASE(ARG)
HTML_PRINT RESULTS
;HTML_PRINTだとLINECOUNTがバグるため手動でCOUNT調整
L_C:1 -= RESULT
PRINTL
CALL PRINT_STATE_TALENT(ARG)
HTML_PRINT RESULTS
L_C:1 -= RESULT
PRINTL
CALL PRINT_STATE_CLOTH(ARG)
HTML_PRINT RESULTS
L_C:1 -= RESULT
PRINTL
IF ARG != MASTER
	CALL PRINT_STATE_好感度詳細(ARG)
	IF TALENT:ARG:従業員
		CALL PRINT_STATE_仕事レベル表示(ARG)
	ENDIF
ELSE
	CALL PRINT_STATE_仕事レベル表示(ARG)
	CALL PRINT_ITEMCOUNT
	CALL PRINT_STATE_あなた特殊能力
	PRINTL
ENDIF
L_C:1 = LINECOUNT - L_C:1
IF  L_C:0 > L_C:1
	FOR LOCAL, 0, L_C:0 - L_C:1
		PRINTL
	NEXT
ENDIF

@戦闘能力表示画面(ARG)
#DIMS 装備証名
#DIM 表示アビ数
#DIM アビ番号
#DIM L_C
;立ち絵表示
CALL 画像描画終了
L_C = LINECOUNT

;顔グラと経験の表示
IF GETBIT(画像サイズ切り替えOPTION記憶, 3)
	LOCALS = <div rect='4687, 237, 1125, 3000'>%短冊グラ表示用文字列関数(ARG)%<br>
	LOCALS += @"<br><br><br><br><br><br><br><br><br><br>"
	LOCALS += @"<br><br><br><br><br><br><br><br><br><br>"
	LOCALS += @"　　　探索経験：{EXP:ARG:探索経験, 5}<br>"
	LOCALS += @"　同行勝利経験：{EXP:ARG:同行勝利経験, 5}<br>"
	LOCALS += "</div>"
	HTML_PRINT LOCALS
ELSE
	LOCALS = <div rect='4687, 237, 1125, 2000'>%顔グラ表示用文字列関数(ARG)%<br>
	LOCALS += @"<br><br><br><br><br><br><br><br><br><br>"
	LOCALS += @"　　　探索経験：{EXP:ARG:探索経験, 5}<br>"
	LOCALS += @"　同行勝利経験：{EXP:ARG:同行勝利経験, 5}<br>"
	LOCALS += "</div>"
	HTML_PRINT LOCALS
ENDIF

PRINTFORM ■%NAME:ARG%
PRINTL
PRINTL
LOCALS = <div rect='0,0,1125,2387'>□ ステータス □------<br>
LOCALS += @"Lv　　　　：{基礎BATTLE_STATE:ARG:Lv, 5}<br>"
LOCALS += @"EXP 　　　：{基礎BATTLE_STATE:ARG:蓄積EXP, 5}<br>"
LOCALS += @"　LvUPまで：{戦闘経験値テーブル:(基礎BATTLE_STATE:ARG:Lv - 1) - 基礎BATTLE_STATE:ARG:蓄積EXP, 5}<br>"
IF 装備ステータス補正保存:ARG:属性
	LOCALS:1 = %属性数値文字色変換_HTML(装備ステータス補正保存:ARG:属性 - 10)%
	LOCALS += @"<font color='#%LOCALS:1%'>属性　　　：%属性数値文字列変換(装備ステータス補正保存:ARG:属性 - 10), 5%</font><br>"
	IF 基礎BATTLE_STATE:ARG:属性 == 装備ステータス補正保存:ARG:属性 - 10
		LOCAL = 40
		LOCAL:1 = 25
	ELSE
		LOCAL = 30
		LOCAL:1 = 30
	ENDIF
	IF 装備ステータス補正保存:ARG:属性 - 10 < 4
		LOCALS:1 = %属性数値文字色変換_HTML(有利属性変換(装備ステータス補正保存:ARG:属性 - 10))%
		LOCALS += @"<font color='#%LOCALS:1%'>　対%属性数値文字列変換(有利属性変換(装備ステータス補正保存:ARG:属性 - 10))%</font>　　攻撃力＋{LOCAL}％<br>"
		LOCALS:1 = %属性数値文字色変換_HTML(不利属性変換(装備ステータス補正保存:ARG:属性 - 10))%
		LOCALS += @"<font color='#%LOCALS:1%'>　対%属性数値文字列変換(不利属性変換(装備ステータス補正保存:ARG:属性 - 10))%</font>　　攻撃力－{LOCAL:1}％<br>"
	ELSEIF 装備ステータス補正保存:ARG:属性 - 10 < 6
		LOCALS:1 = %属性数値文字色変換_HTML(有利属性変換(装備ステータス補正保存:ARG:属性 - 10))%
		LOCALS += @"<font color='#%LOCALS:1%'>　対%属性数値文字列変換(有利属性変換(装備ステータス補正保存:ARG:属性 - 10))%</font>　　攻撃力＋{LOCAL}％<br>"
	ENDIF
	SIF 基礎BATTLE_STATE:ARG:属性 == 装備ステータス補正保存:ARG:属性 - 10
		LOCALS += @"　その他　攻撃力＋5％<br>"
ELSE
	LOCALS:1 = %属性数値文字色変換_HTML(基礎BATTLE_STATE:ARG:属性)%
	LOCALS += @"<font color='#%LOCALS:1%'>属性　　　：%属性数値文字列変換(基礎BATTLE_STATE:ARG:属性), 5%</font><br>"
	IF 基礎BATTLE_STATE:ARG:属性 < 4
		LOCALS:1 = %属性数値文字色変換_HTML(有利属性変換(基礎BATTLE_STATE:ARG:属性))%
		LOCALS += @"<font color='#%LOCALS:1%'>　対%属性数値文字列変換(有利属性変換(基礎BATTLE_STATE:ARG:属性))%</font>　　攻撃力＋30％<br>"
		LOCALS:1 = %属性数値文字色変換_HTML(不利属性変換(基礎BATTLE_STATE:ARG:属性))%
		LOCALS += @"<font color='#%LOCALS:1%'>　対%属性数値文字列変換(不利属性変換(基礎BATTLE_STATE:ARG:属性))%</font>　　攻撃力－30％<br>"
	ELSEIF 基礎BATTLE_STATE:ARG:属性 < 6
		LOCALS:1 = %属性数値文字色変換_HTML(有利属性変換(基礎BATTLE_STATE:ARG:属性))%
		LOCALS += @"<font color='#%LOCALS:1%'>　対%属性数値文字列変換(有利属性変換(基礎BATTLE_STATE:ARG:属性))%</font>　　攻撃力＋30％<br>"
	ENDIF
ENDIF
LOCALS += @"得意武器　：%得意武器数値文字列変換(基礎BATTLE_STATE:ARG:得意武器), 5%<br>"
LOCALS += @"タイプ　　：%タイプ数値文字列変換(基礎BATTLE_STATE:ARG:ステータスタイプ保存), 5%<br>"
LOCALS += @"最大ＨＰ　：{基礎BATTLE_STATE:ARG:最大ＨＰ + 装備ステータス補正保存:ARG:最大ＨＰ, 5}<br>"
LOCALS += @"最大ＭＰ　：{基礎BATTLE_STATE:ARG:最大ＭＰ + 装備ステータス補正保存:ARG:最大ＭＰ, 5}<br>"
LOCALS += @"攻撃力　　：{基礎BATTLE_STATE:ARG:攻撃力 + 装備ステータス補正保存:ARG:攻撃力, 5}<br>"
LOCALS += @"防御力　　：{基礎BATTLE_STATE:ARG:防御力 + 装備ステータス補正保存:ARG:防御力, 5}<br>"
LOCALS += @"回復力　　：{基礎BATTLE_STATE:ARG:回復力 + 装備ステータス補正保存:ARG:回復力, 5}<br>"
LOCALS += @"行動速度　：{基礎BATTLE_STATE:ARG:行動速度 + 装備ステータス補正保存:ARG:行動速度, 5}<br>"
LOCALS += @"DA率　　　：{基礎BATTLE_STATE:ARG:DA率 + 装備ステータス補正保存:ARG:DA率, 5}\%<br>"
LOCALS += @"TA率　　　：{基礎BATTLE_STATE:ARG:TA率 + 装備ステータス補正保存:ARG:TA率, 5}\%<br>"
LOCALS += @"命中率　　：{基礎BATTLE_STATE:ARG:命中率 + 装備ステータス補正保存:ARG:命中率, 5}\%<br>"
LOCALS += @"回避率　　：{基礎BATTLE_STATE:ARG:回避率 + 装備ステータス補正保存:ARG:回避率, 5}\%<br></div>"

詳細文文字列受け渡し変数 = 
LOCALS += "<div rect='1250,0,3750,787'>□ 装備指輪 □--------------------------------------------------<br>"
IF 装備ステータス補正保存:ARG:装備指輪番号 == 0
	LOCALS += "なし<br>"
	LOCALS += "<br>"
	LOCALS += "<br>"
	LOCALS += "<br>"
	LOCALS += "<br>"
ELSE
	TRYCALLFORM 指輪基礎効果文章表示_%DT_CELL_GETS("所持指輪データベース", 装備ステータス補正保存:ARG:装備指輪番号, "指輪名", 1)%(装備ステータス補正保存:ARG:装備指輪番号)
	LOCALS += @"■%DT_CELL_GETS("所持指輪データベース", 装備ステータス補正保存:ARG:装備指輪番号, "指輪名", 1)%<br>%詳細文文字列受け渡し変数%<br>"
ENDIF
LOCALS += "<br></div>"
詳細文文字列受け渡し変数 = 
LOCALS += "<div rect='1250,787,3750,787'>□ 装備耳飾り □------------------------------------------------<br>"
IF 装備ステータス補正保存:ARG:装備耳飾り番号 == 0
	LOCALS += "なし<br>"
	LOCALS += "<br>"
	LOCALS += "<br>"
	LOCALS += "<br>"
	LOCALS += "<br>"
ELSE
	TRYCALLFORM 耳飾り基礎効果文章表示_%DT_CELL_GETS("所持耳飾りデータベース", 装備ステータス補正保存:ARG:装備耳飾り番号, "耳飾り名", 1)%(装備ステータス補正保存:ARG:装備耳飾り番号)
	LOCALS += @"■%DT_CELL_GETS("所持耳飾りデータベース", 装備ステータス補正保存:ARG:装備耳飾り番号, "耳飾り名", 1)%<br>%詳細文文字列受け渡し変数%<br>"
ENDIF
LOCALS += "<br></div>"

詳細文文字列受け渡し変数 = 
装備証名 = %DT_CELL_GETS("所持ジョブ証データベース", 装備ステータス補正保存:ARG:装備証番号, "ジョブ証名", 1)%
LOCALS += "<div rect='1250,1575,3750,787'>□ ジョブの証 □------------------------------------------------<br>"
IF 装備ステータス補正保存:ARG:装備証番号
	LOCALS += @"%装備証名%<br>"
	LOCALS += "説明："
	TRYCALLFORM 証関数_%DT_CELL_GETS("所持ジョブ証データベース", 装備ステータス補正保存:ARG:装備証番号, "ジョブ証名", 1)%("フレーバー")
	LOCALS += @"%詳細文文字列受け渡し変数%"
	LOCALS += "</div>"
ELSE
	LOCALS += @"装備なし<br>"
	LOCALS += "<br>"
	LOCALS += "<br>"
	LOCALS += "<br>"
	LOCALS += "<br>"
	LOCALS += "<br></div>"
ENDIF

LOCALS += "<div rect='0,2375,5000,900'>□ 奥義・アビリティ □-------------------------------------------------------------------<br>"
VARSET RESULTS

CALL 奥義名呼び出し(ARG)
CALL 奥義威力呼び出し(ARG, 1)
LOCALS += "　　　奥義　　："
IF ツールチップ切り替えOPTION記憶 == 0
	LOCALS += @"<nonbutton title='<br>%詳細文文字列受け渡し変数%<br>'>[%TSTR:コマンド名受渡%]</nonbutton>"
ELSE
	LOCALS += @"<nonbutton>[%TSTR:コマンド名受渡%]</nonbutton>"
ENDIF
LOCALS += "<br>"

VARSET RESULTS
表示アビ数 = ENUMFUNCBEGINSWITH(@"固有アビ_{NO:ARG}_")
IF 表示アビ数
	LOCALS += "固有アビリティ："
	LOCAL = 16
	FOR アビ番号, 0, 表示アビ数
		CALLFORM %RESULTS:アビ番号%("アビ名", ARG)
		IF TSTR:コマンド名受渡 != ""
			LOCAL += STRLENS(TSTR:コマンド名受渡) + 2
			IF LOCAL > 100
				LOCALS += "<br>　　　　　　　　"
				LOCAL = 16
			ENDIF
			IF ツールチップ切り替えOPTION記憶 == 0
				CALLFORM %RESULTS:アビ番号%("アビ説明文", ARG)
				LOCALS += @"<nonbutton title='<br>%詳細文文字列受け渡し変数%<br>'>[%TSTR:コマンド名受渡%]</nonbutton>"
			ELSE
				LOCALS += @"<nonbutton>[%TSTR:コマンド名受渡%]</nonbutton>"
			ENDIF
		ENDIF
	NEXT
	LOCALS += "<br>" 
ELSE
	VARSET RESULTS
	LOCALS += "汎用アビリティ："
	表示アビ数 = ENUMFUNCBEGINSWITH("汎用固有アビ_")
	FOR アビ番号, 0, 表示アビ数
		TSTR:コマンド名受渡 = 
		CALLFORM %RESULTS:アビ番号%("アビ名", ARG)
		IF TSTR:コマンド名受渡 != ""
			IF ツールチップ切り替えOPTION記憶 == 0
				CALLFORM %RESULTS:アビ番号%("アビ説明文", ARG)
				LOCALS += @"<nonbutton title='<br>%詳細文文字列受け渡し変数%<br>'>[%TSTR:コマンド名受渡%]</nonbutton>　"
			ELSE
				LOCALS += @"<nonbutton>[%TSTR:コマンド名受渡%]</nonbutton>　"
			ENDIF
		ENDIF
	NEXT
	LOCALS += "<br>"
ENDIF

IF 装備証名 == ""
	LOCALS += "証アビリティ：なし"
ELSE
	LOCAL:1 = 0
	LOCALS += "証アビリティ："
	VARSET RESULTS
	LOCAL:1 = 0
	表示アビ数 = ENUMFUNCBEGINSWITH(@"証アビ関数_%DT_CELL_GETS("所持ジョブ証データベース", 装備ステータス補正保存:ARG:装備証番号, "ジョブ証名", 1)%_")
	FOR LOCAL, 0, 表示アビ数
		TSTR:コマンド名受渡 = 
		CALLFORM %RESULTS:LOCAL%(@"アビ名", ARG)
		IF TSTR:コマンド名受渡 != ""
			IF ツールチップ切り替えOPTION記憶 == 0
				CALLFORM %RESULTS:LOCAL%(@"アビ説明文", ARG)
				LOCALS += @"<nonbutton title='<br>%詳細文文字列受け渡し変数%<br>'>[%TSTR:コマンド名受渡%]</nonbutton>　"
			ELSE
				LOCALS += @"<nonbutton>[%TSTR:コマンド名受渡%]</nonbutton>　"
			ENDIF
			LOCAL:1 += 1
		ENDIF
		IF LOCAL:1 == 4
			LOCALS:1 += "<br>　　　　　　　　"
			LOCAL:1 = 0
		ENDIF
	NEXT
ENDIF
LOCALS += "<br>"
LOCALS += "<br>"
LOCALS += "<br>"
LOCALS += "<br></div>"

HTML_PRINT LOCALS, 1

FOR LOCAL, 0, 29 - (LINECOUNT - L_C)
	PRINTL
NEXT

TRYCALLFORM 口上_ステータス画面_固有特性解説_{NO:ARG}(ARG)


@性的能力表示画面(ARG)
#DIM L_C

;ここで立ち絵表示
CALL 画像描画終了
L_C = LINECOUNT

IF GETBIT(画像サイズ切り替えOPTION記憶, 2)
	HTML_PRINT @"<div rect='4062, 237, 1125, 2250'>%短冊グラ表示用文字列関数(ARG)%</div>", 1
ELSE
	HTML_PRINT @"<div rect='4062, 237, 1125, 1125'>%顔グラ表示用文字列関数(ARG)%</div>", 1
ENDIF
PRINTL
PRINTFORM ■%NAME:ARG%
PRINTL
IF ARG != MASTER
	PRINT 　
	CALL 通常時体力ゲージ表示(ARG)
	PRINTL 
	PRINT 　
	CALL 通常時性欲ゲージ表示(ARG)
	PRINTL 
ENDIF
CALL PRINT_STATE_ABL(ARG)
PRINTL
CALL PRINT_STATE_性癖(ARG)
PRINTL

IF フレーバー素質:ARG:素質表示設定 == 1
	PRINTL □ フレーバー素質 □-----------------------------------------------------------
	LOCAL = 0
	FOR COUNT, 1, 10
		IF フレーバー素質:ARG:COUNT
			IF COUNT == 6
				PRINTL
				LOCAL = 0
			ENDIF
			PRINTFORM %ERDNAME(フレーバー素質, COUNT), 10, LEFT%：%GET_フレーバー素質NAME(COUNT, フレーバー素質:ARG:COUNT), 12, LEFT%
			LOCAL += 1
			IF LOCAL == 3
				LOCAL = 0
				PRINTL
			ENDIF
		ENDIF
	NEXT
	SIF LOCAL
		PRINTL
	PRINTL
ENDIF

; IF ARG != MASTER
; 	PRINTL □ 刻印 □---------------------------------------------------------------------
; 	PRINT 　
; 	PRINT_MARK ARG
; ENDIF

IF あなた特殊能力:発情鑑定術 || あなた特殊能力:危険日感知
	IF あなた特殊能力:発情鑑定術 && あなた特殊能力:危険日感知
		PRINTL □ 発情期/危険日 □------------------------------------------------------------
	ELSEIF あなた特殊能力:発情鑑定術
		PRINTL □ 発情期 □-------------------------------------------------------------------
	ELSE
		PRINTL □ 危険日 □-------------------------------------------------------------------
	ENDIF
	; IF あなた特殊能力:発情鑑定術
	; 	PRINTFORM 　\@あなた特殊能力:発情鑑定術 && あなた特殊能力:危険日感知?発情期：#\@
	; 	IF 発情期判定(ARG)
	; 		IF CFLAG:ARG:発情期フラグ < 0
	; 			SETCOLOR カラーパレット("えっちな色")
	; 			PRINT 発情中
	; 			PRINT_IMG "えっちハート"
	; 			RESETCOLOR
	; 			PRINTFORML 　残り：{4 + TALENT:ARG:性欲 + CFLAG:ARG:発情期フラグ, 2}日
	; 		ELSE
	; 			PRINTFORML 発情期まであと{CFLAG:ARG:発情期フラグ + 1, 2}日
	; 		ENDIF
	; 	ELSE
	; 		PRINTL なし
	; 	ENDIF
	; ENDIF
	IF あなた特殊能力:危険日感知
		PRINT 　
		LOCAL = CFLAG:ARG:危険日 - DAY % PREG_CYCLE(ARG)
		SIF LOCAL < 0
			LOCAL = PREG_CYCLE(ARG) - ABS(LOCAL)
		IF TALENT:ARG:妊娠
			SETCOLOR カラーパレット("ピンク")
			PRINT 妊娠中
			PRINT_IMG "えっちハート"
		ELSEIF RISKY_DAY(ARG) == 2
			LOCALS = 
			LOCAL = 0
			IF GETBIT(TALENT:ARG:性別, 0) && TALENT:ARG:Ｖ感度 > -2
				LOCALS += "超危険日"
				SETBIT LOCAL, 0
			ENDIF
			IF 発情期判定(ARG)
				SIF LOCAL
					LOCALS += "・"
				LOCALS += "強発情中"
				SETBIT LOCAL, 1
			ENDIF
			IF LOCAL
				SETCOLOR カラーパレット("赤ピンク")
				PRINT_IMG "えっちハート"
				PRINTFORM %LOCALS%
				PRINT_IMG "えっちハート"
				RESETCOLOR
				IF GETBIT(LOCAL, 1) == 1 && GETBIT(LOCAL, 0) == 0
					PRINTFORM 　発情期終了まであと 2日
				ELSE
					PRINTFORM 　危険日終了まであと 2日
				ENDIF
			ENDIF
		ELSEIF RISKY_DAY(ARG) == 1
			LOCALS = 
			LOCAL = 0
			IF GETBIT(TALENT:ARG:性別, 0) && TALENT:ARG:Ｖ感度 > -2
				LOCALS += "危険日"
				SETBIT LOCAL, 0
			ENDIF
			IF 発情期判定(ARG)
				SIF LOCAL
					LOCALS += "・"
				LOCALS += "発情中"
				SETBIT LOCAL, 1
			ENDIF
			IF LOCAL
				SETCOLOR カラーパレット("薄ピンク")
				PRINTFORM %LOCALS%
				PRINT_IMG "えっちハート"
				RESETCOLOR
				IF GETBIT(LOCAL, 1) == 1 && GETBIT(LOCAL, 0) == 0
					PRINTFORM 　発情期終了まであと{1 + (LOCAL / 9 *2), 2}日
				ELSE
					PRINTFORM 　危険日終了まであと{1 + (LOCAL / 9 *2), 2}日
				ENDIF
			ENDIF
		ELSEIF RISKY_DAY(ARG) == -1
			LOCALS = 
			LOCAL = 0
			IF GETBIT(TALENT:ARG:性別, 0) && TALENT:ARG:Ｖ感度 > -2
				LOCALS += "安全日"
				SETBIT LOCAL, 0
			ENDIF
			IF LOCAL
				SETCOLOR 0xccffff
				PRINTFORM %LOCALS%
				RESETCOLOR
				IF 発情期判定(ARG) && GETBIT(LOCAL, 0) == 0
					PRINTFORM 　発情期まであと{LOCAL, 2}日
				ELSE
					PRINTFORM 　危険日まであと{LOCAL, 2}日
				ENDIF
			ENDIF
		ELSEIF RISKY_DAY(ARG) == -2
			PRINTFORM 妊娠不可
		ELSE
			IF 発情期判定(ARG)
				PRINTFORM 発情期まであと{LOCAL, 2}日
			ELSEIF GETBIT(TALENT:ARG:性別, 0) && TALENT:ARG:Ｖ感度 > -2
				PRINTFORM 危険日まであと{LOCAL, 2}日
			ENDIF
		ENDIF
		RESETCOLOR
		IF TCVAR:ARG:排卵誘発薬
			SETCOLOR カラーパレット("えっちな色")
			PRINT （排卵誘発中！）
			RESETCOLOR
		ELSEIF TCVAR:ARG:避妊薬
			SETCOLOR 0xccffff
			PRINT （避妊中）
			RESETCOLOR
		ENDIF
		PRINTL
	ENDIF
	PRINTL
ENDIF
IF ARG != MASTER
	PRINTL □ 干渉 □---------------------------------------------------------------------
	IF 干渉強化変数:ARG:干渉種類 > 0
		LOCALS = <div rect='112,0,3750,375'>
		CALLFORM 干渉強化関数_{干渉強化変数:ARG:干渉種類}("名前")
		LOCALS += @"■%TSTR:コマンド名受渡%<br><br></div><div rect='225,225,3750,2500'>"
		CALLFORM 干渉強化関数_{干渉強化変数:ARG:干渉種類}("詳細文章")
		LOCALS += @"%TSTR:コマンド名受渡%<br></div>"
		HTML_PRINT LOCALS, 1
		FOR LOCAL, 0, STRCOUNT(LOCALS, "br")
			PRINTL 
		NEXT
	ELSE
		PRINTL 　なし
	ENDIF
ENDIF
CALL PRINT_STATE_EXP(ARG)
CALL PRINT_STATE_EX(ARG)

;子供は血縁関係タブに移動
;IF EXP:ARG:妊娠経験 > 0
;	PRINTL
;	PRINTL □ 子供 □---------------------------------------------------------------------
;	PRINTFORML 　%CALLNAME:MASTER%との子供：{CFLAG:ARG:MASTERの子供妊娠回数, 2}人
;	PRINTFORML 　それ以外の子供：{EXP:ARG:妊娠経験 - CFLAG:ARG:MASTERの子供妊娠回数, 2}人
;ENDIF
SIF TALENT:ARG:妊娠
	CALL PRINT_STATE_妊娠状態(ARG)
PRINTL 

IF ARG == MASTER
	CALL 恋慕対象者表示
	CALL 妊娠カウント表示
ENDIF

FOR LOCAL, 0, 29 - (LINECOUNT - L_C)
	PRINTL
NEXT

@キャラ解説表示画面(ARG)
;ここで立ち絵表示
CALL 画像描画終了
IF GETBIT(画像サイズ切り替えOPTION記憶, 2)
	HTML_PRINT @"<div rect='4062, 37, 1125, 2250'>%短冊グラ表示用文字列関数(ARG)%</div>", 1
ELSE
	HTML_PRINT @"<div rect='4062, 37, 1125, 1125'>%顔グラ表示用文字列関数(ARG)%</div>", 1
ENDIF
PRINTFORM ■%NAME:ARG%
PRINTL
IF ARG != MASTER
	PRINT 　
	CALL 通常時体力ゲージ表示(ARG)
	PRINTL 
	PRINT 　
	CALL 通常時性欲ゲージ表示(ARG)
	PRINTL 
ENDIF
PRINTL
PRINTL □ 解説 □---------------------------------------------------------------------
IF CSTR:ARG:出演作品 != ""
	PRINTFORML ■出典：%CSTR:ARG:出演作品%
	PRINTL
ENDIF

CALLFORM 口上_ステータス画面_キャラ解説_{NO:ARG}(ARG)

IF K < 18
	FOR LOCAL, 0, 18 - K
		PRINTL
	NEXT
ENDIF


@血縁関係表示画面(ARG)
#DIM L_C
#DIM MOTHER
#DIM FATHER
#DIM BIRTHDAY
#DIM LAP
#DIM CHILD_COUNT
#DIMS CHILD_NAME
#DIM MASTERとの子供人数
#DIM 子供表示人数

;ここで立ち絵表示
CALL 画像描画終了
L_C = LINECOUNT

IF GETBIT(画像サイズ切り替えOPTION記憶, 2)
	HTML_PRINT @"<div rect='4062, 237, 1125, 2250'>%短冊グラ表示用文字列関数(ARG)%</div>", 1
ELSE
	HTML_PRINT @"<div rect='4062, 237, 1125, 1125'>%顔グラ表示用文字列関数(ARG)%</div>", 1
ENDIF
PRINTL
PRINTFORM ■%NAME:ARG%
PRINTL
IF ARG != MASTER
	PRINT 　
	CALL 通常時体力ゲージ表示(ARG)
	PRINTL 
	PRINT 　
	CALL 通常時性欲ゲージ表示(ARG)
	PRINTL 
ENDIF
PRINTL 

BIRTHDAY = GET_PERSON_生年日(CFLAG:ARG:人物番号)
IF BIRTHDAY >= 0
	PRINTL □ 誕生日 □-------------------------------------------------------------------
	PRINT 　
	LAP = GET_PERSON_出会った周回(CFLAG:ARG:人物番号)
	SIF LAP >= 0 && LAP != 周回回数累計:0
		PRINTFORM {LAP + 1}周目 
	PRINTFORML {BIRTHDAY / 120 + 1}年目 %月登録:((BIRTHDAY / 30) % 4)% {BIRTHDAY % 30 + 1}日(%曜日登録:(BIRTHDAY % 7)%)
	PRINTL 
ENDIF

MOTHER = FIND_MOTHER_CHARA_FROM_PERSON_ID(CFLAG:ARG:人物番号)
IF MOTHER >= 0
	PRINTL □ 母親 □---------------------------------------------------------------------
	PRINTFORML 　%NAME:MOTHER%
	PRINTL 
ENDIF

FATHER = FIND_FATHER_CHARA_FROM_PERSON_ID(CFLAG:ARG:人物番号)
IF FATHER >= 0
	PRINTL □ 父親 □---------------------------------------------------------------------
	PRINTFORML 　%NAME:FATHER%
	PRINTL 
ENDIF
子供表示人数 = 27 - (LINECOUNT - L_C) - 4

CALL FIND_CHILDREN(CFLAG:ARG:人物番号)
CHILD_COUNT = RESULT
IF CHILD_COUNT > 0
	PRINTL □ 子供 □---------------------------------------------------------------------
	MASTERとの子供人数 = 0
	FOR LOCAL, 1, (1 + CHILD_COUNT)
		SIF GET_PERSON_父親(RESULT:LOCAL) == CFLAG:MASTER:人物番号
			MASTERとの子供人数 ++
	NEXT
	PRINTFORML 　%CALLNAME:MASTER%との子供：{MASTERとの子供人数, 2}人
	PRINTFORML 　それ以外の子供：{EXP:ARG:出産経験 - MASTERとの子供人数, 2}人
	PRINTL 
	FOR LOCAL, (1 + 子供表示人数 * 能力表示現在ページ), MIN(1 + CHILD_COUNT, 1 + 子供表示人数 * (能力表示現在ページ + 1))
		PRINTFORM 　%"第" + TOSTR(LOCAL + MAX(0, EXP:ARG:出産経験 - CHILD_COUNT)) + "子", 7, LEFT% 
		CHILD_NAME '= GET_PERSON_呼び名(RESULT:LOCAL)
		IF CHILD_NAME != ""
			PRINTFORM %CHILD_NAME%
			IF GET_PERSON_性別(RESULT:LOCAL) == 2
				PRINT (♂) 
			ELSE
				PRINT (♀) 
			ENDIF
		ENDIF
		BIRTHDAY = GET_PERSON_生年日(RESULT:LOCAL)
		IF BIRTHDAY >= 0
			LAP = GET_PERSON_出会った周回(RESULT:LOCAL)
			SIF LAP >= 0 && LAP != 周回回数累計:0
				PRINTFORM {LAP + 1}周目 
			PRINTFORM {BIRTHDAY / 120 + 1}年目 %月登録:((BIRTHDAY / 30) % 4)% {BIRTHDAY % 30 + 1}日(%曜日登録:(BIRTHDAY % 7)%)に
		ENDIF
		FATHER = FIND_FATHER_CHARA_FROM_PERSON_ID(RESULT:LOCAL)
		IF FATHER >= 0
			PRINTFORM %CALLNAME:FATHER%との間に生まれた
		ELSEIF BIRTHDAY >= 0
			PRINT 生まれた
		ENDIF
		IF CHILD_NAME == ""
			IF GET_PERSON_性別(RESULT:LOCAL) == 2
				PRINT 男の子
			ELSE
				PRINT 女の子
			ENDIF
		ENDIF
		PRINTL 
	NEXT

ENDIF

FOR LOCAL, 0, 27 - (LINECOUNT - L_C)
	PRINTL
NEXT

PRINTL 
IF CHILD_COUNT > 0
	IF CHILD_COUNT > 子供表示人数 && 能力表示現在ページ > 0
		PRINTBUTTONLC "[900]前のページへ", 900
	ELSE
		SETCOLOR カラーパレット("グレーアウト")
		PRINTLC [---]前のページへ
		RESETCOLOR
	ENDIF
	IF CHILD_COUNT > 子供表示人数 && 能力表示現在ページ < CHILD_COUNT / (子供表示人数 + 1)
		PRINTBUTTONLC "[901]次のページへ", 901
	ELSE
		SETCOLOR カラーパレット("グレーアウト")
		PRINTLC [---]次のページへ
		RESETCOLOR
	ENDIF
ENDIF
PRINTL 

@CHOICE_LOVE(ARG)
#DIM 恋慕枠数
;現在の恋慕対象者表示
全表示フラグ:0 = 1
$省略LOOP
CALL 恋慕対象者表示
LOCAL:2 = RESULT
恋慕枠数 = 恋慕枠判定()

;まず恋慕対象かどうかを判定
LOCAL:1 = -1
FOR LOCAL, 0, LOCAL:2
	IF 恋慕対象フラグ:LOCAL == ARG
		LOCAL:1 = LOCAL
		BREAK
	ENDIF
NEXT

IF TALENT:ARG:恋慕
;既に恋慕達成済み時はそもそも選択肢すら出ない
	SETCOLOR カラーパレット("えっちな色")
	PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:ARG%と既に恋人です
	PRINT_IMG "えっちハート"
	RESETCOLOR
	PRINT　　　
ELSEIF LOCAL:1 >= 0
	PRINTBUTTONLC @"[1] %CALLNAME:ARG%を恋慕対象から外す", 1
ELSE
	PRINTBUTTONLC @"[0] %CALLNAME:ARG%を恋慕対象にする", 0
ENDIF
PRINTBUTTONC "[9] 戻る", 9

$INPUT_LOOP
INPUT

IF TALENT:ARG:恋慕
	;既に恋慕達成済み時は何もしない
	RETURN 0
ELSEIF RESULT == 0
	;既に対象の時何もせず戻す
	SIF LOCAL:1 >= 0
		GOTO INPUT_LOOP
	IF 恋慕枠数 <= LOCAL:2
		;もう枠がない場合、入れ替える
		IF 恋慕枠数 == 1
			IF TALENT:(恋慕対象フラグ:0):恋慕
				PRINTFORMW 恋慕対象の%CALLNAME:(恋慕対象フラグ:0)%は既に%CALLNAME:PLAYER%と恋人のため、外すことが出来ません。
				RETURN 0
			ELSE
				PRINTFORML 現在の恋慕対象に%CALLNAME:(恋慕対象フラグ:0)%が選ばれていますが、%CALLNAME:ARG%に変更しますか？
				PRINTBUTTONLC "[0] はい", 0
				PRINTBUTTONLC "[999] いいえ", 999
			ENDIF
		ELSE
			PRINTFORML 現在の恋慕枠は一杯です。誰かと入れ替えますか？
			FOR LOCAL, 0, 恋慕枠数
				IF 恋慕対象フラグ:LOCAL
					IF TALENT:(恋慕対象フラグ:LOCAL):恋慕
						SETCOLOR 0x666666
						PRINTFORML [{LOCAL}]%CALLNAME:(恋慕対象フラグ:LOCAL)%は恋人のため外すことが出来ません
						RESETCOLOR
					ELSE
						PRINTBUTTON @"[{LOCAL}]%CALLNAME:(恋慕対象フラグ:LOCAL)%と入れ替える", LOCAL
						PRINTL
					ENDIF
				ENDIF
			NEXT
			PRINTBUTTONLC "[999] いいえ", 999
		ENDIF
		$INPUT_LOOP2
		INPUT
		IF RESULT < 999
			IF TALENT:(恋慕対象フラグ:RESULT):恋慕
				GOTO INPUT_LOOP2
			ELSEIF 恋慕対象フラグ:RESULT < 1
				GOTO INPUT_LOOP2
			ENDIF
			CFLAG:(恋慕対象フラグ:RESULT):ゲージ起動分類 = 0
			PRINTFORM %CALLNAME:(恋慕対象フラグ:RESULT)%を恋慕対象から外し、
			PRINTFORMW %CALLNAME:ARG%を恋慕対象に設定しました。
			恋慕対象フラグ:RESULT = ARG
			CFLAG:ARG:ゲージ起動分類 = 1
		ENDIF
	ELSE
		;枠が余ってる場合、そのまま入れる
		FOR LOCAL, 0, 恋慕枠数
			IF !恋慕対象フラグ:LOCAL
				恋慕対象フラグ:LOCAL = ARG
				BREAK
			ENDIF
		NEXT
		CFLAG:ARG:ゲージ起動分類 = 1
		PRINTFORMW %CALLNAME:ARG%を恋慕対象に設定しました。
		RETURN 0
	ENDIF
ELSEIF RESULT == 1
	恋慕対象フラグ:(LOCAL:1) = 0
	CFLAG:ARG:ゲージ起動分類 = 0
	PRINTFORML %CALLNAME:ARG%を恋慕対象から外しました。
	PRINTW （恋慕レベルは現状のまま維持されます）
	RETURN 0
ELSEIF RESULT == 9
	RETURN 0
ELSEIF RESULT == 200
	INVERTBIT 全表示フラグ:0, 0
	GOTO 省略LOOP
ENDIF
RETURN 0

@恋慕枠判定()
#FUNCTION

SELECTCASE あなた特殊能力:ハーレム
	CASE 1
		RETURNF 3
	CASE 2
		RETURNF 5
	CASE 3
		RETURNF 10
	CASE 4
		RETURNF 100
	CASEELSE
		RETURNF 1
ENDSELECT

@恋慕対象者表示
#DIM 恋慕枠数

DRAWLINE
恋慕枠数 = 恋慕枠判定()
PRINTFORML 現在の恋慕対象者　　上限:{恋慕枠数}人
LOCAL:2 = 0
FOR LOCAL, 0, 恋慕枠数
	SIF 全表示フラグ:0 == 0 && LOCAL > 9
		SKIPDISP 1
	IF 恋慕対象フラグ:LOCAL
		PRINTPLAINFORM %CALLNAME:(恋慕対象フラグ:LOCAL), 25, LEFT%　　恋慕レベル:{CFLAG:(恋慕対象フラグ:LOCAL):恋慕レベル} / 100
		PRINTL
		LOCAL:2 += 1
	ENDIF
NEXT
SKIPDISP 0
SIF LOCAL:2 == 0
	PRINTL なし

IF 全表示フラグ:0 == 0 && LOCAL:2 > 10
	PRINTFORML 他{LOCAL:2 - 10, 2}名
	PRINTBUTTON "[200] 全員表示", 200
	PRINTL
ELSEIF 全表示フラグ:0 && LOCAL:2 > 10
	PRINTBUTTON "[200] 表示省略", 200
	PRINTL
ENDIF

RETURN LOCAL:2

@PRINT_STATE_CLOTH(ARG)
LOCALS = □ 服装 □---------------------------------------------------------------------<br>
VARSET LOCAL
IF CSTR:ARG:着せ替え服 != ""
	IF CSTR:ARG:着せ替え服 == "浴衣"
		LOCALS += "　■ユカタヴィラ"
	ELSE
		LOCALS += @"　■%CSTR:ARG:着せ替え服%"
	ENDIF
	SIF CSTR:ARG:着せ替え服追加名 != ""
		LOCALS += @"（%CSTR:ARG:着せ替え服追加名%）"
ELSE
	LOCALS += @"　■%CSTR:ARG:服名称%"
ENDIF

LOCALS += "<br>"
LOCALS += "　　"
;鎧
IF TEQUIP:ARG:上半身鎧あり
	LOCALS += "[上半身鎧]"
	LOCAL = 1
ENDIF
;鎧
IF TEQUIP:ARG:下腹部前鎧あり && TEQUIP:ARG:下腹部後鎧あり
	LOCALS += "[下半身鎧]<br>"
	LOCALS += "　　"
ELSEIF TEQUIP:ARG:下腹部後鎧あり
	LOCALS += "[下半身後方鎧]<br>"
	LOCALS += "　　"
ELSEIF TEQUIP:ARG:下腹部前鎧あり
	LOCALS += "[下半身前方鎧]<br>"
	LOCALS += "　　"
ELSEIF LOCAL
	LOCALS += "<br>"
	LOCALS += "　　"
ENDIF

;上半身服
IF TEQUIP:ARG:上半身服あり
	LOCALS += "[上半身服"
	LOCAL = 9
	IF TEQUIP:ARG:脇出し
		LOCALS += "（脇露出）"
		LOCAL += 10
	ENDIF
	IF TEQUIP:ARG:へそ出し
		LOCALS += "（へそ露出）"
		LOCAL += 12
	ENDIF
	IF TEQUIP:ARG:はだけ可
		LOCALS += "（はだけ可）"
		LOCAL += 12
	ENDIF
	IF TEQUIP:ARG:手差し込み可
		LOCALS += "（差し込み可）"
		LOCAL += 14
	ENDIF
	IF TEQUIP:ARG:パイズリ可
		LOCAL += 18
		IF LOCAL > 70
			LOCALS += "<br>"
			LOCALS += "　　 　　　　"
			LOCAL = 0
		ENDIF
		LOCALS += "（着衣パイズリ可）"
	ENDIF
	IF TEQUIP:ARG:上半身下着抜き取り可 && TEQUIP:ARG:上半身下着あり
		LOCAL += 18
		IF LOCAL > 70
			LOCALS += "<br>"
			LOCALS += "　　 　　　　"
			LOCAL = 0
		ENDIF
		LOCALS += "（ブラ抜き取り可）"
	ENDIF
	LOCALS += "]<br>"
	LOCALS += "　　"
ENDIF



;下半身服
IF TEQUIP:ARG:下半身服あり
	LOCALS += "[下半身服"
	SIF TEQUIP:ARG:スカートめくり可
		LOCALS += "（めくり可）"
	SIF TEQUIP:ARG:ずらし可
		LOCALS += "（ずらし可）"
	SIF TEQUIP:ARG:下半身下着抜き取り可 && TEQUIP:ARG:下半身下着あり
		LOCALS += "（下着抜き取り可）"
	LOCALS += "]<br>"
	LOCALS += "　　"
ENDIF

IF TEQUIP:ARG:上下一体タイツ
	LOCALS += "[上下タイツ"
	SIF TEQUIP:ARG:上タイツ破れ || TEQUIP:ARG:下タイツ破れ
		LOCALS += "（破れ）"
	LOCALS += "]"
ELSEIF TEQUIP:ARG:上タイツあり
	LOCALS += "[インナー"
	SIF TEQUIP:ARG:上タイツ破れ
		LOCALS += "（破れ）"
	LOCALS += "]"
ELSEIF TEQUIP:ARG:下タイツあり
	LOCALS += "[タイツ"
	SIF TEQUIP:ARG:下タイツ破れ
		LOCALS += "（破れ）"
	LOCALS += "]"
ENDIF


SIF TEQUIP:ARG:上半身下着あり == 0
	LOCALS += "[ノーブラ]"
SIF TEQUIP:ARG:上半身下着あり == 1
	LOCALS += "[ブラ着用]"
SIF TEQUIP:ARG:上半身下着あり == 2
	LOCALS += "[ニプレス]"
SIF TEQUIP:ARG:下半身下着あり == 0
	LOCALS += "[ノーパン]"
SIF TEQUIP:ARG:下半身下着あり == 1
	LOCALS += "[下着着用]"
SIF TEQUIP:ARG:下半身下着あり == 2
	LOCALS += "[前張り]"
SIF TEQUIP:ARG:下半身下着あり == 3
	LOCALS += "[ふんどし]"

LOCALS += "<br>"
RESULTS = %LOCALS%
RETURN STRCOUNT(LOCALS, "<br>") - 2

@PRINT_STATE_ABL(ARG)
PRINTL
PRINTL □ 能力 □---------------------------------------------------------------------
LOCAL:1 = 0
FOR LOCAL, 0, 30
	SIF !STRLENS(ABLNAME:LOCAL)
		CONTINUE
	SIF LOCAL > 50 && ARG != MASTER
		CONTINUE
	;非表示
	IF  GROUPMATCH(LOCAL, 16, 17, 21, 22, 23, 24, 25, 26, 27, 28)
		CONTINUE
	ENDIF
	PRINTFORM %ABLNAME:LOCAL,12%:{ABL:ARG:LOCAL,5,LEFT} 
	LOCAL:1 ++
	SIF LOCAL:1 % 4 == 0
		PRINTL 
NEXT
SIF LOCAL:1 % 4
	PRINTL 
LOCAL:1 = 0
FOR LOCAL, 21, 31
	SIF !STRLENS(ABLNAME:LOCAL)
		CONTINUE
	SIF LOCAL == 30 && ARG != MASTER
		CONTINUE
	;非表示
	IF  GROUPMATCH(LOCAL, 23, 25, 26, 28)
		CONTINUE
	ENDIF
	PRINTFORM %ABLNAME:LOCAL,12%:{ABL:ARG:LOCAL,5,LEFT} 
	LOCAL:1 ++
	SIF LOCAL:1 % 4 == 0
		PRINTL 
NEXT
SIF LOCAL:1 % 4
	PRINTL 
;PRINTL □ 性技 □---------------------------------------------------------------------
;LOCAL:1 = 0
;FOR LOCAL,50,60
;	SIF !STRLENS(ABLNAME:LOCAL)
;		CONTINUE
;	SIF !(TALENT:ARG:2 & 1) && (LOCAL == 52 || LOCAL == 54)
;		CONTINUE
;	PRINTFORM %ABLNAME:LOCAL,12%:{ABL:ARG:LOCAL,5,LEFT} 
;	LOCAL:1 ++
;	SIF LOCAL:1 % 4 == 0
;		PRINTL 
;NEXT
@PRINT_STATE_BASE(ARG)
#DIM ループ用
#DIMS 名前格納
#DIMS 性癖段階
#DIM 予定時間

LOCALS = □ 情報 □---------------------------------------------------------------------<br>
LOCALS += @"　種族：%GET_TALENTNAME(200,TALENT:ARG:種族)%"
IF CSTR:ARG:特殊種族 != ""
	LOCALS += @" [%CSTR:ARG:特殊種族%]"
ENDIF
SIF TALENT:ARG:角あり
	LOCALS += "（角あり）"
SIF TALENT:ARG:尻尾あり
	LOCALS += "（尻尾）"
SIF TALENT:ARG:エルーン耳
	LOCALS += "（エルーン耳）"
SIF TALENT:ARG:ハーヴィン耳
	LOCALS += "（ハーヴィン耳）"
SIF TALENT:ARG:発情期あり > 0
	LOCALS += "（発情期あり）"
SIF TALENT:ARG:発情期あり < 0
	LOCALS += "（発情期なし）"
IF 発情期判定(ARG)
	SELECTCASE RISKY_DAY(ARG)
		CASE 1
			LOCALS += @"<font color='#%カラーパレット_HTML("薄ピンク")%'>（発情中<img src='えっちハート'>）</font>"
		CASE 2
			LOCALS += @"<font color='#%カラーパレット_HTML("赤ピンク")%'>（<img src='えっちハート'>強発情中<img src='えっちハート'>）</font>"
	ENDSELECT
ENDIF
IF TALENT:ARG:妊娠
	LOCALS += @"<font color='#%カラーパレット_HTML("ピンク")%'>"
	IF PREGNANCY_PROGRESS(ARG) == 5
		LOCALS += "（臨月"
	ELSE
		LOCALS += "（妊娠中"
	ENDIF
	LOCALS += "<img src='えっちハート'>）</font>"
ELSEIF あなた特殊能力:危険日感知 && GETBIT(TALENT:ARG:性別, 0) && TALENT:ARG:Ｖ感度 > -2
	SELECTCASE RISKY_DAY(ARG)
		CASE 1
			LOCALS += @"<font color='#%カラーパレット_HTML("薄ピンク")%'>（危険日<img src='えっちハート'>）</font>"
		CASE 2
			LOCALS += @"<font color='#%カラーパレット_HTML("赤ピンク")%'>（<img src='えっちハート'>超危険日<img src='えっちハート'>）</font>"
		CASE -1
			LOCALS += @"<font color='#ccffff'>（安全日）</font>"
	ENDSELECT
ENDIF
LOCALS += "<br><br>"

名前格納 = 
名前格納 += "　性別："
IF TALENT:ARG:性別 == 1
	名前格納 += "女性"
	SIF TALENT:ARG:処女
		名前格納 += "（処女）"
ELSEIF TALENT:ARG:性別 == 2
	名前格納 += "男性"
	SIF TALENT:ARG:非童貞 == 0
		名前格納 += "（童貞）"
ELSEIF TALENT:ARG:性別 == 3
	名前格納 += "ふたなり"
	SIF TALENT:ARG:非童貞 == 0
		名前格納 += "(童貞)"
	SIF TALENT:ARG:処女
		名前格納 += "（処女）"
ELSE
	名前格納 += "その他"
ENDIF
名前格納 = %名前格納, 27,LEFT%

名前格納 += "<button value='132'>"
IF TALENT:ARG:性別 != 2
	IF GETBIT(身体項目表示切り替えOPTION, 1)
		名前格納 += "バスト：" + DECIMAL_STRING(BASE:ARG:バスト, 1)+ "cm" + GET_CUPSIZE(ARG)
	ELSE
		名前格納 += "バストサイズ：" + GET_TALENTNAME(205,TALENT:ARG:バストサイズ)
	ENDIF
ENDIF
名前格納 += "</button>"
LOCALS += @"%名前格納%<br>"

IF GETBIT(身体項目表示切り替えOPTION, 0)
	LOCALS:1 = 　身長：%DECIMAL_STRING(BASE:ARG:身長, 1)%cm
	LOCALS += @"%LOCALS:1, 25, LEFT%"
ENDIF
IF GETBIT(身体項目表示切り替えOPTION, 2)
	LOCALS:1 = 　体型：%GET_TALENTNAME(163, TALENT:ARG:体型)%
	LOCALS += @"%LOCALS:1, 25, LEFT%"
	LOCALS += @"体格：%GET_TALENTNAME(161,TALENT:ARG:体格)%"
	LOCALS += "<br>"
ENDIF
LOCALS += "<br>"

IF ARG
	IF ツールチップ切り替えOPTION記憶 == 0
		LOCALS += @"<nonbutton title = '%性知識段階表示(知識素質:ARG:性知識, ARG, 1)%'>　性知識Ｌｖ：%性知識段階表示(知識素質:ARG:性知識, ARG), 13, LEFT%</nonbutton><nonbutton>肉体熟知：{EXP:ARG:肉体熟知, 5}</nonbutton><br>"
	ELSE
		LOCALS += @"<nonbutton>　性知識Ｌｖ：%性知識段階表示(知識素質:ARG:性知識, ARG), 13, LEFT%</nonbutton><nonbutton>肉体熟知：{EXP:ARG:肉体熟知, 5}</nonbutton><br>"
	ENDIF
ENDIF
IF ARG == MASTER || TALENT:ARG:ランダムキャラ
	LOCALS += "　容姿："
	LOCALS += @"%GET_TALENTNAME(160,TALENT:ARG:容姿), 17, LEFT%"
	LOCALS += "年齢："
	LOCALS += @"%GET_TALENTNAME(164,TALENT:ARG:年齢), 17, LEFT%"
	LOCALS += "<br>"
ENDIF
IF TALENT:ARG:性別 & 2
	LOCALS += @"　男性器サイズ：%GET_TALENTNAME(162,TALENT:ARG:男性器サイズ)%"
	LOCALS += "<br>"
ENDIF
IF あなた特殊能力:フレンドリーセンス
	REPLACE CSTR:ARG:プレゼント好み, "_", "、"
	LOCALS += @"　プレゼントの好み：%RESULTS%"
	LOCALS += "<br>"
ENDIF
IF あなた特殊能力:睡眠診断 > 0
	IF CFLAG:ARG:睡眠
		CALL CHARA_ROUTINE_STATUS(ARG, "起床時刻")
		予定時間 = RESULT + TCVAR:ARG:行動時間ゆらぎ
		LOCALS += @"　起床予定時間：{予定時間 / 60}時 {予定時間 % 60}分"
	ELSE
		CALL CHARA_ROUTINE_STATUS(ARG, "就寝時刻")
		予定時間 = RESULT + TCVAR:ARG:行動時間ゆらぎ
		LOCALS += @"　就寝予定時間：{予定時間 / 60}時 {予定時間 % 60}分"
	ENDIF
	LOCALS += "<br>"
ENDIF
IF CFLAG:ARG:元気になる薬フラグ > 0
	LOCALS += @"　<font color='#%カラーパレット_HTML("黄")%'>元気になる薬使用中：残り%TOFULL(TOSTR(CFLAG:ARG:元気になる薬フラグ))%日</font>"
	LOCALS += "<br>"
ENDIF

RESULTS = %LOCALS%
RETURN STRCOUNT(LOCALS, "<br>") - 2

@PRINT_STATE_弱点コマンド(ARG)
#DIMS 弱点コマンドSPLIT用配列, 10
LOCALS = □ 弱点コマンド □---------------------<br>

FOR LOCAL, 0, 8
	SIF 弱点コマンド枠:ARG:LOCAL == ""
		BREAK
	IF 弱点看破:ARG:LOCAL || 弱点コマンド全表示OPTION
		弱点コマンド欄表示名 = 
		IF STRFIND(弱点コマンド枠:ARG:LOCAL, "_") >= 0
			SPLIT 弱点コマンド枠:ARG:LOCAL, "_", 弱点コマンドSPLIT用配列
			SELECTCASE TOINT(弱点コマンドSPLIT用配列:0)
				CASE 370 TO 374
					LOCAL:1 = TARGET
					TARGET = TOINT(弱点コマンドSPLIT用配列:1)
					CALLFORMF COMNAME%弱点コマンドSPLIT用配列:0%
					IF 弱点コマンド欄表示名 != ""
						LOCALS += @"　[キャラ固有：%弱点コマンド欄表示名%]<br>"
					ELSE
						LOCALS += @"　[キャラ固有：%TSTR:コマンド名受渡%]<br>"
					ENDIF
					TARGET = LOCAL:1
				CASE 380 TO 384
					LOCAL:1 = CFLAG:MASTER:現在位置
					LOCAL:2 = CFLAG:MASTER:現在マップ種別
					CFLAG:MASTER:現在位置 = TOINT(弱点コマンドSPLIT用配列:2)
					CFLAG:MASTER:現在マップ種別 = TOINT(弱点コマンドSPLIT用配列:1)
					CALLFORMF COMNAME%弱点コマンドSPLIT用配列:0%
					IF 弱点コマンド欄表示名 != ""
						LOCALS += @"　[場所固有：%弱点コマンド欄表示名%]<br>"
					ELSE
						LOCALS += @"　[場所固有：%TSTR:コマンド名受渡%]<br>"
					ENDIF
					CFLAG:MASTER:現在位置 = LOCAL:1
					CFLAG:MASTER:現在マップ種別 = LOCAL:2
				CASE 385 TO 389
					;お祭りコマンドは弱点にしない
				CASEELSE
					CALLFORMF COMNAME%弱点コマンドSPLIT用配列:0%
					IF 弱点コマンド欄表示名 != ""
						LOCALS += @"　[%弱点コマンド欄表示名%派生："
					ELSE
						LOCALS += @"　[%TSTR:コマンド名受渡%派生："
					ENDIF
					弱点コマンド欄表示名 = 
					CALLFORMF COMNAME_S%弱点コマンド枠:ARG:LOCAL%
					IF 弱点コマンド欄表示名 != ""
						LOCALS += @"%弱点コマンド欄表示名%]<br>"
					ELSE
						LOCALS += @"%TSTR:コマンド名受渡%]<br>"
					ENDIF
			ENDSELECT
		ELSE
			CALLFORMF COMNAME%弱点コマンド枠:ARG:LOCAL%
			IF 弱点コマンド欄表示名 != ""
				LOCALS += @"　[%弱点コマンド欄表示名%]<br>"
			ELSE
				LOCALS += @"　[%TSTR:コマンド名受渡%]<br>"
			ENDIF
		ENDIF
	ELSE
		LOCALS += @"　[？？？？？]<br>"
	ENDIF
NEXT
LOCAL:1 = 0
FOR LOCAL, 8, 50
	SIF 弱点コマンド枠:ARG:LOCAL == ""
		BREAK
	LOCAL:1 += 1
NEXT
IF LOCAL:1 > 0
	LOCALS += @"　他{LOCAL:1, 2}件　　<button value='998'>[998] 全て表示</button>"
ENDIF
RESULTS = %LOCALS%
RETURN STRCOUNT(LOCALS, "<br>") - 2

@PRINT_STATE_TALENT(ARG)
#DIM ループ用
#DIMS 名前格納
#DIMS 性癖段階
#DIM 改行カウント
LOCAL:1 = 0
LOCALS = □ 素質 □---------------------------------------------------------------------<br>　
改行カウント = 1
名前格納 = 
FOR ループ用,8,160
	IF STRLENS(GET_TALENTNAME(ループ用,TALENT:ARG:ループ用, 0))
		;男の時、V感覚はスキップ
		SIF ループ用 == 102 && GETBIT(TALENT:ARG:性別, 0) == 0
			CONTINUE
		IF STRLENS(名前格納) > 48
			名前格納 = 
			LOCALS += "<br>　"
			改行カウント += 1
		ENDIF
		名前格納 += GET_TALENTNAME(ループ用,TALENT:ARG:ループ用, 0)
		IF ツールチップ切り替えOPTION記憶 == 0
			LOCALS += @"<nonbutton title='%GET_TALENTNAME(ループ用,TALENT:ARG:ループ用, 1)%'>[%GET_TALENTNAME(ループ用,TALENT:ARG:ループ用, 0)%]</nonbutton>"
		ELSE
			LOCALS += @"<nonbutton>[%GET_TALENTNAME(ループ用,TALENT:ARG:ループ用, 0)%]</nonbutton>"
		ENDIF
	ENDIF
NEXT
FOR ループ用,220,300
	IF STRLENS(GET_TALENTNAME(ループ用,TALENT:ARG:ループ用))
		IF STRLENS(名前格納) > 50
			名前格納 = 
			LOCALS += "<br>　"
			改行カウント += 1
		ENDIF
		名前格納 += GET_TALENTNAME(ループ用,TALENT:ARG:ループ用)
		IF ツールチップ切り替えOPTION記憶 == 0
			LOCALS += @"<nonbutton title='%GET_TALENTNAME(ループ用,TALENT:ARG:ループ用, 1)%'>[%GET_TALENTNAME(ループ用,TALENT:ARG:ループ用)%]</nonbutton>"
		ELSE
			LOCALS += @"<nonbutton>[%GET_TALENTNAME(ループ用,TALENT:ARG:ループ用)%]</nonbutton>"
		ENDIF
	ENDIF
NEXT
FOR ループ用,0,10
	IF GETVARS(@"CSTR:ARG:固有素質{ループ用}") !=""
		IF STRLENS(名前格納) > 48
			名前格納 = 
			LOCALS += "<br>　"
			改行カウント += 1
		ENDIF
		名前格納 += @"[%GETVARS(@"CSTR:ARG:固有素質{ループ用}")%]"
		IF ツールチップ切り替えOPTION記憶 == 0
			RESULTS = 
			TRYCCALLFORM 固有素質解説_{NO:ARG}_%GETVARS(@"CSTR:ARG:固有素質{ループ用}")%
				LOCALS += @"<nonbutton title='%RESULTS%'>[%GETVARS(@"CSTR:ARG:固有素質{ループ用}")%]</nonbutton>"
			CATCH
				LOCALS += @"<nonbutton title='<br>固有素質<br>'>[%GETVARS(@"CSTR:ARG:固有素質{ループ用}")%]</nonbutton>"
			ENDCATCH
		ELSE
			LOCALS += @"<nonbutton>[%GETVARS(@"CSTR:ARG:固有素質{ループ用}")%]</nonbutton>"
		ENDIF
	ENDIF
NEXT
FOR ループ用,1,100
	IF 知識素質:ARG:ループ用
		IF STRLENS(名前格納) > 48
			名前格納 = 
			LOCALS += "<br>　"
			改行カウント += 1
		ENDIF
		名前格納 += @"[%ERDNAME(知識素質,ループ用)%Lv{知識素質:ARG:ループ用}]"
		IF ツールチップ切り替えOPTION記憶 == 0
			LOCALS += @"<nonbutton title='%GET_知識素質TOOLTIP(ループ用)%'>[%ERDNAME(知識素質,ループ用)%Lv{知識素質:ARG:ループ用}]</nonbutton>"
		ELSE
			LOCALS += @"<nonbutton>[%ERDNAME(知識素質,ループ用)%Lv{知識素質:ARG:ループ用}]</nonbutton>"
		ENDIF
	ENDIF
NEXT

LOCALS += "<br>"
改行カウント += 1
RESULTS = %LOCALS%
RETURN 改行カウント - 2

@PRINT_STATE_性癖(ARG)
#DIM ループ用
#DIMS 名前格納
#DIMS 性癖段階

PRINTL □ 性癖 □---------------------------------------------------------------------
FOR LOCAL, 1, 100
	IF 性癖素質:ARG:LOCAL
		TSTR:ツールチップ受渡 = ERDNAME(性癖素質, LOCAL)
		TRYCALLFORM 性癖素質_TOOLTIP_%ERDNAME(性癖素質, LOCAL)%
		SELECTCASE 性癖素質:ARG:LOCAL
			CASE 1
				性癖段階 = 興味
			CASE 2
				性癖段階 = 愛好
			CASE 3
				性癖段階 = 中毒
		ENDSELECT
		IF STRLENS(名前格納) + STRLENS(@"[%ERDNAME(性癖素質, LOCAL)%(%性癖段階%)]") > 70
			名前格納 = 
			HTML_PRINT @"　%LOCALS%"
			LOCALS = 
		ENDIF
		名前格納 += @"[%ERDNAME(性癖素質, LOCAL)%(%性癖段階%)]"
		IF ツールチップ切り替えOPTION記憶 == 0
			LOCALS += @"<nonbutton title='%TSTR:ツールチップ受渡%'>[%ERDNAME(性癖素質, LOCAL)%(%性癖段階%)]</nonbutton>"
		ELSE
			LOCALS += @"<nonbutton>[%ERDNAME(性癖素質, LOCAL)%(%性癖段階%)]</nonbutton>"
		ENDIF
	ENDIF
NEXT
SIF 名前格納 != ""
	HTML_PRINT @"　%LOCALS%"
名前格納 = 
LOCALS = 

@PRINT_ITEMCOUNT
#DIM ループ用
PRINTL
PRINTL □ 所持品 □-------------------------------------------------------------------
PRINTFORML 　所持金：%NUM_FORMAT(MONEY)%%MONEYLABEL%
PRINTFORML 　所持ZP：{FLAG:ZP所持量}
PRINT 　
LOCAL:1 = 0
FOR ループ用,0,200
	IF ITEM:ループ用
		PRINTFORMLC %ITEMNAME:ループ用%：{ITEM:ループ用}個
		LOCAL:1 += 1
		IF LOCAL:1 == 3
			PRINTL
			PRINT 　
			LOCAL:1 = 0
		ENDIF
	ENDIF
NEXT

FOR ループ用,1000,1100
	IF ITEM:ループ用
		PRINTFORMLC %ITEMNAME:ループ用%：{ITEM:ループ用}個
		LOCAL:1 += 1
		IF LOCAL:1 == 3
			PRINTL
			PRINT 　
			LOCAL:1 = 0
		ENDIF
	ENDIF
NEXT

FOR ループ用,1900,2000
	IF ITEM:ループ用
		PRINTFORMLC %ITEMNAME:ループ用%：{ITEM:ループ用}個
		LOCAL:1 += 1
		IF LOCAL:1 == 3
			PRINTL
			PRINT 　
			LOCAL:1 = 0
		ENDIF
	ENDIF
NEXT
PRINTL

@PRINT_STATE_EXP(ARG)
PRINTL
IF EXISTFUNCTION(@"経験文章表現_{NO:ARG}")
	PRINT □ 経験 □--
	PRINTBUTTON "[ 表示切替 ]", 150
	PRINTL -------------------------------------------------------
	IF EXP表示切替
		CALLFORM 経験文章表現_{NO:ARG}(ARG)
		PRINTL 
		RETURN 0
	ENDIF
ELSE
	PRINTL □ 経験 □---------------------------------------------------------------------
ENDIF
LOCAL:1 = 0
FOR LOCAL, 0, 200
	;EXP.CSVに登録されていなければCONTINUE
	;SIF !STRLENS(EXPNAME:LOCAL)
	;	CONTINUE
	;未経験を表示させない
	SIF !EXP:ARG:LOCAL
		CONTINUE
	;肉体熟知は別枠
	SIF LOCAL == 89
		CONTINUE
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && LOCAL == 51
		CONTINUE
	ELSEIF TALENT:ARG:2 & 1 && LOCAL == 52
		CONTINUE
	ENDIF
	PRINTFORM %EXPNAME:LOCAL,18%:{EXP:ARG:LOCAL,5,LEFT}　
	LOCAL:1 += 1
	SIF LOCAL:1 % 3 == 0
		PRINTL 
NEXT
PRINTL

@PRINT_STATE_JUEL(ARG)
PRINTL
PRINTL □ 宝珠 □---------------------------------------------------------------------
LOCAL:1 = 0
FOR LOCAL,0,22
	;PALAM.CSVに名称が登録されていないとCONTINUE
	SIF !STRLENS(PALAMNAME:LOCAL)
		CONTINUE
	;潤滑
	SIF LOCAL == 9
		CONTINUE
	;1000,1000000,単位で区切る
	LOCALS = {JUEL:ARG:LOCAL}
	SIF JUEL:ARG:LOCAL > 1000000
		LOCALS = {JUEL:ARG:LOCAL / 1000}k
	SIF JUEL:ARG:LOCAL > 1000000000
		LOCALS = {JUEL:ARG:LOCAL / 1000000}m
	PRINTFORM %PALAMNAME:LOCAL,6%の珠:%LOCALS,7,LEFT%
	LOCAL:1 += 1
	;改行or表示位置あけ
	SIF LOCAL:1 % 4 == 0
		PRINTL 
NEXT
;否定の珠
LOCALS = {JUEL:ARG:100}
SIF JUEL:ARG:100 > 1000000
	LOCALS = {JUEL:ARG:100 / 1000}k
PRINTFORM %PALAMNAME:100,6%の珠:%LOCALS,7,LEFT%

@PRINT_STATE_EX(ARG)
SIF EX:ARG:0 + EX:ARG:1 + EX:ARG:2 + EX:ARG:3 + EX:ARG:10 + EX:ARG:11 == 0
	RETURN 0
LOCAL:1 = 0
PRINTL
PRINTL □ 絶頂回数 □-----------------------------------------------------------------
FOR LOCAL,0,20
	SIF !STRLENS(EXNAME:LOCAL)
		CONTINUE
	SIF !EX:LOCAL
		CONTINUE
	PRINTFORM %EXNAME:LOCAL,12%:{EX:ARG:LOCAL,5,LEFT}
	LOCAL:1 ++
	SIF LOCAL:1 % 4 == 0
	PRINTL
NEXT
PRINTL

@PRINT_好感度レベル表示(ARG)
IF あなた特殊能力:フレンドリーセンス
	PRINTFORM 好感度:Lv{CFLAG:ARG:好感度レベル}　
ELSE
	PRINTFORM 好感度:
ENDIF
PRINTFORM \@ !CFLAG:ARG:好感度レベル? 初対面#%CSTR:ARG:現在の好感度表示%\@

@PRINT_恋慕レベル表示(ARG)
#DIM 恋慕相手

PRINTL 
IF DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:ARG:人物番号} Or 対象キャラ２ = {CFLAG:ARG:人物番号}) And 関係性種別 = '恋慕'")
	IF CFLAG:ARG:ゲージ起動分類 == 1
		SETCOLOR カラーパレット("グレーアウト")
		PRINTFORM 　恋慕度:Lv{CFLAG:ARG:恋慕レベル}　
		PRINTL
		IF FLAG:口上表示切り替えオプション > 0
			TRYCALLFORM 口上_ステータス画面_恋慕コメント_{NO:ARG}(ARG)
		ENDIF
		RESETCOLOR
	ENDIF
	IF CFLAG:ARG:人物番号 == DT_CELL_GET("関係性データベース", RESULT:1, "対象キャラ１", 1)
		恋慕相手 = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", RESULT:1, "対象キャラ２", 1))
	ELSE
		恋慕相手 = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", RESULT:1, "対象キャラ１", 1))
	ENDIF
	PRINTFORML 　恋慕成立相手：%NAME:恋慕相手%
ELSE
	IF CFLAG:ARG:ゲージ起動分類 == 1
		PRINTFORM 　恋慕度:Lv{CFLAG:ARG:恋慕レベル}　
		SELECTCASE CFLAG:ARG:恋慕レベル
			CASE 0
				PRINT 脈ナシ
			CASE IS <= 関係閾値:1
				PRINT ちょっと気になる
			CASE IS <= 関係閾値:2
				PRINT なんかいいかも
			CASE IS <= 関係閾値:3
				PRINT 好意を自覚
			CASE IS <= 関係閾値:4
				PRINT ラブラブ
			CASEELSE
				PRINT 最愛の人
		ENDSELECT
		PRINTL
		IF FLAG:口上表示切り替えオプション > 0
			TRYCALLFORM 口上_ステータス画面_恋慕コメント_{NO:ARG}(ARG)
		ENDIF
	ENDIF
	IF TALENT:ARG:恋慕
		PRINTFORML 　恋慕成立相手：%NAME:MASTER%
	ENDIF
ENDIF

@INFO_恋慕レベル表示(対象キャラ)
#DIM 対象キャラ

IF CFLAG:対象キャラ:ゲージ起動分類 == 1
	PRINTFORM 恋慕度:Lv{CFLAG:対象キャラ:恋慕レベル}　
	SELECTCASE CFLAG:対象キャラ:恋慕レベル
		CASE 0
			PRINT 脈ナシ
		CASE IS <= 関係閾値:1
			PRINT ちょっと気になる
		CASE IS <= 関係閾値:2
			PRINT なんかいいかも
		CASE IS <= 関係閾値:3
			PRINT 好意を自覚
		CASE IS <= 関係閾値:4
			PRINT ラブラブ
		CASEELSE
			PRINT 最愛の人
	ENDSELECT
	PRINTL
ENDIF

@PRINT_セフレ相手表示(ARG)
#DIM 関係数
#DIM セフレ相手

IF TALENT:ARG:セフレ
	PRINTL
	PRINTFORML 　セフレ成立相手：%NAME:MASTER% 
ENDIF

VARSET RESULT
関係数 = DT_SELECT("関係性データベース", @"(対象キャラ１ = {CFLAG:ARG:人物番号} Or 対象キャラ２ = {CFLAG:ARG:人物番号}) And 関係性種別 = 'セフレ'")
SIF 関係数 < 1
	RETURN 0

SIF TALENT:ARG:セフレ == 0
	PRINTL
FOR LOCAL, 1, 関係数 + 1
	IF CFLAG:ARG:人物番号 == DT_CELL_GET("関係性データベース", RESULT:LOCAL, "対象キャラ１", 1)
		セフレ相手 = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", RESULT:LOCAL, "対象キャラ２", 1))
	ELSE
		セフレ相手 = FIND_CHARA_FROM_PERSON_ID(DT_CELL_GET("関係性データベース", RESULT:LOCAL, "対象キャラ１", 1))
	ENDIF
	PRINTFORML 　セフレ成立相手：%NAME:セフレ相手%
NEXT

@PRINT_STATE_好感度詳細(ARG)
IF ARG
	PRINTL □ 関係 □---------------------------------------------------------------------
	PRINT 　
	CALL PRINT_好感度レベル表示(ARG)
	PRINTL
	IF CFLAG:ARG:友情度 >= 100500
		PRINTFORM 　　├ 要素：友情  MAX!!
		HTML_PRINT "<shape type='rect' param='62, 56, 2625, 37' color='red'>", 1
	ELSE
		IF あなた特殊能力:フレンドリーセンス
			PRINTFORM 　　├ 要素：友情 {CFLAG:ARG:友情度,6}
			SIF CFLAG:ARG:友情度 > 239
				HTML_PRINT @"<shape type='rect' param='62, 56,{CFLAG:ARG:友情度 * 7 / 268}, 37' color='#4db56a'>", 1
		ELSE
			PRINTFORM 　　├ 要素：友情
		ENDIF
	ENDIF
	PRINTL
	IF FLAG:口上表示切り替えオプション > 0
		TRYCCALLFORM 口上_ステータス画面_友情コメント_{NO:ARG}(ARG)
		CATCH
			PRINTL 　　│
		ENDCATCH
	ENDIF
	IF CFLAG:ARG:信頼度 >= 100500
		PRINTFORM 　　├ 要素：信頼  MAX!!
		HTML_PRINT "<shape type='rect' param='62, 56, 2625, 37' color='red'>", 1
	ELSE
		IF あなた特殊能力:フレンドリーセンス
			PRINTFORM 　　├ 要素：信頼 {CFLAG:ARG:信頼度,6}
			SIF CFLAG:ARG:信頼度 > 239
				HTML_PRINT @"<shape type='rect' param='62, 56,{CFLAG:ARG:信頼度 * 7 / 268}, 37' color='#4db56a'>", 1
		ELSE
			PRINTFORM 　　├ 要素：信頼
		ENDIF
	ENDIF
	PRINTL
	IF FLAG:口上表示切り替えオプション > 0
		TRYCCALLFORM 口上_ステータス画面_信頼コメント_{NO:ARG}(ARG)
		CATCH
			PRINTL 　　│
		ENDCATCH
	ENDIF
	IF CFLAG:ARG:恋心度 >= 100500
		PRINTFORM 　　├ 要素：恋心  MAX!!
		HTML_PRINT "<shape type='rect' param='62, 56, 2625, 37' color='red'>", 1
	ELSE
		IF あなた特殊能力:フレンドリーセンス
			PRINTFORM 　　├ 要素：恋心 {CFLAG:ARG:恋心度,6}
			SIF CFLAG:ARG:恋心度 > 239
				HTML_PRINT @"<shape type='rect' param='62, 56,{CFLAG:ARG:恋心度 * 7 / 268}, 37' color='#4db56a'>", 1
		ELSE
			PRINTFORM 　　├ 要素：恋心
		ENDIF
	ENDIF
	PRINTL
	IF FLAG:口上表示切り替えオプション > 0
		TRYCCALLFORM 口上_ステータス画面_恋心コメント_{NO:ARG}(ARG)
		CATCH
			PRINTL 　　│
		ENDCATCH
	ENDIF
	IF CSTR:ARG:キャラ固有好感度要素 == ""
		IF CFLAG:ARG:性愛度 >= 100500
			PRINTFORM 　　└ 要素：性愛  MAX!!
			HTML_PRINT "<shape type='rect' param='62, 56, 2625, 37' color='red'>", 1
		ELSE
			IF あなた特殊能力:フレンドリーセンス
				PRINTFORM 　　└ 要素：性愛 {CFLAG:ARG:性愛度,6}
				SIF CFLAG:ARG:性愛度 > 239
					HTML_PRINT @"<shape type='rect' param='62, 56,{CFLAG:ARG:性愛度 * 7 / 268}, 37' color='#4db56a'>", 1
			ELSE
				PRINTFORM 　　└ 要素：性愛
			ENDIF
		ENDIF
		PRINTL
		IF FLAG:口上表示切り替えオプション > 0
			TRYCALLFORM 口上_ステータス画面_性愛コメント_{NO:ARG}(ARG)
		ENDIF
	ELSE
		IF CFLAG:ARG:性愛度 >= 100500
			PRINTFORM 　　├ 要素：性愛  MAX!!
			HTML_PRINT "<shape type='rect' param='62, 56, 2625, 37' color='red'>", 1
		ELSE
			IF あなた特殊能力:フレンドリーセンス
				PRINTFORM 　　├ 要素：性愛 {CFLAG:ARG:性愛度,6}
				SIF CFLAG:ARG:性愛度 > 239
					HTML_PRINT @"<shape type='rect' param='62, 56,{CFLAG:ARG:性愛度 * 7 / 268}, 37' color='#4db56a'>", 1
			ELSE
				PRINTFORM 　　├ 要素：性愛
			ENDIF
		ENDIF
		PRINTL
		IF FLAG:口上表示切り替えオプション > 0
			TRYCCALLFORM 口上_ステータス画面_性愛コメント_{NO:ARG}(ARG)
			CATCH
				PRINTL 　　│
			ENDCATCH
		ENDIF
		IF CFLAG:ARG:キャラ固有好感度 >= 100500
			PRINTFORM 　　└ 要素：%CSTR:ARG:キャラ固有好感度要素%  MAX!!
			HTML_PRINT "<shape type='rect' param='62, 56, 2625, 37' color='red'>", 1
		ELSE
			IF あなた特殊能力:フレンドリーセンス
				PRINTFORM 　　└ 要素：%CSTR:ARG:キャラ固有好感度要素% {CFLAG:ARG:キャラ固有好感度,6}
				SIF CFLAG:ARG:キャラ固有好感度 > 239
					HTML_PRINT @"<shape type='rect' param='62, 56,{CFLAG:ARG:キャラ固有好感度 * 7 / 268}, 37' color='#4db56a'>", 1
			ELSE
				PRINTFORM 　　└ 要素：%CSTR:ARG:キャラ固有好感度要素%
			ENDIF
		ENDIF
		PRINTL
		IF FLAG:口上表示切り替えオプション > 0
			PRINT  
			TRYCALLFORM 口上_ステータス画面_キャラ固有好感度要素コメント_{NO:ARG}(ARG)
		ENDIF
	ENDIF
ENDIF
CALL PRINT_恋慕レベル表示(ARG)
CALL PRINT_セフレ相手表示(ARG)
PRINTL

@PRINT_STATE_仕事レベル表示(ARG)
PRINTL □ 仕事レベル □---------------------------------------------------------------
FOR LOCAL, 1, 10
	IF 現在仕事数字文字列変換(LOCAL) == ""
		BREAK
	ENDIF
	SIF 仕事レベル存在チェック(LOCAL) == 0
		CONTINUE
	SIF LOCAL == 2
		CONTINUE
	PRINTFORMLC 　%現在仕事数字文字列変換(LOCAL)%レベル：{仕事レベル:ARG:LOCAL}
NEXT
PRINTL
PRINTL ・常駐業務レベル
IF 現在仕事:ARG:0 == 2
	LOCALS = %MAP_GET("常駐業務名", GETPLACENAME(現在仕事:ARG:1 / 10000, 現在仕事:ARG:1 % 10000))%
ELSE
	LOCALS = 常駐業務でないため該当させない
ENDIF
FOR LOCAL, 0, 30
	IF 常駐業務レベル:ARG:LOCAL > 0 || ERDNAME(常駐業務レベル, LOCAL) == LOCALS
		PRINTFORMLC 　%ERDNAME(常駐業務レベル, LOCAL)%レベル：{常駐業務レベル:ARG:LOCAL}
	ENDIF
NEXT
PRINTL

@妊娠カウント表示
SIF !EXP:MASTER:孕ませ経験 && !EXP:MASTER:妊娠経験
	RETURN 0
DRAWLINE
IF EXP:MASTER:孕ませ経験
	PRINTFORML %CALLNAME:MASTER%が孕ませた回数　　： {EXP:MASTER:孕ませ経験}回
	PRINTFORML %CALLNAME:MASTER%が産ませた子供の数： {MASTERの子供の数:0}人
ENDIF
IF EXP:MASTER:妊娠経験
	PRINTFORML %CALLNAME:MASTER%が孕まされた回数　： {EXP:MASTER:妊娠経験}回
	PRINTFORML %CALLNAME:MASTER%が産んだ子供の数　： {MASTERの子供の数:1}人
ENDIF

@PRINT_STATE_妊娠状態(ARG)
PRINTL
PRINTL □ 妊娠状態 □-----------------------------------------------------------------
PRINTFORML 　妊娠経過期間:{CFLAG:ARG:妊娠経過日}
PRINT 　
SELECTCASE PREGNANCY_SIZE(ARG)
	CASE 0
		PRINTL 腹部の見た目は妊娠前と一切変化していない
	CASE 1
		PRINTL 僅かに膨らんでいるが、服越しだと殆ど分からないだろう
	CASE 2
		PRINTL 服越しだとまだ分かりづらいが、撫でるとはっきりとした膨らみを感じる
	CASE 3
		PRINTL 腹部は小高く盛り上がっており、妊娠したことは遠目からでも一目瞭然だ
	CASE 4
		PRINTL 大きな腹部の中で、子供は順調にすくすくと育っているようだ
	CASE 5
		IF 異種妊娠(ARG) && (TALENT:ARG:種族 == 種族_ハーヴィン || TALENT:ARG:体格 == -2)
			PRINTFORML 大きくせり出したボテ腹は尋常ではないサイズで、異種族の雄に孕まされた事が一目瞭然だ
		ELSE
			PRINTL 重そうに張り詰めた臨月腹は、否が応でも見る者に出産間近なことを想起させる
		ENDIF
	CASE 6
		PRINTFORML \@INRANGE(TALENT:(CFLAG:ARG:子の父親):種族, 1,5) && 異種妊娠(ARG)?%GET_TALENTNAME(200,TALENT:(CFLAG:ARG:子の父親):種族)%#異種族\@との子供を出産間近の臨月腹はあまりに巨大で、歩くのも大変そうだ
ENDSELECT
SIF (GROUPMATCH(TALENT:ARG:種族,種族_ドラフ,種族_ハーヴィン) || TALENT:ARG:体格 < 0) && TALENT:ARG:バストサイズ >= 2 && PREGNANCY_SIZE(ARG) >= 4
	PRINTFORML 　%CALLNAME:ARG%の巨大な胸部は大きく突き出したボテ腹に押し上げられている
SIF TALENT:ARG:母乳体質 == 3
	PRINTFORML 　胸は妊娠の影響で少し大きくなり、子供のための母乳を蓄えている



@弱点コマンド全表示(対象キャラ番号)
#DIM 対象キャラ番号
#DIMS 弱点コマンドSPLIT用配列, 10

DRAWLINE
VARSET LOCALS
VARSET LOCAL
FOR LOCAL, 0, 50
	SIF 弱点コマンド枠:対象キャラ番号:LOCAL == ""
		CONTINUE
	IF 弱点看破:対象キャラ番号:LOCAL || 弱点コマンド全表示OPTION
		弱点コマンド欄表示名 = 
		IF STRFIND(弱点コマンド枠:対象キャラ番号:LOCAL, "_") >= 0
			SPLIT 弱点コマンド枠:対象キャラ番号:LOCAL, "_", 弱点コマンドSPLIT用配列
			SELECTCASE TOINT(弱点コマンドSPLIT用配列:0)
				CASE 370 TO 374
					LOCAL:1 = TARGET
					TARGET = TOINT(弱点コマンドSPLIT用配列:1)
					CALLFORMF COMNAME%弱点コマンドSPLIT用配列:0%
					IF 弱点コマンド欄表示名 != ""
						LOCALS:1 = {LOCAL, 2}：[キャラ固有：%弱点コマンド欄表示名%]
					ELSE
						LOCALS:1 = {LOCAL, 2}：[キャラ固有：%TSTR:コマンド名受渡%]
					ENDIF
					TARGET = LOCAL:1
				CASE 380 TO 384
					LOCAL:1 = CFLAG:MASTER:現在位置
					LOCAL:2 = CFLAG:MASTER:現在マップ種別
					CFLAG:MASTER:現在位置 = TOINT(弱点コマンドSPLIT用配列:2)
					CFLAG:MASTER:現在マップ種別 = TOINT(弱点コマンドSPLIT用配列:1)
					CALLFORMF COMNAME%弱点コマンドSPLIT用配列:0%
					IF 弱点コマンド欄表示名 != ""
						LOCALS:1 = {LOCAL, 2}：[場所固有：%弱点コマンド欄表示名%]
					ELSE
						LOCALS:1 = {LOCAL, 2}：[場所固有：%TSTR:コマンド名受渡%]
					ENDIF
					CFLAG:MASTER:現在位置 = LOCAL:1
					CFLAG:MASTER:現在マップ種別 = LOCAL:2
				CASE 385 TO 389
					;お祭りコマンドは弱点にしない
				CASEELSE
					CALLFORMF COMNAME%弱点コマンドSPLIT用配列:0%
					IF 弱点コマンド欄表示名 != ""
						LOCALS:1 = {LOCAL, 2}：[%弱点コマンド欄表示名%派生：
					ELSE
						LOCALS:1 = {LOCAL, 2}：[%TSTR:コマンド名受渡%派生：
					ENDIF
					弱点コマンド欄表示名 = 
					CALLFORMF COMNAME_S%弱点コマンド枠:対象キャラ番号:LOCAL%
					IF 弱点コマンド欄表示名 != ""
						LOCALS:1 += @"%弱点コマンド欄表示名%]"
					ELSE
						LOCALS:1 += @"%TSTR:コマンド名受渡%]"
					ENDIF
			ENDSELECT
		ELSE
			CALLFORMF COMNAME%弱点コマンド枠:対象キャラ番号:LOCAL%
			IF 弱点コマンド欄表示名 != ""
				LOCALS:1 = {LOCAL, 2}：[%弱点コマンド欄表示名%]
			ELSE
				LOCALS:1 = {LOCAL, 2}：[%TSTR:コマンド名受渡%]
			ENDIF
		ENDIF
	ELSE
		LOCALS:1 = {LOCAL, 2}：[？？？？？]
	ENDIF
	LOCAL:10 += 1
	IF LOCAL:10 % 3 == 0
		LOCALS += @"%LOCALS:1, 30, LEFT%<br>"
	ELSE
		LOCALS += @"%LOCALS:1, 30, LEFT%　　　"
	ENDIF
NEXT

HTML_PRINT LOCALS
WAIT

@PRINT_STATE_あなた特殊能力
VARSET LOCAL
PRINTL
PRINTL □ 特殊能力 □-----------------------------------------------------------------
PRINT 　
FOR LOCAL, 0, VARSIZE("あなた特殊能力")
	IF あなた特殊能力:LOCAL > 0
		PRINTFORMLC %GET_あなた特殊能力NAME(LOCAL, あなた特殊能力:LOCAL, 0)%
		LOCAL:1 += 1
		IF LOCAL:1 % 3 == 0
			PRINTL
			PRINT 　
		ENDIF
	ENDIF
NEXT
