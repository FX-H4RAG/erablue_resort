@CUSTOM_CHARAMAKE(ARG)
;NOは適当に設定
NO:ARG = 0

;種族(1=ヒューマン 2=エルーン 3=ドラフ 4=ハーヴィン 5=星晶獣 6=その他)
TALENT:ARG:種族 = 1
;性別(1=女性 2= 男性 3=ふたなり)
TALENT:ARG:2 = 1
SIF ARG == 0
	TALENT:ARG:2 = 2
;体力
MAXBASE:ARG:0 = 2000
BASE:ARG:0 = MAXBASE:ARG:0

CALL NAME_CUSTOM(ARG)

;簡易的に種族と性別だけ選ぶ形のを仮置きする
CALL 仮置きCUSTOM_TERMINAL(ARG)

@仮置きCUSTOM_TERMINAL(ARG)
$CUSTOM_LOOP
IF TALENT:ARG:2 & 1
	SIF !(TALENT:ARG:2 & 2)
		EXP:ARG:射精経験 = 0
ELSE
	ABL:ARG:Ｖ感覚 = 0
	EXP:ARG:噴乳経験 = 0
	EXP:ARG:Ｖ経験 = 0
	EXP:ARG:パイズリ経験 = 0
	EXP:ARG:Ｖ性交経験 = 0
	EXP:ARG:Ｖ拡張経験 = 0
	TALENT:ARG:Ｖ感度 = 0
	TALENT:ARG:処女 = 0
	TALENT:ARG:母乳体質 = 0
	TALENT:ARG:バストサイズ = 0
ENDIF

IF TALENT:ARG:2 == 1
	EQUIP:ARG:上半身下着あり = 1
	EQUIP:ARG:スカートめくり可 = 1
	EQUIP:ARG:下半身下着抜き取り可 = 1
	TALENT:ARG:処女 = 1
	TALENT:ARG:童貞 = 0
ELSEIF TALENT:ARG:2 == 2
	EQUIP:ARG:上半身下着あり = 0
	EQUIP:ARG:スカートめくり可 = 0
	EQUIP:ARG:下半身下着抜き取り可 = 0
	TALENT:ARG:処女 = 0
	TALENT:ARG:童貞 = 1
ELSE
	EQUIP:ARG:上半身下着あり = 1
	EQUIP:ARG:スカートめくり可 = 1
	EQUIP:ARG:下半身下着抜き取り可 = 1
	TALENT:ARG:童貞 = 1
	TALENT:ARG:処女 = 1
ENDIF

SELECTCASE TALENT:ARG:種族
	CASE 1
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 0
	CASE 2
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 1
		TALENT:ARG:エルーン耳 = 1
		TALENT:ARG:ハーヴィン耳 = 0
	CASE 3
		TALENT:ARG:角あり = 1
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 0
		IF TALENT:ARG:2 == 2
			TALENT:ARG:体格 = 2
		ELSE
			TALENT:ARG:体格 = -1
		ENDIF
	CASE 4
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 1
		TALENT:ARG:体格 = -2
	CASEELSE
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 0
ENDSELECT
CALL SET_BODYSIZE(ARG,0)

$個別素質変更_LOOP
PRINTL
DRAWLINE
PRINTL 各種ステータスを決定してください。
DRAWLINE
PRINTL ■種族
FOR LOCAL, 1, 7
	IF TALENT:ARG:200 == LOCAL
		PRINTFORM [%GET_TALENTNAME(200, LOCAL)%]
		SIF LOCAL != 6
			PRINT  / 
	ELSE
		SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(200, LOCAL)%]", 100 + LOCAL
		RESETCOLOR
		SIF LOCAL != 6
			PRINT  / 
	ENDIF
NEXT
PRINTL
PRINTL ■性別
SELECTCASE TALENT:ARG:2
	CASE 2
		PRINT [男性]
		PRINT  / 
		SETCOLOR 0x666666
		PRINTBUTTON "[女性]", 201
		RESETCOLOR
		PRINT  / 
		SETCOLOR 0x666666
		PRINTBUTTON "[ふたなり]", 203
		RESETCOLOR
	CASE 1
		SETCOLOR 0x666666
		PRINTBUTTON "[男性]", 202
		RESETCOLOR
		PRINT  / 
		PRINT [女性]
		PRINT  / 
		SETCOLOR 0x666666
		PRINTBUTTON "[ふたなり]", 203
		RESETCOLOR
	CASE 3
		SETCOLOR 0x666666
		PRINTBUTTON "[男性]", 202
		RESETCOLOR
		PRINT  / 
		SETCOLOR 0x666666
		PRINTBUTTON "[女性]", 201
		RESETCOLOR
		PRINT  / 
		PRINT [ふたなり]
ENDSELECT
PRINTL
DRAWLINE
PRINTL ■容姿
FOR LOCAL, -1, 3
	IF TALENT:ARG:容姿 == LOCAL
		IF LOCAL == 0
			PRINTFORM [モブ顔]
		ELSE
			PRINTFORM [%GET_TALENTNAME(160, LOCAL)%]
		ENDIF
		SIF LOCAL != 6
			PRINT  / 
	ELSE
		SETCOLOR 0x666666
		IF LOCAL == 0
			PRINTBUTTON "[モブ顔]", 300
		ELSE
			PRINTBUTTON @"[%GET_TALENTNAME(160, LOCAL)%]", 300 + LOCAL
		ENDIF
		RESETCOLOR
		SIF LOCAL != 6
			PRINT  / 
	ENDIF
NEXT
PRINTL
PRINTL ■年齢
FOR LOCAL, -2, 3
	IF TALENT:ARG:年齢 == LOCAL
		PRINTFORM [%GET_TALENTNAME(164, LOCAL)%]
		SIF LOCAL != 6
			PRINT  / 
	ELSE
		SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(164, LOCAL)%]", 350 + LOCAL
		RESETCOLOR
		SIF LOCAL != 6
			PRINT  / 
	ENDIF
NEXT
PRINTL
PRINTFORML ■体格 \@ BASE:ARG:身長 ? （現状%DECIMAL_STRING(BASE:ARG:身長, 1), 6, RIGHT%cm）# \@
FOR LOCAL, -2, 3
	IF TALENT:ARG:体格 == LOCAL
		IF LOCAL == 0
			PRINTFORM [普通]
		ELSE
			PRINTFORM [%GET_TALENTNAME(161, LOCAL)%]
		ENDIF
		SIF LOCAL != 6
			PRINT  / 
	ELSE
		SETCOLOR 0x666666
		IF LOCAL == 0
			PRINTBUTTON "[普通]", 400
		ELSE
			PRINTBUTTON @"[%GET_TALENTNAME(161, LOCAL)%]", 400 + LOCAL
		ENDIF
		RESETCOLOR
		SIF LOCAL != 6
			PRINT  / 
	ENDIF
NEXT
PRINTL
PRINTL ■体型
FOR LOCAL, -3, 4
	IF TALENT:ARG:体型 == LOCAL
		IF LOCAL == 0
			PRINTFORM [普通]
		ELSE
			PRINTFORM [%GET_TALENTNAME(163, LOCAL)%]
		ENDIF
		SIF LOCAL != 6
			PRINT  / 
	ELSE
		SETCOLOR 0x666666
		IF LOCAL == 0
			PRINTBUTTON "[普通]", 450
		ELSE
			PRINTBUTTON @"[%GET_TALENTNAME(163, LOCAL)%]", 450 + LOCAL
		ENDIF
		RESETCOLOR
		SIF LOCAL != 6
			PRINT  / 
	ENDIF
NEXT
PRINTL
IF TALENT:ARG:2 & 2
	PRINTL ■男性器サイズ
	FOR LOCAL, -2, 3
		IF TALENT:ARG:男性器サイズ == LOCAL
			PRINTFORM [%GET_TALENTNAME(162, LOCAL)%]
			SIF LOCAL != 6
				PRINT  / 
		ELSE
			SETCOLOR 0x666666
			PRINTBUTTON @"[%GET_TALENTNAME(162, LOCAL)%]", 500 + LOCAL
			RESETCOLOR
			SIF LOCAL != 6
				PRINT  / 
		ENDIF
	NEXT
	PRINTL
ENDIF
IF TALENT:ARG:2 & 1
	PRINTFORML ■バストサイズ \@ BASE:ARG:バスト ? （現状%DECIMAL_STRING(BASE:ARG:バスト, 1), 6, RIGHT%cm %GET_CUPSIZE(ARG), LOCAL, LEFT%）# \@
	FOR LOCAL, -2, 4
		IF TALENT:ARG:バストサイズ == LOCAL
			PRINTFORM [%GET_TALENTNAME(205, LOCAL)%]
			SIF LOCAL != 6
				PRINT  / 
		ELSE
			SETCOLOR 0x666666
			PRINTBUTTON @"[%GET_TALENTNAME(205, LOCAL)%]", 600 + LOCAL
			RESETCOLOR
			SIF LOCAL != 6
				PRINT  / 
		ENDIF
	NEXT
	PRINTL
ENDIF


	PRINTL
PRINTBUTTON "これで決定する", 999
PRINTL


$INPUT_LOOP
INPUT

SELECTCASE RESULT
	CASE 101 TO 106
		TALENT:ARG:200 = RESULT - 100
		CALL SET_BODYSIZE(ARG,0)
		GOTO CUSTOM_LOOP
	CASE 201 TO 203
		TALENT:ARG:2 = RESULT - 200
		CALL SET_BODYSIZE(ARG,0)
		GOTO CUSTOM_LOOP
	CASE 299 TO 302
		TALENT:ARG:容姿 = RESULT - 300
		GOTO 個別素質変更_LOOP
	CASE 348 TO 352
		TALENT:ARG:年齢 = RESULT - 350
		GOTO 個別素質変更_LOOP
	CASE 398 TO 402
		IF TALENT:ARG:200 == 4 && RESULT > 399
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		TALENT:ARG:体格 = RESULT - 400
		CALL SET_BODYSIZE(ARG,0)
		GOTO 個別素質変更_LOOP
	CASE 447 TO 453
		TALENT:ARG:体型 = RESULT - 450
		CALL SET_BODYSIZE(ARG,2)
		GOTO 個別素質変更_LOOP
	CASE 498 TO 502
		IF GETBIT(TALENT:ARG:性別, 1) == 0
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		TALENT:ARG:男性器サイズ = RESULT - 500
		GOTO 個別素質変更_LOOP
	CASE 598 TO 603
		IF GETBIT(TALENT:ARG:性別, 0) == 0
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		TALENT:ARG:バストサイズ = RESULT - 600
		CALL SET_BODYSIZE(ARG,2)
		GOTO 個別素質変更_LOOP
	CASE 999
		RETURN 0
	CASEELSE
		GOTO INPUT_LOOP
ENDSELECT

;名前の設定
@NAME_CUSTOM(ARG)
PRINTL キャラのNAMEを入力してください
IF STRLENS(NAME:ARG)
	PRINTFORM 現在のNAME：%NAME:ARG%      
	PRINTBUTTON "[変更しない]",""
	PRINTL
ENDIF
INPUTS
IF ARG == 0 && RESULTS == ""
	NAME:ARG = %NAME:ARG%
ELSE
	NAME:ARG = %RESULTS%
ENDIF

PRINTL キャラのCALLNAMEを入力してください
IF STRLENS(CALLNAME:ARG)
	PRINTFORM 現在のCALLNAME：%CALLNAME:ARG%      
	PRINTBUTTON "[変更しない]",""
	PRINTL
ENDIF
INPUTS
IF ARG == 0 && RESULTS == ""
	CALLNAME:ARG = %CALLNAME:ARG%
ELSE
	CALLNAME:ARG = %RESULTS%
ENDIF

;ベースの設定
@BASE_CUSTOM(ARG)
PRINT 体力:
BAR BASE:ARG:0,MAXBASE:ARG:0,16
PRINTFORM ({BASE:ARG:0,4}/{MAXBASE:ARG:0,4})
IF BASE:ARG:0 != 1000
	PRINTBUTTON "[－]", 0
ELSE
	SETCOLOR 0x969696
	PRINT [－]
	RESETCOLOR
ENDIF
IF BASE:ARG:0 != 3000
	PRINTBUTTON "[＋]", 1
ELSE
	SETCOLOR 0x969696
	PRINT [＋]
	RESETCOLOR
ENDIF
PRINTL
;素質の設定
@TALENT_CUSTOM(ARG,ARG:1)
VARSET LOCAL
FOR LOCAL,ARG:1 * 40,40 + ARG:1 * 40
	IF LOCAL % 10 == 0
		PRINTL
		SIF LOCAL:1 % 2
			PRINTL
		LOCAL:1 = 0
	ENDIF
	;陥落素質ほか
	SIF (LOCAL >= 3 && LOCAL <= 5) || LOCAL == 9 || LOCAL == 34 || (LOCAL >= 72 && LOCAL <= 76) || LOCAL == 84 || LOCAL == 85 || (LOCAL >= 153 && LOCAL <= 155) || (LOCAL >= 211 && LOCAL <= 215)
		CONTINUE
	;TALENT.CSVに登録されていない
	SIF TALENTNAME:LOCAL == ""
		CONTINUE
	;オンオフ型の素質(TALENTNAMEと素質表示が一致)
	IF TALENTNAME:LOCAL == GET_TALENTNAME(LOCAL,1)
		SIF !TALENT:ARG:LOCAL
			SETCOLOR 100,100,100
		LOCALS = [{LOCAL,3}] - %TALENTNAME:LOCAL%
	;童貞
	ELSEIF LOCAL == 1
		SIF TALENT:ARG:LOCAL
			SETCOLOR 100,100,100
		LOCALS = [{LOCAL,3}] - %TALENTNAME:LOCAL%
	;量的素質
	ELSE
		SIF !TALENT:ARG:LOCAL
			SETCOLOR 100,100,100
		LOCALS =  [{LOCAL,3}] - %TALENTNAME:LOCAL%
		IF STRLENS(GET_TALENTNAME(LOCAL,TALENT:ARG:LOCAL))
			LOCALS:1 =  …【%GET_TALENTNAME(LOCAL,TALENT:ARG:LOCAL)%】
		ELSE
			LOCALS:1 = …
		ENDIF
		LOCALS += LOCALS:1
	ENDIF
	PRINTFORM %LOCALS,35,LEFT%
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL
	RESETCOLOR
NEXT
PRINTL

IF ARG:1 != 0
	PRINTBUTTON "[998] - 前のページ                 ",998
ELSE
	SETCOLOR 0x969696
	PRINTFORM %"[---] - 前のページ",35,LEFT%
	RESETCOLOR
ENDIF
IF ARG:1 != 5
	PRINTBUTTON "[999] - 次のページ",999
ELSE
	SETCOLOR 0x969696
	PRINTFORM %"[---] - 次のページ",35,LEFT%
	RESETCOLOR
ENDIF
PRINTL

@ABL_CUSTOM(ARG)
LOCAL:1 = 0
FOR LOCAL,0,50
	SIF !STRLENS(ABLNAME:LOCAL)
		CONTINUE
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && (LOCAL == 17 || LOCAL == 32)
		CONTINUE
	ELSEIF TALENT:ARG:2 & 1 && (LOCAL == 18 || LOCAL == 33)
		CONTINUE
	ENDIF
	;中毒も無し
	SIF LOCAL >= 30
		CONTINUE
	PRINTFORM %ABLNAME:LOCAL,12%:{ABL:ARG:LOCAL,5,LEFT} 
	IF ABL:ARG:LOCAL != 0
		PRINTBUTTON "[－]", LOCAL * 2
	ELSE
		SETCOLOR 0x969696
		PRINT [－]
		RESETCOLOR
	ENDIF
	IF ABL:ARG:LOCAL != 2
		PRINTBUTTON "[＋]", LOCAL * 2 + 1
	ELSE
		SETCOLOR 0x969696
		PRINT [＋]
		RESETCOLOR
	ENDIF
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL 
NEXT
PRINTL
@EXP_CUSTOM(ARG)
VARSET LOCAL
FOR LOCAL,0,200
	SIF !STRLENS(EXPNAME:LOCAL)
		CONTINUE
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && LOCAL == 41
		CONTINUE
	ELSEIF TALENT:ARG:2 & 1 && LOCAL == 42
		CONTINUE
	ENDIF
	PRINTFORM [{LOCAL,2}] - %EXPNAME:LOCAL,12%:{EXP:ARG:LOCAL,5,LEFT} 
	LOCAL:1 += 1
	SIF LOCAL:1 % 3 == 0
		PRINTL 
NEXT
PRINTL

