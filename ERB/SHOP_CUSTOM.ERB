@CUSTOM_CHARAMAKE(ARG)
;NOは適当に設定
NO:ARG = 0

;種族(1=ヒューマン 2=エルーン 3=ドラフ 4=ハーヴィン 5=星晶獣 6=その他)
TALENT:ARG:種族 = 1
;性別(1=女性 2= 男性 3=ふたなり)
TALENT:ARG:2 = 1
SIF ARG == 0
	TALENT:ARG:2 = 2
;気力、体力
MAXBASE:ARG:0 = 2000
BASE:ARG:0 = MAXBASE:ARG:0
MAXBASE:ARG:1 = 2000
BASE:ARG:1 = MAXBASE:ARG:1

CALL NAME_CUSTOM(ARG)

;技能が確定するまで一旦封印
;CALL CUSTOM_TERMINAL(ARG)

;簡易的に種族と性別だけ選ぶ形のを仮置きする
CALL 仮置きCUSTOM_TERMINAL(ARG)

@仮置きCUSTOM_TERMINAL(ARG)
$CUSTOM_LOOP
IF TALENT:ARG:2 & 1
	SIF !(TALENT:ARG:2 & 2)
		EXP:ARG:射精経験 = 0
ELSE
	ABL:ARG:Ｖ感覚 = 0
	EXP:ARG:噴乳経験 = 0
	EXP:ARG:Ｖ経験 = 0
	EXP:ARG:パイズリ経験 = 0
	EXP:ARG:Ｖ性交経験 = 0
	EXP:ARG:Ｖ拡張経験 = 0
	TALENT:ARG:Ｖ感度 = 0
	TALENT:ARG:処女 = 0
	TALENT:ARG:母乳体質 = 0
	TALENT:ARG:バストサイズ = 0
ENDIF

IF TALENT:ARG:2 == 1
	EQUIP:ARG:上半身下着あり = 1
	EQUIP:ARG:スカートめくり可 = 1
	EQUIP:ARG:下半身下着抜き取り可 = 1
	TALENT:ARG:処女 = 1
	TALENT:ARG:童貞 = 0
ELSEIF TALENT:ARG:2 == 2
	EQUIP:ARG:上半身下着あり = 0
	EQUIP:ARG:スカートめくり可 = 0
	EQUIP:ARG:下半身下着抜き取り可 = 0
	TALENT:ARG:処女 = 0
	TALENT:ARG:童貞 = 1
ELSE
	EQUIP:ARG:上半身下着あり = 1
	EQUIP:ARG:スカートめくり可 = 1
	EQUIP:ARG:下半身下着抜き取り可 = 1
	TALENT:ARG:童貞 = 1
	TALENT:ARG:処女 = 1
ENDIF

SELECTCASE TALENT:ARG:種族
	CASE 1
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 0
	CASE 2
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 1
		TALENT:ARG:エルーン耳 = 1
		TALENT:ARG:ハーヴィン耳 = 0
	CASE 3
		TALENT:ARG:角あり = 1
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 0
		IF TALENT:ARG:2 == 2
			TALENT:ARG:体格 = 2
		ELSE
			TALENT:ARG:体格 = -1
		ENDIF
	CASE 4
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 1
		TALENT:ARG:体格 = -2
	CASEELSE
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 0
ENDSELECT

$個別素質変更_LOOP
DRAWLINE
PRINTL 各種ステータスを決定してください。
DRAWLINE
PRINTL ■種族
FOR LOCAL, 1, 7
	IF TALENT:ARG:200 == LOCAL
		PRINTFORM [%GET_TALENTNAME(200, LOCAL)%]
		SIF LOCAL != 6
			PRINT  / 
	ELSE
		SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(200, LOCAL)%]", 100 + LOCAL
		RESETCOLOR
		SIF LOCAL != 6
			PRINT  / 
	ENDIF
NEXT
PRINTL
PRINTL ■性別
SELECTCASE TALENT:ARG:2
	CASE 2
		PRINT [男性]
		PRINT  / 
		SETCOLOR 0x666666
		PRINTBUTTON "[女性]", 201
		RESETCOLOR
		PRINT  / 
		SETCOLOR 0x666666
		PRINTBUTTON "[ふたなり]", 203
		RESETCOLOR
	CASE 1
		SETCOLOR 0x666666
		PRINTBUTTON "[男性]", 202
		RESETCOLOR
		PRINT  / 
		PRINT [女性]
		PRINT  / 
		SETCOLOR 0x666666
		PRINTBUTTON "[ふたなり]", 203
		RESETCOLOR
	CASE 3
		SETCOLOR 0x666666
		PRINTBUTTON "[男性]", 202
		RESETCOLOR
		PRINT  / 
		SETCOLOR 0x666666
		PRINTBUTTON "[女性]", 201
		RESETCOLOR
		PRINT  / 
		PRINT [ふたなり]
ENDSELECT
PRINTL
DRAWLINE
PRINTL ■美醜
FOR LOCAL, -1, 3
	IF TALENT:ARG:美醜 == LOCAL
		IF LOCAL == 0
			PRINTFORM [フツメン]
		ELSE
			PRINTFORM [%GET_TALENTNAME(160, LOCAL)%]
		ENDIF
		SIF LOCAL != 6
			PRINT  / 
	ELSE
		SETCOLOR 0x666666
		IF LOCAL == 0
			PRINTBUTTON "[フツメン]", 300
		ELSE
			PRINTBUTTON @"[%GET_TALENTNAME(160, LOCAL)%]", 300 + LOCAL
		ENDIF
		RESETCOLOR
		SIF LOCAL != 6
			PRINT  / 
	ENDIF
NEXT
PRINTL
PRINTL ■体格
FOR LOCAL, -2, 3
	IF TALENT:ARG:体格 == LOCAL
		IF LOCAL == 0
			PRINTFORM [普通]
		ELSE
			PRINTFORM [%GET_TALENTNAME(161, LOCAL)%]
		ENDIF
		SIF LOCAL != 6
			PRINT  / 
	ELSE
		SETCOLOR 0x666666
		IF LOCAL == 0
			PRINTBUTTON "[普通]", 400
		ELSE
			PRINTBUTTON @"[%GET_TALENTNAME(161, LOCAL)%]", 400 + LOCAL
		ENDIF
		RESETCOLOR
		SIF LOCAL != 6
			PRINT  / 
	ENDIF
NEXT
PRINTL
IF TALENT:ARG:2 & 2
	PRINTL ■男性器サイズ
	FOR LOCAL, -2, 3
		IF TALENT:ARG:男性器サイズ == LOCAL
			IF LOCAL == 0
				PRINTFORM [平凡]
			ELSE
				PRINTFORM [%GET_TALENTNAME(162, LOCAL)%]
			ENDIF
			SIF LOCAL != 6
				PRINT  / 
		ELSE
			SETCOLOR 0x666666
			IF LOCAL == 0
				PRINTBUTTON "[平凡]", 500
			ELSE
				PRINTBUTTON @"[%GET_TALENTNAME(162, LOCAL)%]", 500 + LOCAL
			ENDIF
			RESETCOLOR
			SIF LOCAL != 6
				PRINT  / 
		ENDIF
	NEXT
	PRINTL
ENDIF
IF TALENT:ARG:2 & 1
	PRINTL ■バストサイズ
	FOR LOCAL, -2, 4
		IF TALENT:ARG:バストサイズ == LOCAL
			PRINTFORM [%GET_TALENTNAME(205, LOCAL)%]
			SIF LOCAL != 6
				PRINT  / 
		ELSE
			SETCOLOR 0x666666
			PRINTBUTTON @"[%GET_TALENTNAME(205, LOCAL)%]", 600 + LOCAL
			RESETCOLOR
			SIF LOCAL != 6
				PRINT  / 
		ENDIF
	NEXT
	PRINTL
ENDIF


PRINTBUTTON "これで決定する", 999
PRINTL


$INPUT_LOOP
INPUT

SELECTCASE RESULT
	CASE 101 TO 106
		TALENT:ARG:200 = RESULT - 100
		GOTO CUSTOM_LOOP
	CASE 201 TO 203
		TALENT:ARG:2 = RESULT - 200
		GOTO CUSTOM_LOOP
	CASE 299 TO 302
		TALENT:ARG:美醜 = RESULT - 300
		GOTO 個別素質変更_LOOP
	CASE 398 TO 402
		IF TALENT:ARG:200 == 4 && RESULT > 399
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		TALENT:ARG:体格 = RESULT - 400
		GOTO 個別素質変更_LOOP
	CASE 498 TO 502
		IF TALENT:ARG:性別 == 1
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		TALENT:ARG:男性器サイズ = RESULT - 500
		GOTO 個別素質変更_LOOP
	CASE 598 TO 603
		IF TALENT:ARG:性別 == 2
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		TALENT:ARG:バストサイズ = RESULT - 600
		GOTO 個別素質変更_LOOP
	CASE 999
		RETURN 0
	CASEELSE
		GOTO INPUT_LOOP
ENDSELECT
@CUSTOM_TERMINAL(ARG)
#DIM ループ用
#DIM カウント用

;排他処理
IF TALENT:ARG:2 & 1
	SIF !(TALENT:ARG:2 & 2)
		EXP:ARG:射精経験 = 0
ELSE
	ABL:ARG:Ｖ感覚 = 0
	EXP:ARG:噴乳経験 = 0
	EXP:ARG:Ｖ経験 = 0
	EXP:ARG:パイズリ経験 = 0
	EXP:ARG:Ｖ性交経験 = 0
	EXP:ARG:Ｖ拡張経験 = 0
	TALENT:ARG:Ｖ感度 = 0
	TALENT:ARG:処女 = 0
	TALENT:ARG:母乳体質 = 0
	TALENT:ARG:バストサイズ = 0
ENDIF
IF TALENT:ARG:2 & 2
ELSE
	TALENT:ARG:童貞 = 0
ENDIF
IF TALENT:ARG:処女 == 1
	ABL:ARG:Ｖ感覚 = 0
	EXP:ARG:Ｖ経験 = 0
	EXP:ARG:Ｖ性交経験 = 0
	EXP:ARG:Ｖ拡張経験 = 0
ELSEIF TALENT:ARG:2 & 1 && EXP:ARG:Ｖ経験 == 0
	EXP:ARG:Ｖ経験 = 1
ENDIF

SELECTCASE TALENT:ARG:種族
	CASE 1
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 0
	CASE 2
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 1
		TALENT:ARG:エルーン耳 = 1
		TALENT:ARG:ハーヴィン耳 = 0
	CASE 3
		TALENT:ARG:角あり = 1
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 0
	CASE 4
		TALENT:ARG:角あり = 0
		TALENT:ARG:尻尾あり = 0
		TALENT:ARG:エルーン耳 = 0
		TALENT:ARG:ハーヴィン耳 = 1
ENDSELECT

IF TALENT:ARG:処女 == 1
	ABL:ARG:Ｖ感覚 = 0
	EXP:ARG:Ｖ経験 = 0
	EXP:ARG:Ｖ性交経験 = 0
	EXP:ARG:Ｖ拡張経験 = 0
ELSEIF TALENT:ARG:2 & 1 && EXP:ARG:Ｖ経験 == 0
	EXP:ARG:Ｖ経験 = 1
ENDIF

DRAWLINE
PRINTSL NAME:ARG
PRINT 体力:
BAR BASE:ARG:0,MAXBASE:ARG:0,16
PRINTFORM ({BASE:ARG:0,4}/{MAXBASE:ARG:0,4}) 
PRINT 気力:
BAR BASE:ARG:1,MAXBASE:ARG:1,16
PRINTFORM ({BASE:ARG:1,4}/{MAXBASE:ARG:1,4})
PRINTL 
CALL PRINT_STATE_ABL(ARG)
PRINTL 
CALL PRINT_STATE_TALENT(ARG)
PRINTL 
CALL PRINT_STATE_EXP(ARG)
PRINTL
DRAWLINE
;最終決定
IF LOCAL:100 == 1000
	DRAWLINE
	PRINTL これで本当によろしいですか？
;名前の設定
ELSEIF LOCAL:100 == 1001
	CALL NAME_CUSTOM(ARG)
	LOCAL:100 = 0
	RESTART
;ベース設定
ELSEIF LOCAL:100 == 1002
	CALL BASE_CUSTOM(ARG)
;素質設定
ELSEIF LOCAL:100 == 1003
	CALL TALENT_CUSTOM(ARG,LOCAL:101)
;能力設定
ELSEIF LOCAL:100 == 1004
	CALL ABL_CUSTOM(ARG)
;経験設定
ELSEIF LOCAL:100 == 1005
	CALL EXP_CUSTOM(ARG)
ENDIF

DRAWLINE
PRINTFORML [1000] - \@(LOCAL:100 == 1000) ? はい # 決定\@  [1001] - 名前の変更  [1002] - 体力、気力の設定
PRINTL [1003] - 素質の設定  [1004] - 能力の設定  [1005] - 経験の設定
INPUT
IF RESULT == 1000 && LOCAL:100 == 1000
	RETURN 0
ELSEIF RESULT >= 1000
	LOCAL:100 = RESULT
	RESTART
ENDIF
;ベース設定
IF LOCAL:100 == 1002
	IF RESULT == 0 && MAXBASE:ARG:0 > 1000
		MAXBASE:ARG:0 -= 100
	ELSEIF RESULT == 1 && MAXBASE:ARG:0 < 3000
		MAXBASE:ARG:0 += 100
	ELSEIF RESULT == 2 && MAXBASE:ARG:1 > 1000
		MAXBASE:ARG:1 -= 100
	ELSEIF RESULT == 3 && MAXBASE:ARG:1 < 3000
		MAXBASE:ARG:1 += 100
	ENDIF
	BASE:ARG:0 = MAXBASE:ARG:0
	BASE:ARG:1 = MAXBASE:ARG:1
	RESTART
;素質設定
ELSEIF LOCAL:100 == 1003
	;ページ送り
	IF RESULT == 998 && LOCAL:101
		LOCAL:101 --
	ENDIF
	IF RESULT == 999
		LOCAL:101 ++
	ENDIF
	SIF RESULT < 0 || RESULT > 500 || TALENTNAME:RESULT == ""
		RESTART
	;陥落素質ほか
	SIF (RESULT >= 3 && RESULT <= 5) || RESULT == 9 || RESULT == 34 || (RESULT >= 72 && RESULT <= 76) || RESULT == 84 || RESULT == 85 || (RESULT >= 153 && RESULT <= 155) || (RESULT >= 211 && RESULT <= 215)
		RESTART
	;性別
	IF RESULT == 2
		IF TALENT:ARG:RESULT == 3
			TALENT:ARG:RESULT = 1
		ELSE
			TALENT:ARG:RESULT ++
		ENDIF
	;種族
	ELSEIF RESULT == 200
		IF TALENT:ARG:RESULT == 6
			TALENT:ARG:RESULT = 1
		ELSE
			TALENT:ARG:RESULT ++
		ENDIF
	;性別嗜好
	ELSEIF RESULT == 81
		IF TALENT:ARG:RESULT == 3
			TALENT:ARG:RESULT = -1
		ELSE
			TALENT:ARG:RESULT ++
		ENDIF
	;オンオフ型の素質
	ELSEIF TALENTNAME:RESULT == GET_TALENTNAME(RESULT,1) || RESULT == 1
		TALENT:ARG:RESULT = !TALENT:ARG:RESULT
	;量的素質
	ELSE
		;性別
		IF LOCAL == 2
			IF TALENT:ARG:RESULT == 3
				TALENT:ARG:RESULT = 1
			ELSE
				TALENT:ARG:RESULT ++
			ENDIF
		;端まで行くと-1を掛ける
		ELSEIF STRLENS(GET_TALENTNAME(RESULT,TALENT:ARG:RESULT + 1)) || TALENT:ARG:RESULT == -1
			TALENT:ARG:RESULT ++
		ELSE
			TALENT:ARG:RESULT = -1 * TALENT:ARG:RESULT
		ENDIF
	ENDIF
;能力設定
ELSEIF LOCAL:100 == 1004
	LOCAL = RESULT / 2
	SIF !STRLENS(ABLNAME:LOCAL)
		RESTART
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && (LOCAL == 17 || LOCAL == 32)
		RESTART
	ELSEIF TALENT:ARG:2 & 1 && (LOCAL == 18 || LOCAL == 33)
		RESTART
	ENDIF
	;中毒関係
	SIF LOCAL >= 30
		RESTART
	;奇数(+)
	IF RESULT % 2
		IF ABL:ARG:LOCAL >= 2
			RESTART
		ELSE
			ABL:ARG:LOCAL ++
		ENDIF
	;偶数(-)
	ELSE
		IF ABL:ARG:LOCAL <= 0
			RESTART
		ELSE
			ABL:ARG:LOCAL --
		ENDIF
	ENDIF
ELSEIF LOCAL:100 == 1005
	LOCAL = RESULT
	SIF !STRLENS(EXPNAME:LOCAL)
		RESTART
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && LOCAL == 41
		RESTART
	ELSEIF TALENT:ARG:2 & 1 && LOCAL == 42
		RESTART
	ENDIF
	PRINTFORM %EXPNAME:LOCAL%を入力してください      
	PRINTBUTTON "[キャンセル]",-1
	INPUT
	SIF RESULT < 0
		RESTART
	EXP:ARG:LOCAL = MIN(RESULT,50)
ELSEIF LOCAL:100 == 1006
	
ENDIF
RESTART

;名前の設定
@NAME_CUSTOM(ARG)
PRINTL キャラのNAMEを入力してください
IF STRLENS(NAME:ARG)
	PRINTFORM 現在のNAME：%NAME:ARG%      
	PRINTBUTTON "[変更しない]",""
	PRINTL
ENDIF
INPUTS
IF ARG == 0 && RESULTS == ""
	NAME:ARG = %NAME:ARG%
ELSE
	NAME:ARG = %RESULTS%
ENDIF

PRINTL キャラのCALLNAMEを入力してください
IF STRLENS(CALLNAME:ARG)
	PRINTFORM 現在のCALLNAME：%CALLNAME:ARG%      
	PRINTBUTTON "[変更しない]",""
	PRINTL
ENDIF
INPUTS
IF ARG == 0 && RESULTS == ""
	CALLNAME:ARG = %CALLNAME:ARG%
ELSE
	CALLNAME:ARG = %RESULTS%
ENDIF

;ベースの設定
@BASE_CUSTOM(ARG)
PRINT 体力:
BAR BASE:ARG:0,MAXBASE:ARG:0,16
PRINTFORM ({BASE:ARG:0,4}/{MAXBASE:ARG:0,4})
IF BASE:ARG:0 != 1000
	PRINTBUTTON "[－]", 0
ELSE
	SETCOLOR 0x969696
	PRINT [－]
	RESETCOLOR
ENDIF
IF BASE:ARG:0 != 3000
	PRINTBUTTON "[＋]", 1
ELSE
	SETCOLOR 0x969696
	PRINT [＋]
	RESETCOLOR
ENDIF
PRINTL
PRINT 気力:
BAR BASE:ARG:1,MAXBASE:ARG:1,16
PRINTFORM ({BASE:ARG:1,4}/{MAXBASE:ARG:1,4})
IF BASE:ARG:1 != 1000
	PRINTBUTTON "[－]", 2
ELSE
	SETCOLOR 0x969696
	PRINT [－]
	RESETCOLOR
ENDIF
IF BASE:ARG:1 != 3000
	PRINTBUTTON "[＋]", 3
ELSE
	SETCOLOR 0x969696
	PRINT [＋]
	RESETCOLOR
ENDIF
PRINTL
;素質の設定
@TALENT_CUSTOM(ARG,ARG:1)
VARSET LOCAL
FOR LOCAL,ARG:1 * 40,40 + ARG:1 * 40
	IF LOCAL % 10 == 0
		PRINTL
		SIF LOCAL:1 % 2
			PRINTL
		LOCAL:1 = 0
	ENDIF
	;陥落素質ほか
	SIF (LOCAL >= 3 && LOCAL <= 5) || LOCAL == 9 || LOCAL == 34 || (LOCAL >= 72 && LOCAL <= 76) || LOCAL == 84 || LOCAL == 85 || (LOCAL >= 153 && LOCAL <= 155) || (LOCAL >= 211 && LOCAL <= 215)
		CONTINUE
	;TALENT.CSVに登録されていない
	SIF TALENTNAME:LOCAL == ""
		CONTINUE
	;オンオフ型の素質(TALENTNAMEと素質表示が一致)
	IF TALENTNAME:LOCAL == GET_TALENTNAME(LOCAL,1)
		SIF !TALENT:ARG:LOCAL
			SETCOLOR 100,100,100
		LOCALS = [{LOCAL,3}] - %TALENTNAME:LOCAL%
	;童貞
	ELSEIF LOCAL == 1
		SIF TALENT:ARG:LOCAL
			SETCOLOR 100,100,100
		LOCALS = [{LOCAL,3}] - %TALENTNAME:LOCAL%
	;量的素質
	ELSE
		SIF !TALENT:ARG:LOCAL
			SETCOLOR 100,100,100
		LOCALS =  [{LOCAL,3}] - %TALENTNAME:LOCAL%
		IF STRLENS(GET_TALENTNAME(LOCAL,TALENT:ARG:LOCAL))
			LOCALS:1 =  …【%GET_TALENTNAME(LOCAL,TALENT:ARG:LOCAL)%】
		ELSE
			LOCALS:1 = …
		ENDIF
		LOCALS += LOCALS:1
	ENDIF
	PRINTFORM %LOCALS,35,LEFT%
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL
	RESETCOLOR
NEXT
PRINTL

IF ARG:1 != 0
	PRINTBUTTON "[998] - 前のページ                 ",998
ELSE
	SETCOLOR 0x969696
	PRINTFORM %"[---] - 前のページ",35,LEFT%
	RESETCOLOR
ENDIF
IF ARG:1 != 5
	PRINTBUTTON "[999] - 次のページ",999
ELSE
	SETCOLOR 0x969696
	PRINTFORM %"[---] - 次のページ",35,LEFT%
	RESETCOLOR
ENDIF
PRINTL

@ABL_CUSTOM(ARG)
LOCAL:1 = 0
FOR LOCAL,0,50
	SIF !STRLENS(ABLNAME:LOCAL)
		CONTINUE
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && (LOCAL == 17 || LOCAL == 32)
		CONTINUE
	ELSEIF TALENT:ARG:2 & 1 && (LOCAL == 18 || LOCAL == 33)
		CONTINUE
	ENDIF
	;中毒も無し
	SIF LOCAL >= 30
		CONTINUE
	PRINTFORM %ABLNAME:LOCAL,12%:{ABL:ARG:LOCAL,5,LEFT} 
	IF ABL:ARG:LOCAL != 0
		PRINTBUTTON "[－]", LOCAL * 2
	ELSE
		SETCOLOR 0x969696
		PRINT [－]
		RESETCOLOR
	ENDIF
	IF ABL:ARG:LOCAL != 2
		PRINTBUTTON "[＋]", LOCAL * 2 + 1
	ELSE
		SETCOLOR 0x969696
		PRINT [＋]
		RESETCOLOR
	ENDIF
	LOCAL:1 ++
	SIF LOCAL:1 % 2 == 0
		PRINTL 
NEXT
PRINTL
@EXP_CUSTOM(ARG)
VARSET LOCAL
FOR LOCAL,0,200
	SIF !STRLENS(EXPNAME:LOCAL)
		CONTINUE
	;同性愛関係
	IF !(TALENT:ARG:2 & 1) && LOCAL == 41
		CONTINUE
	ELSEIF TALENT:ARG:2 & 1 && LOCAL == 42
		CONTINUE
	ENDIF
	PRINTFORM [{LOCAL,2}] - %EXPNAME:LOCAL,12%:{EXP:ARG:LOCAL,5,LEFT} 
	LOCAL:1 += 1
	SIF LOCAL:1 % 3 == 0
		PRINTL 
NEXT
PRINTL

