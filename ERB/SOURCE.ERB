@SOURCE_CHECK
#DIM FLAG保存

SELECTCOM = TFLAG:SELECTCOM受け渡し
SELECTCOM_S = %RSTR:SELECTCOM_S受け渡し%

;調教SELECTCOMの時間経過
CALL TRACHECK_TIME

FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	CFLAG:LOCAL:勃起度 = BASE:LOCAL:勃起
NEXT


FLAG保存 = FLAG:モード管理
SIF TFLAG:オートコマンドフラグ > 0
	FLAG:モード管理 = 0
;-------------------------------------------------
;@COMの呼び出し
;-------------------------------------------------
RESULT = 0
IF RFLAG:サイドボタン操作
ELSEIF TFLAG:オートコマンドフラグ >= 3000
	LOCAL = TARGET
	TARGET = PLAYER
	PLAYER = AUTOCOM_相手番号保存
	IF AUTOCOM_派生保存 != ""
		RSTR:SELECTCOM_S受け渡し = {TFLAG:オートコマンドフラグ - 3000}_%AUTOCOM_派生保存%
		CALLFORMF COMTYPE_S_%RSTR:SELECTCOM_S受け渡し%
		IF オートコマンド許可 == 3
			CALL オートコマンド許可取り()
		ENDIF
		IF RESULT > -1
			IF STRFIND(AUTOCOM_派生保存, "9_") == 0
				自慰ターゲット保存 = AUTOCOM_相手番号保存
				CALL CALL_COM(自慰ターゲット保存)
			ELSE
				CALL CALL_COM(TARGET)
			ENDIF
		ENDIF
	ELSE
		SELECTCOM = TFLAG:オートコマンドフラグ - 3000
		CALLFORMF COMTYPE_{SELECTCOM}
		IF オートコマンド許可 == 3
			CALL オートコマンド許可取り()
		ENDIF
		IF RESULT > -1
			CALL CALL_COM(TARGET)
		ENDIF
	ENDIF
	PLAYER = TARGET
	TARGET = LOCAL
ELSEIF TFLAG:オートコマンドフラグ > 2000
	TRYCALLFORM ALLAUTOCOM{TFLAG:オートコマンドフラグ - 2000}
ELSEIF TFLAG:オートコマンドフラグ > 1000
	TRYCALLFORM AUTOCOM{TFLAG:オートコマンドフラグ - 1000}
ELSEIF TFLAG:オートコマンドフラグ > 0
	TRYCALLFORM PRE_AUTOCOM{TFLAG:オートコマンドフラグ}
ELSEIF TFLAG:オートコマンドフラグ == -1
	CALL 自室キャラ訪問処理
ELSE
	;複数モード時か否か
	IF FLAG:モード管理 == 2
		LOCAL:10 = TARGET
		;同時モード時は全員分回す
		VARSET 体位モード例外配列
		FOR LOCAL, 0, 同時モード_選択数
			SIF 同時モード_選択キャラ:LOCAL < 0
				BREAK
			TARGET = 同時モード_選択キャラ:LOCAL
			CALL CALL_COM(TARGET)
			IF RESULT == -1
				RETURN -1
			ENDIF
		NEXT
		VARSET 体位モード例外配列
		TARGET = LOCAL:10
	ELSE
		CALL CALL_COM(TARGET)
	ENDIF
ENDIF
IF RESULT == -1
	TFLAG:オートコマンドフラグ = 0

	TFLAG:オートコマンド連続発動 = MAX(TFLAG:オートコマンド連続発動 - 1, 0)
	IF TFLAG:オートコマンド連続発動
		FLAG:オートコマンド判定中フラグ = 1
		CALL BEFORE_AUTOCOM
		TFLAG:オートコマンドフラグ = RESULT
		FLAG:オートコマンド判定中フラグ = 0
	ENDIF
	
	RETURN -1
ENDIF
SIF !RESULT && MODE_確認_プレイヤー側("ALL", PLAYER) < 0
	GOTO SKIP
;-------------------------------------------------
;モード持続時チェック
;-------------------------------------------------
CALL MODE_CHECK
;-------------------------------------------------
;キャラ個別チェック
;-------------------------------------------------
FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	;野外オナニー絶頂判定
	IF TCVAR:LOCAL:野外オナニー_絶頂
		CALL 野外オナニー実処理(LOCAL, TCVAR:LOCAL:野外オナニー_部位)
		;相手を覗いている時以外（コマンドが覗きでない、対象がTARGETでない）は表示カット
		SIF SELECTCOM != 404 || RFLAG:コマンド結果受渡し変数２ != LOCAL
			RCVAR:LOCAL:表示カット = 1
		CFLAG:LOCAL:隠密 = 0
	ENDIF

	;装着系は後で考える
	SIF LOCAL > 0 && !CFLAG:LOCAL:同室
		CONTINUE
	;-------------------------------------------------
	;中毒充足のチェック
	;-------------------------------------------------
NEXT
;童貞喪失
IF !TALENT:PLAYER:1 && TALENT:PLAYER:2 & 2
	SELECTCASE SELECTCOM
		CASE 60
			PRINTFORMW 童貞喪失(%CALLNAME:PLAYER%)
			SETBIT TALENT:PLAYER:非童貞, 0
		CASE 62
			SETBIT TALENT:PLAYER:非童貞, 1
	ENDSELECT
ENDIF

;-------------------------------------------------
;射精我慢処理、実際には絶頂我慢
;-------------------------------------------------
IF あなた射精我慢フラグ && PLAYER == MASTER
	IF CUP:PLAYER:快Ｃ + PALAM:PLAYER:快Ｃ > 9999
		CUP:PLAYER:快Ｃ = 9999 - PALAM:PLAYER:快Ｃ
	ENDIF
	IF CUP:PLAYER:快Ｂ + PALAM:PLAYER:快Ｂ > 9999
		CUP:PLAYER:快Ｂ = 9999 - PALAM:PLAYER:快Ｂ
	ENDIF
	IF CUP:PLAYER:快Ｖ + PALAM:PLAYER:快Ｖ > 9999
		CUP:PLAYER:快Ｖ = 9999 - PALAM:PLAYER:快Ｖ
	ENDIF
	IF CUP:PLAYER:快Ａ + PALAM:PLAYER:快Ａ > 9999
		CUP:PLAYER:快Ａ = 9999 - PALAM:PLAYER:快Ａ
	ENDIF
	IF CUP:PLAYER:快Ｓ + PALAM:PLAYER:快Ｓ > 9999
		CUP:PLAYER:快Ｓ = 9999 - PALAM:PLAYER:快Ｓ
	ENDIF
ENDIF


FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	;装着系は後で考える
	SIF LOCAL > 0 && !CFLAG:LOCAL:同室 && !CFLAG:LOCAL:快楽計算フラグ
		CONTINUE
	CALL SOURCE_補正処理(LOCAL)
NEXT

;射精周りのフラグ設定
; CALL SAMEN_SHOOT_FLAGSETTING
$SKIP

;-------------------------------------------------
;マップごとの特殊処理を呼び出す
;-------------------------------------------------
TRYCALLFORM COM_END_PLACEEVENT_{CFLAG:MASTER:現在マップ種別}_{CFLAG:PLAYER:現在位置}

;-------------------------------------------------
;キャラごとの特殊処理を呼び出す
;-------------------------------------------------
TRYCALLFORM COM_END_CHARAEVENT_{NO:TARGET}

;-------------------------------------------------
;調教口上
;-------------------------------------------------
CALL KOJO_MESSAGE_COM
CALL 挿入アニメ表示(TARGET, "_挿入", FLAG_挿入アニメフィルタ)
TFLAG:メッセージ重複防止 = 1
;同時モードなら助手のぶんも表示
; IF GETBIT(FLAG:モード管理, 0) && TFLAG:オートコマンドフラグ == 0
; 	LOCAL = TARGET
; 	TARGET = 助手は誰だ()
; 	CALL KOJO_MESSAGE_COM
; 	CALL 挿入アニメ表示(TARGET, "_挿入", FLAG_挿入アニメフィルタ)
; 	TARGET = LOCAL
; ENDIF
TFLAG:メッセージ重複防止 = 0
;-------------------------------------------------
;EXP取得表示
;-------------------------------------------------
LOCAL:2 = 0
LOCAL:3 = 0
FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	FOR LOCAL:1, 0, 200
		SIF RCVAR:LOCAL:表示カット
			CONTINUE
		IF EXP_UP(LOCAL:1,LOCAL)
			LOCAL:2 ++
			LOCAL:3 ++
			SIF LOCAL:2 == 1
			PRINTL
			LOCALS = %EXPNAME:(LOCAL:1)%
			PRINTFORM %LOCALS,15,LEFT%
			IF LOCAL:1 == 89
				LOCALS = +{EXP_UP(LOCAL:1,LOCAL)}
				PRINTFORML %LOCALS,4,LEFT%(%CALLNAME:PLAYER% > %CALLNAME:LOCAL%)
			ELSE
				LOCALS = +{EXP_UP(LOCAL:1,LOCAL)}
				PRINTFORML %LOCALS,4,LEFT%(%CALLNAME:LOCAL%)
			ENDIF
		ENDIF
	NEXT
	LOCAL:2 = 0
NEXT
SIF LOCAL:3
	WAIT
;-------------------------------------------------
;ABLUP
;-------------------------------------------------
FOR LOCAL,0,CHARANUM
	SIF RCVAR:LOCAL:表示カット
		CONTINUE
	CALL SOURCE_ABLUP(LOCAL)
NEXT
;-------------------------------------------------
;触手の射精時の口上
;-------------------------------------------------
;CALL KOJO_MESSAGE_PALAMCNG_E
;触手射精未実装なので一旦コメントアウト
;-------------------------------------------------
;奴隷の絶頂時の口上
;-------------------------------------------------
CALL MESSAGE_PALAMCNG_絶頂強度表示
IF NOWEX:TARGET:多重絶頂
	CALL KOJO_ORGASM_絶頂
	PRINTL
ENDIF
;-------------------------------------------------
;奴隷射精時の口上
;-------------------------------------------------
IF NOWEX:TARGET:射精
	SIF NOWEX:TARGET:射精 > 1
		PRINT 大量
	PRINTFORML 射精(%CALLNAME:TARGET%)
	CALL KOJO_ORGASM_射精
	PRINTL
ENDIF
;-------------------------------------------------
;調教者射精時
;-------------------------------------------------
IF NOWEX:PLAYER:射精
	SIF NOWEX:PLAYER:射精 > 1
		PRINT 大量
	PRINTFORML 射精(%CALLNAME:PLAYER%)
	RFLAG:フィニッシュフラグ = NOWEX:PLAYER:射精
	CALL EVENT_SHOOT_PLAYER
	PRINTL
ELSE
	RFLAG:フィニッシュフラグ = 0
ENDIF
;-------------------------------------------------
;調教後の口上
;-------------------------------------------------
CALL KOJO_MESSAGE_PALAMCNG_AFTER
PRINTL
;-------------------------------------------------
;パラメータ変動とそれによる口上
;-------------------------------------------------
;CALL KOJO_MESSAGE_PALAMCNG_D
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	CALL 技巧上昇判定(LOCAL)
	SIF RESULT
		TRYCALLFORM KOJO_SKILLUP_技巧_{NO:LOCAL}(LOCAL)
	SIF LOCAL == MASTER
		CONTINUE
	CALL 性知識上昇判定(LOCAL)
	SIF RESULT
		TRYCALLFORM KOJO_SKILLUP_性知識_{NO:LOCAL}(LOCAL)
NEXT
;-------------------------------------------------
;刻印取得のチェック
;-------------------------------------------------
;CALL MARK_GOT_CHECK(TARGET)
;-------------------------------------------------
;刻印取得による口上
;-------------------------------------------------
;CALL KOJO_MESSAGE_MARKCNG
;-------------------------------------------------
;屈服・快楽堕ちの上昇による[抵抗]の消滅チェック
;-------------------------------------------------
CALL JUJUN_UP_CHECK(TARGET)
;-------------------------------------------------
;調教ソースの表示
;-------------------------------------------------
CALL SHOW_SOURCE(TARGET)
;-------------------------------------------------
;数値変動の表示
;-------------------------------------------------
LOCAL:1 = 0
FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	IF DOWNBASE:LOCAL:体力
		PRINTFORM 体力-{DOWNBASE:LOCAL:体力} 
		LOCAL:1 ++
	ENDIF
	SIF LOCAL:1
		PRINTFORML (%CALLNAME:LOCAL%)
	LOCAL:1 = 0
NEXT
PRINTL 
;-------------------------------------------------
;関係性ゲージ上昇
;-------------------------------------------------
;反感不快との相殺の関係で先に恋慕度から判定
CALL Love_CALC(TARGET)
CALL FAVOR_CALC(TARGET)

;-------------------------------------------------
;パラメータの上昇＆表示（DOWNもここで）
;-------------------------------------------------
VARSET LOCAL
FOR LOCAL:1, 0, CHARANUM
	SIF CFLAG:(LOCAL:1):滞在期間 < 0
		CONTINUE
	SIF RCVAR:(LOCAL:1):表示カット
		SKIPDISP 1
	LOCAL:3 = 0
	FOR LOCAL,0,100
		;コマンド入力前のCUPを追加
		CUP:(LOCAL:1):LOCAL += TCVAR:(LOCAL:1):(600 + LOCAL)
		SIF PALAMNAME:LOCAL == ""
			CONTINUE
		SIF CUP:(LOCAL:1):LOCAL - CDOWN:(LOCAL:1):LOCAL == 0
			CONTINUE
		IF !LOCAL:3
			PRINTL
			PRINTFORML %CALLNAME:(LOCAL:1)%
		ENDIF
		LOCAL:3 ++
		LOCAL:2 = CUP:(LOCAL:1):LOCAL - CDOWN:(LOCAL:1):LOCAL + TCVAR:(LOCAL:1):(600 + LOCAL)
		PRINTFORM %PALAMNAME:LOCAL,8,LEFT% {PALAM:(LOCAL:1):LOCAL,10,LEFT} + {CUP:(LOCAL:1):LOCAL,10,LEFT} =   {PALAM:(LOCAL:1):LOCAL + CUP:(LOCAL:1):LOCAL,10,LEFT}
		IF 干渉強化変数:(LOCAL:1):干渉種類 - 5 == LOCAL && LOCAL < 5
			SETCOLOR カラーパレット("黄")
			PRINT 　干渉効果発動中！
			RESETCOLOR
		ENDIF
		PRINTL
		PALAM:(LOCAL:1):LOCAL += CUP:(LOCAL:1):LOCAL - CDOWN:(LOCAL:1):LOCAL
		PALAM:(LOCAL:1):LOCAL = MAX(0, PALAM:(LOCAL:1):LOCAL)
		CUP:(LOCAL:1):LOCAL = 0
		CDOWN:(LOCAL:1):LOCAL = 0
	NEXT
	;干渉ランク上昇判定
	IF 干渉強化変数:(LOCAL:1):干渉ランク < 5
		IF 干渉経験値テーブル:(干渉強化変数:(LOCAL:1):干渉ランク) <= 干渉強化変数:(LOCAL:1):干渉経験値
			干渉強化変数:(LOCAL:1):干渉ランク += 1
			PRINTFORMW %CALLNAME:(LOCAL:1)%の干渉ランクが上昇した！
		ENDIF
	ENDIF
	SKIPDISP 0
	LOCALS '= CHECK_体位(LOCAL:1)
	SIF LOCALS != "" && GROUPMATCH(TCVAR:(LOCAL:1):被射精位置, 1, 2)
		CALL アニメ表示_射精(LOCALS, PLAYER)
NEXT
FOR LOCAL, 0, 100
	CVARSET TCVAR, 600 + LOCAL, 0
NEXT

;-------------------------------------------------
;ZPの取得量表示
;-------------------------------------------------
IF RFLAG:ZP取得量
	PRINTL
	PRINTFORM ZP:{FLAG:ZP所持量 - RFLAG:ZP取得量}
	FOR LOCAL, 0, CHARANUM
		IF RCVAR:LOCAL:ZP取得量
			PRINTFORM  + {RCVAR:LOCAL:ZP取得量}(%CALLNAME:LOCAL%)
		ENDIF
	NEXT
	PRINTFORML = {FLAG:ZP所持量}
ENDIF

;-------------------------------------------------
;性癖取得の判定
;-------------------------------------------------
;同室者のみ
FOR LOCAL, 0, CHARANUM
	SIF TARGET:LOCAL < 1
		BREAK
	CALL 性癖取得チェック(TARGET:LOCAL)
	CALL 性癖成長チェック(TARGET:LOCAL)
NEXT

;非同室者含む
FOR LOCAL, 1, CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 1
		CONTINUE
	CALL 性癖非同室成長チェック(TARGET:LOCAL)
NEXT

FLAG:モード管理 = FLAG保存

;-------------------------------------------------
;調教ソースの表示（DOWNもここで）
;-------------------------------------------------
@SHOW_SOURCE(ARG)
SIF RCVAR:ARG:表示カット
	RETURN 0
;PRINTL 　
SIF SOURCE:ARG:快Ｃ > 0
	PRINTFORM 快Ｃ({SOURCE:ARG:快Ｃ})
SIF SOURCE:ARG:快Ｖ > 0
	PRINTFORM 快Ｖ({SOURCE:ARG:快Ｖ})
SIF SOURCE:ARG:快Ａ > 0
	PRINTFORM 快Ａ({SOURCE:ARG:快Ａ})
SIF SOURCE:ARG:快Ｂ > 0
	PRINTFORM 快Ｂ({SOURCE:ARG:快Ｂ})
SIF SOURCE:ARG:快Ｓ > 0
	PRINTFORM 快Ｓ({SOURCE:ARG:快Ｓ})

IF ARG == PLAYER
	PRINTL 　
	RETURN 0
ENDIF

SIF SOURCE:ARG:情愛 > 0
	PRINTFORM 情愛({SOURCE:ARG:情愛})
SIF SOURCE:ARG:性行動 > 0
	PRINTFORM 性行動({SOURCE:ARG:性行動})
SIF SOURCE:ARG:達成 > 0
	PRINTFORM 達成感({SOURCE:ARG:達成})
SIF SOURCE:ARG:苦痛 > 0
	PRINTFORM 痛み({SOURCE:ARG:苦痛})
SIF SOURCE:ARG:恐怖 > 0
	PRINTFORM 恐れ({SOURCE:ARG:恐怖})
SIF SOURCE:ARG:液体 > 0
	PRINTFORM 液体({SOURCE:ARG:液体})
SIF SOURCE:ARG:欲情 > 0
	PRINTFORM 欲情({SOURCE:ARG:欲情})
SIF SOURCE:ARG:恭順 > 0
	PRINTFORM 恭順({SOURCE:ARG:恭順})
SIF SOURCE:ARG:露出 > 0
	PRINTFORM 露出({SOURCE:ARG:露出})
SIF SOURCE:ARG:屈従 > 0
	PRINTFORM 屈服({SOURCE:ARG:屈従})
SIF SOURCE:ARG:歓楽 > 0
	PRINTFORM 歓楽({SOURCE:ARG:歓楽})
SIF SOURCE:ARG:征服 > 0
	PRINTFORM 征服({SOURCE:ARG:征服})
SIF SOURCE:ARG:受動 > 0
	PRINTFORM 受動({SOURCE:ARG:受動})
SIF SOURCE:ARG:不潔 > 0
	PRINTFORM 不潔({SOURCE:ARG:不潔})
SIF SOURCE:ARG:鬱屈 > 0
	PRINTFORM 鬱屈({SOURCE:ARG:鬱屈})
SIF SOURCE:ARG:逸脱 > 0
	PRINTFORM 逸脱({SOURCE:ARG:逸脱})
SIF SOURCE:ARG:反感 > 0
	PRINTFORM 反発({SOURCE:ARG:反感})
PRINTL 　


;-------------------------------------------------
;SOURCE_補正処理、他の場所でも呼べるように分離
;-------------------------------------------------
@SOURCE_補正処理(対象キャラ)
#DIM 対象キャラ

;-------------------------------------------------
;経験加算
;-------------------------------------------------
CALL SOURCE_EXP(対象キャラ)
;-------------------------------------------------
;干渉力による補正処理
;-------------------------------------------------
CALL 干渉補正実処理関数(対象キャラ, "快楽増加時")
;-------------------------------------------------
;お祭りなどイベントによる補正処理
;-------------------------------------------------
IF 開催予定日取り == DAY
	CALLFORM MOVE_ROUTINE_%開催予定祭り名%(対象キャラ, "お祭り補正条件")
	IF RESULT
		CALLFORM MOVE_ROUTINE_%開催予定祭り名%(対象キャラ, "お祭り補正内容")
	ENDIF
ENDIF
;-------------------------------------------------
;絶頂処理
;-------------------------------------------------
CALL ORGASM_ADD(対象キャラ)
;-------------------------------------------------
;調教対象の噴乳チェック
;-------------------------------------------------
CALL TARGET_MILK_CHECK(対象キャラ)
;-------------------------------------------------
;中毒充足チェック
;-------------------------------------------------
;MADA
;-------------------------------------------------
;苦痛快楽経験、奉仕快楽経験のチェック
;-------------------------------------------------
CALL EXP_GOT_CHECK(対象キャラ)
;-------------------------------------------------
;絶頂時の性愛度上昇
;-------------------------------------------------
SIF CFLAG:対象キャラ:うふふ && 対象キャラ != MASTER
	CALL SOURCE_CALC_好感度要素_性愛度UP(対象キャラ, PLAYER, RCVAR:対象キャラ:絶頂の強度 * 100)

