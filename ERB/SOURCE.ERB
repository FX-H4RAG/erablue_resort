@SOURCE_CHECK
SELECTCOM = TFLAG:SELECTCOM受け渡し
SELECTCOM_S = %RSTR:SELECTCOM_S受け渡し%

;調教SELECTCOMの時間経過
CALL TRACHECK_TIME

;助手マニュアルモードならPLAYERを一時的に変更
IF FLAG:モード管理 % 8 == 2
	LOCAL:1 = PLAYER
	PLAYER = 助手は誰だ()
ENDIF

FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	; ;性交フラグの設定
	; IF TEQUIP:LOCAL:106 == 1
	; 	TEQUIP:LOCAL:50 = PLAYER
	; ELSE
	; 	TEQUIP:LOCAL:50 = -1
	; ENDIF
	; IF TEQUIP:LOCAL:107 == 1
	; 	TEQUIP:LOCAL:51 = PLAYER
	; ELSE
	; 	TEQUIP:LOCAL:51 = -1
	; ENDIF
	
	; ;逆レイプ
	; SIF TEQUIP:LOCAL:101 == 6
	; 	TEQUIP:PLAYER:50 = LOCAL
	; ;逆アナル
	; SIF TEQUIP:LOCAL:101 == 7
	; 	TEQUIP:PLAYER:51 = LOCAL
	; ;体位リセット
	; SIF TEQUIP:LOCAL:50 < 0 && TEQUIP:LOCAL:51 < 0
	; 	TEQUIP:LOCAL:体位 = 0
	;勃起度の保存
	CFLAG:LOCAL:勃起度 = BASE:LOCAL:勃起
NEXT

;助手モードで切り替えたPLAYERを戻す
SIF FLAG:モード管理 % 8 == 2
	PLAYER = LOCAL:1

;-------------------------------------------------
;@COMの呼び出し
;-------------------------------------------------
IF TFLAG:オートコマンドフラグ > 2000
	TRYCALLFORM ALLAUTOCOM{TFLAG:オートコマンドフラグ - 2000}
ELSEIF TFLAG:オートコマンドフラグ > 1000
	TRYCALLFORM AUTOCOM{TFLAG:オートコマンドフラグ - 1000}
ELSEIF TFLAG:オートコマンドフラグ > 0
	TRYCALLFORM PRE_AUTOCOM{TFLAG:オートコマンドフラグ}
ELSEIF TFLAG:オートコマンドフラグ == -1
	CALL 自室キャラ訪問処理
ELSE
	;複数モード時か否か
	IF FLAG:モード管理 % 8
		;同時モード時は通常処理に加えてTARGETを切り替えてもう一度。
		;将来的にはCAN_COMが両方通らないならば処理を行わないようにしたい。
		IF GETBIT(FLAG:モード管理, 0)
			CALL CALL_COM(TARGET)
			LOCAL = TARGET
			TARGET = 助手は誰だ()
			CALL CALL_COM(TARGET)
			TARGET = LOCAL
		;助手モード時はPLAYERを助手に切り替えて処理する
		ELSEIF GETBIT(FLAG:モード管理, 1)
			LOCAL = PLAYER
			PLAYER = 助手は誰だ()
			CALL CALL_COM(TARGET)
			PLAYER = LOCAL
		ENDIF
	ELSE
		CALL CALL_COM(TARGET)
	ENDIF
ENDIF
SIF RESULT == -1
	RETURN -1
SIF !RESULT
	GOTO SKIP
FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	;野外オナニー絶頂判定
	IF TCVAR:LOCAL:野外オナニー_絶頂
		CALL 野外オナニー実処理(LOCAL, TCVAR:LOCAL:野外オナニー_部位)
		;相手を覗いている時以外（コマンドが覗きでない、対象がTARGETでない）は表示カット
		SIF SELECTCOM != 404 || RFLAG:コマンド結果受渡し変数２ != LOCAL
			RCVAR:LOCAL:表示カット = 1
		CFLAG:LOCAL:隠密 = 0
	ENDIF

	;ローター挿入
	IF CFLAG:LOCAL:ローター挿入
		CALL SOURCE_CALC_快Ｖ(LOCAL, -1, TIME_PROGRESS(TFLAG:行動前時刻) * 10)
		CFLAG:LOCAL:ローター挿入 = MAX(CFLAG:LOCAL:ローター挿入 - TIME_PROGRESS(TFLAG:行動前時刻),0)
		IF CFLAG:LOCAL:ローター挿入 == 0
			PRINTFORMW %CALLNAME:LOCAL%が装着していたローターの電池が切れた
			IF TALENT:LOCAL:処女 == 0
				EXP:LOCAL:Ｖ経験 += 9
			ENDIF
		ENDIF
	ENDIF
	IF CFLAG:LOCAL:ローターA挿入
		CALL SOURCE_CALC_快Ａ(LOCAL, -1, TIME_PROGRESS(TFLAG:行動前時刻) * 10)
		CFLAG:LOCAL:ローターA挿入 = MAX(CFLAG:LOCAL:ローターA挿入 - TIME_PROGRESS(TFLAG:行動前時刻),0)
		IF CFLAG:LOCAL:ローターA挿入 == 0
			PRINTFORMW %CALLNAME:LOCAL%が装着していたローターの電池が切れた
			EXP:LOCAL:Ａ経験 += 9
		ENDIF
	ENDIF

	;装着系は後で考える
	SIF LOCAL > 0 && !CFLAG:LOCAL:同室
		CONTINUE
	;-------------------------------------------------
	;中毒充足のチェック
	;-------------------------------------------------
	;-------------------------------------------------
	;モード持続時チェック
	;-------------------------------------------------
	CALL MODE_CHECK(LOCAL)
	;-------------------------------------------------
	;経験加算
	;-------------------------------------------------
	CALL SOURCE_EXP(LOCAL)
NEXT
;童貞喪失
IF !TALENT:PLAYER:1 && TALENT:PLAYER:2 & 2
	SELECTCASE SELECTCOM
		CASE 60,62
			PRINTW 童貞喪失
	ENDSELECT
ENDIF
IF !TALENT:TARGET:1 && TALENT:TARGET:2 & 2
	SELECTCASE SELECTCOM
		CASE 64,92,93,94,71
			PRINTFORMW 童貞喪失(%CALLNAME:TARGET%)
	ENDSELECT
ENDIF
;-------------------------------------------------
;射精我慢処理、実際には絶頂我慢
;-------------------------------------------------
IF あなた射精我慢フラグ
	IF CUP:PLAYER:快Ｃ + PALAM:PLAYER:快Ｃ > 9999
		CUP:PLAYER:快Ｃ = 9999 - PALAM:PLAYER:快Ｃ
	ENDIF
	IF CUP:PLAYER:快Ｂ + PALAM:PLAYER:快Ｂ > 9999
		CUP:PLAYER:快Ｂ = 9999 - PALAM:PLAYER:快Ｂ
	ENDIF
	IF CUP:PLAYER:快Ｖ + PALAM:PLAYER:快Ｖ > 9999
		CUP:PLAYER:快Ｖ = 9999 - PALAM:PLAYER:快Ｖ
	ENDIF
	IF CUP:PLAYER:快Ａ + PALAM:PLAYER:快Ａ > 9999
		CUP:PLAYER:快Ａ = 9999 - PALAM:PLAYER:快Ａ
	ENDIF
	IF CUP:PLAYER:快Ｓ + PALAM:PLAYER:快Ｓ > 9999
		CUP:PLAYER:快Ｓ = 9999 - PALAM:PLAYER:快Ｓ
	ENDIF
ENDIF


FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	;装着系は後で考える
	SIF LOCAL > 0 && !CFLAG:LOCAL:同室 && !CFLAG:LOCAL:快楽計算フラグ
		CONTINUE
	;-------------------------------------------------
	;干渉力による補正処理
	;-------------------------------------------------
	CALL 干渉補正実処理関数(LOCAL, "快楽増加時")
	;-------------------------------------------------
	;お祭りなどイベントによる補正処理
	;-------------------------------------------------
	IF 開催予定日取り == DAY
		CALLFORM MOVE_ROUTINE_%開催予定祭り名%(LOCAL, "お祭り補正条件")
		IF RESULT
			CALLFORM MOVE_ROUTINE_%開催予定祭り名%(LOCAL, "お祭り補正内容")
		ENDIF
	ENDIF
	;-------------------------------------------------
	;絶頂処理
	;-------------------------------------------------
	CALL ORGASM_ADD(LOCAL)
	;-------------------------------------------------
	;調教対象の噴乳チェック
	;-------------------------------------------------
	CALL TARGET_MILK_CHECK(LOCAL)
	;-------------------------------------------------
	;中毒充足チェック
	;-------------------------------------------------
	;MADA
	;-------------------------------------------------
	;体力の減少
	;-------------------------------------------------
	CALL SOURCE_DOWNBASE(LOCAL)
	;-------------------------------------------------
	;苦痛快楽経験、奉仕快楽経験のチェック
	;-------------------------------------------------
	CALL EXP_GOT_CHECK(LOCAL)
	;-------------------------------------------------
	;絶頂時の性愛度上昇
	;-------------------------------------------------
	SIF CFLAG:LOCAL:うふふ && LOCAL != MASTER
		CALL SOURCE_CALC_好感度要素_性愛度UP(LOCAL, PLAYER, RCVAR:LOCAL:絶頂の強度 * 100)
NEXT

;射精周りのフラグ設定
; CALL SAMEN_SHOOT_FLAGSETTING
$SKIP

;-------------------------------------------------
;マップごとの特殊処理を呼び出す
;-------------------------------------------------
TRYCALLFORM COM_END_PLACEEVENT_{CFLAG:MASTER:現在マップ種別}_{CFLAG:PLAYER:現在位置}

;-------------------------------------------------
;キャラごとの特殊処理を呼び出す
;-------------------------------------------------
TRYCALLFORM COM_END_CHARAEVENT_{NO:TARGET}

;-------------------------------------------------
;調教口上
;-------------------------------------------------
CALL KOJO_MESSAGE_COM
CALL 挿入アニメ表示(TARGET, "_挿入", FLAG_挿入アニメフィルタ)
TFLAG:メッセージ重複防止 = 1
;同時モードなら助手のぶんも表示
IF GETBIT(FLAG:モード管理, 0) && TFLAG:オートコマンドフラグ == 0
	LOCAL = TARGET
	TARGET = 助手は誰だ()
	CALL KOJO_MESSAGE_COM
	CALL 挿入アニメ表示(TARGET, "_挿入", FLAG_挿入アニメフィルタ)
	TARGET = LOCAL
ENDIF
TFLAG:メッセージ重複防止 = 0
;-------------------------------------------------
;EXP取得表示
;-------------------------------------------------
LOCAL:2 = 0
LOCAL:3 = 0
FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	FOR LOCAL:1, 0, 200
		SIF RCVAR:LOCAL:表示カット
			CONTINUE
		IF EXP_UP(LOCAL:1,LOCAL)
			LOCAL:2 ++
			LOCAL:3 ++
			SIF LOCAL:2 == 1
			PRINTL
			LOCALS = %EXPNAME:(LOCAL:1)%
			PRINTFORM %LOCALS,15,LEFT%
			IF LOCAL:1 == 89
				LOCALS = +{EXP_UP(LOCAL:1,LOCAL)}
				PRINTFORML %LOCALS,4,LEFT%(%CALLNAME:PLAYER% > %CALLNAME:LOCAL%)
			ELSE
				LOCALS = +{EXP_UP(LOCAL:1,LOCAL)}
				PRINTFORML %LOCALS,4,LEFT%(%CALLNAME:LOCAL%)
			ENDIF
		ENDIF
	NEXT
	LOCAL:2 = 0
NEXT
SIF LOCAL:3
	WAIT
;-------------------------------------------------
;ABLUP
;-------------------------------------------------
FOR LOCAL,0,CHARANUM
	SIF RCVAR:LOCAL:表示カット
		CONTINUE
	CALL SOURCE_ABLUP(LOCAL)
NEXT
;-------------------------------------------------
;触手の射精時の口上
;-------------------------------------------------
;CALL KOJO_MESSAGE_PALAMCNG_E
;触手射精未実装なので一旦コメントアウト
;-------------------------------------------------
;奴隷の絶頂時の口上
;-------------------------------------------------
CALL MESSAGE_PALAMCNG_絶頂強度表示
IF NOWEX:TARGET:多重絶頂
	CALL KOJO_ORGASM_絶頂
	PRINTL
ENDIF
;-------------------------------------------------
;奴隷射精時の口上
;-------------------------------------------------
IF NOWEX:TARGET:射精
	SIF NOWEX:TARGET:射精 > 1
		PRINT 大量
	PRINTFORML 射精(%CALLNAME:TARGET%)
	CALL KOJO_ORGASM_射精
	PRINTL
ENDIF
;-------------------------------------------------
;調教者射精時
;-------------------------------------------------
IF NOWEX:PLAYER:射精
	SIF NOWEX:PLAYER:射精 > 1
		PRINT 大量
	PRINTFORML 射精(%CALLNAME:PLAYER%)
	RFLAG:フィニッシュフラグ = NOWEX:PLAYER:11
	CALL EVENT_SHOOT_PLAYER
	PRINTL
ELSE
	RFLAG:フィニッシュフラグ = 0
ENDIF
;-------------------------------------------------
;調教後の口上
;-------------------------------------------------
CALL KOJO_MESSAGE_PALAMCNG_AFTER
PRINTL
;-------------------------------------------------
;パラメータ変動とそれによる口上
;-------------------------------------------------
;CALL KOJO_MESSAGE_PALAMCNG_D
FOR LOCAL, 1, CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	CALL 技巧上昇判定(LOCAL)
	SIF RESULT
		TRYCALLFORM KOJO_SKILLUP_技巧_{NO:LOCAL}
	CALL 性知識上昇判定(LOCAL)
	SIF RESULT
		TRYCALLFORM KOJO_SKILLUP_性知識_{NO:LOCAL}
NEXT
;-------------------------------------------------
;刻印取得のチェック
;-------------------------------------------------
;CALL MARK_GOT_CHECK(TARGET)
;-------------------------------------------------
;刻印取得による口上
;-------------------------------------------------
;CALL KOJO_MESSAGE_MARKCNG
;-------------------------------------------------
;屈服・快楽堕ちの上昇による[抵抗]の消滅チェック
;-------------------------------------------------
CALL JUJUN_UP_CHECK(TARGET)
;-------------------------------------------------
;調教ソースの表示
;-------------------------------------------------
CALL SHOW_SOURCE(TARGET)
;-------------------------------------------------
;数値変動の表示
;-------------------------------------------------
LOCAL:1 = 0
FOR LOCAL,0,CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	IF DOWNBASE:LOCAL:体力
		PRINTFORM 体力-{DOWNBASE:LOCAL:体力} 
		LOCAL:1 ++
	ENDIF
	SIF LOCAL:1
		PRINTFORML (%CALLNAME:LOCAL%)
	LOCAL:1 = 0
NEXT
PRINTL 
;-------------------------------------------------
;関係性ゲージ上昇
;-------------------------------------------------
;反感不快との相殺の関係で先に恋慕度から判定
CALL Love_CALC(TARGET)
CALL FAVOR_CALC(TARGET)

;-------------------------------------------------
;パラメータの上昇＆表示（DOWNもここで）
;-------------------------------------------------
VARSET LOCAL
FOR LOCAL:1, 0, CHARANUM
	SIF CFLAG:(LOCAL:1):滞在期間 < 0
		CONTINUE
	SIF RCVAR:(LOCAL:1):表示カット
		SKIPDISP 1
	LOCAL:3 = 0
	FOR LOCAL,0,100
		;コマンド入力前のCUPを追加
		CUP:(LOCAL:1):LOCAL += TCVAR:(LOCAL:1):(600 + LOCAL)
		SIF PALAMNAME:LOCAL == ""
			CONTINUE
		SIF CUP:(LOCAL:1):LOCAL - CDOWN:(LOCAL:1):LOCAL == 0
			CONTINUE
		IF !LOCAL:3
			PRINTL
			PRINTFORML %CALLNAME:(LOCAL:1)%
		ENDIF
		LOCAL:3 ++
		LOCAL:2 = CUP:(LOCAL:1):LOCAL - CDOWN:(LOCAL:1):LOCAL + TCVAR:(LOCAL:1):(600 + LOCAL)
		PRINTFORM %PALAMNAME:LOCAL,8,LEFT% {PALAM:(LOCAL:1):LOCAL,10,LEFT} + {CUP:(LOCAL:1):LOCAL,10,LEFT} =   {PALAM:(LOCAL:1):LOCAL + CUP:(LOCAL:1):LOCAL,10,LEFT}
		DEBUGPRINTFORML LOCAL{LOCAL}:干渉種類{干渉強化変数:(LOCAL:1):干渉種類}
		IF 干渉強化変数:(LOCAL:1):干渉種類 - 5 == LOCAL
			SETCOLOR カラーパレット("黄")
			PRINT 　干渉効果発動中！
			RESETCOLOR
		ENDIF
		PRINTL
		PALAM:(LOCAL:1):LOCAL += CUP:(LOCAL:1):LOCAL - CDOWN:(LOCAL:1):LOCAL
		PALAM:(LOCAL:1):LOCAL = MAX(0, PALAM:(LOCAL:1):LOCAL)
		CUP:(LOCAL:1):LOCAL = 0
		CDOWN:(LOCAL:1):LOCAL = 0
	NEXT
	;干渉ランク上昇判定
	IF 干渉強化変数:(LOCAL:1):干渉ランク < 5
		IF 干渉経験値テーブル:(干渉強化変数:(LOCAL:1):干渉ランク) <= 干渉強化変数:(LOCAL:1):干渉経験値
			干渉強化変数:(LOCAL:1):干渉ランク += 1
			PRINTFORMW %CALLNAME:(LOCAL:1)%の干渉ランクが上昇した！
		ENDIF
	ENDIF
	SKIPDISP 0
	LOCALS '= CHECK_体位(LOCAL:1)
	SIF LOCALS != "" && GROUPMATCH(TCVAR:(LOCAL:1):被射精位置, 1, 2)
		CALL アニメ表示_射精(LOCALS, PLAYER)
NEXT
FOR LOCAL, 0, 100
	CVARSET TCVAR, 600 + LOCAL, 0
NEXT

;-------------------------------------------------
;ZPの取得量表示
;-------------------------------------------------
IF RFLAG:ZP取得量
	PRINTL
	PRINTFORM ZP:{FLAG:ZP所持量 - RFLAG:ZP取得量}
	FOR LOCAL, 0, CHARANUM
		IF RCVAR:LOCAL:ZP取得量
			PRINTFORM  + {RCVAR:LOCAL:ZP取得量}(%CALLNAME:LOCAL%)
		ENDIF
	NEXT
	PRINTFORML = {FLAG:ZP所持量}
ENDIF

;-------------------------------------------------
;性癖取得の判定
;-------------------------------------------------
;同室者のみ
FOR LOCAL, 0, CHARANUM
	SIF TARGET:LOCAL < 1
		BREAK
	CALL 性癖取得チェック(TARGET:LOCAL)
	CALL 性癖成長チェック(TARGET:LOCAL)
NEXT

;非同室者含む
FOR LOCAL, 1, CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 1
		CONTINUE
	CALL 性癖非同室成長チェック(TARGET:LOCAL)
NEXT

;-------------------------------------------------
;調教ソースの表示（DOWNもここで）
;-------------------------------------------------
@SHOW_SOURCE(ARG)
SIF RCVAR:ARG:表示カット
	RETURN 0
;PRINTL 　
SIF SOURCE:ARG:快Ｃ > 0
	PRINTFORM 快Ｃ({SOURCE:ARG:快Ｃ})
SIF SOURCE:ARG:快Ｖ > 0
	PRINTFORM 快Ｖ({SOURCE:ARG:快Ｖ})
SIF SOURCE:ARG:快Ａ > 0
	PRINTFORM 快Ａ({SOURCE:ARG:快Ａ})
SIF SOURCE:ARG:快Ｂ > 0
	PRINTFORM 快Ｂ({SOURCE:ARG:快Ｂ})
SIF SOURCE:ARG:快Ｓ > 0
	PRINTFORM 快Ｓ({SOURCE:ARG:快Ｓ})
SIF SOURCE:ARG:情愛 > 0
	PRINTFORM 情愛({SOURCE:ARG:情愛})
SIF SOURCE:ARG:性行動 > 0
	PRINTFORM 性行動({SOURCE:ARG:性行動})
SIF SOURCE:ARG:達成 > 0
	PRINTFORM 達成感({SOURCE:ARG:達成})
SIF SOURCE:ARG:苦痛 > 0
	PRINTFORM 痛み({SOURCE:ARG:苦痛})
SIF SOURCE:ARG:恐怖 > 0
	PRINTFORM 恐れ({SOURCE:ARG:恐怖})
SIF SOURCE:ARG:液体 > 0
	PRINTFORM 液体({SOURCE:ARG:液体})
SIF SOURCE:ARG:欲情 > 0
	PRINTFORM 欲情({SOURCE:ARG:欲情})
SIF SOURCE:ARG:恭順 > 0
	PRINTFORM 恭順({SOURCE:ARG:恭順})
SIF SOURCE:ARG:露出 > 0
	PRINTFORM 露出({SOURCE:ARG:露出})
SIF SOURCE:ARG:屈従 > 0
	PRINTFORM 屈服({SOURCE:ARG:屈従})
SIF SOURCE:ARG:歓楽 > 0
	PRINTFORM 歓楽({SOURCE:ARG:歓楽})
SIF SOURCE:ARG:征服 > 0
	PRINTFORM 征服({SOURCE:ARG:征服})
SIF SOURCE:ARG:受動 > 0
	PRINTFORM 受動({SOURCE:ARG:受動})
SIF SOURCE:ARG:不潔 > 0
	PRINTFORM 不潔({SOURCE:ARG:不潔})
SIF SOURCE:ARG:鬱屈 > 0
	PRINTFORM 鬱屈({SOURCE:ARG:鬱屈})
SIF SOURCE:ARG:逸脱 > 0
	PRINTFORM 逸脱({SOURCE:ARG:逸脱})
SIF SOURCE:ARG:反感 > 0
	PRINTFORM 反発({SOURCE:ARG:反感})
PRINTL 　
