
;-------------------------------------------------------------------------------------------
;射精先の選択肢関連の処理
;-------------------------------------------------------------------------------------------

@EVENT_SHOOT_CHARA(射精キャラ)
#DIM 射精キャラ
#DIM 射精先キャラ, 射精先キャラ最大数
#DIM 射精番号配列, 射精先キャラ最大数
#DIM 口上用キャラ番号
#DIM 対象キャラ番号
#DIM 選択肢数
#DIM 射精先番号
VARSET LOCAL
VARSET 射精先キャラ, -1
VARSET 射精番号配列, -1
;0では駄目なので初期化

;射精キャラを登録
RFLAG:口上用射精キャラ番号 = 射精キャラ

;どこに射精するかを決定する

;ハーヴィンオナホ中なら選択なしでそのまま
IF MODE_確認_プレイヤー側("ハーヴィンオナホＶ", 射精キャラ) > -1
	射精番号配列:0 = 0
	射精先キャラ:0 = DT_CELL_GET("体位モードデータベース", MODE_確認_プレイヤー側("ハーヴィンオナホＶ", 射精キャラ), "対象キャラ")
	LOCAL = 0
	GOTO 射精実処理部分
ENDIF
IF MODE_確認_プレイヤー側("ハーヴィンオナホＡ", 射精キャラ) > -1
	射精番号配列:0 = 1
	射精先キャラ:0 = DT_CELL_GET("体位モードデータベース", MODE_確認_プレイヤー側("ハーヴィンオナホＡ", 射精キャラ), "対象キャラ")
	LOCAL = 0
	GOTO 射精実処理部分
ENDIF
IF MODE_確認_プレイヤー側("挿入歩きＶ", 射精キャラ) > -1
	射精番号配列:0 = 0
	射精先キャラ:0 = DT_CELL_GET("体位モードデータベース", MODE_確認_プレイヤー側("挿入歩きＶ", 射精キャラ), "対象キャラ")
	LOCAL = 0
	GOTO 射精実処理部分
ENDIF
IF MODE_確認_プレイヤー側("挿入歩きＡ", 射精キャラ) > -1
	射精番号配列:0 = 1
	射精先キャラ:0 = DT_CELL_GET("体位モードデータベース", MODE_確認_プレイヤー側("挿入歩きＡ", 射精キャラ), "対象キャラ")
	LOCAL = 0
	GOTO 射精実処理部分
ENDIF
IF MODE_確認_ターゲット側("ハーヴィンディルドＶ", 射精キャラ) > -1
	射精番号配列:0 = 0
	射精先キャラ:0 = DT_CELL_GET("体位モードデータベース", MODE_確認_ターゲット側("ハーヴィンディルドＶ", 射精キャラ), "実行キャラ")
	LOCAL = 0
	GOTO 射精実処理部分
ENDIF
IF MODE_確認_ターゲット側("ハーヴィンディルドＡ", 射精キャラ) > -1
	射精番号配列:0 = 1
	射精先キャラ:0 = DT_CELL_GET("体位モードデータベース", MODE_確認_ターゲット側("ハーヴィンディルドＡ", 射精キャラ), "実行キャラ")
	LOCAL = 0
	GOTO 射精実処理部分
ENDIF

;射精先の登録
選択肢数 = 0
IF MODE_確認_ターゲット側("ALL", 射精キャラ) > -1 || MODE_確認_プレイヤー側("ALL", 射精キャラ) > -1
	FOR 射精先番号, 0, 30
		;まずあなたに対しての判定、同室時のみ
		IF CFLAG:射精キャラ:現在位置 == CFLAG:MASTER:現在位置 && CFLAG:射精キャラ:現在マップ種別 == CFLAG:MASTER:現在マップ種別 && CFLAG:射精キャラ:隠密 == CFLAG:MASTER:隠密
			TRYCALLFORM SHOOT_ABLE_{射精先番号}(射精キャラ, MASTER)
			IF RESULT
				射精先キャラ:選択肢数 = MASTER
				射精番号配列:選択肢数 = 射精先番号
				選択肢数 += 1
			ENDIF
		ENDIF
		;同室キャラに対して各射精先を判定していく
		FOR LOCAL, 1, CHARANUM
			SIF CFLAG:射精キャラ:現在位置 != CFLAG:LOCAL:現在位置 || CFLAG:射精キャラ:現在マップ種別 != CFLAG:LOCAL:現在マップ種別
				CONTINUE
			SIF LOCAL == 射精キャラ
				CONTINUE
			SIF CFLAG:射精キャラ:隠密 != CFLAG:LOCAL:隠密
				CONTINUE
			TRYCALLFORM SHOOT_ABLE_{射精先番号}(射精キャラ, LOCAL)
			IF RESULT
				射精先キャラ:選択肢数 = LOCAL
				射精番号配列:選択肢数 = 射精先番号
				選択肢数 += 1
			ENDIF
		NEXT
	NEXT

	;ランダムで選択肢から選択
	SIF 選択肢数 > 0
		LOCAL = RAND:選択肢数
ENDIF

IF 選択肢数 == 0
	;モードが何もないなら30番確定
	RFLAG:フィニッシュ場所 = 30
	TRYCALLFORM MESSAGE_SHOOT_30(射精キャラ, -1)
	WAIT
	RETURN 0
ENDIF

$射精実処理部分
;射精実処理
RFLAG:フィニッシュ場所 = 射精番号配列:LOCAL
CALLFORM SHOOT_PALAM_{RFLAG:フィニッシュ場所}(射精キャラ, 射精先キャラ:LOCAL)
PRINTL
DRAWLINE
TRYCALLFORM MESSAGE_SHOOT_{RFLAG:フィニッシュ場所}(射精キャラ, 射精先キャラ:LOCAL)
WAIT
PRINTL 
CALL SHOOT絶頂計算(射精先キャラ:LOCAL)
SIF RCVAR:(射精先キャラ:LOCAL):被射精絶頂の強度
	CALL SHOOT絶頂口上(射精先キャラ:LOCAL)
PRINTL 
TRYCCALLFORM KOJO_ORGASM_被射精_{NO:(射精先キャラ:LOCAL)}(射精先キャラ:LOCAL)
	WAIT
CATCH
ENDCATCH
RFLAG:フィニッシュ相手番号 = 射精先キャラ:LOCAL
