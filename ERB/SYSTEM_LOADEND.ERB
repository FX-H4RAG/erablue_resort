

;-------------------------------------------------------------------------------------------
;ロードの後に実行される一連の処理
;主にキャラのCSVが前回プレイから新たに増えた時など
;-------------------------------------------------------------------------------------------

@SYSTEM_LOADEND
#DIM 追加キャラ保存, 100
#DIM 互換維持用変数, 100

#DIMS 比較用一時保存配列, 1000
#DIMS 素材名分割用, 2
VARSET 比較用一時保存配列

VARSET 追加キャラ保存
LOCAL:1 = 0

;番号ミスの互換処理、Ver0.030にて記述
GETCHARA 2001
IF RESULT > 0 && NAME:RESULT == "[誓いの再臨]ソフィーティア"
	NO:RESULT = 1009
ENDIF
GETCHARA 2002
IF RESULT > 0 && NAME:RESULT == "[一途な鶴の化身]いろは"
	NO:RESULT = 1010
ENDIF
GETCHARA 2000
IF RESULT > 0 && (NAME:RESULT == "[牛乳姫]楠舞　神夜" || NAME:RESULT == "[牛乳姫]楠舞神夜")
	NO:RESULT = 1011
ENDIF


;CSVを全部読んでキャラがADDってなければADDる
FOR LOCAL, 1, キャラクタ数上限
	SIF !EXISTCSV(LOCAL)
		CONTINUE
	SIF LOCAL == 999
		CONTINUE
	SIF GETCHARA(LOCAL) >= 0
		CONTINUE
	追加キャラ保存:(LOCAL:1) = CHARANUM
	ADDCHARA LOCAL
	LOCAL:1 += 1
NEXT

SIF LOCAL:1
	CALL コマンド存在チェック

FOR LOCAL, 0, LOCAL:1
	CALL キャラクター初期化(追加キャラ保存:LOCAL)
NEXT

;ロード時処理
FOR LOCAL, 0, CHARANUM
	TRYCALLFORM 口上側LOADENDパラメータ変更_{NO:LOCAL}
	SIF !BASE:LOCAL:身長
		CALL INIT_BODYSIZE(LOCAL)
	IF !RELATION_VAL:0:0 && FINDELEMENT(追加キャラ保存, LOCAL) < 0
		CALL SET_RELATION(LOCAL)
	ENDIF
NEXT
SIF !RELATION_VAL:0:0
	RELATION_VAL:0:0 = 1
CALL 戦闘用データベースセット
CALL 従業員業務用マップセット

ボタン化用カラーマトリクス:0:0 = 768,256,256,  0,  0
ボタン化用カラーマトリクス:1:0 = 256,768,256,  0,  0
ボタン化用カラーマトリクス:2:0 = 256,256,256,  0,  0
ボタン化用カラーマトリクス:3:0 =   0,  0,  0,256,  0
ボタン化用カラーマトリクス:4:0 =   0,  0,  0,  0,256

;------------------------------------------------------------------------------------------
;旧セーブとの互換のために入れる処理
;------------------------------------------------------------------------------------------
;空艇港の誤字統一
LOCAL:1 = 0
FOR LOCAL, 0, DT_ROW_LENGTH("各イベント用変数配列")
	IF DT_CELL_GETS("各イベント用変数配列", LOCAL, "イベント名") == "大拡張！空挺港"
		互換維持用変数 = LOCAL
		LOCAL:1 = 1
		BREAK
	ENDIF
NEXT
IF LOCAL:1
	DT_ROW_ADD "各イベント用変数配列", "イベント名", "大拡張！空艇港", "イベントカテゴリ", "メインシナリオ", "イベント発生フラグ", DT_CELL_GET("各イベント用変数配列", 互換維持用変数, "イベント発生フラグ"), "イベント完了フラグ", DT_CELL_GET("各イベント用変数配列", 互換維持用変数, "イベント完了フラグ")
	DT_ROW_REMOVE "各イベント用変数配列", DT_CELL_GET("各イベント用変数配列", 互換維持用変数, "id")
	
	IF 空挺港初来訪イベントフラグ
		空艇港初来訪イベントフラグ = 1
		空挺港初来訪イベントフラグ = 0
	ENDIF
	FOR LOCAL, 0, 10
		FOR LOCAL:1, 0, 5
			空艇港店舗状況:LOCAL:(LOCAL:1) = 空挺港店舗状況:LOCAL:(LOCAL:1)
		NEXT
	NEXT
ENDIF

;追加TALENT、追加CSTRのチェック
FOR LOCAL, 1, CHARANUM
	IF 基礎BATTLE_STATE:LOCAL:クリティカルダメージ率 == 0
		基礎BATTLE_STATE:LOCAL:クリティカルダメージ率 = 150
	ENDIF
	IF 基礎BATTLE_STATE:LOCAL:敵対心 == 0
		基礎BATTLE_STATE:LOCAL:敵対心 = 100
	ENDIF

	IF CSVCSTR(NO:LOCAL, 50) != ""
		CSTR:LOCAL:特殊種族 = %CSVCSTR(NO:LOCAL, 50)%
	ENDIF
	IF CSVCSTR(NO:LOCAL, 51) != ""
		CSTR:LOCAL:出演作品 = %CSVCSTR(NO:LOCAL, 51)%
	ENDIF
	FOR LOCAL:1, 227, 240
		TALENT:LOCAL:(LOCAL:1) = CSVTALENT(NO:LOCAL, LOCAL:1)
	NEXT
	SIF CFLAG:LOCAL:オナニー覚えたて > 0 && CFLAG:LOCAL:オナニー覚えたて < 100
		CFLAG:LOCAL:オナニー覚えたて += 99
	IF 現在仕事:LOCAL:0 == 2 && 現在仕事:LOCAL:1 == 0
		現在仕事:LOCAL:0 = 1
	ENDIF
	;ここまで0.032で消す

	SIF CFLAG:LOCAL:長期雇用 && TALENT:LOCAL:従業員
		CFLAG:LOCAL:長期雇用 = 0

	SIF 現在仕事:LOCAL:1 == 102
		現在仕事:LOCAL:1 = 2
	;ここまで0.033で消す
NEXT

;素材アイテムデータベースが多重登録されていた問題
LOCAL:1 = 0
FOR LOCAL, 0, DT_ROW_LENGTH("所持素材データベース")
	SIF DT_CELL_GETS("所持素材データベース", LOCAL, "素材アイテム名") == ""
		BREAK
	IF !MATCH(比較用一時保存配列, DT_CELL_GETS("所持素材データベース", LOCAL, "素材アイテム名"))
		比較用一時保存配列:(LOCAL:1) = %DT_CELL_GETS("所持素材データベース", LOCAL, "素材アイテム名")%
		LOCAL:1 += 1
	ELSE
		DT_ROW_REMOVE "所持素材データベース", DT_CELL_GET("所持素材データベース", LOCAL, "id")
		LOCAL -= 1
	ENDIF
NEXT

CALL エロ衣装登録処理("逆バニー")
CALL エロ衣装登録処理("マイクロビキニ")

IF DT_COLUMN_EXIST("所持指輪データベース", "装備者キャラ番号")
	DT_COLUMN_ADD "所持指輪データベース", "装備キャラ番号", 3
	FOR LOCAL, 0, DT_ROW_LENGTH("所持指輪データベース")
		DT_CELL_SET "所持指輪データベース", LOCAL, "装備キャラ番号", DT_CELL_GET("所持指輪データベース", LOCAL, "装備者キャラ番号")
	NEXT
	DT_COLUMN_REMOVE "所持指輪データベース", "装備者キャラ番号"
ENDIF
IF DT_COLUMN_EXIST("所持耳飾りデータベース", "装備者キャラ番号")
	DT_COLUMN_ADD "所持耳飾りデータベース", "装備キャラ番号", 3
	FOR LOCAL, 0, DT_ROW_LENGTH("所持耳飾りデータベース")
		DT_CELL_SET "所持耳飾りデータベース", LOCAL, "装備キャラ番号", DT_CELL_GET("所持耳飾りデータベース", LOCAL, "装備者キャラ番号")
	NEXT
	DT_COLUMN_REMOVE "所持耳飾りデータベース", "装備者キャラ番号"
ENDIF
IF DT_COLUMN_EXIST("所持素材データベース", "所持数")
	DT_COLUMN_ADD "所持素材データベース", "所持素材数", 3
	DT_COLUMN_ADD "所持素材データベース", "累計入手素材数", 3
	DT_COLUMN_ADD "所持素材データベース", "素材値相場", 3
	FOR LOCAL, 0, DT_ROW_LENGTH("所持素材データベース")
		DT_CELL_SET "所持素材データベース", LOCAL, "所持素材数", DT_CELL_GET("所持素材データベース", LOCAL, "所持数")
		DT_CELL_SET "所持素材データベース", LOCAL, "累計入手素材数", DT_CELL_GET("所持素材データベース", LOCAL, "累計入手数")
		DT_CELL_SET "所持素材データベース", LOCAL, "素材値相場", DT_CELL_GET("所持素材データベース", LOCAL, "素材相場")
	NEXT
	DT_COLUMN_REMOVE "所持素材データベース", "所持数"
	DT_COLUMN_REMOVE "所持素材データベース", "累計入手数"
	DT_COLUMN_REMOVE "所持素材データベース", "素材相場"
ENDIF

;ここまで0.032で消す

;調教システム改定による体力数値の変動
IF 体力数値変更済みフラグ == 0
	体力数値変更済みフラグ = 1
	FOR LOCAL, 1, CHARANUM
		BASE:LOCAL:体力 = CSVBASE(NO:LOCAL, 0)
		MAXBASE:LOCAL:体力 = CSVBASE(NO:LOCAL, 0)
		CALL 体力変動素質適用処理(LOCAL)
		BASE:LOCAL:体力 = MAXBASE:LOCAL:体力
		BASE:LOCAL:性欲 = MIN(MAXBASE:LOCAL:性欲, BASE:LOCAL:性欲)
	NEXT
ENDIF
DT_COLUMN_ADD "所持素材データベース", "プレゼントカテゴリ"

IF クライアントサイズ変更通知 == 0
	SETCOLOR 0xFF0000
	PRINTL ＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊
	PRINTL 開発の進行により、推奨するクライアントサイズが980*900から1600*900に変更されました。
	PRINTL emuera.configは自動で切り替わらないため、手動での変更をお願いします。
	PRINTL メニューバーの「ヘルプ」＞「設定」＞「ウィンドウ」＞「ウィンドウ幅」を変更してください。
	PRINTL ＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊＊
	FORCEWAIT
	RESETCOLOR
	クライアントサイズ変更通知 = 1
ENDIF

SIF スイートルーム部屋数 == 0
	スイートルーム部屋数 = 1

DT_COLUMN_ADD "所持素材データベース", "食材分類"

IF DT_EXIST("料理メニューデータベース") == 0
	CALL 食堂メニューデータベースセット
ENDIF

SIF 開発済み料理名フラグ文字列 == ""
	開発済み料理名フラグ文字列 += "_"
;ここまで0.033で消す

;------------------------------------------------------------------------------------------
;ここまで互換のための処理
;------------------------------------------------------------------------------------------


FOR LOCAL, 1, CHARANUM
	;キャラ体力再計算
	CALL 体力変動素質適用処理(LOCAL)

	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	;部屋の無いキャラがいないかチェック
	IF キャラクター部屋検索(LOCAL) == -1 && CFLAG:LOCAL:スイートルーム滞在フラグ == 0
		IF TALENT:LOCAL:従業員
			FOR LOCAL:1, 0, 従業員キャラ上限
				IF 従業員部屋割り配列:(LOCAL:1) < 1
					従業員部屋割り配列:(LOCAL:1) = LOCAL
					CFLAG:LOCAL:現在マップ種別 = 999
					CFLAG:LOCAL:現在位置 = LOCAL:1 + 1000
					RETURN 1
				ENDIF
			NEXT
		ELSE
			CALL 滞在者部屋割り配列挿入処理(LOCAL)
		ENDIF
	ENDIF

	;複数の部屋に入っちゃってないかチェック
	LOCAL:2 = 0
	IF TALENT:LOCAL:従業員
		FOR LOCAL:1, 0, 従業員キャラ上限
			IF 従業員部屋割り配列:(LOCAL:1) == LOCAL
				IF LOCAL:2
					従業員部屋割り配列:(LOCAL:1) = 0
				ELSE
					LOCAL:2 = 1
				ENDIF
			ENDIF
		NEXT
	ELSE
		FOR LOCAL:1, 0, 100
			IF 滞在者部屋割り配列:(LOCAL:1) == LOCAL
				IF LOCAL:2
					滞在者部屋割り配列:(LOCAL:1) = 0
				ELSE
					LOCAL:2 = 1
				ENDIF
			ENDIF
		NEXT
	ENDIF
NEXT

CALL イベント追加チェック
CALL イベント削除チェック
IF TFLAG:ゲームフェイズ管理 == 0
	CALL イベント発生条件チェック
ENDIF

;新しく素材アイテムが増えてないかチェック
CALL 素材アイテム登録処理



;念のため変数リセット
CALL TURN_RESET
