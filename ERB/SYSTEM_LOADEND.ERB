

;-------------------------------------------------------------------------------------------
;ロードの後に実行される一連の処理
;主にキャラのCSVが前回プレイから新たに増えた時など
;-------------------------------------------------------------------------------------------

@SYSTEM_LOADEND
#DIM 追加キャラ保存, 100
VARSET 追加キャラ保存
LOCAL:1 = 0

;CSVを全部読んでキャラがADDってなければADDる
FOR LOCAL, 1, キャラクタ数上限
	SIF !EXISTCSV(LOCAL)
		CONTINUE
	SIF LOCAL == 999
		CONTINUE
	SIF GETCHARA(LOCAL) >= 0
		CONTINUE
	追加キャラ保存:(LOCAL:1) = CHARANUM
	ADDCHARA LOCAL
	LOCAL:1 += 1
NEXT

FOR LOCAL, 0, LOCAL:1
	LOCAL:2 = 追加キャラ保存:LOCAL
	CALL キャラクター初期化(LOCAL:2)
NEXT

ボタン化用カラーマトリクス:0:0 = 768,256,256,  0,  0
ボタン化用カラーマトリクス:1:0 = 256,768,256,  0,  0
ボタン化用カラーマトリクス:2:0 = 256,256,256,  0,  0
ボタン化用カラーマトリクス:3:0 =   0,  0,  0,256,  0
ボタン化用カラーマトリクス:4:0 =   0,  0,  0,  0,256

;旧セーブとの互換のために入れるデータベースセット処理
;Ver0.03に上げる時に消すこと！！！！！！
IF LASTLOAD_VERSION < 21
	CALL アイテムデータベースセット
	CALL イベントデータベースセット
ENDIF
SIF 従業員キャラ上限 < 1
	従業員キャラ上限 = 2
SIF 施設改造度:0:0 == 0
	施設改造度:0:0 = 1
;ここまで互換のための処理

CALL イベント追加チェック
IF TFLAG:100 == 0
	CALL イベント発生条件チェック
ENDIF
