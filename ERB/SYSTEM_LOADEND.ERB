

;-------------------------------------------------------------------------------------------
;ロードの後に実行される一連の処理
;主にキャラのCSVが前回プレイから新たに増えた時など
;-------------------------------------------------------------------------------------------

@SYSTEM_LOADEND
#DIM 追加キャラ保存, 100
#DIM 互換維持用変数, 100
#DIM 同行配列番号
#DIM L_C

#DIMS 比較用一時保存配列, 1000
#DIMS 素材名分割用, 2
#DIMS 同行キャラ文字列一時保存
#DIMS CSTR切り分け文字列格納, 2
VARSET 比較用一時保存配列
VARSET 同時モード_選択キャラ, -1

VARSET 追加キャラ保存
LOCAL:1 = 0

ダンジョン表示モード = 

CALL 人物データベースセット

;CSVを全部読んでキャラがADDってなければADDる
FOR LOCAL, 1, キャラクタ数上限
	SIF !EXISTCSV(LOCAL)
		CONTINUE
	SIF LOCAL == 999
		CONTINUE
	SIF GETCHARA(LOCAL) >= 0
		CONTINUE
	追加キャラ保存:(LOCAL:1) = CHARANUM
	ADDCHARA LOCAL
	CALL ADD_PERSON_FROM_CHARA(追加キャラ保存:(LOCAL:1))
	LOCAL:1 += 1
NEXT

SIF LOCAL:1
	CALL コマンド存在チェック

FOR LOCAL, 0, LOCAL:1
	CALL キャラクター初期化(追加キャラ保存:LOCAL)
NEXT

;ロード時処理
FOR LOCAL, 0, CHARANUM
	TRYCALLFORM 口上側LOADENDパラメータ変更_{NO:LOCAL}
	SIF !BASE:LOCAL:身長
		CALL INIT_BODYSIZE(LOCAL)
	IF !RELATION_VAL:0:0 && FINDELEMENT(追加キャラ保存, LOCAL) < 0
		CALL SET_RELATION(LOCAL)
	ENDIF

	;追加同行キャラチェック
	同行キャラ文字列一時保存 = %CSVCSTR(NO:LOCAL, GETNUM(CSTR, "同行キャラ"))%
	VARSET LOCALS
	REPLACE 同行キャラ文字列一時保存, " ", ""
	SPLIT RESULTS, "%", LOCALS
	LOCAL:2 = RESULT - 1
	FOR LOCAL:10, 0, LOCAL:2
		VARSET CSTR切り分け文字列格納
		SPLIT LOCALS:(LOCAL:10), "_", CSTR切り分け文字列格納
		;無い場合のみ追加する
		IF FINDELEMENT(同行候補キャラ番号:LOCAL:0, FINDCHARA(NAME, CSTR切り分け文字列格納:0)) < 0
			;空いてる場所を検索して突っ込む
			同行配列番号 = FINDELEMENT(同行候補キャラ番号:LOCAL:0, 0)
			IF 同行配列番号 > 0
				同行候補キャラ番号:LOCAL:同行配列番号 = FINDCHARA(NAME, CSTR切り分け文字列格納:0)
				同行候補キャラ確率:LOCAL:同行配列番号 = TOINT(CSTR切り分け文字列格納:1)
			ENDIF
			L_C = FINDELEMENT(RELATION_NO:LOCAL:0, 0)
			IF L_C > 0
				RELATION_NO:LOCAL:L_C = 同行候補キャラ番号:LOCAL:同行配列番号
				RELATION_VAL:LOCAL:L_C += 同行候補キャラ確率:LOCAL:同行配列番号 * 100
			ENDIF
		ENDIF
	NEXT

	;固有ステータスチェック
	IF EXISTFUNCTION(@"戦闘初期ステータス設定_{NO:LOCAL}")
		;固有ステがあるのにステタイプが汎用なら修正
		IF 基礎BATTLE_STATE:LOCAL:ステータスタイプ保存 > -1
			CALLFORM 戦闘初期ステータス設定_{NO:LOCAL}(LOCAL)
			CALL レベルアップ処理(LOCAL)
			CALL 口上変数初期化
		ENDIF
	ENDIF
NEXT
SIF !RELATION_VAL:0:0
	RELATION_VAL:0:0 = 1
CALL 戦闘用データベースセット
CALL 従業員業務用マップセット
CALL 体位モードデータベースセット

ボタン化用カラーマトリクス:0:0 = 768,256,256,  0,  0
ボタン化用カラーマトリクス:1:0 = 256,768,256,  0,  0
ボタン化用カラーマトリクス:2:0 = 256,256,256,  0,  0
ボタン化用カラーマトリクス:3:0 =   0,  0,  0,256,  0
ボタン化用カラーマトリクス:4:0 =   0,  0,  0,  0,256

;------------------------------------------------------------------------------------------
;旧セーブとの互換のために入れる処理
;------------------------------------------------------------------------------------------

;追加TALENT、追加CSTRのチェック
FOR LOCAL, 1, CHARANUM
	SIF NO:LOCAL == 999
		CFLAG:LOCAL:招待不可フラグ = 0

	SIF 発情期判定(LOCAL) == 0
		CFLAG:LOCAL:発情期フラグ = 0

	IF 基礎BATTLE_STATE:LOCAL:ゲージ上昇効率_被ダメ時 == 0
		IF 基礎BATTLE_STATE:LOCAL:ステータスタイプ保存 == 2
			基礎BATTLE_STATE:LOCAL:ゲージ上昇効率_被ダメ時 = 150
		ELSE
			基礎BATTLE_STATE:LOCAL:ゲージ上昇効率_被ダメ時 = 100
		ENDIF
	ENDIF

	;発情期フラグの更新
	IF 発情期判定(LOCAL)
		IF 発情期切り替えOPTION記憶 || TALENT:LOCAL:性欲 == -2
			CFLAG:LOCAL:発情期フラグ = 0
		ELSE
			IF CFLAG:LOCAL:発情期フラグ < 0
				CFLAG:LOCAL:発情期フラグ = RISKY_DAY(LOCAL) * -1
				IF CFLAG:LOCAL:発情期フラグ > -1
					;発情期が終わった時、性欲を大きく減らす
					BASE:LOCAL:性欲 = MAX(0, BASE:LOCAL:性欲 - 1500)
				ENDIF
			ELSE
				CFLAG:LOCAL:発情期フラグ = RISKY_DAY(LOCAL) * -1
			ENDIF
		ENDIF
		;ドラフorドラフまぜまぜは発情期に母乳体質化＆発情期でなくなったら母乳を止める
		IF 素質切り替えOPTION記憶:母乳体質_発情期_なし == 0
			IF (TALENT:LOCAL:種族 == 3 || キャラ一致チェック(LOCAL, "[六竜の『金』]ガレヲン")) && TALENT:LOCAL:母乳体質 != 3
				IF CFLAG:LOCAL:発情期フラグ < 0
					SIF TALENT:LOCAL:母乳体質 == 0
						TALENT:LOCAL:母乳体質 = 2
				ELSE
					SIF TALENT:LOCAL:母乳体質 == 2
						TALENT:LOCAL:母乳体質 = 0
				ENDIF
			ENDIF
		ENDIF
	ENDIF

	IF パーティ編成許可制 == 0 && 基礎BATTLE_STATE:LOCAL:Lv > 1
		CFLAG:LOCAL:探索参加許可フラグ = 1
	ENDIF
	
	SIF CFLAG:LOCAL:現在位置 == 303 && CFLAG:LOCAL:現在マップ種別 == 999
		CFLAG:LOCAL:現在マップ種別 = 0
	;ここまで0.037で消す

	TALENT:LOCAL:無関心 = CSVTALENT(NO:LOCAL, GETNUM(TALENT, "無関心"))
	TALENT:LOCAL:情熱的 = CSVTALENT(NO:LOCAL, GETNUM(TALENT, "情熱的"))

	;ここまで0.038で消す
NEXT

IF パーティ編成許可制 == 0
	パーティ編成許可制 = 1
ENDIF

IF 乗機サブパイロットフォロー == 0
	DT_COLUMN_ADD "所持乗機データベース", "サブ搭乗数", 1
	DT_COLUMN_ADD "所持乗機データベース", "サブ搭乗数_強化後", 1
	FOR LOCAL, 0, DT_ROW_LENGTH("所持乗機データベース")
		IF DT_CELL_GETS("所持乗機データベース", LOCAL, "乗機名") == "コロッサスレプリカ"
			DT_CELL_SET "所持乗機データベース", LOCAL, "サブ搭乗数", 2
			DT_CELL_SET "所持乗機データベース", LOCAL, "サブ搭乗数_強化後", 2
		ELSE
			DT_CELL_SET "所持乗機データベース", LOCAL, "サブ搭乗数", 1
			DT_CELL_SET "所持乗機データベース", LOCAL, "サブ搭乗数_強化後", 1
		ENDIF
	NEXT
	乗機サブパイロットフォロー = 1
ENDIF

SIF MAP_EXIST("自動探索対象ダンジョン") == 0
	CALL 自動探索用マップセット
;ここまで0.037にしたとき消す、互換を35に

LOCAL:1 = 0
FOR LOCAL, 0, CHARANUM
	IF !CFLAG:LOCAL:人物番号
		CALL ADD_PERSON_FROM_CHARA(LOCAL)
		LOCAL:1 = 1
	ELSEIF GET_PERSON_出生順(CFLAG:LOCAL:人物番号) == -129
		CALL SET_PERSON_出生順(CFLAG:LOCAL:人物番号, 127)
		LOCAL:1 = 1
	ENDIF
NEXT
SIF LOCAL:1 == 1
	CALL ユニークキャラ親子関係セット

;ここまで0.038にしたとき消す、互換を36に

;------------------------------------------------------------------------------------------
;ここまで互換のための処理
;------------------------------------------------------------------------------------------


FOR LOCAL, 1, CHARANUM
	;キャラ体力再計算
	CALL 体力変動素質適用処理(LOCAL)

	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	;部屋の無いキャラがいないかチェック
	IF キャラクター部屋検索(LOCAL) == -1 && CFLAG:LOCAL:スイートルーム滞在フラグ == 0
		IF TALENT:LOCAL:従業員
			FOR LOCAL:1, 0, 従業員キャラ上限
				IF 従業員部屋割り配列:(LOCAL:1) < 1
					従業員部屋割り配列:(LOCAL:1) = LOCAL
					CFLAG:LOCAL:現在マップ種別 = 999
					CFLAG:LOCAL:現在位置 = LOCAL:1 + 1000
					RETURN 1
				ENDIF
			NEXT
		ELSE
			CALL 滞在者部屋割り配列挿入処理(LOCAL)
		ENDIF
	ENDIF

	;複数の部屋に入っちゃってないかチェック
	LOCAL:2 = 0
	IF TALENT:LOCAL:従業員
		FOR LOCAL:1, 0, 従業員キャラ上限
			IF 従業員部屋割り配列:(LOCAL:1) == LOCAL
				IF LOCAL:2
					従業員部屋割り配列:(LOCAL:1) = 0
				ELSE
					LOCAL:2 = 1
				ENDIF
			ENDIF
		NEXT
	ELSE
		FOR LOCAL:1, 0, 100
			IF 滞在者部屋割り配列:(LOCAL:1) == LOCAL
				IF LOCAL:2
					滞在者部屋割り配列:(LOCAL:1) = 0
				ELSE
					LOCAL:2 = 1
				ENDIF
			ENDIF
		NEXT
	ENDIF

	;弱点コマンドの設定がされているかどうかチェック
	IF EXISTFUNCTION(@"弱点コマンド変更_{NO:LOCAL}")
		VARSET 弱点コマンド枠:LOCAL:0
		TRYCALLFORM 弱点コマンド変更_{NO:LOCAL}
	ENDIF

	;画像フォルダが有効かチェック
	CALL 画像フォルダ有効チェック(LOCAL)
NEXT

CALL イベント追加チェック
CALL イベント削除チェック
IF TFLAG:ゲームフェイズ管理 == 0
	CALL イベント発生条件チェック
ENDIF

;新しく素材アイテムが増えてないかチェック
CALL 素材アイテム登録処理

;念のため変数リセット
CALL TURN_RESET
CALL 最大レベル算出保存
