;────────────────────────────────────
;絶頂系の処理など
;────────────────────────────────────
;-------------------------------------------------
;絶頂処理
;-------------------------------------------------
@ORGASM_ADD(ARG)
#DIM 絶頂残値
#DIM 強度一時保存
#DIM ソート用配列, 5

C = 0
V = 0
A = 0
B = 0
S = 0

絶頂残値 = 絶頂残値算出処理(ARG)


;絶頂Ｃ
NOWEX:ARG:Ｃ絶頂 = 0
DO
	IF CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ - CDOWN:ARG:快Ｃ >= PALAMLV:4
		強度一時保存 = (CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ - CDOWN:ARG:快Ｃ) / PALAMLV:4
		NOWEX:ARG:Ｃ絶頂 += 強度一時保存
		C = NOWEX:ARG:Ｃ絶頂
		CDOWN:ARG:快Ｃ += 強度一時保存 * PALAMLV:4 - 絶頂残値
	ENDIF
LOOP CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ - CDOWN:ARG:快Ｃ >= PALAMLV:4
;絶頂Ｖ
NOWEX:ARG:Ｖ絶頂 = 0
DO
	IF CUP:ARG:快Ｖ + PALAM:ARG:快Ｖ - CDOWN:ARG:快Ｖ >= PALAMLV:4
		強度一時保存 = (CUP:ARG:快Ｖ + PALAM:ARG:快Ｖ - CDOWN:ARG:快Ｖ) / PALAMLV:4
		NOWEX:ARG:Ｖ絶頂 += 強度一時保存
		V = NOWEX:ARG:Ｖ絶頂
		CDOWN:ARG:快Ｖ += 強度一時保存 * PALAMLV:4 - 絶頂残値
	ENDIF
LOOP CUP:ARG:快Ｖ + PALAM:ARG:快Ｖ - CDOWN:ARG:快Ｖ >= PALAMLV:4
;絶頂Ａ
NOWEX:ARG:Ａ絶頂 = 0
DO
	IF CUP:ARG:快Ａ + PALAM:ARG:快Ａ - CDOWN:ARG:快Ａ >= PALAMLV:4
		強度一時保存 = (CUP:ARG:快Ａ + PALAM:ARG:快Ａ - CDOWN:ARG:快Ａ) / PALAMLV:4
		NOWEX:ARG:Ａ絶頂 += 強度一時保存
		A = NOWEX:ARG:Ａ絶頂
		CDOWN:ARG:快Ａ += 強度一時保存 * PALAMLV:4 - 絶頂残値
	ENDIF
LOOP CUP:ARG:快Ａ + PALAM:ARG:快Ａ - CDOWN:ARG:快Ａ >= PALAMLV:4
;絶頂Ｂ
NOWEX:ARG:Ｂ絶頂 = 0
DO
	IF CUP:ARG:快Ｂ + PALAM:ARG:快Ｂ - CDOWN:ARG:快Ｂ >= PALAMLV:4
		強度一時保存 = (CUP:ARG:快Ｂ + PALAM:ARG:快Ｂ - CDOWN:ARG:快Ｂ) / PALAMLV:4
		NOWEX:ARG:Ｂ絶頂 += 強度一時保存
		B = NOWEX:ARG:Ｂ絶頂
		CDOWN:ARG:快Ｂ += 強度一時保存 * PALAMLV:4 - 絶頂残値
	ENDIF
LOOP CUP:ARG:快Ｂ + PALAM:ARG:快Ｂ - CDOWN:ARG:快Ｂ >= PALAMLV:4
;絶頂Ｓ
NOWEX:ARG:Ｓ絶頂 = 0
DO
	IF CUP:ARG:快Ｓ + PALAM:ARG:快Ｓ - CDOWN:ARG:快Ｓ >= PALAMLV:4
		強度一時保存 = (CUP:ARG:快Ｓ + PALAM:ARG:快Ｓ - CDOWN:ARG:快Ｓ) / PALAMLV:4
		NOWEX:ARG:Ｓ絶頂 += 強度一時保存
		S = NOWEX:ARG:Ｓ絶頂
		CDOWN:ARG:快Ｓ += 強度一時保存 * PALAMLV:4 - 絶頂残値
	ENDIF
LOOP CUP:ARG:快Ｓ + PALAM:ARG:快Ｓ - CDOWN:ARG:快Ｓ >= PALAMLV:4


; ;絶頂Ｃ
; IF CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ >= PALAMLV:4 * 5
; 	C = 5
; 	CDOWN:ARG:快Ｃ = (PALAMLV:4 * 5 - 絶頂残値)
; 	NOWEX:ARG:Ｃ絶頂 = 5
; 	;PRINTFORML 究極絶頂Ｃ\@ARG == PLAYER?(調教者)#\@
; ELSEIF CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ >= PALAMLV:4 * 4
; 	C = 4
; 	CDOWN:ARG:快Ｃ = (PALAMLV:4 * 4 - 絶頂残値)
; 	NOWEX:ARG:Ｃ絶頂 = 4
; 	;PRINTFORML 最強絶頂Ｃ\@ARG == PLAYER?(調教者)#\@
; ELSEIF CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ >= PALAMLV:4 * 3
; 	C = 3
; 	CDOWN:ARG:快Ｃ = (PALAMLV:4 * 3 - 絶頂残値)
; 	NOWEX:ARG:Ｃ絶頂 = 3
; 	;PRINTFORML 超強絶頂Ｃ\@ARG == PLAYER?(調教者)#\@
; ELSEIF CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ >= PALAMLV:4 * 2
; 	C = 2
; 	CDOWN:ARG:快Ｃ = (PALAMLV:4 * 2 - 絶頂残値)
; 	NOWEX:ARG:Ｃ絶頂 = 2
; 	;PRINTFORML 強絶頂Ｃ\@ARG == PLAYER?(調教者)#\@
; ELSEIF CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ >= PALAMLV:4
; 	C = 1
; 	CDOWN:ARG:快Ｃ = (PALAMLV:4 - 絶頂残値)
; 	NOWEX:ARG:Ｃ絶頂 = 1
; 	;PRINTFORML 絶頂Ｃ\@ARG == PLAYER?(調教者)#\@
; ENDIF
; ;CDOWN:ARG:0で下げても絶頂以上なら
; ;その値-1になるように調整（10000で絶頂なら9999）
; SIF CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ - CDOWN:ARG:快Ｃ >= PALAMLV:4
; 	CDOWN:ARG:快Ｃ = CUP:ARG:快Ｃ + PALAM:ARG:快Ｃ - PALAMLV:4 + 1

;-------------------------------------------------
;絶頂時の追加処理
;-------------------------------------------------
RCVAR:ARG:絶頂の強度 = C + V + A + B + S
IF ARG == PLAYER
	TFLAG:調教者絶頂強度 = C + V + A + B + S
ENDIF
IF ARG == TARGET
	RCVAR:ARG:絶頂の強度 = C + V + A + B + S
	;快楽刻印の処理
	IF RCVAR:ARG:絶頂の強度 > 3 && MARK:ARG:1 < 3
		RCVAR:ARG:快楽強度 = 3
	;Rが3以上で快楽刻印２に相当
	ELSEIF RCVAR:ARG:絶頂の強度 > 2 && MARK:ARG:1 < 2
		RCVAR:ARG:快楽強度 = 2
	;Rが2以上で快楽刻印１に相当
	ELSEIF RCVAR:ARG:絶頂の強度 > 1 && MARK:ARG:1 < 1
		RCVAR:ARG:快楽強度 = 1
	ELSEIF RCVAR:ARG:絶頂の強度 >= 1
		RCVAR:ARG:快楽強度 = 0
	ENDIF
ENDIF

;オナニーの場合はここを通らないので、通った時点で＝あなたによる絶頂とする
;例外はジータ-ルリアの感覚共有だけなのでそこだけ弾く
IF ARG != MASTER && PLAYER == MASTER && RCVAR:ARG:絶頂の強度 > 0
	IF !CFLAG:ARG:快楽計算フラグ || CFLAG:ARG:うふふ
		IF 初体験日:ARG:あなたの手で初絶頂 == 0
			初体験日:ARG:あなたの手で初絶頂 = DAY
			CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>初めて%CALLNAME:ARG%を絶頂させた</font>","うふふ")
			CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>初めて%CALLNAME:MASTER%の手で絶頂した</font>","うふふ")
		ENDIF
	ENDIF
ENDIF

;同時絶頂による補正
L = 0
SELECTCASE MIN(C, V, A, B, S)
	CASE IS >= 5
		L = 5
		C *= 60
		V *= 60
		A *= 60
		B *= 60
		S *= 60
	CASE 4
		L = 5
		C *= 48
		V *= 48
		A *= 48
		B *= 48
		S *= 48
	CASE 3
		L = 5
		C *= 36
		V *= 36
		A *= 36
		B *= 36
		S *= 36
	CASE 2
		L = 5
		C *= 24
		V *= 24
		A *= 24
		B *= 24
		S *= 24
	CASE 1
		L = 5
		C *= 12
		V *= 12
		A *= 12
		B *= 12
		S *= 12
	CASEELSE
		;絶頂状態部位がいくつあるかを検索
		SELECTCASE GROUPMATCH(0, C, V, A, B, S)
			CASE 1
				L = 4
				LOCAL = 8
			CASE 2
				L = 3
				LOCAL = 4
			CASE 3
				L = 2
				LOCAL = 2
			CASE 4
				L = 1
				LOCAL = 1
			ENDSELECT
		C *= LOCAL
		V *= LOCAL
		A *= LOCAL
		B *= LOCAL
		S *= LOCAL
ENDSELECT

IF C
	SOURCE:ARG:露出 += 500 * C
	SOURCE:ARG:屈従 += 200 * C
	CUP:ARG:潤滑 += 1000 * C
ENDIF
IF V
	SOURCE:ARG:欲情 += 800 * V
	SOURCE:ARG:恭順 += 500 * V
	SOURCE:ARG:露出 += 700 * V
	SOURCE:ARG:屈従 += 400 * V
	CUP:ARG:潤滑 += 1500 * V
ENDIF
IF A
	SOURCE:ARG:欲情 += 500 * A
	SOURCE:ARG:恭順 += 700 * A
	SOURCE:ARG:露出 += 900 * A
	SOURCE:ARG:屈従 += 500 * A
	SOURCE:ARG:逸脱 += 200 * A
	CUP:ARG:潤滑 += 1500 * A
ENDIF
IF B
	SOURCE:ARG:露出 += 500 * B
	SOURCE:ARG:屈従 += 200 * B
	CUP:ARG:潤滑 += 800 * B
	SIF TALENT:ARG:性別  == 10
		SOURCE:ARG:逸脱 += 100 * B
ENDIF
IF S
	SOURCE:ARG:露出 += 500 * S
	SOURCE:ARG:屈従 += 200 * S
	SOURCE:ARG:逸脱 += 100 * S
	CUP:ARG:潤滑 += 500 * S
ENDIF

NOWEX:ARG:多重絶頂 = L

;スコアを増やす
ソート用配列 = NOWEX:ARG:Ｃ絶頂, NOWEX:ARG:Ｖ絶頂, NOWEX:ARG:Ａ絶頂, NOWEX:ARG:Ｂ絶頂, NOWEX:ARG:Ｓ絶頂
ARRAYSORT ソート用配列, BACK
NOWEX:ARG:スコア += SUMARRAY(ソート用配列, 0, 4) * POWER(2, MIN(3, NOWEX:ARG:多重絶頂 - 1)) + ソート用配列:4

;絶頂回数を増やす
EX:ARG:Ｃ絶頂 += NOWEX:ARG:Ｃ絶頂
EX:ARG:Ｖ絶頂 += NOWEX:ARG:Ｖ絶頂
EX:ARG:Ａ絶頂 += NOWEX:ARG:Ａ絶頂
EX:ARG:Ｂ絶頂 += NOWEX:ARG:Ｂ絶頂
EX:ARG:Ｓ絶頂 += NOWEX:ARG:Ｓ絶頂
EX:ARG:多重絶頂 += L

;絶頂経験を増やす
EXP:ARG:絶頂経験 += RCVAR:ARG:絶頂の強度
EXP:ARG:Ｃ絶頂経験 += NOWEX:ARG:Ｃ絶頂
EXP:ARG:Ｖ絶頂経験 += NOWEX:ARG:Ｖ絶頂
EXP:ARG:Ａ絶頂経験 += NOWEX:ARG:Ａ絶頂
EXP:ARG:Ｂ絶頂経験 += NOWEX:ARG:Ｂ絶頂
EXP:ARG:Ｓ絶頂経験 += NOWEX:ARG:Ｓ絶頂
SIF RCVAR:ARG:露出コマンドフラグ
	EXP:ARG:露出絶頂経験 += RCVAR:ARG:絶頂の強度
SIF RCVAR:ARG:苦痛コマンドフラグ && CUP:ARG:苦痛 >= MAX(1, 100 - (ABL:ARG:マゾ性感 * 20))
	EXP:ARG:苦痛絶頂経験 += RCVAR:ARG:絶頂の強度
SIF RCVAR:ARG:嗜虐コマンドフラグ
	EXP:ARG:嗜虐絶頂経験 += RCVAR:ARG:絶頂の強度
SIF RCVAR:ARG:奉仕コマンドフラグ
	EXP:ARG:奉仕絶頂経験 += RCVAR:ARG:絶頂の強度
SIF RCVAR:ARG:道具コマンドフラグ
	EXP:ARG:道具絶頂経験 += RCVAR:ARG:絶頂の強度
SIF RCVAR:ARG:触手コマンドフラグ
	EXP:ARG:触手絶頂経験 += RCVAR:ARG:絶頂の強度
IF MODE_存在判定_ターゲット側("Ｃ自慰系", ARG) && NOWEX:ARG:Ｃ絶頂
	EXP:ARG:自慰経験 += 1
	EXP:ARG:調教自慰経験 += 1
ENDIF
IF MODE_存在判定_ターゲット側("Ｖ自慰系", ARG) && NOWEX:ARG:Ｖ絶頂
	EXP:ARG:自慰経験 += 1
	EXP:ARG:調教自慰経験 += 1
ENDIF
IF MODE_存在判定_ターゲット側("Ａ自慰系", ARG) && NOWEX:ARG:Ａ絶頂
	EXP:ARG:自慰経験 += 1
	EXP:ARG:調教自慰経験 += 1
ENDIF
IF MODE_存在判定_ターゲット側("Ｂ自慰系", ARG) && NOWEX:ARG:Ｂ絶頂
	EXP:ARG:自慰経験 += 1
	EXP:ARG:調教自慰経験 += 1
ENDIF

;性知識経験値を増やす
;性知識マイナスは上昇しない。無知シチュ
SIF 知識素質:ARG:性知識 > -1
	性知識経験値:ARG:0 += RCVAR:ARG:絶頂の強度

;絶頂による欲望ＬＶアップ
IF ARG == TARGET && ARG
	;自制心があると１下がる
	SIF TALENT:ARG:自制心
		L -= 1
	IF ABL:ARG:欲望 < L
		ABL:ARG:欲望 = L
		PRINT そして欲望がLV
		PRINTV L
		PRINTL になった
	ENDIF
ENDIF

;-------------------------------------------------
;絶頂時の追加処理
;-------------------------------------------------

;射精
IF TALENT:ARG:性別 & 2 && ARG == PLAYER
	CALL 射精処理_コマンド(ARG, TFLAG:調教者絶頂強度)
ELSEIF TALENT:ARG:性別 & 2
	CALL 射精処理_コマンド(ARG, RCVAR:ARG:絶頂の強度, RCVAR:ARG:前立腺開発フラグ)
ENDIF

;ZPの取得
IF RCVAR:ARG:絶頂の強度 > 0
	CALL 絶頂体力減少処理(ARG, NOWEX:ARG:Ｃ絶頂, NOWEX:ARG:Ｖ絶頂, NOWEX:ARG:Ａ絶頂, NOWEX:ARG:Ｂ絶頂, NOWEX:ARG:Ｓ絶頂)
	CALL ZP_取得処理(ARG, RCVAR:ARG:絶頂の強度)
	CFLAG:ARG:体力成長値 += RCVAR:ARG:絶頂の強度 * 5
ENDIF

;連れ込み状態時、満足したか
;ついでに中毒欲求不満の判定も行う
CALL 中毒連れ込み判定_逆アナル(ARG)
IF RESULT
	IF NOWEX:ARG:Ｃ絶頂 && MODE_存在判定_完全一致("Ａ挿入系", ARG, MASTER)
		CFLAG:ARG:中毒欲求不満フラグ = -1
		SIF 連れ込みパターン == "逆アナル"
			RCVAR:ARG:連れ込み満足フラグ = 1
		NOWEX:ARG:スコア += NOWEX:ARG:多重絶頂
	ENDIF
ENDIF
CALL 中毒連れ込み判定_搾乳プレイ(ARG)
IF RESULT
	IF NOWEX:ARG:Ｂ絶頂 && MODE_存在判定_ターゲット側("搾乳機", ARG)
		CFLAG:ARG:中毒欲求不満フラグ = -1
		SIF 連れ込みパターン == "搾乳プレイ"
			RCVAR:ARG:連れ込み満足フラグ = 1
		NOWEX:ARG:スコア += NOWEX:ARG:多重絶頂
	ENDIF
ENDIF
CALL 中毒連れ込み判定_触手プレイ(ARG)
IF RESULT
	IF RCVAR:ARG:絶頂の強度 && MODE_存在判定_ターゲット側("触手系", ARG)
		CFLAG:ARG:中毒欲求不満フラグ = -1
		SIF 連れ込みパターン == "触手プレイ"
			RCVAR:ARG:連れ込み満足フラグ = 1
		NOWEX:ARG:スコア += NOWEX:ARG:多重絶頂
	ENDIF
ENDIF

IF 連れ込みパターン == "一般" || 連れ込みパターン == "発情期"
	SIF RCVAR:ARG:絶頂の強度 + TCVAR:ARG:うふふ中Ｃ絶頂累積 + TCVAR:ARG:うふふ中Ｂ絶頂累積 + TCVAR:ARG:うふふ中Ｖ絶頂累積 + TCVAR:ARG:うふふ中Ａ絶頂累積 + TCVAR:ARG:うふふ中Ｓ絶頂累積 > 2
		RCVAR:ARG:連れ込み満足フラグ = 1
ENDIF

RETURN 1


@MESSAGE_PALAMCNG_絶頂強度表示(対象キャラ)
#DIM 対象キャラ
IF NOWEX:対象キャラ:多重絶頂 > 3
	DRAWLINE
	PRINTFORML 昇　天(%CALLNAME:対象キャラ%)
ELSEIF NOWEX:対象キャラ:多重絶頂 > 2
	DRAWLINE
	PRINTFORML 超強絶頂(%CALLNAME:対象キャラ%)
ELSEIF NOWEX:対象キャラ:多重絶頂 > 1
	DRAWLINE
	PRINTFORML 強絶頂(%CALLNAME:対象キャラ%)
ELSEIF NOWEX:対象キャラ:多重絶頂 >= 1
	DRAWLINE
	PRINTFORML 絶頂(%CALLNAME:対象キャラ%)
ENDIF

SIF 対象キャラ == MASTER
	RETURN 0

;絶頂による欲望ＬＶアップ
LOCAL = NOWEX:対象キャラ:多重絶頂
;自制心があると１下がる
SIF TALENT:対象キャラ:自制心
	LOCAL -= 1

IF ABL:対象キャラ:欲望 < LOCAL
	ABL:対象キャラ:欲望 ++
	PRINTFORML そして%CALLNAME:対象キャラ%の欲望がLV{LOCAL}になった
ENDIF
;-------------------------------------------------
;調教対象の噴乳チェック
;-------------------------------------------------
@TARGET_MILK_CHECK(ARG)
SIF !TALENT:ARG:母乳体質
	RETURN 0

LOCAL = CUP:ARG:快Ｃ / 5 + CUP:ARG:快Ｖ / 5 + CUP:ARG:快Ａ / 5 + CUP:ARG:快Ｂ * 3

;自制心
SIF TALENT:ARG:自制心
	LOCAL /= 2

;快感に素直
SIF TALENT:ARG:快感応答 > 0
	TIMES LOCAL , 1.20

;快感の否定
SIF TALENT:ARG:快感応答 < 0 && !TALENT:ARG:淫乱
	TIMES LOCAL , 0.80

;B敏感
SIF TALENT:ARG:Ｂ感度 > 0
	TIMES LOCAL , 1.20

;媚薬
SIF TEQUIP:ARG:媚薬
	LOCAL *= 2

;利尿剤
SIF TEQUIP:ARG:利尿剤
	LOCAL /= 2

;調教者が幼児退行
SIF TALENT:PLAYER:幼児／幼児退行
	TIMES LOCAL , 1.20

;調教者が幼稚
SIF TALENT:PLAYER:幼稚
	TIMES LOCAL , 1.20

;貧乳
SIF TALENT:ARG:バストサイズ < 0
	TIMES LOCAL , 0.50
LOCAL = TIME_PROGRESS(TFLAG:行動前時刻) * 2 + LOCAL / 2
BASE:ARG:母乳 += LOCAL
SIF !CFLAG:ARG:うふふ
	BASE:ARG:母乳 = MIN(BASE:ARG:母乳, MAXBASE:ARG:母乳 - 1)

IF  BASE:ARG:母乳 > MAXBASE:ARG:母乳 * 2
	LOCAL = 1
ELSEIF BASE:ARG:母乳 > MAXBASE:ARG:母乳
	LOCAL = 0
ELSE
	RETURN 0
ENDIF
LOCAL ++
IF EXP:ARG:噴乳経験 < EXPLV:1
	SOURCE:ARG:露出 += 5000 * LOCAL
	SOURCE:ARG:屈従 += 2500 * LOCAL
	SOURCE:ARG:不潔 += 1000 * LOCAL
ELSEIF EXP:ARG:噴乳経験 < EXPLV:2
	SOURCE:ARG:露出 += 2500 * LOCAL
	SOURCE:ARG:屈従 += 1500 * LOCAL
	SOURCE:ARG:不潔 += 750 * LOCAL
ELSEIF EXP:ARG:噴乳経験 < EXPLV:3
	SOURCE:ARG:露出 += 1500 * LOCAL
	SOURCE:ARG:屈従 += 750 * LOCAL
	SOURCE:ARG:不潔 += 500 * LOCAL
ELSEIF EXP:ARG:噴乳経験 < EXPLV:4
	SOURCE:ARG:露出 += 750 * LOCAL
	SOURCE:ARG:屈従 += 400 * LOCAL
	SOURCE:ARG:不潔 += 350 * LOCAL
ELSEIF EXP:ARG:噴乳経験 < EXPLV:5
	SOURCE:ARG:露出 += 500 * LOCAL
	SOURCE:ARG:屈従 += 250 * LOCAL
	SOURCE:ARG:不潔 += 200 * LOCAL
ELSE
	SOURCE:ARG:露出 += 250 * LOCAL
	SOURCE:ARG:屈従 += 150 * LOCAL
ENDIF
	PRINTFORML %CALLNAME:ARG%は乳首から\@ LOCAL == 2 ? 大量の # \@母乳を噴出した
EXP:ARG:噴乳経験 += LOCAL
;Ｂに母乳汚れ
SIF BASE:ARG:母乳 >= MAXBASE:ARG:母乳
	BASE:ARG:母乳 = MAXBASE:ARG:母乳 - 1
NOWEX:ARG:噴乳 += LOCAL
EX:ARG:噴乳 += LOCAL
NOWEX:ARG:スコア += LOCAL

;ビデオ
IF TEQUIP:ARG:ビデオ撮影
	IF NOWEX:ARG:噴乳 == 1
		LOCALS = MILK/
	ELSEIF NOWEX:ARG:噴乳 == 2
		LOCALS = MILK/MILK/
	ENDIF
	TSTR:1 += LOCALS
ENDIF


@絶頂体力減少処理(キャラ番号, Ｃ絶頂強度, Ｖ絶頂強度, Ａ絶頂強度, Ｂ絶頂強度, Ｓ絶頂強度)
#DIM キャラ番号
#DIM Ｃ絶頂強度
#DIM Ｖ絶頂強度
#DIM Ａ絶頂強度
#DIM Ｂ絶頂強度
#DIM Ｓ絶頂強度
#DIM 絶頂保存配列, 5
#DIM 部位番号
#DIM 体力減少量計算値
#DIM 追加体力減少量計算値
#DIM 最大体力減少量
#DIM 最大絶頂部位

絶頂保存配列:0 = Ｃ絶頂強度
絶頂保存配列:1 = Ｖ絶頂強度
絶頂保存配列:2 = Ａ絶頂強度
絶頂保存配列:3 = Ｂ絶頂強度
絶頂保存配列:4 = Ｓ絶頂強度

追加体力減少量計算値 = 0
FOR 部位番号, 0, 5
	IF 絶頂保存配列:部位番号 > 0
		SELECTCASE 部位番号
			CASE 0
				IF GETBIT(TALENT:キャラ番号:性別, 1)
					;男性　基礎150
					体力減少量計算値 = 150 - EXP:キャラ番号:Ｃ絶頂経験
				ELSE
					;女性　基礎80
					体力減少量計算値 = 80 - EXP:キャラ番号:Ｃ絶頂経験
				ENDIF
			CASE 1
				;基礎150
				体力減少量計算値 = 150 - EXP:キャラ番号:Ｖ絶頂経験
				IF RCVAR:キャラ番号:Ｇスポ開発フラグ
					追加体力減少量計算値 += MAX(0, 150 - EXP:キャラ番号:Ｇスポ開発 / 2)
				ENDIF
				IF RCVAR:キャラ番号:ポルチオ開発フラグ
					追加体力減少量計算値 += MAX(0, 150 - EXP:キャラ番号:ポルチオ開発 / 2)
				ENDIF
			CASE 2
				IF GETBIT(TALENT:キャラ番号:性別, 1)
					;男性　基礎150
					体力減少量計算値 = 150 - EXP:キャラ番号:Ａ絶頂経験
				ELSE
					;女性　基礎200
					体力減少量計算値 = 200 - EXP:キャラ番号:Ａ絶頂経験
				ENDIF
				IF RCVAR:キャラ番号:前立腺開発フラグ
					追加体力減少量計算値 += MAX(0, 150 - EXP:キャラ番号:前立腺開発 / 2)
				ENDIF
				IF RCVAR:キャラ番号:Ｓ字結腸開発フラグ
					追加体力減少量計算値 += MAX(0, 200 - EXP:キャラ番号:Ｓ字結腸開発 / 2)
				ENDIF
			CASE 3
				IF !GETBIT(TALENT:キャラ番号:性別, 0)
					;男性　基礎250
					体力減少量計算値 = 250 - EXP:キャラ番号:Ｂ絶頂経験
				ELSE
					;女性　基礎100
					体力減少量計算値 = 100 - EXP:キャラ番号:Ｂ絶頂経験
				ENDIF
				IF RCVAR:キャラ番号:乳首開発フラグ
					追加体力減少量計算値 += MAX(0, 100 - EXP:キャラ番号:乳首開発 / 2)
				ENDIF
			CASE 4
				;基礎250
				体力減少量計算値 = 250 - EXP:キャラ番号:Ｓ絶頂経験
				IF RCVAR:キャラ番号:角開発フラグ
					追加体力減少量計算値 += MAX(0, 200 - EXP:キャラ番号:角開発 / 2)
				ENDIF
				IF RCVAR:キャラ番号:尻尾開発フラグ
					追加体力減少量計算値 += MAX(0, 200 - EXP:キャラ番号:尻尾開発 / 2)
				ENDIF
		ENDSELECT
		;経験によって減らせるのは50まで
		体力減少量計算値 = MAX(50, 体力減少量計算値)
		体力減少量計算値 += 追加体力減少量計算値

		;素質による上下
		体力減少量計算値 -= TALENT:キャラ番号:性欲 * 5
		体力減少量計算値 -= TALENT:キャラ番号:快感応答 * 5

		;発情期
		SIF CFLAG:キャラ番号:発情期フラグ < 0
			体力減少量計算値 -= 20

		;最低値は20
		体力減少量計算値 = MAX(20, 体力減少量計算値)

		IF 絶頂保存配列:部位番号 > 1
			;強い絶頂の分は1/4して計算
			体力減少量計算値 = 体力減少量計算値 + (体力減少量計算値 * (絶頂保存配列:部位番号 - 1) / 4)
		ENDIF

		;最後に配列に計算値を保存
		絶頂保存配列:部位番号 = 体力減少量計算値
	ENDIF
	IF 部位番号 == 0 || 絶頂保存配列:部位番号 > 最大体力減少量
		最大体力減少量 = 絶頂保存配列:部位番号
		最大絶頂部位 = 部位番号
	ENDIF
NEXT

IF MATCH(絶頂保存配列, 0) < 4
	;他部位絶頂パターン
	;最も大きい絶頂をそのままにし、他の絶頂計算値を1/2にして合算する
	FOR 部位番号, 0, 5
		SIF 最大絶頂部位 != 部位番号
			絶頂保存配列:部位番号 = 絶頂保存配列:部位番号 / 2
	NEXT
ENDIF
体力減少量計算値 = SUMARRAY(絶頂保存配列)

DOWNBASE:キャラ番号:体力 += 体力減少量計算値
CALL SOURCE_DOWNBASE(キャラ番号)
TCVAR:キャラ番号:うふふ中消費体力累積 += 体力減少量計算値
CALL 満足ゲージ上昇処理(キャラ番号)

@絶頂相手算出(ARG)
#FUNCTION
#DIM 絶頂部位
#DIM 最大絶頂強度
;あなた以外も色々動くようになったので、状況に合わせて口上用に絶頂相手を決定する

絶頂部位 = -1
最大絶頂強度 = 0
FOR LOCAL, 0, 5
	IF NOWEX:ARG:LOCAL > 最大絶頂強度
		絶頂部位 = LOCAL
		最大絶頂強度 = NOWEX:ARG:LOCAL
	ENDIF
NEXT

IF MODE_存在判定_ターゲット側("Ｖ挿入系", ARG) && 絶頂部位 == 部位_Ｖ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "実行キャラ", 1)
ENDIF
IF MODE_存在判定_ターゲット側("ハーヴィンオナホＶ", ARG) && 絶頂部位 == 部位_Ｖ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "実行キャラ", 1)
ENDIF
IF MODE_存在判定_ターゲット側("挿入歩きＶ", ARG) && 絶頂部位 == 部位_Ｖ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "実行キャラ", 1)
ENDIF
IF MODE_存在判定_プレイヤー側("ハーヴィンディルドＶ", ARG) && 絶頂部位 == 部位_Ｖ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "対象キャラ", 1)
ENDIF

IF MODE_存在判定_ターゲット側("Ａ挿入系", ARG) && 絶頂部位 == 部位_Ａ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "実行キャラ", 1)
ENDIF
IF MODE_存在判定_ターゲット側("ハーヴィンオナホＡ", ARG) && 絶頂部位 == 部位_Ａ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "実行キャラ", 1)
ENDIF
IF MODE_存在判定_ターゲット側("挿入歩きＡ", ARG) && 絶頂部位 == 部位_Ａ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "実行キャラ", 1)
ENDIF
IF MODE_存在判定_プレイヤー側("ハーヴィンディルドＡ", ARG) && 絶頂部位 == 部位_Ａ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "対象キャラ", 1)
ENDIF

IF MODE_存在判定_ターゲット側("触手挿入", ARG) && 絶頂部位 == 部位_Ｖ
	RETURNF MASTER
ENDIF
IF MODE_存在判定_ターゲット側("触手アナル挿入", ARG) && 絶頂部位 == 部位_Ａ
	RETURNF MASTER
ENDIF

IF MODE_存在判定_プレイヤー側("Ｖ挿入系", ARG) && 絶頂部位 == 部位_Ｃ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "対象キャラ", 1)
ELSEIF MODE_存在判定_プレイヤー側("Ａ挿入系", ARG) && 絶頂部位 == 部位_Ｃ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "対象キャラ", 1)
ELSEIF MODE_存在判定_プレイヤー側("ハーヴィンオナホＶ", ARG) && 絶頂部位 == 部位_Ｃ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "対象キャラ", 1)
ELSEIF MODE_存在判定_プレイヤー側("挿入歩きＶ", ARG) && 絶頂部位 == 部位_Ｃ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "対象キャラ", 1)
ELSEIF MODE_存在判定_プレイヤー側("ハーヴィンオナホＡ", ARG) && 絶頂部位 == 部位_Ｃ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "対象キャラ", 1)
ELSEIF MODE_存在判定_プレイヤー側("挿入歩きＡ", ARG) && 絶頂部位 == 部位_Ｃ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "対象キャラ", 1)
ELSEIF MODE_存在判定_ターゲット側("ハーヴィンディルドＶ", ARG) && 絶頂部位 == 部位_Ｃ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "実行キャラ", 1)
ELSEIF MODE_存在判定_ターゲット側("ハーヴィンディルドＡ", ARG) && 絶頂部位 == 部位_Ｃ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "実行キャラ", 1)
ENDIF

IF MODE_存在判定_ターゲット側("触手クリ責め", ARG) && 絶頂部位 == 部位_Ｃ
	RETURNF MASTER
ENDIF
IF MODE_存在判定_ターゲット側("触手ペニス責め", ARG) && 絶頂部位 == 部位_Ｃ
	RETURNF MASTER
ENDIF
IF MODE_存在判定_ターゲット側("パイズリ系", ARG) && 絶頂部位 == 部位_Ｃ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "実行キャラ", 1)
ENDIF
IF MODE_存在判定_ターゲット側("手コキ系", ARG) && 絶頂部位 == 部位_Ｃ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "実行キャラ", 1)
ENDIF
IF MODE_存在判定_ターゲット側("フェラチオ系", ARG) && 絶頂部位 == 部位_Ｃ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "実行キャラ", 1)
ENDIF
IF MODE_存在判定_ターゲット側("シックスナイン", ARG) && 絶頂部位 == 部位_Ｃ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "実行キャラ", 1)
ELSEIF MODE_存在判定_プレイヤー側("シックスナイン", ARG) && 絶頂部位 == 部位_Ｃ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "対象キャラ", 1)
ENDIF
IF MODE_存在判定_ターゲット側("ボテ腹ズリ", ARG) && 絶頂部位 == 部位_Ｃ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "実行キャラ", 1)
ENDIF

IF MODE_存在判定_ターゲット側("触手乳首責め", ARG) && 絶頂部位 == 部位_Ｂ
	RETURNF MASTER
ENDIF
IF MODE_存在判定_ターゲット側("乳首吸い系", ARG) && 絶頂部位 == 部位_Ｂ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "実行キャラ", 1)
ENDIF

IF MODE_存在判定_ターゲット側("触手口辱", ARG) && 絶頂部位 == 部位_Ｓ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "実行キャラ", 1)
ENDIF
IF MODE_存在判定_ターゲット側("キス系", ARG) && 絶頂部位 == 部位_Ｓ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "実行キャラ", 1)
ELSEIF MODE_存在判定_プレイヤー側("キス系", ARG) && 絶頂部位 == 部位_Ｓ
	RETURNF DT_CELL_GET("体位モードデータベース", モードID受渡し, "対象キャラ", 1)
ENDIF

IF MODE_存在判定_ターゲット側("アイテム系", ARG)
	RETURNF MASTER
ENDIF

IF FLAG:モード管理 == 1
	RETURNF PLAYER
ENDIF

RETURNF MASTER


@絶頂残値算出処理(対象キャラ)
#FUNCTION
#DIM 対象キャラ
#DIM 絶頂残値

;基礎は1000
絶頂残値 = 1000

;イキやすさ素質で分岐
SELECTCASE TALENT:対象キャラ:イキやすさ
	CASE -1
		絶頂残値 -= 3000
	CASE 1
		絶頂残値 += 2000
	CASE 2
		絶頂残値 += 5000
ENDSELECT

;発情期は+2000
SIF CFLAG:対象キャラ:発情期フラグ < 0
	絶頂残値 += 2000

RETURNF 絶頂残値
