{
    ;
配置レイヤー
1 SUB
2 MAIN
3 EFFECT
4 EFFECT2
5 TEXT
6 TEXT2 CHECKBOX
7 - 11 CLICKICON
}
@おさわりモード(キャラ番号, おさわりスタンス)
#DIM キャラ番号
#DIM レイヤー番号 = 50
#DIMS おさわりフォルダ = "おさわりフォルダ"
#DIMS 対象フォルダ
#DIMS 対象ファイルアーカイブ,100,100,100 ;カテゴリ分けしてファイル名を預かる
#DIM 対象ファイル数,100,100
#DIMS 汎用ファイルアーカイブ,100,100,100 ;おさわりファイルなしファイルを預かる
#DIM 汎用ファイル数,100,100
#DIM CONST 衣装種類数 = 15
#DIM CONST 差分種類数 = 3
{
    #DIMS CONST 衣装種類 =
    "短冊_デフォルト", "短冊_水着", "短冊_湯浴み着", "短冊_全裸", "短冊_浴衣",
    "短冊_部屋着", "短冊_おしゃれ着", "短冊_バニー服", "短冊_メイド服", "短冊_マイクロビキニ",
    "短冊_逆バニー", "短冊_チャイナドレス", "短冊_ミニチャイナ", "短冊_セクシーランジェリー", "短冊_ミニスカサンタ", 
}
{
    #DIMS CONST 差分種類 = 
    "表情", "欲情", "絶頂"
}
#DIM 現在衣装種類ID ;シーン外で現在着用している衣装
#DIM 適正衣装種類ID ;次回選択される種類、シーンに適切な理想の種類を記憶しておく　ex.コスプレ、脱衣状態など
#DIM 適正差分種類ID
#DIM 対象衣装種類ID ;実際にセットする種類
#DIM 対象差分種類ID
#DIM 対象ファイルID
#DIM 選択コマンドID ;おさわり内でのID
#DIMS 選択コマンド文字列
#DIM クリック座標,2 ;0=x,1=y
#DIM クリック色指定
#DIM 弱点コマンド指定
#DIM おさわり容認カウンタ,100
#DIM おさわり実効カウンタ,100
#DIM おさわり警告カウンタ
#DIM おさわり警告フラグ
#DIM おさわり実効フラグ
#DIM おさわりスタンス ;0=弱セクハラ,1=強セクハラ,2=マッサージ（予定）

#DIM CONST 画像位置 = 620, 180
#DIM CONST 画像サイズ = 2 ;SCALE

{
    #DIMS CONST システムコマンド = 
    "", "おさわりモード終了", "画像ロック", "おさわり強度", "リンパの話をする", "テストコマンド（脱着衣）",
    "有効グラフィック全体化"
}
#DIM CONST システムコマンド位置基準 = 0, 170
#DIM CONST システムコマンドサイズ = 400, 40
#DIM CONST システムコマンド間隔 = 50

#DIMS システムコマンド選択
#DIM システムコマンド格納, 10

#DIM CONST 有効選択コマンド位置基準 = 1020, 170 ;x= 画像位置(620) + WIDTH(360) + 余白(40)
#DIM CONST 有効選択コマンドサイズ = 400, 40
#DIM CONST 有効選択コマンド間隔 = 50
#DIM CONST 有効選択コマンド表示数 = 6 ;コマンド候補最大数

#DIM 有効選択コマンドIDリスト,20
#DIM 有効選択コマンド数
#DIMS 有効選択コマンド名格納,20
#DIM 有効グラフィック全体化フラグ ;2=解除不可

#DIM キーボード選択コマンドID ;キーボード押下時の入力を変換するための預かり

#DIM 画像切り替えカウンタ
#DIM 画像切り替えフラグ
#DIM 画像ロックフラグ
#DIMS メッセージ予約 ;システムメッセージ窓用
#DIMS プリントメッセージ保存 ;HTML_PRINT向け保存

;テスト用カウンタ
#DIM TIME保存
#DIM DYNAMIC クリックカウンタ
#DIM DYNAMIC コマンドカウンタ
#DIM DYNAMIC タイマカウンタ

VARSET メッセージ予約
VARSET プリントメッセージ保存
TIME保存 = TIME

SIF CSTR:キャラ番号:画像フォルダ == ""
    CSTR:キャラ番号:画像フォルダ = {NO:キャラ番号}%CALLNAME:キャラ番号%

対象フォルダ '= "resources\\" + CSTR:キャラ番号:画像フォルダ

SELECTCASE CSTR:TARGET:着せ替え服
    CASE "水着"
        現在衣装種類ID = 1
    CASE "湯浴み着"
        現在衣装種類ID = 2
    CASE "全裸"
        現在衣装種類ID = 3
    CASE "浴衣"
        現在衣装種類ID = 4
    CASE "部屋着"
        現在衣装種類ID = 5
    CASE "おしゃれ着"
        現在衣装種類ID = 6
    CASE "バニー服"
        現在衣装種類ID = 7
    CASE "メイド服"
        現在衣装種類ID = 8
    CASE "マイクロビキニ"
        現在衣装種類ID = 9
    CASE "逆バニー"
        現在衣装種類ID = 10
    CASE "チャイナドレス"
        現在衣装種類ID = 11
    CASE "ミニチャイナ"
        現在衣装種類ID = 12
    CASE "セクシーランジェリー"
        現在衣装種類ID = 13
    CASE "ミニスカサンタ"
        現在衣装種類ID = 14
    CASEELSE ;デフォルト
        現在衣装種類ID = 0
ENDSELECT

;ランダムキャラ、汎用立ち絵はテンキー有効化状態にしてアーカイブ作成をまたぐ
IF CSTR:キャラ番号:汎用立ち絵登録文字列 != "" && ENUMFILES("resources/999" + CALLNAME:TARGET, "*.png") <= 0
	CSTR:キャラ番号:画像フォルダ = 汎用グラフィック
    VARSET 汎用ファイルアーカイブ
    VARSET 汎用ファイル数
    汎用ファイルアーカイブ:0:0:0 '= "短冊_" + CSTR:キャラ番号:汎用立ち絵登録文字列 + ".png"
    汎用ファイル数:0:0 = 1
    有効グラフィック全体化フラグ = 2
    FOR LOCAL, 0, 50
        PRINTL
    NEXT
    PRINTL テンキーを押しておさわりしよう！
    GOTO ランダムキャラ用スキップ
ENDIF

;個別画像持ちキャラは該当の画像があるかチェックする 湯浴み着は全裸も通す
IF CSTR:TARGET:着せ替え服 == "湯浴み着"
    IF EXISTFILE(対象フォルダ + "/短冊_" + CSTR:TARGET:着せ替え服 + ".png") ||  EXISTFILE(対象フォルダ + "/短冊_デフォルト.png") || EXISTFILE(対象フォルダ + "/短冊_全裸.png")
    ELSE ;どれもない
        PRINTL 着用中の衣装画像がないため実行できません
        WAIT
        RETURN
    ENDIF
    SIF !EXISTFILE(対象フォルダ + "/短冊_" + CSTR:TARGET:着せ替え服 + ".png") ;全裸画像を使っている
        現在衣装種類ID = 3
ELSE
    IF EXISTFILE(対象フォルダ + "/短冊_" + CSTR:TARGET:着せ替え服 + ".png") ||  EXISTFILE(対象フォルダ + "/短冊_デフォルト.png")
    ELSE ;どれもない
        PRINTL 着用中の衣装画像がないため実行できません
        WAIT
        RETURN
    ENDIF
ENDIF

;実行確定したのでログを流す
FOR LOCAL, 0, 50
    PRINTL
NEXT

;おさわりファイルがないときはテンキー有効化で固定する
IF ENUMFILES(対象フォルダ + "/" + おさわりフォルダ + "/", @"*.png") > 0
    有効グラフィック全体化フラグ = 0
ELSE
    有効グラフィック全体化フラグ = 2
    PRINTL テンキーを押しておさわりしよう！
ENDIF

;アーカイブ作成
IF !有効グラフィック全体化フラグ
    VARSET 対象ファイルアーカイブ
    VARSET 対象ファイル数
    FOR LOCAL, 0, 衣装種類数
        FOR LOCAL:1, 0, 差分種類数
            対象ファイル数:LOCAL:(LOCAL:1) = ENUMFILES(対象フォルダ + "/" + おさわりフォルダ + "/", 衣装種類:LOCAL + "_差分_" + 差分種類:(LOCAL:1) + "*.png")
            FOR LOCAL:2, 0, 対象ファイル数:LOCAL:(LOCAL:1)
                対象ファイルアーカイブ:LOCAL:(LOCAL:1):(LOCAL:2) '= SUBSTRING(RESULTS:(LOCAL:2), 1 + STRLENS(対象フォルダ) + 1 + STRLENS(おさわりフォルダ), -1)
            NEXT
        NEXT
        VARSET RESULTS
        RESULT = ENUMFILES(対象フォルダ + "/" + おさわりフォルダ + "/", 衣装種類:LOCAL + ".png")
        対象ファイルアーカイブ:LOCAL:0:(対象ファイル数:LOCAL:0) '= SUBSTRING(RESULTS:0, 1 + STRLENS(対象フォルダ) + 1 + STRLENS(おさわりフォルダ), -1)
        対象ファイル数:LOCAL:0 += RESULT
    NEXT
ENDIF
VARSET 汎用ファイルアーカイブ
VARSET 汎用ファイル数
FOR LOCAL, 0, 衣装種類数
    FOR LOCAL:1, 0, 差分種類数
        汎用ファイル数:LOCAL:(LOCAL:1) = ENUMFILES(対象フォルダ + "/", 衣装種類:LOCAL + "_差分_" + 差分種類:(LOCAL:1) + "*.png")
        FOR LOCAL:2, 0, 汎用ファイル数:LOCAL:(LOCAL:1)
            汎用ファイルアーカイブ:LOCAL:(LOCAL:1):(LOCAL:2) '= SUBSTRING(RESULTS:(LOCAL:2), 1 + STRLENS(対象フォルダ), -1)
        NEXT
    NEXT
    VARSET RESULTS
    RESULT = ENUMFILES(対象フォルダ + "/", 衣装種類:LOCAL + ".png")
    汎用ファイルアーカイブ:LOCAL:0:(汎用ファイル数:LOCAL:0) '= SUBSTRING(RESULTS:0, 1 + STRLENS(対象フォルダ), -1)
    汎用ファイル数:LOCAL:0 += RESULT
NEXT

$ランダムキャラ用スキップ

    PRINTL 怒られないようにクリックを繰り返して
    PRINTL コマンドを発動させよう！
    
;初期画像の設定
適正衣装種類ID = 現在衣装種類ID
IF PALAM:TARGET:欲情 >= PALAMLV:4 || CFLAG:TARGET:発情期フラグ < 0
    適正差分種類ID = 1
    メッセージ予約 = %CALLNAME:TARGET%は熱のこもった目でこちらを見つめている・・・
ELSEIF PALAM:TARGET:欲情 >= PALAMLV:2
    適正差分種類ID = 0
    メッセージ予約 = %CALLNAME:TARGET%はどことなく期待しているようにも見える・・・
ELSE
    適正差分種類ID = 0
    メッセージ予約 = %CALLNAME:TARGET%はおさわりできるような雰囲気ではない・・・
ENDIF

;最初のCOM実行までのクリック数をランダムにする
FOR LOCAL, 0, 100
    おさわり実効カウンタ:LOCAL = RAND:100
NEXT
VARSET おさわり警告カウンタ


VARSET 画像ロックフラグ
画像切り替えカウンタ = 5


;演出の都合上リソース開放が前後するので、他から持ち込んでいる場合のため開放しておく
GDISPOSE レイヤー番号 + 2
GDISPOSE レイヤー番号 + 3


$画像切り替え ;--------------------------------------ループ地点１


;画像切り替え時に容認リセット
VARSET おさわり容認カウンタ


;システムウィンドウ
;スタンスごとに表示するコマンドを指定する
CBGREMOVERANGE -6, -6
CBGREMOVERANGE -5, -5

;条件に合うシステムコマンドをスタックしていく
VARSET システムコマンド格納
SIF 有効グラフィック全体化フラグ < 2
    ARRAYSHIFT システムコマンド格納, 1, 6 ;有効グラフィック全体化
;ARRAYSHIFT システムコマンド格納, 1, 5 ;脱衣テスト
SIF おさわりスタンス == 2 ;リンパマッサージ
    ARRAYSHIFT システムコマンド格納, 1, 4
SIF (おさわりスタンス == 0 && !GLOBAL_COMABLE_TYPE("セクハラ強")) || おさわりスタンス == 1
    ARRAYSHIFT システムコマンド格納, 1, 3
ARRAYSHIFT システムコマンド格納, 1, 2
;おさわり終了を最後に入れる
ARRAYSHIFT システムコマンド格納, 1, 1

;スタックしたコマンドを並べる
GDISPOSE レイヤー番号 + 10,
GCREATE レイヤー番号 + 10, 1600, 900
GSETBRUSH レイヤー番号 + 10, 0xFF101010
GFILLRECTANGLE レイヤー番号 + 10, 0, 0, 画像位置:0, システムコマンド位置基準:1 + システムコマンド間隔 * 8
GSETFONT レイヤー番号 + 10, GETFONT(), 18
FOR LOCAL, 0, 10
    SIF システムコマンド格納:LOCAL == 0
        BREAK
    GSETBRUSH レイヤー番号 + 10, 0xFFC0C0C0
    GFILLRECTANGLE レイヤー番号 + 10, システムコマンド位置基準:0, システムコマンド位置基準:1 + LOCAL * システムコマンド間隔, システムコマンドサイズ:0, システムコマンドサイズ:1
    GSETBRUSH レイヤー番号 + 10, 0xFF101010
    GFILLRECTANGLE レイヤー番号 + 10, システムコマンド位置基準:0 + 5, システムコマンド位置基準:1 + LOCAL * システムコマンド間隔 + 5, システムコマンドサイズ:0 - 10, システムコマンドサイズ:1 - 10
    GSETPEN レイヤー番号 + 10, 0xFFC0C0C0, 1
    GDRAWTEXT レイヤー番号 + 10, "[" + TOSTR(LOCAL + 1) + "] " + システムコマンド:(システムコマンド格納:LOCAL), システムコマンド位置基準:0 + 10, システムコマンド位置基準:1 + LOCAL * システムコマンド間隔 + 10
NEXT
;チェックボックスが必要なコマンドには四角をわたす
GDRAWTEXT レイヤー番号 + 10, "□", システムコマンド位置基準:0 + 370, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 2) * システムコマンド間隔 + 10
SIF (おさわりスタンス == 0 && !GLOBAL_COMABLE_TYPE("セクハラ強")) || おさわりスタンス == 1
    GDRAWTEXT レイヤー番号 + 10, "□", システムコマンド位置基準:0 + 370, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 3) * システムコマンド間隔 + 10
SIF 有効グラフィック全体化フラグ < 2
    GDRAWTEXT レイヤー番号 + 10, "□", システムコマンド位置基準:0 + 370, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 6) * システムコマンド間隔 + 10

CBGSETG レイヤー番号 + 10, 0, 0, -5


画像切り替えフラグ = 0
SIF 画像ロックフラグ
    GOTO 画像切り替えスキップ


IF !有効グラフィック全体化フラグ
    VARSET 有効選択コマンドIDリスト,100 ;無効にする
    ;複数枚あるとき現在値と違うIDの選出,一枚ならID:0
    ;対象差分がないときは適正衣装、デフォルトの順に差分チェック
    SELECTCASE 対象ファイル数:適正衣装種類ID:適正差分種類ID
        CASE 0
            IF 対象ファイル数:適正衣装種類ID:1 && (PALAM:TARGET:欲情 >= PALAMLV:4 || CFLAG:TARGET:発情期フラグ < 0)
                対象衣装種類ID = 適正衣装種類ID
                対象差分種類ID = 1
            ELSEIF 対象ファイル数:適正衣装種類ID:0
                対象衣装種類ID = 適正衣装種類ID
                対象差分種類ID = 0
            ELSEIF 対象ファイル数:0:1 && (PALAM:TARGET:欲情 >= PALAMLV:4 || CFLAG:TARGET:発情期フラグ < 0)
                対象衣装種類ID = 0
                対象差分種類ID = 1
            ELSEIF 対象ファイル数:0:0
                対象衣装種類ID = 0
                対象差分種類ID = 0
            ELSE
            ENDIF
            IF 対象ファイル数:対象衣装種類ID:対象差分種類ID > 1
                FOR LOCAL, 0, 10
                    LOCAL:1 = RAND:(対象ファイル数:対象衣装種類ID:対象差分種類ID)
                    SIF 対象ファイルID == LOCAL:1
                        CONTINUE
                    対象ファイルID = LOCAL:1
                    BREAK
                NEXT
            ELSE
                対象ファイルID = 0
            ENDIF
        CASE 1
            対象衣装種類ID = 適正衣装種類ID
            対象差分種類ID = 適正差分種類ID
            対象ファイルID = 0
        CASEELSE
            対象衣装種類ID = 適正衣装種類ID
            対象差分種類ID = 適正差分種類ID
            FOR LOCAL, 0, 10
                LOCAL:1 = RAND:(対象ファイル数:対象衣装種類ID:対象差分種類ID)
                SIF 対象ファイルID == LOCAL:1
                    CONTINUE
                対象ファイルID = LOCAL:1
                BREAK
            NEXT
    ENDSELECT

    CBGSETG レイヤー番号 + 3, 0, 0, -1
    CBGREMOVERANGE -2, -2

    GDISPOSE レイヤー番号
    GCREATEFROMFILE レイヤー番号, @"%CSTR:キャラ番号:画像フォルダ%/%対象ファイルアーカイブ:対象衣装種類ID:対象差分種類ID:対象ファイルID%"

    FOR LOCAL, 0, 5
        GDISPOSE レイヤー番号 + 1
        GCREATE レイヤー番号 + 1, 1600, 900
        CALL カットイン用カラーマトリクス取得(50 - LOCAL * 10)
        GDRAWG レイヤー番号 + 1, レイヤー番号, 画像位置:0, 画像位置:1, GWIDTH(レイヤー番号) * 画像サイズ, GHEIGHT(レイヤー番号) * 画像サイズ, 0, 0, GWIDTH(レイヤー番号), GHEIGHT(レイヤー番号), カットイン用カラーマトリクス:0:0
        CBGSETG レイヤー番号 + 1, 0, 0, -2
        AWAIT 50
    NEXT

    ;次回フェードインに使う画像を作っておく
    GDISPOSE レイヤー番号 + 2
    GCREATEFROMFILE レイヤー番号 + 2, @"%CSTR:キャラ番号:画像フォルダ%/%対象ファイルアーカイブ:対象衣装種類ID:対象差分種類ID:対象ファイルID%"
    GDISPOSE レイヤー番号 + 3
    GCREATE レイヤー番号 + 3, 1600, 900
    GDRAWG レイヤー番号 + 3, レイヤー番号, 画像位置:0, 画像位置:1, GWIDTH(レイヤー番号 + 2) * 画像サイズ, GHEIGHT(レイヤー番号 + 2) * 画像サイズ, 0, 0, GWIDTH(レイヤー番号 + 2), GHEIGHT(レイヤー番号 + 2)

    CBGREMOVERANGE -1, -1

    ;当たり判定の設定
    CBGREMOVEBMAP
    GDISPOSE レイヤー番号 + 20
    GCREATEFROMFILE レイヤー番号 + 20, @"%CSTR:キャラ番号:画像フォルダ%/%おさわりフォルダ%/%対象ファイルアーカイブ:対象衣装種類ID:対象差分種類ID:対象ファイルID%"
    GDISPOSE レイヤー番号 + 21
    GCREATEFROMFILE レイヤー番号 + 21, @"カットイン素材/おさわりパネル.png"
    GDRAWG レイヤー番号 + 21, レイヤー番号 + 20, 画像位置:0, 画像位置:1, GWIDTH(レイヤー番号 + 20), GHEIGHT(レイヤー番号 + 20), 0, 0, GWIDTH(レイヤー番号 + 20), GHEIGHT(レイヤー番号 + 20)
    CBGSETBMAPG レイヤー番号 + 21

    {
    ;当たり判定の可視化 SIZEは上に合わせること
    GDISPOSE レイヤー番号 + 22
    GCREATE レイヤー番号 + 22, 1600, 900
    CALL カットイン用カラーマトリクス取得(50)
    GDRAWG レイヤー番号 + 22, レイヤー番号 + 21, 0, 0, 1600, 900, 0, 0, GWIDTH (レイヤー番号 + 21), GHEIGHT(レイヤー番号 + 21), カットイン用カラーマトリクス:0:0
    CBGSETG レイヤー番号 + 22, 0, 0, -2
    }

ELSE ;汎用ファイル対象の時
    SELECTCASE 汎用ファイル数:適正衣装種類ID:適正差分種類ID
        CASE 0
            IF 汎用ファイル数:適正衣装種類ID:1 && (PALAM:TARGET:欲情 >= PALAMLV:4 || CFLAG:TARGET:発情期フラグ < 0)
                対象衣装種類ID = 適正衣装種類ID
                対象差分種類ID = 1
            ELSEIF 汎用ファイル数:適正衣装種類ID:0
                対象衣装種類ID = 適正衣装種類ID
                対象差分種類ID = 0
            ELSEIF 汎用ファイル数:0:1 && (PALAM:TARGET:欲情 >= PALAMLV:4 || CFLAG:TARGET:発情期フラグ < 0)
                対象衣装種類ID = 0
                対象差分種類ID = 1
            ELSEIF 汎用ファイル数:0:0
                対象衣装種類ID = 0
                対象差分種類ID = 0
            ELSE
            ENDIF
            IF 汎用ファイル数:対象衣装種類ID:対象差分種類ID > 1
                FOR LOCAL, 0, 10
                    LOCAL:1 = RAND:(汎用ファイル数:対象衣装種類ID:対象差分種類ID)
                    SIF 対象ファイルID == LOCAL:1
                        CONTINUE
                    対象ファイルID = LOCAL:1
                    BREAK
                NEXT
            ELSE
                対象ファイルID = 0
            ENDIF
        CASE 1
            対象衣装種類ID = 適正衣装種類ID
            対象差分種類ID = 適正差分種類ID
            対象ファイルID = 0
        CASEELSE
            対象衣装種類ID = 適正衣装種類ID
            対象差分種類ID = 適正差分種類ID
            FOR LOCAL, 0, 10
                LOCAL:1 = RAND:(汎用ファイル数:対象衣装種類ID:対象差分種類ID)
                SIF 対象ファイルID == LOCAL:1
                    CONTINUE
                対象ファイルID = LOCAL:1
                BREAK
            NEXT
    ENDSELECT
    CBGSETG レイヤー番号 + 3, 0, 0, -1
    CBGREMOVERANGE -2, -2

    GDISPOSE レイヤー番号
    GCREATEFROMFILE レイヤー番号, @"%CSTR:キャラ番号:画像フォルダ%/%汎用ファイルアーカイブ:対象衣装種類ID:対象差分種類ID:対象ファイルID%"

    FOR LOCAL, 0, 5
        GDISPOSE レイヤー番号 + 1
        GCREATE レイヤー番号 + 1, 1600, 900
        CALL カットイン用カラーマトリクス取得(50 - LOCAL * 10)
        GDRAWG レイヤー番号 + 1, レイヤー番号, 画像位置:0, 画像位置:1, GWIDTH(レイヤー番号) * 画像サイズ, GHEIGHT(レイヤー番号) * 画像サイズ, 0, 0, GWIDTH(レイヤー番号), GHEIGHT(レイヤー番号), カットイン用カラーマトリクス:0:0
        CBGSETG レイヤー番号 + 1, 0, 0, -2
        AWAIT 50
    NEXT

    ;次回フェードインに使う画像を作っておく
    GDISPOSE レイヤー番号 + 2
    GCREATEFROMFILE レイヤー番号 + 2, @"%CSTR:キャラ番号:画像フォルダ%/%汎用ファイルアーカイブ:対象衣装種類ID:対象差分種類ID:対象ファイルID%"
    GDISPOSE レイヤー番号 + 3
    GCREATE レイヤー番号 + 3, 1600, 900
    GDRAWG レイヤー番号 + 3, レイヤー番号, 画像位置:0, 画像位置:1, GWIDTH(レイヤー番号 + 2) * 画像サイズ, GHEIGHT(レイヤー番号 + 2) * 画像サイズ, 0, 0, GWIDTH(レイヤー番号 + 2), GHEIGHT(レイヤー番号 + 2)

    CBGREMOVERANGE -1, -1

    ;当たり判定の設定
    CBGREMOVEBMAP
    GDISPOSE レイヤー番号 + 20
    GCREATEFROMFILE レイヤー番号 + 20, @"%CSTR:キャラ番号:画像フォルダ%/%おさわりフォルダ%/%汎用ファイルアーカイブ:対象衣装種類ID:対象差分種類ID:対象ファイルID%"
    GDISPOSE レイヤー番号 + 21
    GCREATEFROMFILE レイヤー番号 + 21, @"カットイン素材/おさわりパネル.png"
    GDRAWG レイヤー番号 + 21, レイヤー番号 + 20, 画像位置:0, 画像位置:1, GWIDTH(レイヤー番号 + 20), GHEIGHT(レイヤー番号 + 20), 0, 0, GWIDTH(レイヤー番号 + 20), GHEIGHT(レイヤー番号 + 20)
    CBGSETBMAPG レイヤー番号 + 21

    {
    ;当たり判定の可視化 SIZEは上に合わせること
    GDISPOSE レイヤー番号 + 22
    GCREATE レイヤー番号 + 22, 1600, 900
    CALL カットイン用カラーマトリクス取得(50)
    GDRAWG レイヤー番号 + 22, レイヤー番号 + 21, 0, 0, 1600, 900, 0, 0, GWIDTH (レイヤー番号 + 21), GHEIGHT(レイヤー番号 + 21), カットイン用カラーマトリクス:0:0
    CBGSETG レイヤー番号 + 22, 0, 0, -2
    }

ENDIF

$画像切り替えスキップ

;テンキーおさわり可能コマンドの抽選
IF 有効グラフィック全体化フラグ
    ;有効なコマンドをリスト化する
    ;スタンスごとにLR両方から取り込む
    VARSET 有効選択コマンドIDリスト, 100
    VARSET 有効選択コマンド数
    VARSET 有効選択コマンド名格納
    FOR LOCAL, 0, 20
        選択コマンド文字列 '= おさわりコマンドリスト(0, おさわりスタンス, LOCAL)
        SIF 選択コマンド文字列 == ""
            CONTINUE

        VARSET LOCALS
        VARSET LOCALS:1
        SPLIT 選択コマンド文字列, "_", LOCALS
        SELECTCOM = TOINT(LOCALS)
        IF LOCALS:1 == ""
            RSTR:SELECTCOM_S受け渡し =
        ELSE
            RSTR:SELECTCOM_S受け渡し '= 選択コマンド文字列
        ENDIF
        IF RSTR:SELECTCOM_S受け渡し == ""
            TRYCALLFORM CAN_COM{SELECTCOM}(1)
            IF RESULT
                CALLFORMF COMNAME{SELECTCOM}
                RESULT = FINDELEMENT(有効選択コマンド名格納, TSTR:コマンド名受渡, 0, LOCAL, 1)
                IF RESULT < 0 ;一致がない
                    ARRAYSHIFT 有効選択コマンド名格納, 1, TSTR:コマンド名受渡
                    ARRAYSHIFT 有効選択コマンドIDリスト, 1, LOCAL
                    有効選択コマンド数 ++
                ENDIF
            ENDIF
        ELSE
            TRYCALLFORM CAN_COM_S%RSTR:SELECTCOM_S受け渡し%(1)
            IF RESULT
                CALLFORMF COMNAME_S%RSTR:SELECTCOM_S受け渡し%
                CALLFORM 身体に触れる_ABLE_%TSTR:コマンド名受渡%
                SIF !RESULT
                    CONTINUE  
                RESULT = FINDELEMENT(有効選択コマンド名格納, TSTR:コマンド名受渡, 0, LOCAL, 1)
                IF RESULT < 0 ;一致がない
                    ARRAYSHIFT 有効選択コマンド名格納, 1, TSTR:コマンド名受渡
                    ARRAYSHIFT 有効選択コマンドIDリスト, 1, LOCAL
                    有効選択コマンド数 ++
                ENDIF
            ENDIF
        ENDIF
    NEXT

    ;コマンドをシャッフルして取り出す
    FOR LOCAL, 0, 有効選択コマンド数
        LOCAL:1 = RAND:(有効選択コマンド数 - LOCAL)
        SWAP 有効選択コマンドIDリスト:(LOCAL:1), 有効選択コマンドIDリスト:(有効選択コマンド数 - LOCAL - 1)
        SWAP 有効選択コマンド名格納:(LOCAL:1), 有効選択コマンド名格納:(有効選択コマンド数 - LOCAL - 1)
    NEXT
    
    GDISPOSE レイヤー番号 + 12,
    GCREATE レイヤー番号 + 12, 1600, 900

    GSETFONT レイヤー番号 + 12, GETFONT(), 18
    FOR LOCAL, 0, 有効選択コマンド表示数
        SIF 有効選択コマンド名格納:LOCAL == ""
            BREAK
        GSETBRUSH レイヤー番号 + 12, 0xFFC0C0C0
        GFILLRECTANGLE レイヤー番号 + 12, 有効選択コマンド位置基準:0, 有効選択コマンド位置基準:1 + LOCAL * 有効選択コマンド間隔, 有効選択コマンドサイズ:0, 有効選択コマンドサイズ:1
        GSETBRUSH レイヤー番号 + 12, 0xFF101010
        GFILLRECTANGLE レイヤー番号 + 12, 有効選択コマンド位置基準:0 + 5, 有効選択コマンド位置基準:1 + LOCAL * 有効選択コマンド間隔 + 5, 有効選択コマンドサイズ:0 - 10, 有効選択コマンドサイズ:1 - 10
        GSETPEN レイヤー番号 + 12, 0xFFC0C0C0, 1
        GDRAWTEXT レイヤー番号 + 12, "[num" + TOSTR(LOCAL) + "] " + 有効選択コマンド名格納:LOCAL, 有効選択コマンド位置基準:0 + 10, 有効選択コマンド位置基準:1 + LOCAL * 有効選択コマンド間隔 + 10
    NEXT
    VARSET 有効選択コマンドIDリスト, 100, 有効選択コマンド表示数, 20
    CBGSETG レイヤー番号 + 12, 0, 0, -5
ENDIF


$入力待ち ;-------------------------------------ループ地点２

;毎入力ごとにチェックするものはここ
IF BASE:TARGET:体力 == 0
    プリントメッセージ保存 += @"<br>%CALLNAME:TARGET%の体力が尽きた・・・"
    GOTO 終了処理
ENDIF

CBGREMOVERANGE -6, -6
GDISPOSE レイヤー番号 + 11
GCREATE レイヤー番号 + 11, 1600, 900
GSETFONT レイヤー番号 + 11, GETFONT(), 18
SIF 画像ロックフラグ
    GDRAWTEXT レイヤー番号 + 11, "×", システムコマンド位置基準:0 + 370, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 2) * システムコマンド間隔 + 10
SIF おさわりスタンス == 1
    GDRAWTEXT レイヤー番号 + 11, "×", システムコマンド位置基準:0 + 370, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 3) * システムコマンド間隔 + 10
SIF 有効グラフィック全体化フラグ == 1
    GDRAWTEXT レイヤー番号 + 11, "×", システムコマンド位置基準:0 + 370, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 6) * システムコマンド間隔 + 10

;特記事項がないときTIPSを表示する
IF メッセージ予約 == ""
    SELECTCASE RAND:5
        CASE 0
            GDRAWTEXT レイヤー番号 + 11, "TIPS:各機能切り替えはクリックでも変更できる", システムコマンド位置基準:0, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 0) * システムコマンド間隔 + 10
        CASE 1
            GDRAWTEXT レイヤー番号 + 11, "TIPS:Ｌキーで画像を表示中のものに固定できる", システムコマンド位置基準:0, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 0) * システムコマンド間隔 + 10
        CASE 2
            GDRAWTEXT レイヤー番号 + 11, "TIPS:Ｓキーでおさわり強度を変更できる", システムコマンド位置基準:0, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 0) * システムコマンド間隔 + 10
        CASE 3
            GDRAWTEXT レイヤー番号 + 11, "TIPS:欲情度が低いときはおさわり厳禁", システムコマンド位置基準:0, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 0) * システムコマンド間隔 + 10
        CASE 4
            GDRAWTEXT レイヤー番号 + 11, "TIPS:Ｚキーでおさわりモードが終了できる", システムコマンド位置基準:0, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 0) * システムコマンド間隔 + 10
        CASE 5
            GDRAWTEXT レイヤー番号 + 11, "TIPS:右クリックで別のセクハラができる場所がある", システムコマンド位置基準:0, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 0) * システムコマンド間隔 + 10
        CASE 5
            GDRAWTEXT レイヤー番号 + 11, "TIPS:有効グラフィック全体化時はテンキーでおさわりできる", システムコマンド位置基準:0, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 0) * システムコマンド間隔 + 10
        CASE 6
            GDRAWTEXT レイヤー番号 + 11, "TIPS:NUMLOCKを解除したテンキーは右クリックの効果がある", システムコマンド位置基準:0, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 0) * システムコマンド間隔 + 10
    ENDSELECT
ELSE
    GDRAWTEXT レイヤー番号 + 11, メッセージ予約, システムコマンド位置基準:0, システムコマンド位置基準:1 + FINDELEMENT(システムコマンド格納, 0) * システムコマンド間隔 + 10
ENDIF
VARSET メッセージ予約
CBGSETG レイヤー番号 + 11, 0, 0, -6


VARSET RESULT
INPUTMOUSEKEY ;--------------------入力待ち
クリック座標:0 = RESULT:2
クリック座標:1 = RESULT:3
クリックカウンタ ++


;PRINTFORML DEBUG MOUSEKEY {RESULT}, {RESULT:1} 
;PRINTFORML DEBUG PRINTPOINT X {RESULT:2}, Y {RESULT:3},
;PRINTFORML DEBUG PRINTCOLOR {RESULT:4}

;入力キーの識別と割り当て
選択コマンドID = 100
IF RESULT == 1
    SELECTCASE RESULT:4
    CASE 0x101020
        システムコマンド選択 '= システムコマンド:(システムコマンド格納:0)
    CASE 0x102010
        システムコマンド選択 '= システムコマンド:(システムコマンド格納:1)
    CASE 0x102020
        システムコマンド選択 '= システムコマンド:(システムコマンド格納:2)
    CASE 0x201010
        システムコマンド選択 '= システムコマンド:(システムコマンド格納:3)
    CASE 0x201020
        システムコマンド選択 '= システムコマンド:(システムコマンド格納:4)
    CASE 0x202010
        システムコマンド選択 '= システムコマンド:(システムコマンド格納:5)
    CASE 0x202020
        システムコマンド選択 '= システムコマンド:(システムコマンド格納:6)
    CASE 0x101030
        選択コマンドID = 有効選択コマンドIDリスト:0
    CASE 0x103010
        選択コマンドID = 有効選択コマンドIDリスト:1
    CASE 0x103030
        選択コマンドID = 有効選択コマンドIDリスト:2
    CASE 0x301010
        選択コマンドID = 有効選択コマンドIDリスト:3
    CASE 0x301030
        選択コマンドID = 有効選択コマンドIDリスト:4
    CASE 0x303010
        選択コマンドID = 有効選択コマンドIDリスト:5
    CASE 0x303030
        選択コマンドID = 有効選択コマンドIDリスト:6
    CASE 0x0000C0 ;背中
        選択コマンドID = 0
    CASE 0x00C000 ;耳
        選択コマンドID = 1
    CASE 0x00C0C0 ;角
        選択コマンドID = 2
    CASE 0xC00000 ;尻尾
        選択コマンドID = 3
    CASE 0xC000C0 ;お尻
        選択コマンドID = 4
    CASE 0xC0C000 ;お腹
        選択コマンドID = 5
    CASE 0xC0C0C0 ;頭
        選択コマンドID = 6
    CASE 0x0000A0 ;髪
        選択コマンドID = 7
    CASE 0x00A000 ;脇
        選択コマンドID = 8
    CASE 0x00A0A0 ;配置換えのため欠番（利用可）

    CASE 0xA00000 ;へそ
        選択コマンドID = 9
    CASE 0xA000A0 ;口
        選択コマンドID = 10
    CASE 0xA0A000 ;太もも
        選択コマンドID = 11
    CASE 0xA0A0A0 ;下腹部
        選択コマンドID = 12
    CASE 0x000080 ;乳首
        選択コマンドID = 13
    CASE 0x008000 ;C
        選択コマンドID = 14
    CASE 0x008080 ;スカート
        選択コマンドID = 15
    CASE 0x800000 ;V
        選択コマンドID = 16
    CASE 0x800080 ;B
        選択コマンドID = 17
    CASE 0x808000 ;A
        選択コマンドID = 18
    CASEELSE
        システムコマンド選択 = 
    ENDSELECT
    ;おさわりの範囲
    IF 選択コマンドID < 100
        GOTO おさわり実行処理
    ENDIF
    CALL クリックアイコン(クリック座標:0, クリック座標:1)
ELSEIF RESULT == 3 ;キーボード入力
                    ;押されたキーがどの座標のために押下されたか取得し、座標からコマンドを再度入力する
                    ;これはクリック地点をすり替えるための処理
    SELECTCASE RESULT:2
        CASE 49 ;1
            キーボード選択コマンドID = 0
        CASE 50 ;2
            キーボード選択コマンドID = 1
        CASE 51 ;3
            キーボード選択コマンドID = 2
        CASE 52 ;4
            キーボード選択コマンドID = 3
        CASE 53 ;5
            キーボード選択コマンドID = 4
        CASE 54 ;6
            キーボード選択コマンドID = 5
        CASE 55 ;7
            キーボード選択コマンドID = 6
        ;---------------ショートカット
        CASE 66 ;B 有効グラフィック全体化
            キーボード選択コマンドID = FINDELEMENT(システムコマンド格納, 6)
        CASE 76 ;L 画像ロック
            キーボード選択コマンドID = FINDELEMENT(システムコマンド格納, 2)
        CASE 82 ;R リンパの話をする 正しくはlymph
            キーボード選択コマンドID = FINDELEMENT(システムコマンド格納, 4)
        CASE 83 ;S おさわり強度
            キーボード選択コマンドID = FINDELEMENT(システムコマンド格納, 3)
        CASE 90 ;Z おさわりモード終了
            キーボード選択コマンドID = FINDELEMENT(システムコマンド格納, 1)
        ;----------------
        CASE 96 ;NUM1
            キーボード選択コマンドID = 100
        CASE 97 ;NUM2
            キーボード選択コマンドID = 101
        CASE 98 ;NUM3
            キーボード選択コマンドID = 102
        CASE 99 ;NUM4
            キーボード選択コマンドID = 103
        CASE 100 ;NUM5
            キーボード選択コマンドID = 104
        CASE 101 ;NUM6
            キーボード選択コマンドID = 105
        CASE 102 ;NUM7
            キーボード選択コマンドID = 106
        ;----------------ここからNUMLOCK解除テンキー
        CASE 45 ;NUM1
            キーボード選択コマンドID = 200
        CASE 35 ;NUM2
            キーボード選択コマンドID = 201
        CASE 40 ;NUM3
            キーボード選択コマンドID = 202
        CASE 34 ;NUM4
            キーボード選択コマンドID = 203
        CASE 37 ;NUM5
            キーボード選択コマンドID = 204
        CASE 12 ;NUM6
            キーボード選択コマンドID = 205
        CASE 39 ;NUM7
            キーボード選択コマンドID = 206
        ;-------------
        CASEELSE
            キーボード選択コマンドID = -1
    ENDSELECT
    SELECTCASE キーボード選択コマンドID
        CASE IS >= 100 ;有効選択コマンドは選択コマンドIDを入れて、クリック座標をすり替えて渡す
            IF キーボード選択コマンドID < 200 ;左クリック扱いにする
                RESULT:1 = 0x100000
            ELSE
                キーボード選択コマンドID -= 100
                RESULT:1 = 0
            ENDIF
            選択コマンドID = 有効選択コマンドIDリスト:(キーボード選択コマンドID - 100)
            クリック座標:0 = 有効選択コマンド位置基準:0 + 100
            クリック座標:1 = -900 + 有効選択コマンド位置基準:1 + (キーボード選択コマンドID - 100) * システムコマンド間隔 + 10
            IF 選択コマンドID < 100
                GOTO おさわり実行処理
            ENDIF
            システムコマンド選択 = 
        CASE IS >= 0 ;システムコマンド
            システムコマンド選択 '= システムコマンド:(システムコマンド格納:キーボード選択コマンドID)
            クリック座標:0 = システムコマンド位置基準:0 + 100
            クリック座標:1 = -900 + システムコマンド位置基準:1 + キーボード選択コマンドID * システムコマンド間隔 + 10
            CALL クリックアイコン(クリック座標:0, クリック座標:1)
        CASEELSE ;-1=該当コマンドなし
            システムコマンド選択 = 
    ENDSELECT
ELSE ;マウスホイール
    GOTO 入力待ち
ENDIF

;システム操作はここ
SELECTCASE システムコマンド選択
    CASE "おさわりモード終了"
        GOTO 終了処理
    CASE "画像ロック"
        IF 画像ロックフラグ
            画像ロックフラグ = 0
            メッセージ予約 = 画像切り替えを行います
        ELSE
            画像ロックフラグ = 1
            メッセージ予約 = 表示中の画像で固定します
        ENDIF
    CASE "おさわり強度"
        IF おさわりスタンス == 0
            おさわりスタンス = 1
            メッセージ予約 = おさわり強度を強くします
        ELSEIF おさわりスタンス == 1
            おさわりスタンス = 0
            メッセージ予約 = おさわり強度を弱くします
        ENDIF
    CASE "リンパの話をする"
        VARSET おさわり容認カウンタ
        PRINTL
        PRINTFORML リンパなら仕方ないと%CALLNAME:TARGET%は納得したようだ
    {
        ;衣装チェンジ
    CASE "テストコマンド（脱着衣）"
        IF 適正衣装種類ID != 3
            適正衣装種類ID = 3
            メッセージ予約 = 脱衣テスト
            GOTO 画像切り替え
        ELSE
            適正衣装種類ID = 現在衣装種類ID
            メッセージ予約 = 着衣テスト
            GOTO 画像切り替え
        ENDIF
    }
    CASE "有効グラフィック全体化"
        IF 有効グラフィック全体化フラグ == 1
            有効グラフィック全体化フラグ = 0
            メッセージ予約 = ボタン操作を無効にし、おさわり判定のあるグラフィックに限定して表示します
            GOTO 画像切り替え
        ELSEIF 有効グラフィック全体化フラグ == 0
            有効グラフィック全体化フラグ = 1
            メッセージ予約 = ボタン操作を有効にし、おさわり判定のないグラフィックを表示候補に追加します
            GOTO 画像切り替え
        ELSE
        ENDIF
ENDSELECT

GOTO 入力待ち


$おさわり実行処理

IF RESULT:1 == 0x100000 ;左クリック
    選択コマンド文字列 '= おさわりコマンドリスト(0, おさわりスタンス, 選択コマンドID)
ELSE
    選択コマンド文字列 '= おさわりコマンドリスト(1, おさわりスタンス, 選択コマンドID)
ENDIF

;対応コマンドなしは空振り
IF 選択コマンド文字列 == ""
    CALL クリックアイコン(クリック座標:0, クリック座標:1)
    GOTO 入力待ち
ENDIF

;CAN_COM 実行できないときは空振り
VARSET LOCALS
VARSET LOCALS:1
SPLIT 選択コマンド文字列, "_", LOCALS
SELECTCOM = TOINT(LOCALS)
IF LOCALS:1 == ""
    RSTR:SELECTCOM_S受け渡し =
ELSE
    RSTR:SELECTCOM_S受け渡し '= 選択コマンド文字列
ENDIF
IF RSTR:SELECTCOM_S受け渡し == ""
    TRYCALLFORM CAN_COM{SELECTCOM}(1)
ELSE
    TRYCALLFORM CAN_COM_S%RSTR:SELECTCOM_S受け渡し%(1)
    IF RESULT
        CALLFORMF COMNAME_S%RSTR:SELECTCOM_S受け渡し%
        CALLFORM 身体に触れる_ABLE_%TSTR:コマンド名受渡%
    ENDIF
ENDIF
IF !RESULT
    CALL クリックアイコン(クリック座標:0, クリック座標:1)
    GOTO 入力待ち
ENDIF


弱点コマンド指定 = 0
;WEEK
IF RSTR:SELECTCOM_S受け渡し == ""
    LOCALS:1 '= TOSTR(SELECTCOM)
ELSE
    LOCALS:1 '= RSTR:SELECTCOM_S受け渡し
ENDIF
FOR LOCAL, 0, 500
    IF 弱点コマンド枠:キャラ番号:LOCAL == LOCALS:1
        弱点コマンド指定 = 1
        BREAK
    ENDIF
    SIF 弱点コマンド枠:キャラ番号:LOCAL == ""
        BREAK
NEXT

;COMとして実効があるか
おさわり実効フラグ = 0
おさわり実効カウンタ:選択コマンドID += RAND(20, 40)
IF おさわり実効カウンタ:選択コマンドID > 100
    おさわり実効カウンタ:選択コマンドID = おさわり実効カウンタ:選択コマンドID % 100
    おさわり実効フラグ = 1
    ;その気でないときは一発警告
    IF PALAM:TARGET:欲情 < PALAMLV:2 && CFLAG:TARGET:発情期フラグ >= 0
        おさわり容認カウンタ:選択コマンドID = 100
    ENDIF
ENDIF

;COUNT
おさわり警告フラグ = 0
おさわり容認カウンタ:選択コマンドID += RAND(5, 10)
IF おさわり容認カウンタ:選択コマンドID > 100
    おさわり容認カウンタ:選択コマンドID = おさわり容認カウンタ:選択コマンドID % 100
    おさわり警告フラグ = 1
    クリック色指定 = 1
ENDIF

;COLOR
IF !おさわり警告フラグ
    クリック色指定 = 0
    SELECTCASE おさわり容認カウンタ:選択コマンドID
        CASE IS < 25
            クリック色指定 = 3
        CASE IS < 50
            クリック色指定 = 2
        CASE IS < 75
            クリック色指定 = 4
        CASEELSE
            クリック色指定 = 5
    ENDSELECT
ENDIF

CALL クリックアイコン(クリック座標:0, クリック座標:1, クリック色指定)

;COM反映ブロック
IF おさわり実効フラグ
    コマンドカウンタ ++
    CALL ハートエフェクト(画像位置:0, 画像位置:1, GWIDTH(レイヤー番号) * 画像サイズ, GHEIGHT(レイヤー番号) * 画像サイズ, 弱点コマンド指定)

    画像切り替えカウンタ --
    IF !画像切り替えカウンタ
        画像切り替えカウンタ = 5
        画像切り替えフラグ = 1
    ENDIF

    ;今回初めて欲情になったとき
    IF PALAM:TARGET:欲情 >= PALAMLV:4 && LOCAL:11 < PALAMLV:4
        LOCAL:11 = PALAM:TARGET:欲情
        適正差分種類ID = 1
        画像切り替えフラグ = 1
    ENDIF
    LOCAL:11 = PALAM:TARGET:欲情

    ;適正差分を指定する（現状では絶頂から復帰するための処理）
    IF PALAM:TARGET:欲情 >= PALAMLV:4
        適正差分種類ID = 1
    ELSE
        適正差分種類ID = 0
    ENDIF

    PRINTL

   ;------------------------コマンド実行処理の抽出
   ;DRAWLINE,WAIT,SOURCE表示などをよけてテキストの抽出とCOM実行をする
   ;CALL_COM
    RESULT = 0
    TCVAR:TARGET:弱点コマンドフラグ = 0
    弱点コマンドカテゴリ = 
    IF RSTR:SELECTCOM_S受け渡し != ""
        TRYCALLFORMF COMTYPE_S_%RSTR:SELECTCOM_S受け渡し%
        TRYCALLFORM COM_S%RSTR:SELECTCOM_S受け渡し%
        LOCAL:1 = RESULT
        IF LOCAL:1 >= 0
            LOCALS = %RSTR:SELECTCOM_S受け渡し%
            IF STRFIND(コマンド初回フラグ:TARGET:0, @"-%LOCALS%-") < 0
                コマンド初回フラグ_一時保存:TARGET:0 += @"-%LOCALS%-"
            ENDIF
            IF STRFIND(コマンド初回フラグ_PLAYER:PLAYER:0, @"-%LOCALS%-") < 0
                コマンド初回フラグ_PLAYER_一時保存:PLAYER:0 += @"-%LOCALS%-"
            ENDIF
            IF STRFIND(弱点コマンドカテゴリ, "リダイレクト") >= 0
                LOCALS = %SUBSTRING(弱点コマンドカテゴリ, 12)%
            ENDIF
            ; IF 弱点コマンドカテゴリ != ""
                FOR LOCAL, 0, 50
                    IF 弱点コマンド枠:TARGET:LOCAL == LOCALS
                        TCVAR:TARGET:弱点コマンドフラグ = 1
                        IF 弱点看破:TARGET:LOCAL == 0
                            弱点看破:TARGET:LOCAL = 1
                            LOCAL:4 = 0
                            LOCAL:2 = FINDLASTELEMENT(弱点コマンド枠:TARGET:0, ".")
                            FOR LOCAL:3, 0, LOCAL:2 + 1
                                SIF 弱点看破:TARGET:(LOCAL:3) == 0
                                    LOCAL:4 ++
                            NEXT
                            IF LOCAL:4 == 0
                                CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:TARGET%の弱点をすべて看破した</font><img src='えっちハート'>","うふふ")
                                CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:MASTER%にすべての弱点を看破された</font><img src='えっちハート'>","うふふ")
                            ENDIF
                        ENDIF
                    ENDIF
                NEXT
            ; ENDIF
        ENDIF
        SPLIT RSTR:SELECTCOM_S受け渡し, "_", RESULTS
        ;RETURN LOCAL:1
    ELSE
        TRYCALLFORMF COMTYPE_{SELECTCOM}
        TRYCALLFORM COM{SELECTCOM}
        LOCAL:1 = RESULT
        IF LOCAL:1 >= 0
            SELECTCASE SELECTCOM
                CASE 370 TO 374
                    LOCALS = {SELECTCOM}_{NO:TARGET}
                CASE 380 TO 384
                    LOCALS = {SELECTCOM}_{CFLAG:MASTER:現在マップ種別}_{CFLAG:MASTER:現在位置}
                CASE 385 TO 389
                    LOCALS = {SELECTCOM}_%開催予定祭り名%
                CASE 400
                    LOCALS = {SELECTCOM}
                    ;追い出され・入室不可判定
                    CALL 入室不可処理
                CASEELSE
                    LOCALS = {SELECTCOM}
            ENDSELECT
            IF STRFIND(コマンド初回フラグ:TARGET:0, @"-%LOCALS%-") < 0
                コマンド初回フラグ_一時保存:TARGET:0 += @"-%LOCALS%-"
            ENDIF
            IF STRFIND(コマンド初回フラグ_PLAYER:PLAYER:0, @"-%LOCALS%-") < 0
                コマンド初回フラグ_PLAYER_一時保存:PLAYER:0 += @"-%LOCALS%-"
            ENDIF
            ; IF STRFIND(弱点コマンドカテゴリ, "リダイレクト") >= 0
            ; 	LOCALS = %SUBSTRING(弱点コマンドカテゴリ, 12)%
            ; ENDIF
            ; IF 弱点コマンドカテゴリ != ""
                FOR LOCAL, 0, 50
                    IF 弱点コマンド枠:TARGET:LOCAL == LOCALS
                        TCVAR:TARGET:弱点コマンドフラグ = 1
                        IF 弱点看破:TARGET:LOCAL == 0
                            弱点看破:TARGET:LOCAL = 1
                            LOCAL:4 = 0
                            LOCAL:2 = FINDLASTELEMENT(弱点コマンド枠:TARGET:0, ".")
                            FOR LOCAL:3, 0, LOCAL:2 + 1
                                SIF 弱点看破:TARGET:(LOCAL:3) == 0
                                    LOCAL:4 ++
                            NEXT
                            IF LOCAL:4 == 0
                                CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:TARGET%の弱点をすべて看破した</font><img src='えっちハート'>","うふふ")
                                CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>%CALLNAME:MASTER%にすべての弱点を看破された</font><img src='えっちハート'>","うふふ")
                            ENDIF
                        ENDIF
                    ENDIF
                NEXT
            ; ENDIF
        ENDIF
        ;RETURN LOCAL:1
    ENDIF
    ;-------------------------------------
    SKIPDISP 1
    CALL SOURCE_補正処理(TARGET)
    CALL SOURCE_ABLUP(TARGET)
    SKIPDISP 0
    ;------------対面プレイのテキストの抽出
    IF FLAG:汎用文章表示切り替えオプション > 0
        IF TCVAR:TARGET:弱点コマンドフラグ
            SETCOLOR カラーパレット("えっちな色")
            PRINTL <<<弱点コマンド！！>>>
            RESETCOLOR
        ENDIF
        IF RSTR:SELECTCOM_S受け渡し != ""
            TRYCCALLFORM MESSAGE_COM_S%RSTR:SELECTCOM_S受け渡し%
            CATCH
            ENDCATCH
        ELSE
            TRYCCALLFORM MESSAGE_COM{SELECTCOM}
            CATCH
            ENDCATCH
        ENDIF
    ENDIF
    IF K < 1 && LOCAL:1 == 1
        ;通常口上も複数人プレイ口上も無かった場合、非口上メッセージを出す
        ;口上なしで汎用喘ぎを使うならここで表示
        IF CFLAG:TARGET:汎用喘ぎ禁止フラグ == 1
            ;禁止時はなにもしない
        ELSEIF TFLAG:オートコマンドフラグ == 0
            IF TALENT:TARGET:性別 == 2
                SIF GETBIT(汎用喘ぎ使用OPTION_男, 0) && GETBIT(汎用喘ぎ使用OPTION_男, 1) == 0
                    CALL 汎用喘ぎ処理(TARGET)
            ELSE
                SIF GETBIT(汎用喘ぎ使用OPTION, 0) && GETBIT(汎用喘ぎ使用OPTION, 1) == 0
                    CALL 汎用喘ぎ処理(TARGET)
            ENDIF
        ENDIF
        IF RSTR:SELECTCOM_S受け渡し != ""
            TRYCCALLFORM MESSAGE_COM_S%RSTR:SELECTCOM_S受け渡し%_非口上時
            CATCH
            ENDCATCH
        ELSE
            TRYCCALLFORM MESSAGE_COM{SELECTCOM}_非口上時
            CATCH
            ENDCATCH
        ENDIF
    ENDIF

    ;絶頂時のグラフィック切り替え予約と画面効果
    ;抽出テキスト
    IF NOWEX:TARGET:多重絶頂 || NOWEX:TARGET:射精
        適正差分種類ID = 2 ;絶頂
        画像切り替えカウンタ = 3
        画像切り替えフラグ = 1

        IF NOWEX:TARGET:多重絶頂
            ;セクハラS絶頂があるので地の文で教える
            SIF NOWEX:TARGET:4
                PRINTFORML %CALLNAME:TARGET%は小刻みに肌をひくつかせて絶頂した 
            IF FLAG:汎用文章表示切り替えオプション > 0
                CALL PALAM_MESSAGE_通常絶頂
            ENDIF
            IF FLAG:口上表示切り替えオプション <= 0
                BREAK
            ENDIF
            TRYCALLFORM KOJO_ORGASM_通常絶頂_{NO:TARGET}(TARGET)
        ENDIF
        IF NOWEX:TARGET:射精
            SIF NOWEX:TARGET:射精 > 1
                PRINT 大量
            PRINTFORML 射精(%CALLNAME:TARGET%)
            IF FLAG:汎用文章表示切り替えオプション > 0
                CALL PALAM_MESSAGE_射精
            ENDIF
            IF FLAG:口上表示切り替えオプション <= 0
                BREAK
            ENDIF
                TRYCALLFORM KOJO_ORGASM_射精_{NO:TARGET}(TARGET)
            PRINTL
        ENDIF

        ;FLASH
        GDISPOSE レイヤー番号 + 4
        GCREATE レイヤー番号 + 4, 1600, 900
        GSETBRUSH レイヤー番号 + 4, 0xC0FFFFFF
        GFILLRECTANGLE レイヤー番号 + 4, 画像位置:0, 画像位置:1, GWIDTH(レイヤー番号) * 画像サイズ, GHEIGHT(レイヤー番号) * 画像サイズ
        CBGSETG レイヤー番号 + 4, 0, 0, -3
        AWAIT 50
        CBGREMOVERANGE -3, -3
        AWAIT 50
        CBGSETG レイヤー番号 + 4, 0, 0, -3
        AWAIT 50
        CBGREMOVERANGE -3, -3
        AWAIT 50
        FOR LOCAL, 0, 5
            GDISPOSE レイヤー番号 + 4
            GCREATE レイヤー番号 + 4, 1600, 900
            GSETBRUSH レイヤー番号 + 4, 0xA0FFFFFF - 0x10000000 * LOCAL
            GFILLRECTANGLE レイヤー番号 + 4, 画像位置:0, 画像位置:1, GWIDTH(レイヤー番号) * 画像サイズ, GHEIGHT(レイヤー番号) * 画像サイズ
            CBGSETG レイヤー番号 + 4, 0, 0, -3
            AWAIT 50
        NEXT
        CBGREMOVERANGE -3, -3

    ENDIF

    ;ステータス変動　表示はSKIP
    SKIPDISP 1
    CALL 技巧上昇判定(TARGET)
        SIF RESULT
            TRYCALLFORM KOJO_SKILLUP_技巧_{NO:TARGET}(TARGET)
    CALL 性知識上昇判定(TARGET)
        SIF RESULT
            TRYCALLFORM KOJO_SKILLUP_性知識_{NO:TARGET}(TARGET)
    CALL JUJUN_UP_CHECK(TARGET)
    CALL Love_CALC(TARGET)
    CALL FAVOR_CALC(TARGET)
    CALL 性癖取得チェック(TARGET)
    CALL 性癖成長チェック(TARGET)

    ;--------------------SOURCE_CHECKから抽出　中身はそのまま
        SIF CFLAG:TARGET:滞在期間 < 0
            CONTINUE
        SIF RCVAR:TARGET:表示カット
            SKIPDISP 1
        LOCAL:3 = 0
        FOR LOCAL,0,100
            ;コマンド入力前のCUPを追加
            CUP:TARGET:LOCAL += TCVAR:TARGET:(600 + LOCAL)
            SIF PALAMNAME:LOCAL == ""
                CONTINUE
            SIF CUP:TARGET:LOCAL - CDOWN:TARGET:LOCAL == 0
                CONTINUE
            IF !LOCAL:3
                PRINTL
                PRINTFORML %CALLNAME:TARGET%
            ENDIF
            LOCAL:3 ++
            LOCAL:2 = CUP:TARGET:LOCAL - CDOWN:TARGET:LOCAL + TCVAR:TARGET:(600 + LOCAL)
            PRINTFORM %PALAMNAME:LOCAL,8,LEFT% {PALAM:TARGET:LOCAL,10,LEFT} + {CUP:TARGET:LOCAL,10,LEFT} =   {PALAM:TARGET:LOCAL + CUP:TARGET:LOCAL,10,LEFT}
            IF 干渉強化変数:TARGET:干渉種類 - 5 == LOCAL
                SETCOLOR カラーパレット("黄")
                PRINT 　干渉効果発動中！
                RESETCOLOR
            ENDIF
            PRINTL
            PALAM:TARGET:LOCAL += CUP:TARGET:LOCAL - CDOWN:TARGET:LOCAL
            PALAM:TARGET:LOCAL = MAX(0, PALAM:TARGET:LOCAL)
            CUP:TARGET:LOCAL = 0
            CDOWN:TARGET:LOCAL = 0
        NEXT
        ;干渉ランク上昇判定
        IF 干渉強化変数:TARGET:干渉ランク < 5
            IF 干渉経験値テーブル:(干渉強化変数:TARGET:干渉ランク) <= 干渉強化変数:TARGET:干渉経験値
                干渉強化変数:TARGET:干渉ランク += 1
                PRINTFORMW %CALLNAME:TARGET%の干渉ランクが上昇した！
            ENDIF
        ENDIF
        SKIPDISP 0
    ;-----------------------------------------------------
    CALL TURN_RESET
ENDIF


IF おさわり警告フラグ
    VARSET おさわり容認カウンタ
    IF PALAM:TARGET:欲情 < PALAMLV:2
        プリントメッセージ保存 += @"<br>%CALLNAME:TARGET%は咎めるような視線をこちらに向けている<br>あまり乗り気ではないようだ・・・<br>"
    ELSE
        プリントメッセージ保存 += @"<br>%CALLNAME:TARGET%は咎めるような視線をこちらに向けている<br>もう少し丁寧にやった方がよさそうだ・・・<br>"
    ENDIF
    FOR LOCAL, 0, 10
        AWAIT 50
    NEXT
    おさわり警告カウンタ ++
    IF おさわり警告カウンタ > 2
       プリントメッセージ保存 += @"<br>%CALLNAME:TARGET%に制止されてしまった<br>どうやら怒らせてしまったようだ・・・<br>"
       HTML_PRINT プリントメッセージ保存
       プリントメッセージ保存 = 
       GOTO 終了処理
    ENDIF
ENDIF

HTML_PRINT プリントメッセージ保存
プリントメッセージ保存 = 

IF 画像切り替えフラグ
    GOTO 画像切り替え
ENDIF

GOTO 入力待ち

$終了処理

TIME = TIME保存 + (TIME - TIME保存) / 3
;PRINTFORM  CLICK {クリックカウンタ} COM {コマンドカウンタ} {(TIME - TIME保存) / 60}時間{(TIME - TIME保存) % 60}分経過
IF (TIME - TIME保存) / 60 > 0 ;1h以上経過
    プリントメッセージ保存 += @"<br>%CALLNAME:PLAYER%は%CALLNAME:TARGET%の身体を{(TIME - TIME保存) / 60}時間{(TIME - TIME保存) % 60}分たっぷり楽しんだ・・・"
ELSEIF (TIME - TIME保存) % 60 == 0 ;何もしなかった
    プリントメッセージ保存 += @"<br>%CALLNAME:PLAYER%は%CALLNAME:TARGET%に伸ばした手を引っ込めた・・・"
ELSE
    プリントメッセージ保存 += @"<br>%CALLNAME:PLAYER%は%CALLNAME:TARGET%の身体を{(TIME - TIME保存) % 60}分楽しんだ・・・"
ENDIF
HTML_PRINT プリントメッセージ保存

FOR LOCAL, 0, 5
    CBGREMOVERANGE -2, -2
    GDISPOSE レイヤー番号 + 1
    GCREATE レイヤー番号 + 1, 1600, 900
    CALL カットイン用カラーマトリクス取得(LOCAL * 10)
    GDRAWG レイヤー番号 + 1, レイヤー番号, 画像位置:0, 画像位置:1, GWIDTH(レイヤー番号) * 画像サイズ, GHEIGHT(レイヤー番号) * 画像サイズ, 0, 0, GWIDTH(レイヤー番号), GHEIGHT(レイヤー番号), カットイン用カラーマトリクス:0:0
    CBGSETG レイヤー番号 + 1, 0, 0, -2
    AWAIT 50
NEXT

CALL TURN_RESET
CALL 特定キャラモードリセット(TARGET)
CBGCLEAR
SETANIMETIMER -1
GDISPOSE レイヤー番号
GDISPOSE レイヤー番号 + 1
GDISPOSE レイヤー番号 + 2
GDISPOSE レイヤー番号 + 3
GDISPOSE レイヤー番号 + 10
GDISPOSE レイヤー番号 + 11
GDISPOSE レイヤー番号 + 12
GDISPOSE レイヤー番号 + 20
GDISPOSE レイヤー番号 + 22
GDISPOSE 80
GDISPOSE 81
GDISPOSE 82
GDISPOSE 83
SPRITEDISPOSEALL 0
RETURN


@ハートエフェクト(ARG, ARG:1, ARG:2, ARG:3, ARG:4)
#DIM カラーマトリクス, 5, 5

CBGREMOVERANGE -4, -4

;GID 80 に素材を読み込まれている前提
IF !GCREATED(82)
    GDISPOSE 82
    GCREATE 82, 720, 720

    カラーマトリクス:0:0 = 256,   0,   0,   0,   0
    カラーマトリクス:1:0 =   0,  64,   0,   0,   0
    カラーマトリクス:2:0 =   0,   0, 192,   0,   0
    カラーマトリクス:3:0 =   0,   0,   0, 256,   0
    カラーマトリクス:4:0 =   0,   0,   0,   0, 256
    
    GDRAWG 82, 80, 0, 0, 720, 720, 0, 0, 720, 720, カラーマトリクス:0:0
ENDIF

FOR LOCAL, 0, 6
    SPRITEDISPOSE @"クリックアイコン_ハート{LOCAL}"
    SPRITEANIMECREATE @"クリックアイコン_ハート{LOCAL}", 180, 180
    SPRITEANIMEADDFRAME @"クリックアイコン_ハート{LOCAL}", 82, 0, 360, 180, 180,  0, 0, 100
    SPRITEANIMEADDFRAME @"クリックアイコン_ハート{LOCAL}", 82, 180, 360, 180, 180,  0, 0, 100
    SPRITEANIMEADDFRAME @"クリックアイコン_ハート{LOCAL}", 82, 360, 360, 180, 180,    0, 0, 100
    SPRITEANIMEADDFRAME @"クリックアイコン_ハート{LOCAL}", 82, 540, 360, 180, 180, 0,   0, 100
    SPRITEANIMEADDFRAME @"クリックアイコン_ハート{LOCAL}", 82, 0, 540, 180, 180,  0,   0, 100
    SPRITEANIMEADDFRAME @"クリックアイコン_ハート{LOCAL}", 82, 180, 540, 180, 180, 0,  0, 100
    SPRITEANIMEADDFRAME @"クリックアイコン_ハート{LOCAL}", 82, 360, 540, 180, 180, 0,  0, 10000000
NEXT

LOCAL:2 = 3
SIF ARG:4 == 1
    LOCAL:2 = 6
FOR LOCAL, 0, LOCAL:2
    CBGSETSPRITE @"クリックアイコン_ハート{LOCAL}", RAND:(ARG:2) + ARG - 90, - RAND(ARG:3 * 8 / 10) - 90, -4
    AWAIT 10
NEXT


@クリックアイコン(ARG, ARG:1, ARG:2 = 0)
#DIM カラーマトリクス, 5, 5
;GID 80 READ 81 COLORFILTER

IF !GCREATED(80)
    GDISPOSE 80
    GCREATEFROMFILE 80, @"カットイン素材/クリックアイコン.png"
    GDISPOSE 81
    GCREATE 81, 720, 720
    GDRAWG 81, 80, 0, 0, 720, 720, 0, 0, 720, 720
    SETANIMETIMER 50
ENDIF

;GRAFICSの色変え
IF ARG:2 != LOCAL:2
    SELECTCASE ARG:2
        CASE 0
            カラーマトリクス:0:0 = 256,   0,   0,   0,   0
            カラーマトリクス:1:0 =   0, 256,   0,   0,   0
            カラーマトリクス:2:0 =   0,   0, 256,   0,   0
            カラーマトリクス:3:0 =   0,   0,   0, 256,   0
            カラーマトリクス:4:0 =   0,   0,   0,   0, 256
            LOCAL:2 = 0
        CASE 1 ;RED
            カラーマトリクス:0:0 = 256,   0,   0,   0,   0
            カラーマトリクス:1:0 =   0,  64,   0,   0,   0
            カラーマトリクス:2:0 =   0,   0,  64,   0,   0
            カラーマトリクス:3:0 =   0,   0,   0, 192,   0
            カラーマトリクス:4:0 =   0,   0,   0,   0, 256
            LOCAL:2 = 1
        CASE 2 ;GREEN
            カラーマトリクス:0:0 =   0,   0,   0,   0,   0
            カラーマトリクス:1:0 =   0, 256,   0,   0,   0
            カラーマトリクス:2:0 =   0,   0,   0,   0,   0
            カラーマトリクス:3:0 =   0,   0,   0, 192,   0
            カラーマトリクス:4:0 =   0,   0,   0,   0, 256
            LOCAL:2 = 2
        CASE 3 ;BLUE
            カラーマトリクス:0:0 =   0,   0,   0,   0,   0
            カラーマトリクス:1:0 =   0, 256,   0,   0,   0
            カラーマトリクス:2:0 =   0,   0, 256,   0,   0
            カラーマトリクス:3:0 =   0,   0,   0, 192,   0
            カラーマトリクス:4:0 =   0,   0,   0,   0, 256
            LOCAL:2 = 3
        CASE 4 ;YELLOW
            カラーマトリクス:0:0 = 256,   0,   0,   0,   0
            カラーマトリクス:1:0 =   0, 256,   0,   0,   0
            カラーマトリクス:2:0 =   0,   0,  64,   0,   0
            カラーマトリクス:3:0 =   0,   0,   0, 192,   0
            カラーマトリクス:4:0 =   0,   0,   0,   0, 256
            LOCAL:2 = 4
        CASE 5 ;ORANGE
            カラーマトリクス:0:0 = 256,   0,   0,   0,   0
            カラーマトリクス:1:0 =   0, 192,   0,   0,   0
            カラーマトリクス:2:0 =   0,   0,   0,   0,   0
            カラーマトリクス:3:0 =   0,   0,   0, 192,   0
            カラーマトリクス:4:0 =   0,   0,   0,   0, 256
            LOCAL:2 = 5
        CASE 6 ;PINK
            カラーマトリクス:0:0 = 256,   0,   0,   0,   0
            カラーマトリクス:1:0 =   0,   0,   0,   0,   0
            カラーマトリクス:2:0 =   0,   0, 192,   0,   0
            カラーマトリクス:3:0 =   0,   0,   0, 192,   0
            カラーマトリクス:4:0 =   0,   0,   0,   0, 256
            LOCAL:2 = 6
    ENDSELECT
    GDISPOSE 81
    GCREATE 81, 720, 720
    GDRAWG 81, 80, 0, 0, 720, 720, 0, 0, 720, 720, カラーマトリクス:0:0
ENDIF

SIF LOCAL:1 > 5
    LOCAL:1 = 0

SPRITEDISPOSE @"クリックアイコン{LOCAL:1}"
SPRITEANIMECREATE @"クリックアイコン{LOCAL:1}", 180, 180

SPRITEANIMEADDFRAME @"クリックアイコン{LOCAL:1}", 81, 0, 0, 180, 180,  0, 0, 50
SPRITEANIMEADDFRAME @"クリックアイコン{LOCAL:1}", 81, 180, 0, 180, 180,  0, 0, 50
SPRITEANIMEADDFRAME @"クリックアイコン{LOCAL:1}", 81, 360, 0, 180, 180,    0, 0, 50
SPRITEANIMEADDFRAME @"クリックアイコン{LOCAL:1}", 81, 540, 0, 180, 180, 0,   0, 50
SPRITEANIMEADDFRAME @"クリックアイコン{LOCAL:1}", 81, 0, 180, 180, 180,  0,   0, 50
SPRITEANIMEADDFRAME @"クリックアイコン{LOCAL:1}", 81, 180, 180, 180, 180, 0,  0, 50
SPRITEANIMEADDFRAME @"クリックアイコン{LOCAL:1}", 81, 360, 180, 180, 180, 0,  0, 10000000

CBGREMOVERANGE -7 - LOCAL:1, -7 - LOCAL:1

CBGSETSPRITE @"クリックアイコン{LOCAL:1}", ARG - 90, ARG:1 + 90, -7 - LOCAL:1

LOCAL:1 ++


@おさわりコマンドリスト(左右, おさわりスタンス, 選択ID)
#FUNCTIONS
#DIM 左右 ;L=0
#DIM おさわりスタンス
#DIM 選択ID
{
    ;おさわりコマンドテンプレ
	#DIMS CONST おさわりコマンドリスト = 
	"背中", "耳", "角", "尻尾", "お尻", "お腹", "頭", "髪", "脇", "へそ", 
	"口", "太もも", "下腹部", "乳首", "C", "スカート", "V", "B", "A", "", 
}

{
    #DIMS CONST おさわりコマンドリストL0 = 
	"306_1", "306_2", "306_3", "306_4", "306_5", "306_6", "306_7", "306_8", "306_9", "306_11", 
	"311", "306_10", "306_14", "", "", "326", "", "", "306_5", "", 
}
{
    #DIMS CONST おさわりコマンドリストR0 = 
	"306_1", "306_2", "306_12", "306_13", "306_5", "306_6", "306_7", "306_8", "306_9", "306_11", 
	"311", "306_10", "306_14", "", "", "326", "", "", "306_5", "", 
}
{
    #DIMS CONST おさわりコマンドリストL1 = 
	"306_1", "306_2", "306_12", "306_13", "306_5", "306_6", "306_7", "306_8", "306_9", "306_11", 
	"311", "306_10", "306_14", "324", "327", "326", "327", "321", "306_5", "", 
}
{
    #DIMS CONST おさわりコマンドリストR1 = 
	"306_1", "306_2", "306_3", "322", "320", "306_6", "306_7", "306_8", "306_9", "306_11", 
	"20", "306_10", "306_14", "324", "327", "326", "327", "323", "320", "", 
}

SELECTCASE おさわりスタンス
    CASE 0
        IF 左右
            RETURNF おさわりコマンドリストR0:選択ID
        ELSE
            RETURNF おさわりコマンドリストL0:選択ID
        ENDIF
    CASE 1
        IF 左右
            RETURNF おさわりコマンドリストR1:選択ID
        ELSE
            RETURNF おさわりコマンドリストL1:選択ID
        ENDIF
ENDSELECT

