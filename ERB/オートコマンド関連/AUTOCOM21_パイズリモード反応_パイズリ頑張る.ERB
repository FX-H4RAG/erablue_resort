;------------------------------------------------------------------------------
;パイズリ時、頑張ってパイズリをして通常より大きな快楽を与える
;------------------------------------------------------------------------------
@AUTOCOM21
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;奉仕快楽フラグ
TFLAG:30 = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 0
DOWNBASE:気力 += 20

CALL SOURCE_CALC_露出_反感無し(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触_不快無し(TARGET, PLAYER, 150)
CALL SOURCE_CALC_奉仕(TARGET, PLAYER, 200)
CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 50)
LOCAL = (ABL:TARGET:技巧 + TALENT:TARGET:バストサイズ) * 50
CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, LOCAL)

AUTOCOM用変数:TARGET:21:0 = TIME

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:パイズリ経験 ++

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 7
	EXP:PLAYER:同性愛経験 += 7
ENDIF

;愛情経験
SIF TALENT:恋慕 == 1
	EXP:愛情経験 ++

RETURN 1

@AUTOCOM_ABLE21
SIF !TARGET
	RETURN 0
;パイズリモードじゃない時はアウト
SIF MODE_確認("パイズリ系", TARGET) == 0
	RETURN 0
;30分に１回のみ発動
SIF TIME - AUTOCOM用変数:TARGET:21:0 < 31
	RETURN 0
;無関心はアウト、恥じらいは性欲必要
SIF TALENT:無関心
	RETURN 0
SIF TALENT:羞恥心 > 0 && BASE:性欲 < 8000
	RETURN 0
;性知識2必要
SIF 知識素質:性知識 < 2
	RETURN 0
;技巧2必要
SIF ABL:技巧 < 2
	RETURN 0
;基礎確率1/4、素質により変動
LOCAL = MAX(4 - (ABL:奉仕精神 / 3) - TALENT:恋慕 - TALENT:性的興味, 1)
SIF RAND:LOCAL
	RETURN 0
RETURN 1

@MESSAGE_AUTOCOM21
;そもそも技巧2だと巨乳以上のみ、3は普乳、貧乳は4ないとパイズリが出来ないのでそれを前提に記述

PRINTFORM %CALLNAME:TARGET%は

SELECTCASE ABL:技巧
	CASE IS <= 2
		PRINTFORML ぎこちないながらも、その豊満な乳で%CALLNAME:PLAYER%の陰茎を挟み、
		PRINTL 少しでも気持ちよくなってもらおうと頑張っている…
	CASE 3 TO 4
		PRINTFORML こなれた様子で%CALLNAME:PLAYER%の反応を見ながら胸の動きを調節し、
		PRINTFORML %CALLNAME:PLAYER%が感じる動きを探っている…
	CASE IS >= 5
		IF TALENT:バストサイズ < 0
			PRINT 控えめな大きさにも関わらず、巧みに胸を使って
		ELSEIF TALENT:バストサイズ > 1
			PRINT その豊かな乳肉を存分に使って
		ENDIF
		PRINTFORML %CALLNAME:PLAYER%の弱点を的確に責め立てている…
ENDSELECT



