;------------------------------------------------------------------------------
;合意済みの相手
;性欲一定以上だとこっそり乳を露出して挑発してくる
;------------------------------------------------------------------------------
@AUTOCOM2
CALL SOURCE_CALC_露出(TARGET, -1, 100)
AUTOCOM用変数:TARGET:2:0 = TIME

@AUTOCOM_ABLE2
SIF !TARGET
	RETURN 0
;ターゲットが合意済み
SIF !CFLAG:1
	RETURN 0
;うふふ中ではない
SIF CFLAG:うふふ
	RETURN 0
;２時間に１回のみ発動
SIF TIME - AUTOCOM用変数:TARGET:2:0 < 120
	RETURN 0
;快楽刻印必須
SIF MARK:快楽刻印 < 1
	RETURN 0
;生意気必須
SIF TALENT:応答 < 1
	RETURN 0
LOCAL = 8000
;性欲一定以上
SIF BASE:性欲 < 8000
	RETURN 0
;真面目、無関心、恥じらいがあるとアウト
SIF TALENT:自制心 > 0
	RETURN 0
SIF TALENT:無関心
	RETURN 0
SIF TALENT:羞恥心 > 0
	RETURN 0
;性知識0必要
SIF 知識素質:性知識 < 0
	RETURN 0
RETURN 1

@MESSAGE_AUTOCOM2
PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%にだけ見えるよう、こっそりと

IF TEQUIP:上半身鎧あり
	PRINT 鎧を緩めて、
ENDIF

IF TEQUIP:上半身服あり
	IF TEQUIP:はだけ可
		PRINT 少し服をずらし
	ELSE
		PRINT 服の隙間から
	ENDIF
ELSEIF TEQUIP:上タイツあり
	PRINT タイツを大きく広げて
ELSE
	PRINT
ENDIF

IF TALENT:バストサイズ < 0
	PRINT なだらかな胸元と
ELSEIF TALENT:バストサイズ < 2
	PRINT 形の良い二つの丘陵と
ELSE
	PRINT 大きな乳房が作る深い谷間と
ENDIF

IF TEQUIP:上半身下着あり == 1
	PRINT 下着を見せつけてきた…
ELSEIF  TEQUIP:上半身下着あり == 2
	PRINT 先端だけを覆う小さなニプレスを見せつけてきた…
ELSE
	PRINT 綺麗な色の乳首を見せつけてきた…
ENDIF



