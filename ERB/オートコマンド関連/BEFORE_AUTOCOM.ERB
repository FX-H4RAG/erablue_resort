@BEFORE_AUTOCOM
#DIM 行動候補, 500
#DIMS 行動候補文字列, 500
#DIM 同室者番号
#DIM キャラ番号
#DIMS AUTOCOM_候補情報, 100
#DIMS 候補者_AUTO番号_文字列分割, 3
#DIM ランダム格納
#DIM PREAUTO候補者数
#DIM AUTO候補者数
#DIM REVERSEAUTO候補者数
#DIM ALLAUTOCOM番号
#DIM COM番号
#DIM 派生番号
#DIM TARGET保存
#DIM PLAYER保存

VARSET AUTOCOM_候補情報
PREAUTO候補者数 = 0
AUTO候補者数 = 0
REVERSEAUTO候補者数 = 0
AUTOCOM_派生保存 = 

;次ターンがキャラの自動行動かどうかの判定処理
;RETURNの数値をそのままフラグに入れ込むので、0を返せば動かない、他を返せば動く
;ついでにフラグにAUTOCOM番号を入れといて行動内容も保存しとく
;タゲなしの場合は即戻る
IF !TARGET
	IF RFLAG:休憩選択フラグ
		;休憩時TARGETが居ない場合に特殊処理に派生する
		RETURN -1
	ELSEIF RFLAG:自慰選択フラグ && CFLAG:PLAYER:現在位置 == CFLAG:PLAYER:自室位置
		;休憩時TARGETが居ない場合に特殊処理に派生する
		CALL 自宅訪問からの目撃者決定
		RETURN RESULT
	ENDIF
	GOTO 非同室AUTOCOM
ENDIF

SIF FLAG:オートコマンド切り替えオプション == 7
	RETURN 0

;二人同時にオートコマンドの対象になる場合、もう一人を予約する
;現状、射精後の精飲or乳間見せつけのみ
IF GROUPMATCH(TFLAG:オートコマンドフラグ, 1, 2) && !RFLAG:フィニッシュ相手番号 && TFLAG:フィニッシュ相手番号2
	AUTOCOM_相手番号保存 = TFLAG:フィニッシュ相手番号2
	TFLAG:フィニッシュ相手番号2 = 0
	RETURN TFLAG:オートコマンドフラグ
ENDIF

FOR 同室者番号, 1, GET_TARGETNUM() + 1
	;あなたは動かない
	SIF NO:(TARGET:同室者番号) == MASTER
		CONTINUE
	;相手が寝てる場合は次へ
	SIF CFLAG:(TARGET:同室者番号):睡眠
		CONTINUE
	;相手が緊縛されてる場合は次へ
	SIF MODE_確認_ターゲット側("触手緊縛", TARGET:同室者番号) > -1
		CONTINUE
	;相手が強い絶頂してる時は動かない
	SIF NOWEX:(TARGET:同室者番号):多重絶頂 > 1
		CONTINUE
	;食いしばり中は動かない
	SIF TCVAR:(TARGET:同室者番号):食いしばりフラグ
		CONTINUE
	;体力限界中も動かない
	SIF CFLAG:(TARGET:同室者番号):体力限界
		CONTINUE
	;ハーヴィンオナホ
	SIF MODE_確認_ターゲット側("ハーヴィンオナホ系", TARGET:同室者番号) > -1
		CONTINUE

	SIF GETBIT(FLAG:オートコマンド切り替えオプション, 0)
		GOTO オート一般コマンド

	VARSET 行動候補
	VARSET 行動候補文字列

	LOCAL:1 = 0
	;優先コマンドの有無（あらゆる反応に優先して発生する特殊オートコマンド）
	;PRE_AUTOCOMを全部見て行って条件とマッチしたCOM番号を登録しておく
	FOR COM番号, 1, 100
		TRYCCALLFORM PRE_AUTOCOM_ABLE{COM番号}(TARGET:同室者番号)
			IF RESULT
				行動候補:(LOCAL:1) = COM番号
				LOCAL:1 += 1
			ENDIF
		CATCH
		ENDCATCH
	NEXT

	;登録したCOM番号からランダムで選出
	IF 行動候補:0
		IF AUTO候補者数 || REVERSEAUTO候補者数
			;最優先コマンドが優先のため通常AUTOCOMは削除
			VARSET AUTOCOM_候補情報
			AUTO候補者数 = 0
			REVERSEAUTO候補者数 = 0
		ENDIF
		ランダム格納 = RAND:(LOCAL:1)
		AUTOCOM_候補情報:PREAUTO候補者数 = {TARGET:同室者番号}_{行動候補:ランダム格納}
		PREAUTO候補者数 += 1
	ENDIF

	;最優先コマンドが一人でも候補に入っている場合、同じく最優先コマンドしか候補に上がらせないためCONTINUE
	SIF PREAUTO候補者数 > 0
		CONTINUE

	;既に現在のサイクルがオートコマンドだった場合、最優先コマンド以外は発動しない
	SIF TFLAG:オートコマンドフラグ
		RETURN 0

	LOCAL:1 = 0
	;反応コマンドの有無（通常優先度の反応コマンド）
	;AUTOCOMを全部見て行って条件とマッチしたCOM番号+1000を登録しておく
	FOR COM番号, 1, 100
		TRYCCALLFORM AUTOCOM_ABLE{COM番号}(TARGET:同室者番号)
			IF RESULT
				行動候補:(LOCAL:1) = COM番号 + 1000
				LOCAL:1 += 1
			ENDIF
		CATCH
		ENDCATCH
	NEXT

	;登録したCOM番号からランダムで選出
	IF 行動候補:0
		IF REVERSEAUTO候補者数
			;通常オートコマンドが優先のため逆COMは削除
			VARSET AUTOCOM_候補情報
			REVERSEAUTO候補者数 = 0
		ENDIF
		ランダム格納 = RAND:(LOCAL:1)
		AUTOCOM_候補情報:AUTO候補者数 = {TARGET:同室者番号}_{行動候補:ランダム格納}
		AUTO候補者数 += 1
	ENDIF

	$オート一般コマンド
	;既に現在のサイクルがオートコマンドだった場合、最優先コマンド以外は発動しない
	SIF TFLAG:オートコマンドフラグ
		RETURN 0

	;通常のオートコマンドを優先して発動
	SIF AUTO候補者数 > 0
		CONTINUE

	;現状うふふでしか動かないように制限
	SIF CFLAG:(TARGET:同室者番号):うふふ == 0
		CONTINUE

	自慰ターゲット保存 = TARGET:同室者番号
	LOCAL:1 = 0
	TARGET保存 = TARGET
	PLAYER保存 = PLAYER
	TARGET = MASTER
	PLAYER = TARGET:同室者番号
	;反応コマンドの有無（通常優先度の反応コマンド）
	;許可されたCOMを全部見て行って条件とマッチしたCOM番号+3000を登録しておく
	FOR COM番号, 0, 500
		SIF COM番号 >= 370 && COM番号 <= 390
			CONTINUE
		FOR 派生番号, 1, 50
			オートコマンド許可 = 0
			TRYCALLFORMF COMTYPE_S_{COM番号}_{派生番号}
			;履歴にあるならダメ
			SIF CSTR:PLAYER:オートコマンド履歴０ == @"{COM番号 + 3000}_{派生番号}"
				CONTINUE
			SIF CSTR:PLAYER:オートコマンド履歴１ == @"{COM番号 + 3000}_{派生番号}"
				CONTINUE
			SIF CSTR:PLAYER:オートコマンド履歴２ == @"{COM番号 + 3000}_{派生番号}"
				CONTINUE
			SIF CSTR:PLAYER:オートコマンド履歴３ == @"{COM番号 + 3000}_{派生番号}"
				CONTINUE
			SIF CSTR:PLAYER:オートコマンド履歴４ == @"{COM番号 + 3000}_{派生番号}"
				CONTINUE
			;オプションによる制御
			SIF オートコマンド許可 == 1 && GETBIT(FLAG:オートコマンド切り替えオプション, 1)
				CONTINUE
			SIF オートコマンド許可 == 2 && GETBIT(FLAG:オートコマンド切り替えオプション, 2)
				CONTINUE
			;なにかのモード中は自慰しない
			SIF オートコマンド許可 == 2 && (MODE_確認_プレイヤー側("ALL", PLAYER) > -1 || MODE_確認_ターゲット側("ALL", PLAYER) > -1)
				CONTINUE
			IF オートコマンド許可
				TRYCALLFORM COM_ABLE_S{COM番号}_{派生番号}
				IF RESULT
					行動候補文字列:(LOCAL:1) = {COM番号 + 3000}_{派生番号}
					LOCAL:1 += 1
				ENDIF
			ENDIF
		NEXT
	NEXT

	FOR COM番号, 0, 700
		オートコマンド許可 = 0
		TRYCALLFORMF COMTYPE_{COM番号}
		;履歴にあるならダメ
		SIF CSTR:PLAYER:オートコマンド履歴０ == @"{COM番号 + 3000}"
			CONTINUE
		SIF CSTR:PLAYER:オートコマンド履歴１ == @"{COM番号 + 3000}"
			CONTINUE
		SIF CSTR:PLAYER:オートコマンド履歴２ == @"{COM番号 + 3000}"
			CONTINUE
		SIF CSTR:PLAYER:オートコマンド履歴３ == @"{COM番号 + 3000}"
			CONTINUE
		SIF CSTR:PLAYER:オートコマンド履歴４ == @"{COM番号 + 3000}"
			CONTINUE
		;オプションによる制御
		SIF オートコマンド許可 == 1 && GETBIT(FLAG:オートコマンド切り替えオプション, 1)
			CONTINUE
		SIF オートコマンド許可 == 2 && GETBIT(FLAG:オートコマンド切り替えオプション, 2)
			CONTINUE
		IF オートコマンド許可
			TRYCALLFORM COM_ABLE{COM番号}
			IF RESULT
				行動候補文字列:(LOCAL:1) = {COM番号 + 3000}
				LOCAL:1 += 1
			ENDIF
		ENDIF
	NEXT
	TARGET = TARGET保存
	PLAYER = PLAYER保存

	;登録したCOM番号からランダムで選出
	IF LOCAL:1 > 0
		FOR LOCAL, 0, LOCAL:1
			DEBUGPRINTFORML {LOCAL}：%CALLNAME:(TARGET:同室者番号)%：%行動候補文字列:LOCAL%
		NEXT

		ランダム格納 = RAND:(LOCAL:1)
		AUTOCOM_候補情報:REVERSEAUTO候補者数 = {TARGET:同室者番号}_%行動候補文字列:ランダム格納%
		REVERSEAUTO候補者数 += 1
	ENDIF
NEXT

;同室者全員分見終わったら同一優先度からRAND
VARSET 候補者_AUTO番号_文字列分割
IF PREAUTO候補者数 > 0
	ランダム格納 = RAND:PREAUTO候補者数
	SPLIT AUTOCOM_候補情報:ランダム格納, "_", 候補者_AUTO番号_文字列分割
	AUTOCOM_相手番号保存 = TOINT(候補者_AUTO番号_文字列分割:0)
	RETURN TOINT(候補者_AUTO番号_文字列分割:1)
ELSEIF AUTO候補者数 > 0
	ランダム格納 = RAND:AUTO候補者数
	SPLIT AUTOCOM_候補情報:ランダム格納, "_", 候補者_AUTO番号_文字列分割
	AUTOCOM_相手番号保存 = TOINT(候補者_AUTO番号_文字列分割:0)
	RETURN TOINT(候補者_AUTO番号_文字列分割:1)
ELSEIF REVERSEAUTO候補者数 > 0
	ランダム格納 = RAND:REVERSEAUTO候補者数
	SPLIT AUTOCOM_候補情報:ランダム格納, "_", 候補者_AUTO番号_文字列分割
	AUTOCOM_相手番号保存 = TOINT(候補者_AUTO番号_文字列分割:0)
	IF 候補者_AUTO番号_文字列分割:2 != ""
		AUTOCOM_派生保存 = %候補者_AUTO番号_文字列分割:2%
		CALL オートコマンド履歴保存(@"%候補者_AUTO番号_文字列分割:1%_%候補者_AUTO番号_文字列分割:2%")
	ELSE
		CALL オートコマンド履歴保存(@"%候補者_AUTO番号_文字列分割:1%")
	ENDIF
	RETURN TOINT(候補者_AUTO番号_文字列分割:1)
ENDIF

;同室者にオートコマンドが無い時、非同室オートコマンドの判定へ

$非同室AUTOCOM

VARSET AUTOCOM_候補情報
VARSET 行動候補
PREAUTO候補者数 = 0
AUTO候補者数 = 0

;既に現在のサイクルがオートコマンドだった場合、最優先コマンド以外は発動しない
SIF TFLAG:オートコマンドフラグ
	RETURN 0

;うふふ時、飲み会時は非同室オートコマンドは発生しない
SIF TFLAG:調教モード == 2 || TFLAG:調教モード == 3
	RETURN 0

;隠密時は発動しない
SIF CFLAG:PLAYER:隠密
	RETURN 0

;処理の単純化・高速化のためリストは作らず、先頭から順番に判定して引っかかったらそこで止める
;ALLAUTOCOMを作る時は条件設定に注意すること

LOCAL = ENUMFUNCBEGINSWITH("ALLAUTOCOM_ABLE")

FOR キャラ番号, 1, CHARANUM
	SIF CFLAG:キャラ番号:滞在期間 < 0
		CONTINUE
	FOR ALLAUTOCOM番号, 0, LOCAL
		CALLFORM %RESULTS:ALLAUTOCOM番号%(キャラ番号)
		IF RESULT
			AUTOCOM_相手番号保存 = キャラ番号
			LOCALS = %SUBSTRING(RESULTS:ALLAUTOCOM番号, 15, -1)%
			RETURN TOINT(LOCALS) + 2000
		ENDIF
	NEXT
NEXT

RETURN 0

@AUTOCOM用変数リセット()
VARSET TIME_胸部チラ見せ
VARSET TIME_遊びに誘う
VARSET TIME_管理人を労う
VARSET TIME_いちゃつく
VARSET TIME_ボディタッチ
VARSET TIME_フェラ頑張る
VARSET TIME_パイズリ頑張る
VARSET TIME_手コキ頑張る
VARSET TIME_腰が浮く
VARSET TIME_勝手に性感帯
VARSET TIME_膣締め
VARSET TIME_A腰が浮く
VARSET TIME_A勝手に性感帯
VARSET TIME_直腸締め
VARSET TIME_逆アナル突入


@AUTOCOM_相手判別関数(キャラ番号)
#FUNCTION
#DIM キャラ番号
#DIM 同室者番号
#DIM 相手判別変数
相手判別変数 = 0

;ビット演算で返す
;ビット０：１であなたがいる、０でいない ビット１：１で貴方以外の他人がいる、０でいない

;まずあなたがいるかどうか＝隠密状態のチェック
IF CFLAG:PLAYER:隠密
	IF CFLAG:キャラ番号:隠密
		SETBIT 相手判別変数, 0
	ELSE
		CLEARBIT 相手判別変数, 0
	ENDIF
ELSE
	IF CFLAG:キャラ番号:隠密
		CLEARBIT 相手判別変数, 0
	ELSE
		SETBIT 相手判別変数, 0
	ENDIF
ENDIF

;あなた以外がいるかどうか
IF CFLAG:キャラ番号:隠密
	;同時に複数キャラは隠密しないはずなので、隠密時はいない扱い
	CLEARBIT 相手判別変数, 1
ELSE
	CLEARBIT 相手判別変数, 1
	;同室者を見る
	FOR 同室者番号, 1, GET_TARGETNUM() + 1
		SIF キャラ番号 == TARGET:同室者番号
			CONTINUE
		SETBIT 相手判別変数, 1
		BREAK
	NEXT
ENDIF

RETURNF 相手判別変数

@オートコマンド履歴保存(記録文字列)
#DIMS 記録文字列
;オートコマンド履歴の保存
;要求を断った時にも記録出来るよう、種別決定時点で記録する

;うふふ中のみ記録する
IF CFLAG:MASTER:うふふ
	CSTR:AUTOCOM_相手番号保存:オートコマンド履歴４ '= CSTR:AUTOCOM_相手番号保存:オートコマンド履歴３
	CSTR:AUTOCOM_相手番号保存:オートコマンド履歴３ '= CSTR:AUTOCOM_相手番号保存:オートコマンド履歴２
	CSTR:AUTOCOM_相手番号保存:オートコマンド履歴２ '= CSTR:AUTOCOM_相手番号保存:オートコマンド履歴１
	CSTR:AUTOCOM_相手番号保存:オートコマンド履歴１ '= CSTR:AUTOCOM_相手番号保存:オートコマンド履歴０
	CSTR:AUTOCOM_相手番号保存:オートコマンド履歴０ '= 記録文字列
ENDIF

@オートコマンド許可取り()
;許可が必要な場合のオートコマンドの許可表示処理

;相手があなたじゃないなら許可出し無し
SIF TARGET != MASTER
	RETURN 0

IF RSTR:SELECTCOM_S受け渡し != ""
	;派生コマンド
	CALLFORMF COMNAME_S%RSTR:SELECTCOM_S受け渡し%
ELSE
	;通常コマンド
	CALLFORMF COMNAME{SELECTCOM}
ENDIF

DRAWLINE
PRINTPLAINFORM %CALLNAME:PLAYER%が%CALLNAME:TARGET%に[%TSTR:コマンド名受渡%]を要求しているようだ。
PRINTL
PRINTL 実行しますか？
PRINTBUTTON "[0] はい", 0
PRINTL 
PRINTBUTTON "[1] いいえ", 1
PRINTL

BINPUT

IF RESULT == 0
	RETURN 0
ELSE
	PRINTFORMW %CALLNAME:TARGET%は%CALLNAME:PLAYER%の要求を断った…
	RETURN -1
ENDIF