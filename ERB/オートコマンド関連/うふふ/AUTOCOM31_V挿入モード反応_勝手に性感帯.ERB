;------------------------------------------------------------------------------
;V挿入時、自分で性感帯を刺激しはじめる
;------------------------------------------------------------------------------
@AUTOCOM31
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_快Ｖ(AUTOCOM_相手番号保存, PLAYER, 600)
CALL SOURCE_CALC_露出_反感無し(AUTOCOM_相手番号保存, PLAYER, 80)
CALL SOURCE_CALC_接触_不快無し(AUTOCOM_相手番号保存, PLAYER, 200)
CALL SOURCE_CALC_快Ｃ(PLAYER, AUTOCOM_相手番号保存, 500)

TIME_勝手に性感帯:AUTOCOM_相手番号保存:0 = TIME

;-------------------------------------------------
;部位決定
;-------------------------------------------------
LOCALS = 
SIF EXP:AUTOCOM_相手番号保存:Ｇスポ開発 > 99
	LOCALS = Ｇスポ開発フラグ
IF EXP:AUTOCOM_相手番号保存:ポルチオ開発 > 99
	IF LOCALS == ""
		LOCALS = ポルチオ開発フラグ
	ELSE
		;両方100以上の場合はランダムで部位決定
		SIF RAND:2
			LOCALS = ポルチオ開発フラグ
	ENDIF
ENDIF

SIF LOCALS != ""
	RCVAR:AUTOCOM_相手番号保存:LOCALS = 4
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:AUTOCOM_相手番号保存:Ｖ性交経験 ++

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(AUTOCOM_相手番号保存,PLAYER)
	EXP:AUTOCOM_相手番号保存:同性愛経験 += 4
	EXP:PLAYER:同性愛経験 += 4
ENDIF

EXP:AUTOCOM_相手番号保存:愛情経験 += TALENT:AUTOCOM_相手番号保存:恋慕 * 2

RETURN 1

@AUTOCOM_ABLE31(キャラ番号)
#DIM キャラ番号
;V挿入時のみ
SIF MODE_確認_ターゲット側("Ｖ挿入系", キャラ番号) < 0
	RETURN 0
;30分に１回のみ発動
SIF TIME - TIME_勝手に性感帯:キャラ番号:0 < 31
	RETURN 0
;欲情PALAMLv4必要
SIF PALAM:キャラ番号:欲情 < PALAMLV:4
	RETURN 0
;無関心はアウト、真面目、恥じらいは性欲必要
SIF TALENT:キャラ番号:自制心 > 0 && BASE:キャラ番号:性欲 < 1500
	RETURN 0
SIF TALENT:キャラ番号:無関心 > 0
	RETURN 0
SIF TALENT:キャラ番号:羞恥心 > 0 && BASE:キャラ番号:性欲 < 1500
	RETURN 0
;技巧2必要
SIF ABL:キャラ番号:技巧 < 2
	RETURN 0
;性感帯開発必須
SIF EXP:キャラ番号:ポルチオ開発 < 100 && EXP:キャラ番号:Ｇスポ開発 < 100
	RETURN 0
;基礎確率1/4、素質により変動
LOCAL = MAX(4 - TALENT:キャラ番号:性的興味 - TALENT:キャラ番号:快感応答 - TALENT:キャラ番号:性欲, 1)
SIF RAND:LOCAL
	RETURN 0
RETURN 1

@MESSAGE_AUTOCOM31
PRINTFORM %CALLNAME:AUTOCOM_相手番号保存%は
IF 知識素質:AUTOCOM_相手番号保存:性知識 <= 0
	PRINTFORML 本能的に腰を動かし、自分の"良い"ところに%CALLNAME:PLAYER%の陰茎を当てて快楽を堪能している…
ELSEIF TALENT:AUTOCOM_相手番号保存:羞恥心 > 0
	PRINTFORML %CALLNAME:PLAYER%に気づかれないよう、自分の性感帯に当たるよう腰を動かしている
	PRINTFORML それを指摘してやると、%CALLNAME:AUTOCOM_相手番号保存%は顔を真っ赤に染めてしまった…
ELSE
	PRINTFORML 腰の動きを調整し、自身の性感帯に陰茎を当てて快楽を堪能している…
ENDIF

