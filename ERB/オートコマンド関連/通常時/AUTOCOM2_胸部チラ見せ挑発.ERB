;------------------------------------------------------------------------------
;合意済みの相手
;性欲一定以上だとこっそり乳を露出して挑発してくる
;------------------------------------------------------------------------------
@AUTOCOM2
CALL SOURCE_CALC_露出_反感無し(AUTOCOM_相手番号保存, PLAYER, 100)
TIME_胸部チラ見せ:AUTOCOM_相手番号保存:0 = TIME

RETURN 1

@AUTOCOM_ABLE2(キャラ番号)
#DIM キャラ番号
;あなたが必要
SIF GETBIT(AUTOCOM_相手判別関数(キャラ番号), 0) == 0
	RETURN 0
;うふふ済み
SIF !初体験日:キャラ番号:初うふふ
	RETURN 0
;露出癖１以上
SIF ABL:キャラ番号:露出癖 < 1
	RETURN 0
;うふふ中ではない
SIF CFLAG:キャラ番号:うふふ
	RETURN 0
;２時間に１回のみ発動
SIF TIME - TIME_胸部チラ見せ:キャラ番号:0 < 120
	RETURN 0
;生意気必須
SIF TALENT:キャラ番号:応答 < 1
	RETURN 0
;性欲一定以上
SIF BASE:キャラ番号:性欲 < 1500
	RETURN 0
;真面目、無関心、恥じらいがあるとアウト
SIF TALENT:キャラ番号:自制心 > 0
	RETURN 0
SIF TALENT:キャラ番号:無関心 > 0
	RETURN 0
SIF TALENT:キャラ番号:羞恥心 > 0
	RETURN 0
;性知識0必要
SIF 知識素質:キャラ番号:性知識 < 0
	RETURN 0
RETURN 1

@MESSAGE_AUTOCOM2
PRINTFORM %CALLNAME:AUTOCOM_相手番号保存%は%CALLNAME:PLAYER%にだけ見えるよう、こっそりと

IF TEQUIP:AUTOCOM_相手番号保存:上半身鎧あり
	PRINT 鎧を緩めて、
ENDIF

IF TEQUIP:AUTOCOM_相手番号保存:上半身服あり
	IF TEQUIP:AUTOCOM_相手番号保存:はだけ可
		PRINT 少し服をずらし
	ELSE
		PRINT 服の隙間から
	ENDIF
ELSEIF TEQUIP:AUTOCOM_相手番号保存:上タイツあり
	PRINT タイツを大きく広げて
ELSE
	PRINT
ENDIF

IF TALENT:AUTOCOM_相手番号保存:バストサイズ < 0
	PRINT なだらかな胸元と
ELSEIF TALENT:AUTOCOM_相手番号保存:バストサイズ < 2
	PRINT 形の良い二つの丘陵と
ELSE
	PRINT 大きな乳房が作る深い谷間と
ENDIF

IF TEQUIP:AUTOCOM_相手番号保存:上半身下着あり == 1
	PRINT 下着を見せつけてきた…
ELSEIF  TEQUIP:AUTOCOM_相手番号保存:上半身下着あり == 2
	PRINT 先端だけを覆う小さなニプレスを見せつけてきた…
ELSE
	PRINT 綺麗な色の乳首を見せつけてきた…
ENDIF



