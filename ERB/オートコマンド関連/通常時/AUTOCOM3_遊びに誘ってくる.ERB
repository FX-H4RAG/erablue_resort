;------------------------------------------------------------------------------
;通常時、遊べる施設で遊びに誘ってくる
;------------------------------------------------------------------------------
@AUTOCOM3
CALL SOURCE_CALC_歓楽(AUTOCOM_相手番号保存, PLAYER, 500)
CALL SOURCE_CALC_好感度要素_友情度UP(AUTOCOM_相手番号保存, PLAYER, 250)
TIME_遊びに誘う:AUTOCOM_相手番号保存:0 = TIME

@AUTOCOM_ABLE3(キャラ番号)
#DIM キャラ番号
;友人以上
SIF CFLAG:キャラ番号:好感度レベル < 関係閾値:2
	RETURN 0
;４つの好感度要素で友情度が最も高い
SIF CFLAG:キャラ番号:友情度 != MAX(CFLAG:キャラ番号:友情度, CFLAG:キャラ番号:信頼度, CFLAG:キャラ番号:恋心度, CFLAG:キャラ番号:性愛度)
	RETURN 0
;遊ぶ系施設
SIF !ENJOYROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	RETURN 0
;２時間に１回のみ発動
SIF TIME - TIME_遊びに誘う:キャラ番号:0 < 120
	RETURN 0
;うふふ中ではない
SIF CFLAG:キャラ番号:うふふ
	RETURN 0
RETURN 1

@MESSAGE_AUTOCOM3
PRINTFORML %CALLNAME:AUTOCOM_相手番号保存%は%CALLNAME:PLAYER%を誘い、一緒に%GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)%で楽しく遊んだ

;プール系、及び爆乳以上だとぽろりチャンス
LOCAL = RAND:5
IF SWIMROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置) && TALENT:AUTOCOM_相手番号保存:バストサイズ > 1 && LOCAL == 0
	PRINTW 
	PRINTFORML ……なんと！　遊んでいるうちに、%CALLNAME:AUTOCOM_相手番号保存%の水着がズレておっぱいが露出してしまった！
	PRINTFORM %CALLNAME:AUTOCOM_相手番号保存%は
	SELECTCASE TALENT:AUTOCOM_相手番号保存:羞恥心
		CASE -1
			PRINT 笑いながら
		CASE 0
			PRINT 急いで
		CASE 1
			PRINT とても恥ずかしそうに
	ENDSELECT
	PRINTL 水着を直している…
ENDIF


