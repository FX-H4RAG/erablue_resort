;------------------------------------------------------------------------------
;通常時、いちゃつく
;------------------------------------------------------------------------------
@AUTOCOM6
CALL SOURCE_CALC_歓楽(AUTOCOM_相手番号保存, PLAYER, 500)
CALL SOURCE_CALC_好感度要素_性愛度UP(AUTOCOM_相手番号保存, PLAYER, 250)
TIME_ボディタッチ:AUTOCOM_相手番号保存:0 = TIME

RETURN 1

@AUTOCOM_ABLE6(キャラ番号)
#DIM キャラ番号
;あなたが必要
SIF GETBIT(AUTOCOM_相手判別関数(キャラ番号), 0) == 0
	RETURN 0
;友人以上かつ４つの好感度要素で性愛度が最も高い
SIF CFLAG:キャラ番号:好感度レベル < 関係閾値:2
	RETURN 0
LOCAL = 要素最大値算出(キャラ番号)
SIF LOCAL != 3 && LOCAL != 5
	RETURN 0
;２時間に１回のみ発動
SIF TIME - TIME_ボディタッチ:キャラ番号:0 < 120
	RETURN 0
;うふふ中ではない
SIF CFLAG:キャラ番号:うふふ
	RETURN 0
;性知識0必要
SIF 知識素質:キャラ番号:性知識 < 0
	RETURN 0
RETURN 1

@MESSAGE_AUTOCOM6
IF GETBIT(TALENT:AUTOCOM_相手番号保存:性別, 0)
	PRINTFORM %CALLNAME:AUTOCOM_相手番号保存%は%CALLNAME:PLAYER%の手に指を絡めて身を寄せ、自らの
	SELECTCASE TALENT:AUTOCOM_相手番号保存:バストサイズ
		CASE -2
			PRINT ほとんど膨らみの無い
		CASE -1
			PRINT ささやかな
		CASE 0
			;描写なし
		CASE 1
			PRINT 大きな
		CASE 2
			PRINT 豊かな
		CASE 3
			PRINT 非常に豊かな
	ENDSELECT
	PRINTFORML 胸を%CALLNAME:PLAYER%の腕に押し付けてきた…
ELSE
	PRINTFORM %CALLNAME:AUTOCOM_相手番号保存%は%CALLNAME:PLAYER%の腰に手を回し、
	PRINTFORML 強めの力で抱きしめてきた…
ENDIF


