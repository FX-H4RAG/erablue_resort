
@COMNAME355
#FUNCTIONS
TSTR:コマンド名受渡 = 遊びに誘う
IF CFLAG:TARGET:ゲージ起動分類 == 1
	TSTR:コマンド名受渡 = デートに誘う
ENDIF

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_355
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_355
LOCALS = <br>■遊びに誘う<br>
LOCALS += "一定値以上の好感度レベルを持つ相手を、長時間自身に同行させるコマンド。<br>同時に連れ歩けるのは二人まで。<br>同行してくれる時間は３６０分。ただし、一緒に隠密中・うふふ中は同行が解除されない。<br>恋慕対象の相手である場合[デートに誘う]に変化。"
LOCALS += "<br>COMタイプ：日常<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM355
#DIM 同行者保存
#DIM 同行数値記録
同行者保存 = 0
同行数値記録 = 0

IF CFLAG:TARGET:デート && CFLAG:TARGET:デート != MASTER + 100
	PRINTFORMW %CALLNAME:TARGET%は今日一日%CALLNAME:(ABS(CFLAG:TARGET:デート) - 100)%とデートしているため連れ出せません
	RETURN -1
ENDIF

IF CFLAG:TARGET:ゲージ起動分類 == 1
	RFLAG:コマンド結果受渡し変数 = 1
	PRINTFORMW %CALLNAME:TARGET%とデートに行きます。
	CFLAG:TARGET:デート = MASTER + 100
ELSE
	PRINTFORMW %CALLNAME:TARGET%と一緒に遊びに行きます。
ENDIF
;一度に連れ歩けるのは二人まで
FOR LOCAL,1,CHARANUM
	SIF TARGET:LOCAL < 1
		BREAK
	;TARGETに関してカウントする必要なし
	SIF TARGET:LOCAL == TARGET
		CONTINUE
	IF CFLAG:(TARGET:LOCAL):同行
		IF 同行数値記録
			;既に同行者が2人いるパターン
			IF 同行数値記録 > CFLAG:(TARGET:LOCAL):同行
				CFLAG:(TARGET:LOCAL):同行 = 0
				CFLAG:(TARGET:LOCAL):デート = 0
			ELSE
				CFLAG:同行者保存:同行 = 0
				CFLAG:同行者保存:デート = 0
				同行数値記録 = CFLAG:(TARGET:LOCAL):同行
				同行者保存 = TARGET:LOCAL
			ENDIF
		ELSE
			同行数値記録 = CFLAG:(TARGET:LOCAL):同行
			同行者保存 = TARGET:LOCAL
		ENDIF
	ENDIF
NEXT
SIF CFLAG:TARGET:同行 < 360
	CFLAG:TARGET:同行 = 360
TFLAG:TARGET変更用 = TARGET
TIME += 1

;SOURCEまで回さないのでここで口上を表示させる
RESULT = 0
RESULTS =
IF FLAG:7 > 0
	TRYCALLFORM KOJO_COM_{NO:TARGET}_355(TARGET)
ENDIF
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE355
;連れ出す実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(355)
	RETURN RESULT
;TARGETの好感度レベル
SIF CFLAG:好感度レベル < 1 + 関係閾値:3
	RETURN 0
;他人とデート中はダメ
SIF CFLAG:TARGET:デート && CFLAG:TARGET:デート != MASTER + 100
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
