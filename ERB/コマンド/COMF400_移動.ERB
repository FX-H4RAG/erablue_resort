
@COMNAME400
#FUNCTIONS
TSTR:コマンド名受渡 = 移動

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_400
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("単独可能")

@COM_TOOLTIP_400
LOCALS = <br>■移動<br>
LOCALS += "リゾート内を移動するコマンド。<br>移動にはごくわずかな時間を消費する。"
LOCALS += "<br>COMタイプ：単独可能<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%


;-------------------------------------------------
;移動
;-------------------------------------------------
@COM400
#DIMS AA, 40
#DIMS 出会ったキャラ記録

;移動した瞬間隠密解除
CFLAG:MASTER:隠密 = 0
CFLAG:TARGET:隠密 = 0

CALL 移動時モード消去

;表示マップ種類を代入
TFLAG:マップ種別 = 派生マップ番号取得(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)

;エリア切り替え時の処理
IF TFLAG:エリア切り替え時_目的地格納
	CALLFORM エリア情報登録_{CFLAG:PLAYER:現在マップ種別}("エリア移動可能場所")
	IF CFLAG:PLAYER:現在位置 == RESULT
		CFLAG:PLAYER:現在マップ種別 = TFLAG:エリア切り替え時_目的地格納 - 100
		CALLFORM エリア情報登録_{TFLAG:エリア切り替え時_目的地格納 - 100}("エリア移動可能場所")
		CFLAG:PLAYER:現在位置 = RESULT
		TFLAG:エリア切り替え時_目的地格納 = 0
		TRYCALLFORM エリア来訪時イベント_{CFLAG:PLAYER:現在マップ種別}
		TFLAG:長距離移動時_目的地格納 = 0
		TIME += 15
		RETURN 0
	ELSE
		TFLAG:長距離移動時_目的地格納 = RESULT
	ENDIF
ENDIF

;遠距離移動中の処理
IF TFLAG:長距離移動時_目的地格納
	TIME += 1
	IF TFLAG:長距離移動時_目的地格納 == CFLAG:MASTER:現在位置
		TFLAG:長距離移動時_目的地格納 = 0
		RETURN 0
	ELSEIF 同一場所判定(TFLAG:長距離移動時_目的地格納, CFLAG:MASTER:現在位置)
		TFLAG:長距離移動時_目的地格納 = 0
		RETURN 0
	ENDIF
	CALL CHARA_MOVEMENT
	TFLAG:行動前時刻 = TIME + 1440 * DAY
	;キャラを好感度レベルの降順に並び替え
	VARSET TARGET
	CALL SORT_CFLAG(21)
	LOCAL:1 = 0
	FOR LOCAL,1,CHARANUM
		SIF CFLAG:MASTER:現在マップ種別 != CFLAG:(RESULT:LOCAL):現在マップ種別
			CONTINUE
		SIF CFLAG:MASTER:現在位置 != CFLAG:(RESULT:LOCAL):現在位置
			CONTINUE
		LOCAL:1 ++
		TARGET:(LOCAL:1) = RESULT:LOCAL
	NEXT
	
	出会ったキャラ記録 = 
	;誰かいないかチェック
	FOR LOCAL,1,CHARANUM
		SIF TARGET:LOCAL <= 0
			CONTINUE
		SIF CFLAG:(TARGET:LOCAL):同行
			CONTINUE
		SIF 出会ったキャラ記録 != ""
			出会ったキャラ記録 += "、"
		出会ったキャラ記録 += @"%CALLNAME:(TARGET:LOCAL)%"
	NEXT
	IF 出会ったキャラ記録 != ""
		PRINTFORML %GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:MASTER:現在位置)%で%出会ったキャラ記録%と出会った
		;パンツは一旦封印
		; IF STRLENS(CSTR:MASTER:12) && RAND:2 && !CFLAG:(LOCAL:2):睡眠
		; 	PRINTL 
		; 	PRINTFORMW %CALLNAME:(LOCAL:2)%に見とがめられ身体検査をされた…
		; 	PRINTFORMW 
		; 	PRINTFORMW 
		; 	PRINTFORMW 
		; 	PRINTFORMW くすねたパンティを没収された
		; 	SIF GETBIT (CFLAG:(LOCAL:2):10,0)
		; 		PRINTFORMW %CALLNAME:(LOCAL:2)%に弱みを握られた
		; 	CALL KOJO_EVENT(11, LOCAL:2, 0)
		; 	SETBIT CFLAG:(LOCAL:2):10 ,0
		; 	CSTR:MASTER:12 =
		; 	TFLAG:長距離移動時_目的地格納 = 0
		; 	RETURN 0
		; ELSE
			IF RFLAG:立ち止まらないフラグ
				PRINTL
			ELSE
				PRINTL ……どうしますか？
				PRINTL [0] そのまま移動する
				PRINTL [1] 立ち止まる
				PRINTL 
				PRINTL [9] そのまま移動する（移動が終わるまで立ち止まらない）
				INPUT
				
				IF RESULT == 0
					;無視する場合
				ELSEIF RESULT == 9
					;もう立ち止まらないフラグを立てとく
					RFLAG:立ち止まらないフラグ = 1
				ELSE
					TFLAG:長距離移動時_目的地格納 = 0
					TFLAG:エリア切り替え時_目的地格納 = 0
					RETURN 0
				ENDIF
			ENDIF
		; ENDIF
	ENDIF
	
		LOCAL:1 = IN_ROOM("MIN", 18, "CFLAG", 6)
	;遠距離移動
	IF CAN_MOVE_1(CFLAG:PLAYER:現在マップ種別, CFLAG:MASTER:現在位置, TFLAG:長距離移動時_目的地格納) & 1
		CFLAG:MASTER:現在位置 = (TFLAG:長距離移動時_目的地格納)
		;遠距離移動フラグ消去
		TFLAG:長距離移動時_目的地格納 = 0
		SIF !TFLAG:エリア切り替え時_目的地格納
			RETURN 0
		; FOR LOCAL,1,CHARANUM
		; 	SIF CFLAG:LOCAL:同行
		; 		CFLAG:LOCAL:現在位置 = CFLAG:MASTER:現在位置
		; NEXT
	ELSE
		CALLFORM SKIP_MOVE_{CFLAG:PLAYER:現在マップ種別}(TFLAG:長距離移動時_目的地格納)
		;現在位置を更新し、次も移動の扱いにする
		CFLAG:MASTER:現在位置 = RESULT
		;マップ間をまたいで移動時の処理
		CALL マップ遷移判定処理(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
		IF RESULT
			CFLAG:PLAYER:現在位置 = RESULT
		ENDIF
	ENDIF
	RESTART
ENDIF

$DRAW_LOOP
CALL DRAW_MAP

PRINTL 
DRAWLINE
PRINTL 
PRINTFORML 現在{CFLAG:MASTER:現在位置}:%GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:MASTER:現在位置)%にいます

FOR LOCAL,0,21
	SIF CAN_MOVE_1(CFLAG:PLAYER:現在マップ種別, CFLAG:MASTER:現在位置, LOCAL) & 1
		PRINTBUTTONLC @"[{LOCAL}] - %GETPLACENAME(CFLAG:PLAYER:現在マップ種別, LOCAL)%", LOCAL
NEXT
PRINTL
PRINTL 
PRINTBUTTON "[99] - 別のエリアに移動する", 99
IF DAY == 開催予定日取り
	CALLFORM MOVE_ROUTINE_%開催予定祭り名%(0, "特設マップ")
	IF RESULT
		;日程が今日 + 特設マップがある祭りなら特設マップあるよ！　と強調表示
		SETCOLOR カラーパレット("黄")
		PRINT 　　　特設マップ出現中！
		RESETCOLOR
	ENDIF
ENDIF
PRINTL 
PRINTBUTTON @"[999] - 戻る", 999
$INPUT_LOOP
INPUTS

SIF RESULTS == ""
	GOTO INPUT_LOOP
LOCAL:10 = TOINT(RESULTS) - (TOINT(RESULTS) == 0 && RESULTS != "0")
IF LOCAL:10 == -1
	REPLACE RESULTS, CALLNAME:MASTER, "%CALLNAME:MASTER%"
	FOR LOCAL, 1, 18
		SIF GETPLACENAME(CFLAG:PLAYER:現在マップ種別, LOCAL) == RESULTS
			LOCAL:10 = LOCAL
	NEXT
ENDIF
CALL 表示マップ切り替え判定(LOCAL:10)
LOCAL = RESULT
IF LOCAL:10 == 999
	RETURN 0
ELSEIF LOCAL:10 == 99
	;エリア移動選択
	CALL エリア移動選択画面表示
	IF RESULT == -1
		;移動しない場合
		GOTO DRAW_LOOP
	ELSEIF RESULT == 1
		;移動処理
		RESTART
	ENDIF
ELSEIF CFLAG:MASTER:現在位置 == LOCAL:10
	RETURN 0
ELSEIF LOCAL
	GOTO DRAW_LOOP
ELSEIF 存在しない移動先(CFLAG:PLAYER:現在マップ種別, LOCAL:10)
	CLEARLINE 1
	REUSELASTLINE 
	GOTO INPUT_LOOP
ELSE
	;目的地が離れているので、最終目的地として設定
	TFLAG:長距離移動時_目的地格納 = LOCAL:10
	CALLFORM SKIP_MOVE_{CFLAG:PLAYER:現在マップ種別}(LOCAL:10)
	;現在位置を更新し、次も移動の扱いにする
	CFLAG:MASTER:現在位置 = RESULT
	;マップ間をまたいで移動時の処理
	CALL マップ遷移判定処理(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	IF RESULT
		CFLAG:PLAYER:現在位置 = RESULT
	ENDIF
	TIME += 1
	RESTART
ENDIF
TIME += 1
PREVCOM = 400
STR:0 = 移動

RETURN 0



; @HAS_DUPLICATE_KEY, ARG
; #FUNCTION
; 	FOR LOCAL, 1, CHARANUM
; 		IF CFLAG:LOCAL:311 == ARG
; 			SIF TALENT:LOCAL:恋慕
; 				RETURNF 1
; 		ENDIF
; 	NEXT

;-----------------------------------------------------------
;マップ描画処理
;2文字セットの半角数字(01とか)は自動でボタン化、全角数字は認識しない
;-----------------------------------------------------------
@DRAW_MAP(ARGS)
#DIMS AA, 40
#DIM SPOT
#DIM MODE
#DIM LOCATION

#DIM LINENUM
#DIM X_POINT
#DIM FOUND_INT

;AAを取得、なければそのまま帰る
VARSET AA

TRYCCALLFORM MAP_VIEWING_{CFLAG:PLAYER:現在マップ種別}(AA)
CATCH
	RETURN 0
ENDCATCH

;-----------------------------------------------------------
;描画処理
;-----------------------------------------------------------
FOR LINENUM, 0, VARSIZE("AA")
	SIF AA:LINENUM == ""
		CONTINUE
	;行ごとに初期化
	X_POINT = 0
	RESULTS = I
	;行の終端までループ
	WHILE RESULTS != "" && STRLENSU(RESULTS) < 3
		;1文字取り出す
		SUBSTRINGU AA:LINENUM, X_POINT, 1
		;数字ならボタンにする
		IF ISNUMERIC(RESULTS)
			;分岐めんどいから必ず2ケタ取る
			SUBSTRINGU AA:LINENUM, X_POINT, 2
			FOUND_INT = TOINT(RESULTS) + (TFLAG:マップ種別 * 100)
			SIF TOINT(RESULTS) == 0
				FOUND_INT = 0
			SETCOLOR カラーパレット("黄")
			PRINTBUTTON RESULTS, FOUND_INT
			RESETCOLOR
			X_POINT += 2
		;ふつうの文字
		ELSE
			TRYCALLFORM DRAW_MAP_BG_{CFLAG:PLAYER:現在マップ種別}_{TFLAG:マップ種別}(RESULTS)
			X_POINT ++
		ENDIF
	WEND
	PRINTL
NEXT

RESETCOLOR


;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE400
;移動実行判定
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(400)
	RETURN RESULT
SIF CFLAG:うふふ == 2
	RETURN 0
;宿泊エリアでは見えない
SIF CFLAG:PLAYER:現在マップ種別 == 999
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------




;-------------------------------------------------
;エリア移動
;-------------------------------------------------
@エリア移動選択画面表示
DRAWLINE
PRINTL どのエリアに移動しますか？
DRAWLINE
FOR LOCAL, 0, 10
	TSTR:場所名受渡 = 
	SIF CFLAG:PLAYER:現在マップ種別 == LOCAL
		CONTINUE
	TRYCCALLFORM エリア情報登録_{LOCAL}("エリア名")
		IF TSTR:場所名受渡 != ""
			PRINTBUTTON @"[{LOCAL}]%TSTR:場所名受渡%へ移動する", LOCAL
			PRINTL 
		ENDIF
	CATCH
		BREAK
	ENDCATCH
NEXT

;お祭りエリア
IF 開催予定日取り == DAY
	;特設エリアある？
	TRYCALLFORM MOVE_ROUTINE_%開催予定祭り名%(0, "特設マップ")
	;1が帰ってきたらある！
	IF RESULT
		PRINTBUTTON @"[100]%SUBSTRING(開催予定祭り名, 9)%マップへ移動する", 100
		PRINTL 
	ENDIF
ENDIF

PRINTBUTTON "[999]戻る", 999
PRINTL 
$INPUT_LOOP
BINPUT

SELECTCASE RESULT
	CASE 0
		IF CFLAG:PLAYER:現在マップ種別 == 0
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		;０を入れると移動してない時と見分けがつかないため+100した数値を入れる
		TFLAG:エリア切り替え時_目的地格納 = 100
	CASE 1
		IF CFLAG:PLAYER:現在マップ種別 == 1
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		TRYCALLFORM エリア情報登録_1("エリア名")
		IF TSTR:場所名受渡 == ""
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		;+100した数値を入れる
		TFLAG:エリア切り替え時_目的地格納 = 101
	CASE 100
		IF CFLAG:PLAYER:現在マップ種別 == 100
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		TRYCALLFORM エリア情報登録_100("エリア名")
		;０を入れると移動してない時と見分けがつかないため+100した数値を入れる
		TFLAG:エリア切り替え時_目的地格納 = 200
	CASE 999
		RETURN -1
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT

RETURN 1
