
@COMNAME400
#FUNCTIONS
TSTR:コマンド名受渡 = 移動

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_400
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("単独可能")

@COM_TOOLTIP_400
LOCALS = <br>■移動<br>
LOCALS += "リゾート内を移動するコマンド。<br>移動にはごくわずかな時間を消費する。"
LOCALS += "<br>COMタイプ：単独可能<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%


;-------------------------------------------------
;移動
;-------------------------------------------------
@COM400
#DIMS AA, 40
#DIMS 出会ったキャラ記録

;移動した瞬間隠密解除
CFLAG:MASTER:隠密 = 0
CFLAG:TARGET:隠密 = 0

CALL 移動時モード消去(PLAYER)

;表示マップ種類を代入
TFLAG:マップ種別 = 派生マップ番号取得(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)

;エリア切り替え時の処理
IF TFLAG:エリア切り替え時_目的地格納
	CALLFORM エリア情報登録_{CFLAG:PLAYER:現在マップ種別}("エリア移動可能場所")
	IF CFLAG:PLAYER:現在位置 == RESULT
		CFLAG:PLAYER:現在マップ種別 = TFLAG:エリア切り替え時_目的地格納 - 100
		CALLFORM エリア情報登録_{TFLAG:エリア切り替え時_目的地格納 - 100}("エリア移動可能場所")
		CFLAG:PLAYER:現在位置 = RESULT
		TFLAG:マップ種別 = 派生マップ番号取得(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
		TFLAG:エリア切り替え時_目的地格納 = 0
		TRYCALLFORM エリア来訪時イベント_{CFLAG:PLAYER:現在マップ種別}
		TFLAG:長距離移動時_目的地格納 = 0
		IF あなた特殊能力:縦横無尽 && あなた単独()
			TIME += 4
		ELSE
			TIME += 15
		ENDIF
		RETURN 0
	ELSE
		TFLAG:長距離移動時_目的地格納 = RESULT
		CALL SKIP_MOVE(CFLAG:PLAYER:現在マップ種別, TFLAG:長距離移動時_目的地格納)
		;現在位置を更新し、次も移動の扱いにする
		CFLAG:PLAYER:現在位置 = RESULT
	ENDIF
ENDIF

;遠距離移動中の処理
IF TFLAG:長距離移動時_目的地格納
	TIME += 1
	IF TFLAG:長距離移動時_目的地格納 == CFLAG:MASTER:現在位置
		TFLAG:長距離移動時_目的地格納 = 0
		IF !TFLAG:エリア切り替え時_目的地格納
			RETURN 0
		ELSE
			RESTART
		ENDIF
	ELSEIF 同一場所判定(TFLAG:長距離移動時_目的地格納, CFLAG:MASTER:現在位置)
		TFLAG:長距離移動時_目的地格納 = 0
		IF !TFLAG:エリア切り替え時_目的地格納
			RETURN 0
		ELSE
			RESTART
		ENDIF
	ENDIF
	CALL CHARA_MOVEMENT
	TFLAG:行動前時刻 = TIME + 1440 * DAY
	;キャラをTARGET配列に突っ込む
	CALL TARGET_SET()
	
	出会ったキャラ記録 = 
	;誰かいないかチェック
	FOR LOCAL,1,CHARANUM
		SIF TARGET:LOCAL <= 0
			BREAK
		SIF 追従中キャラ(TARGET:LOCAL)
			CONTINUE
		SIF 出会ったキャラ記録 != ""
			出会ったキャラ記録 += "、"
		出会ったキャラ記録 += @"%CALLNAME:(TARGET:LOCAL)%"
	NEXT
	IF 出会ったキャラ記録 != ""
		IF MODE_存在判定_プレイヤー側("全裸散歩", PLAYER)
			VARSET RESULTS
			SPLIT 出会ったキャラ記録, "、", RESULTS
			DRAWLINE
			PRINTFORML %RESULTS:0%がこちらに近づいてくる……！
			PRINTFORMW %CALLNAME:PLAYER%たちは見つかる前に急いでこの場から立ち去った……
			CALL 全裸散歩終了処理()
			TFLAG:長距離移動時_目的地格納 = 0
			TFLAG:エリア切り替え時_目的地格納 = 0
			;エリアまたぎ移動キャンセル用にフラグを突っ込む
			RFLAG:立ち止まらないフラグ = -1
			RETURN -1
		ENDIF
		PRINTFORML %GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:MASTER:現在位置)%で%出会ったキャラ記録%と出会った
		;パンツは一旦封印
		; IF STRLENS(CSTR:MASTER:12) && RAND:2 && !CFLAG:(LOCAL:2):睡眠
		; 	PRINTL 
		; 	PRINTFORMW %CALLNAME:(LOCAL:2)%に見とがめられ身体検査をされた…
		; 	PRINTFORMW 
		; 	PRINTFORMW 
		; 	PRINTFORMW 
		; 	PRINTFORMW くすねたパンティを没収された
		; 	SIF GETBIT (CFLAG:(LOCAL:2):10,0)
		; 		PRINTFORMW %CALLNAME:(LOCAL:2)%に弱みを握られた
		; 	CALL KOJO_EVENT(11, LOCAL:2, 0)
		; 	SETBIT CFLAG:(LOCAL:2):10 ,0
		; 	CSTR:MASTER:12 =
		; 	TFLAG:長距離移動時_目的地格納 = 0
		; 	RETURN 0
		; ELSE
			IF RFLAG:立ち止まらないフラグ > 0
				PRINTL
			ELSE
				PRINTL ……どうしますか？
				PRINTL [0] そのまま移動する
				PRINTL [1] 立ち止まる
				PRINTL 
				PRINTL [9] そのまま移動する（移動が終わるまで立ち止まらない）
				INPUT
				
				IF RESULT == 0
					;無視する場合
				ELSEIF RESULT == 9
					;もう立ち止まらないフラグを立てとく
					RFLAG:立ち止まらないフラグ = 1
				ELSE
					TFLAG:長距離移動時_目的地格納 = 0
					TFLAG:エリア切り替え時_目的地格納 = 0
					;エリアまたぎ移動キャンセル用にフラグを突っ込む
					RFLAG:立ち止まらないフラグ = -1
					RETURN 0
				ENDIF
			ENDIF
		; ENDIF
	ENDIF
	
		LOCAL:1 = IN_ROOM("MIN", 18, "CFLAG", 6)
	;遠距離移動
	IF CAN_MOVE(CFLAG:PLAYER:現在マップ種別, CFLAG:MASTER:現在位置, TFLAG:長距離移動時_目的地格納) & 1
		CFLAG:MASTER:現在位置 = (TFLAG:長距離移動時_目的地格納)
		TFLAG:マップ種別 = 派生マップ番号取得(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
		;遠距離移動フラグ消去
		TFLAG:長距離移動時_目的地格納 = 0
		SIF !TFLAG:エリア切り替え時_目的地格納
			RETURN 0
		; FOR LOCAL,1,CHARANUM
		; 	SIF CFLAG:LOCAL:同行
		; 		CFLAG:LOCAL:現在位置 = CFLAG:MASTER:現在位置
		; NEXT
	ELSE
		CALL SKIP_MOVE(CFLAG:PLAYER:現在マップ種別, TFLAG:長距離移動時_目的地格納)
		;現在位置を更新し、次も移動の扱いにする
		CFLAG:MASTER:現在位置 = RESULT
		TFLAG:マップ種別 = 派生マップ番号取得(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	ENDIF
	RESTART
ENDIF

$DRAW_LOOP
CALL DRAW_MAP
HTML_PRINT RESULTS

PRINTL 
DRAWLINE
PRINTL 
PRINTFORML 現在{CFLAG:MASTER:現在位置}:%GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:MASTER:現在位置)%にいます

FOR LOCAL,0,21
	SIF CAN_MOVE(CFLAG:PLAYER:現在マップ種別, CFLAG:MASTER:現在位置, LOCAL) & 1
		PRINTBUTTONLC @"[{LOCAL}] - %GETPLACENAME(CFLAG:PLAYER:現在マップ種別, LOCAL)%", LOCAL
NEXT
PRINTL
PRINTL 
PRINTBUTTON "[99] - 別のエリアに移動する", 99
IF DAY == 開催予定日取り
	CALLFORM MOVE_ROUTINE_%開催予定祭り名%(0, "特設マップ")
	IF RESULT
		;日程が今日 + 特設マップがある祭りなら特設マップあるよ！　と強調表示
		SETCOLOR カラーパレット("黄")
		PRINT 　　　特設マップ出現中！
		RESETCOLOR
	ENDIF
ELSEIF エリアNEW表示判定()
	;存在するけど訪れたことのないエリアがあるなら強調表示
	SETCOLOR カラーパレット("黄")
	PRINT 　NEW！
	RESETCOLOR
ENDIF
PRINTL 
IF CFLAG:MASTER:現在マップ種別 == 0
	IF 施設改造度:20:0
		PRINTBUTTON "[2020] 自室へ移動", 2020
	ELSE
		PRINTBUTTON "[20] 自室へ移動", 20
	ENDIF
	PRINTL 
ENDIF
PRINTBUTTON @"[999] - 戻る", 999
$INPUT_LOOP
INPUTS

SIF RESULTS == ""
	GOTO INPUT_LOOP
LOCAL:10 = TOINT(RESULTS) - (TOINT(RESULTS) == 0 && RESULTS != "0")
IF LOCAL:10 == -1
	REPLACE RESULTS, CALLNAME:MASTER, "%CALLNAME:MASTER%"
	FOR LOCAL, 1, 18
		SIF GETPLACENAME(CFLAG:PLAYER:現在マップ種別, LOCAL) == RESULTS
			LOCAL:10 = LOCAL
	NEXT
ENDIF
CALL 表示マップ切り替え判定(LOCAL:10)
LOCAL = RESULT
IF LOCAL:10 == 999
	RETURN -1
ELSEIF LOCAL:10 == 99
	;エリア移動選択
	CALL エリア移動選択画面表示
	IF RESULT == -1
		;移動しない場合
		GOTO DRAW_LOOP
	ELSEIF RESULT == 1
		;移動処理
		サイド描画横スクロール保存 = 0
		RESTART
	ENDIF
ELSEIF CFLAG:MASTER:現在位置 == LOCAL:10
	RETURN 0
ELSEIF LOCAL
	サイド描画横スクロール保存 = 0
	GOTO DRAW_LOOP
ELSEIF 存在しない移動先(CFLAG:PLAYER:現在マップ種別, LOCAL:10)
	CLEARLINE 1
	REUSELASTLINE 
	GOTO INPUT_LOOP
ELSE
	;目的地が離れているので、最終目的地として設定
	TFLAG:長距離移動時_目的地格納 = LOCAL:10
	CALL SKIP_MOVE(CFLAG:PLAYER:現在マップ種別, LOCAL:10)
	;現在位置を更新し、次も移動の扱いにする
	CFLAG:MASTER:現在位置 = RESULT
	TFLAG:マップ種別 = 派生マップ番号取得(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	TIME += 1
	RESTART
ENDIF
TIME += 1
PREVCOM = 400
STR:0 = 移動

RETURN 0



; @HAS_DUPLICATE_KEY, ARG
; #FUNCTION
; 	FOR LOCAL, 1, CHARANUM
; 		IF CFLAG:LOCAL:311 == ARG
; 			SIF TALENT:LOCAL:恋慕
; 				RETURNF 1
; 		ENDIF
; 	NEXT

@入室不可処理
#DIM ループ用
#DIM 部屋人数
#DIM 部屋主配列, 100

SELECTCASE CFLAG:PLAYER:現在マップ種別
	CASE 999
		IF CFLAG:PLAYER:現在位置 < 1000
			LOCAL = CFLAG:PLAYER:現在位置
			IF 滞在者部屋割り配列:LOCAL > 0
				;睡眠時
				IF CFLAG:(滞在者部屋割り配列:LOCAL):現在マップ種別 == 999 && CFLAG:(滞在者部屋割り配列:LOCAL):現在位置 == LOCAL
					IF CFLAG:(滞在者部屋割り配列:LOCAL):睡眠 && 陥落チェック(滞在者部屋割り配列:LOCAL) == 0
						PRINTFORML %CALLNAME:(滞在者部屋割り配列:LOCAL)%の部屋には鍵が掛かっている。
						PRINTW どうやら眠っているようだ…
						CFLAG:PLAYER:現在マップ種別 = 0
						CFLAG:PLAYER:現在位置 = 1
						あなた追跡キャラ = 0
					ENDIF
				ENDIF
			ENDIF
		ELSEIF CFLAG:PLAYER:現在位置 >= 1000 && CFLAG:PLAYER:現在位置 < 2000
			LOCAL = CFLAG:PLAYER:現在位置 - 1000
			IF 従業員部屋割り配列:LOCAL > 0
				;睡眠時
				IF CFLAG:(従業員部屋割り配列:LOCAL):現在マップ種別 == 999 && CFLAG:(従業員部屋割り配列:LOCAL):現在位置 == LOCAL
					IF CFLAG:(従業員部屋割り配列:LOCAL):睡眠 && 陥落チェック(従業員部屋割り配列:LOCAL) == 0
						PRINTFORML %CALLNAME:(従業員部屋割り配列:LOCAL)%の部屋には鍵が掛かっている。
						PRINTW どうやら眠っているようだ…
						CFLAG:PLAYER:現在マップ種別 = 0
						CFLAG:PLAYER:現在位置 = 1
						あなた追跡キャラ = 0
					ENDIF
				ENDIF
			ENDIF
		ELSEIF CFLAG:PLAYER:現在位置 >= 2000 && CFLAG:PLAYER:現在位置 < 3000
			LOCAL = CFLAG:PLAYER:現在位置 - 2000
			;部屋の居住者を配列化
			VARSET 部屋主配列
			VARSET RESULTS
			SPLIT スイートルーム部屋割り配列:LOCAL, "_", RESULTS
			部屋人数 = 0
			FOR ループ用, 1, RESULT
				SIF RESULTS:ループ用 == ""
					BREAK
				部屋主配列:部屋人数 = TOINT(RESULTS:ループ用)
				部屋人数 ++
			NEXT
			IF 部屋人数 > 0
				;睡眠時
				LOCAL:2 = 0
				FOR ループ用, 0, 部屋人数
					SIF 部屋主配列:ループ用 < 1
						BREAK
					IF CFLAG:(部屋主配列:ループ用):現在マップ種別 == 999 && CFLAG:(部屋主配列:ループ用):現在位置 == LOCAL + 2000
						SIF CFLAG:(部屋主配列:ループ用):睡眠 && 陥落チェック(部屋主配列:ループ用) == 0
							LOCAL:2 = 1
					ENDIF
				NEXT
				IF LOCAL:2
					IF 部屋人数 > 1
						PRINTFORML %NAME:(部屋主配列:0)%たちの部屋には鍵が掛かっている。
					ELSE
						PRINTFORML %NAME:(部屋主配列:0)%の部屋には鍵が掛かっている。
					ENDIF
					PRINTW どうやら眠っているようだ…
					CFLAG:PLAYER:現在マップ種別 = 0
					CFLAG:PLAYER:現在位置 = 1
					あなた追跡キャラ = 0
				ENDIF
			ENDIF
		ENDIF
	CASE 1
		;店は誰かがいるのに入れないパターンはないので割愛
	CASE 2
		IF CFLAG:PLAYER:現在位置 >= 1000 && CFLAG:PLAYER:現在位置 <= 6000
			LOCAL = CFLAG:PLAYER:現在位置 - 1000
			;部屋の居住者を配列化
			VARSET 部屋主配列
			VARSET RESULTS
			SPLIT 定住者部屋割り配列:LOCAL, "_", RESULTS
			FOR ループ用, 0, RESULT
				SIF RESULTS:ループ用 == ""
					BREAK
				部屋主配列:ループ用 = TOINT(RESULTS:ループ用)
			NEXT
			IF 部屋主配列:0 > 0
				;睡眠時
				LOCAL:2 = 0
				FOR ループ用, 0, 10
					SIF 部屋主配列:ループ用 < 1
						BREAK
					IF CFLAG:(部屋主配列:ループ用):現在マップ種別 == 2 && CFLAG:(部屋主配列:ループ用):現在位置 == LOCAL + 1000
						SIF CFLAG:(部屋主配列:ループ用):睡眠 && 陥落チェック(部屋主配列:ループ用) == 0
							LOCAL:2 = 1
					ENDIF
				NEXT
				IF LOCAL:2
					CALLF GETPLACENAME_2_10(CFLAG:PLAYER:現在位置)
					PRINTFORML %TSTR:場所名受渡%の家には鍵が掛かっている。
					PRINTW どうやら眠っているようだ…
					CFLAG:PLAYER:現在マップ種別 = 2
					CFLAG:PLAYER:現在位置 = 1
					あなた追跡キャラ = 0
				ENDIF
				;全員外出時
				LOCAL:2 = 0
				FOR ループ用, 0, 10
					SIF 部屋主配列:ループ用 < 1
						BREAK
					IF CFLAG:(部屋主配列:ループ用):現在マップ種別 == 2 && CFLAG:(部屋主配列:ループ用):現在位置 == LOCAL + 1000
						LOCAL:2 = 1
					ELSEIF 追従中キャラ(部屋主配列:ループ用)
						LOCAL:2 = 1
					ENDIF
				NEXT
				IF LOCAL:2 == 0
					CALLF GETPLACENAME_2_10(CFLAG:PLAYER:現在位置)
					PRINTFORML %TSTR:場所名受渡%の家には鍵が掛かっている。
					PRINTW どうやら家に誰もいないようだ…
					CFLAG:PLAYER:現在マップ種別 = 2
					CFLAG:PLAYER:現在位置 = 1
					あなた追跡キャラ = 0
				ENDIF
			ENDIF
		ENDIF
ENDSELECT


;-----------------------------------------------------------
;マップ描画処理
;2文字セットの半角数字(01とか)は自動でボタン化、全角数字は認識しない
;-----------------------------------------------------------
@DRAW_MAP(表示幅 = -1, 表示開始数_横 = 0, 表示開始数_縦 = 0)
#DIMS 表示文字列
#DIMS 一時文字列
#DIM 改行数
#DIM 最大横サイズ
#DIM 表示開始数_横
#DIM 表示開始数_縦
#DIM 描画横幅カウント
#DIM 表示幅
#DIMS AA, 50
#DIM SPOT
#DIM MODE
#DIM LOCATION

#DIM LINENUM
#DIM X_POINT
#DIM FOUND_INT

;AAを取得、なければそのまま帰る
VARSET AA
VARSET RESULTS
表示文字列 = 
一時文字列 = 
改行数 = 0

TRYCCALLFORM MAP_VIEWING_{CFLAG:PLAYER:現在マップ種別}(AA)
CATCH
	RETURN 0
ENDCATCH

;-----------------------------------------------------------
;描画処理
;-----------------------------------------------------------
FOR LINENUM, 表示開始数_縦, VARSIZE("AA")
	SIF AA:LINENUM == ""
		CONTINUE
	;行ごとに初期化
	X_POINT = 0
	RESULTS = I
	描画横幅カウント = 0
	;行の終端までループ
	WHILE RESULTS != "" && STRLENSU(RESULTS) < 3
		;指定幅まで描画
		SIF 表示幅 > 0 && 描画横幅カウント > 表示幅 + 表示開始数_横
			BREAK
		;1文字取り出す
		SUBSTRINGU AA:LINENUM, X_POINT, 1
		;数字ならボタンにする
		IF ISNUMERIC(RESULTS)
			;分岐めんどいから必ず2ケタ取る
			SUBSTRINGU AA:LINENUM, X_POINT, 2
			FOUND_INT = TOINT(RESULTS) + (TFLAG:マップ種別 * 100)
			SIF TOINT(RESULTS) == 0
				FOUND_INT = 0
			SELECTCASE CFLAG:PLAYER:現在マップ種別
				CASE 1, 2
					;空艇港
					IF 表示開始数_横 > 描画横幅カウント
						;表示開始未満なら描画しない
					ELSEIF CFLAG:PLAYER:現在位置 < 1000 && TOSTR_ニ桁化(CFLAG:PLAYER:現在位置 % 100) == RESULTS && 派生マップ番号取得(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置) == TFLAG:マップ種別
						一時文字列 += @"<font color='#%カラーパレット_HTML("真っ赤")%'>%RESULTS%</font>"
					ELSE
						一時文字列 += @"<font color='#%カラーパレット_HTML("黄")%'><button value='{FOUND_INT}'>%RESULTS%</button></font>"
					ENDIF
				CASEELSE
					;リゾート
					IF 表示開始数_横 > 描画横幅カウント
						;表示開始未満なら描画しない
					ELSEIF TOSTR_ニ桁化(CFLAG:PLAYER:現在位置 % 100) == RESULTS && 派生マップ番号取得(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置) == TFLAG:マップ種別
						一時文字列 += @"<font color='#%カラーパレット_HTML("真っ赤")%'>%RESULTS%</font>"
					ELSE
						一時文字列 += @"<font color='#%カラーパレット_HTML("黄")%'><button value='{FOUND_INT}'>%RESULTS%</button></font>"
					ENDIF
			ENDSELECT
			X_POINT += 2
		;ふつうの文字
		ELSE
			IF 表示開始数_横 <= 描画横幅カウント
				RESULT = LINENUM
				TRYCALLFORM DRAW_MAP_BG_{CFLAG:PLAYER:現在マップ種別}_{TFLAG:マップ種別}(RESULTS, 一時文字列)
			ENDIF
			X_POINT ++
		ENDIF
		;幅をカウント
		描画横幅カウント += STRLENS(RESULTS)
	WEND
	SIF 最大横サイズ < STRLENS(AA:LINENUM)
		最大横サイズ = STRLENS(AA:LINENUM)
	SIF STRFIND(一時文字列, "<font") >= 0 && STRFIND(一時文字列, "</font") < 0
		一時文字列 += "</font>"
	表示文字列 += @"%一時文字列%<br>"
	改行数 += 1
	一時文字列 = 
NEXT

RESULTS = %表示文字列%
サイド描画_クイック移動マップ縦幅保存 = 改行数
RETURN 最大横サイズ

@DRAW_簡易MAP()
#DIMS 表示文字列

表示文字列 = 
LOCAL:1 = 0
LOCAL:2 = 0
FOR LOCAL, 0, マップ部屋番号上限
	IF 存在しない移動先(CFLAG:PLAYER:現在マップ種別, LOCAL) == 0
		IF LOCAL:2 && LOCAL:1
			表示文字列 += "<br>"
			表示文字列 += "<br>"
			LOCAL:1 = 0
			LOCAL:2 = 0
		ELSEIF LOCAL:2
			表示文字列 += "<br>"
			LOCAL:2 = 0
		ENDIF
		;存在する場所を順番に並べる
		表示文字列 += @"<button value='{LOCAL}'>[{LOCAL, 4}] %GETPLACENAME(CFLAG:PLAYER:現在マップ種別, LOCAL), 20, LEFT%</button>"
		LOCAL:1 ++ 
		IF LOCAL:1 % 3 == 0
			表示文字列 += "<br>"
			LOCAL:1 = 0
		ENDIF
	ENDIF
	SIF LOCAL % 100 == 0
		LOCAL:2 ++
NEXT

RESULTS = %表示文字列%


;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE400
;移動実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(400)
	RETURN RESULT
SIF CFLAG:うふふ == 2
	RETURN 0
SIF CFLAG:PLAYER:うふふ
	RETURN 0
;宿泊エリアでは見えない
SIF CFLAG:PLAYER:現在マップ種別 == 999
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------




;-------------------------------------------------
;エリア移動
;-------------------------------------------------
@エリア移動選択画面表示
DRAWLINE
PRINTL どのエリアに移動しますか？
DRAWLINE
CALL リゾート遠景表示処理("移動処理")

FOR LOCAL, 0, 10
	TSTR:場所名受渡 = 
	SIF CFLAG:PLAYER:現在マップ種別 == LOCAL
		CONTINUE
	TRYCCALLFORM エリア情報登録_{LOCAL}("エリア名")
		IF TSTR:場所名受渡 != ""
			SIF ENUMFUNCENDSWITH(@"大規模エリア建設条件_%TSTR:場所名受渡%") > 0 && MATCH(開拓エリア開発番号, LOCAL) < 1
				CONTINUE
			PRINTBUTTON @"[{LOCAL}]%TSTR:場所名受渡%へ移動する", LOCAL
			;工事中表示
			IF TSTR:場所名受渡 == "空艇港エリア" && イベント完了フラグ確認("渡りに船の定期便") && 渡りに船の定期便_受諾日付 + 3 > DAY
				PRINTFORM 　（現在工事中！　残り%TOFULL(TOSTR(3 - (DAY - 渡りに船の定期便_受諾日付)))%日）
			ENDIF
			;NEW表示
			IF エリアNEW表示判定(TSTR:場所名受渡)
				;存在するけど訪れたことのないエリアがあるなら強調表示
				SETCOLOR カラーパレット("黄")
				PRINT 　NEW！
				RESETCOLOR
			ENDIF
			PRINTL 
		ENDIF
	CATCH
		BREAK
	ENDCATCH
NEXT

;お祭りエリア
IF 開催予定日取り == DAY
	;特設エリアある？
	TRYCALLFORM MOVE_ROUTINE_%開催予定祭り名%(0, "特設マップ")
	;1が帰ってきたらある！
	IF RESULT
		PRINTBUTTON @"[100]%SUBSTRING(開催予定祭り名, 9)%マップへ移動する", 100
		PRINTL 
	ENDIF
ENDIF

PRINTBUTTON "[999]戻る", 999
PRINTL 
$INPUT_LOOP
BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 0
		IF CFLAG:PLAYER:現在マップ種別 == 0
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		;０を入れると移動してない時と見分けがつかないため+100した数値を入れる
		TFLAG:エリア切り替え時_目的地格納 = 100
	CASE 100
		IF CFLAG:PLAYER:現在マップ種別 == 100
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		TRYCALLFORM エリア情報登録_100("エリア名")
		;０を入れると移動してない時と見分けがつかないため+100した数値を入れる
		TFLAG:エリア切り替え時_目的地格納 = 200
	CASE 999
		RETURN -1
	CASEELSE
		IF CFLAG:PLAYER:現在マップ種別 == LOCAL
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		TRYCALLFORM エリア情報登録_{LOCAL}("エリア名")
		IF TSTR:場所名受渡 == ""
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		;+100した数値を入れる
		TFLAG:エリア切り替え時_目的地格納 = LOCAL + 100
ENDSELECT

RETURN 1


;-------------------------------------------------
;その他移動関連補助関数
;-------------------------------------------------
;キャラがPLAYERと一緒に移動している場合は1を返す
@追従中キャラ(キャラ番号)
#FUNCTION
#DIM キャラ番号

SIF CFLAG:キャラ番号:同行
	RETURNF 1
SIF MODE_存在判定_完全一致("ハーヴィンオナホ系", PLAYER, キャラ番号)
	RETURNF 1
SIF MODE_存在判定_完全一致("全裸散歩", PLAYER, キャラ番号)
	RETURNF 1

RETURNF 0

;あなたと一緒に移動しているキャラがいなければ1を返す
@あなた単独()
#FUNCTION

FOR LOCAL, 1, VARSIZE("TARGET")
	SIF TARGET:LOCAL <= 0
		BREAK
	SIF 追従中キャラ(TARGET:LOCAL)
		RETURNF 0
NEXT
RETURNF 1

;追跡設定されている時、追跡を行う
@追跡処理()

IF あなた追跡キャラ
	IF IS_SAME_ROOM(MASTER, あなた追跡キャラ) == 0
		;コマンド入力をスキップし移動処理を入れる
		RFLAG:追跡フラグ = あなた追跡キャラ
		RSTR:追跡時サイド記録 = %サイド描画表示種類%
		サイド描画表示種類 = クイック移動
		IF CFLAG:MASTER:現在マップ種別 == 999
			IF CFLAG:MASTER:現在マップ種別 == CFLAG:あなた追跡キャラ:現在マップ種別
				CFLAG:MASTER:現在位置 = CFLAG:あなた追跡キャラ:現在位置
			ELSE
				CFLAG:MASTER:現在マップ種別 = CFLAG:あなた追跡キャラ:現在マップ種別
				CALLFORM エリア情報登録_{CFLAG:MASTER:現在マップ種別}("エリア移動可能場所")
				CFLAG:MASTER:現在位置 = RESULT
			ENDIF
			サイド描画表示種類 = %RSTR:追跡時サイド記録%
			RETURN 1
		ELSEIF CFLAG:あなた追跡キャラ:現在マップ種別 == 999
			CALLFORM エリア情報登録_{CFLAG:MASTER:現在マップ種別}("エリア移動可能場所")
			IF CFLAG:MASTER:現在位置 != RESULT
				RESULTS = サイド領域_{RESULT}
			ELSE
				CFLAG:MASTER:現在マップ種別 = CFLAG:あなた追跡キャラ:現在マップ種別
				CFLAG:MASTER:現在位置 = CFLAG:あなた追跡キャラ:現在位置
				CALL 入室不可処理
				サイド描画表示種類 = %RSTR:追跡時サイド記録%
				RETURN 1
			ENDIF
		ELSE
			IF CFLAG:MASTER:現在マップ種別 == CFLAG:あなた追跡キャラ:現在マップ種別
				IF CFLAG:あなた追跡キャラ:現在マップ種別 == 1 || CFLAG:あなた追跡キャラ:現在マップ種別 == 2
					IF CFLAG:あなた追跡キャラ:現在位置 >= 1000
						;店舗か居住区なので直接移動
						CFLAG:MASTER:現在位置 = CFLAG:あなた追跡キャラ:現在位置
						CALL 入室不可処理
						サイド描画表示種類 = %RSTR:追跡時サイド記録%
						RETURN 1
					ELSE
						RESULTS = サイド領域_{CFLAG:あなた追跡キャラ:現在位置}
					ENDIF
				ELSE
					RESULTS = サイド領域_{CFLAG:あなた追跡キャラ:現在位置}
				ENDIF
			ELSE
				TFLAG:エリア切り替え時_目的地格納 = CFLAG:あなた追跡キャラ:現在マップ種別 + 100
				RESULTS = サイド領域_99
			ENDIF
		ENDIF
	ENDIF
ENDIF

RETURN 0
