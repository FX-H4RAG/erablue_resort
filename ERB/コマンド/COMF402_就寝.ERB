
@COMNAME402
#FUNCTIONS
TSTR:コマンド名受渡 = 就寝

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_402
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("単独可能")

@COM_TOOLTIP_402
LOCALS = <br>■就寝<br>
LOCALS += "就寝し、次の日に進むコマンド。<br>起床から１２時間以上経過した時に選択可能。"
LOCALS += "<br>COMタイプ：単独可能<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%


;-------------------------------------------------
;就寝
;-------------------------------------------------
@COM402
CFLAG:MASTER:睡眠 = 1
CFLAG:MASTER:就寝時刻 = DAY * 1440 + TIME
FLAG:一日終了 = 1
PRINTFORML {FLAG:1}
RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE402
;移動実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(402)
	RETURN RESULT
;私室設定
IF CFLAG:MASTER:現在マップ種別 == 0
	SIF CFLAG:MASTER:現在位置 != CFLAG:MASTER:311
		RETURN 0
ELSEIF TARGET > 0 && CFLAG:TARGET:現在マップ種別 == 999
	;陥落状態のキャラの部屋なら添い寝可能
	IF 陥落チェック(TARGET) && CFLAG:MASTER:現在位置 == CFLAG:TARGET:現在位置 && CFLAG:MASTER:現在位置 < 1000 && TARGET == 滞在者部屋割り配列:(CFLAG:MASTER:現在位置)
	ELSEIF TALENT:TARGET:従業員 && 陥落チェック(TARGET) && CFLAG:MASTER:現在位置 == CFLAG:TARGET:現在位置 && TARGET == 従業員部屋割り配列:(CFLAG:MASTER:現在位置 - 1000)
	ELSE
		RETURN 0
	ENDIF
ELSE
	RETURN 0
ENDIF
;起床後12時間以上経過してないとダメ
SIF TIME_PROGRESS(CFLAG:MASTER:314) < 720
	RETURN 0
SIF CFLAG:うふふ == 2
	RETURN 0
RETURN 1
;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM402
SIF TARGET == 0
	RETURN 0

IF CFLAG:MASTER:現在位置 == CFLAG:TARGET:現在位置
	IF CFLAG:TARGET:睡眠
		PRINTFORML %CALLNAME:PLAYER%は、%CALLNAME:TARGET%と一緒に眠りについた……
	ELSE
		PRINTFORML %CALLNAME:PLAYER%は、%CALLNAME:TARGET%の姿を見ながら眠りについた……
	ENDIF
ENDIF
