;-------------------------------------------------
;触手乳首責め
;触手系コマンド、レベル1
;-------------------------------------------------
@COMNAME100
#FUNCTIONS
TSTR:コマンド名受渡 = 触手乳首責め


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_100
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("触手系")
;特殊な条件が必要なコマンドなので、弱点コマンドのランダム対象からは外す
;弱点コマンドカテゴリ = B
同時モード許可 = 999


@COM_TOOLTIP_100
LOCALS = <br>■触手乳首責め<br>
LOCALS += "乳首に特化した触手を出し、強い快楽を与えるコマンド。"
LOCALS += "<br>COMタイプ：触手系<br>取得ソース：快Ｂ・露出・接触"
LOCALS += "<br>固有の取得経験：触手操作経験(あなた)、乳首開発(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM100
#DIM 快楽基礎値

CALL MODE_CHANGE("触手乳首責め", PLAYER, TARGET)
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
RCVAR:乳首開発フラグ = 3
RCVAR:触手コマンドフラグ = 1

快楽基礎値 = あなた特殊能力:触手変化術 * 3
CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 快楽基礎値 * 70)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 快楽基礎値 * 20)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 1
	EXP:PLAYER:同性愛経験 += 1
ENDIF

EXP:PLAYER:触手操作経験 ++
RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE100
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(100)
	RETURN RESULT
SIF MODE_確認_ターゲット側("触手乳首責め", TARGET) > -1
	RETURN 0
;Bがない場合駄目
SIF TALENT:TARGET:Ｂ感度 == -2
	RETURN 0
;ニプレスだと不可
SIF TEQUIP:上半身下着あり == 2
	RETURN 0
;将来的には許可する予定だけど（ユリウスとか）一旦あなた以外は封印
SIF PLAYER != MASTER
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM100
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%は

IF FLAG:モード管理 == 2
	PRINT 幾つもの触手を
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT 、
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT の乳首に吸い付かせた
	RETURN 0
ENDIF

PRINTFORM 自らの触手を%CALLNAME:TARGET%の
IF TEQUIP:上半身鎧あり
	PRINT 鎧の隙間から
	IF TEQUIP:上タイツあり
		PRINT タイツの下に
	ENDIF
	PRINT 潜り込ませ、
ELSEIF TEQUIP:上半身服あり
	PRINT 服の隙間から
	IF TEQUIP:上タイツあり
		PRINT タイツの下に
	ENDIF
	PRINT 潜り込ませ、
ELSEIF TEQUIP:上半身下着あり == 1
		IF TEQUIP:下タイツあり
			PRINT タイツの下から
		ENDIF
		PRINT ブラに潜り込ませ、
ENDIF
IF TALENT:陥没乳首 && !TCVAR:陥没乳首露出フラグ
	PRINT まだ乳輪に隠れている
ELSEIF PALAM:欲情 >= PALAMLV:3
	PRINT 硬くそそり勃った
ENDIF
PRINTL 乳首に吸い付かせた

