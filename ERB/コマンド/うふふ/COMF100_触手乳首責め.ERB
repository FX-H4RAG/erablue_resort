;-------------------------------------------------
;触手乳首責め
;触手系コマンド、レベル1
;-------------------------------------------------
@COMNAME100
#FUNCTIONS
TSTR:コマンド名受渡 = 触手乳首責め
IF MODE_確認("触手乳首責め", TARGET)
	TSTR:コマンド名受渡 = 触手乳首責め解除
ENDIF
;複数モード_ダミーをリセット
複数モード_ダミー = 0

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_100
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("触手系")
;特殊な条件が必要なコマンドなので、弱点コマンドのランダム対象からは外す
;弱点コマンドカテゴリ = B
同時モード許可 = 1


@COM_TOOLTIP_100
LOCALS = <br>■触手乳首責め<br>
LOCALS += "乳首に特化した触手を出し、強い快楽を与えるコマンド。"
LOCALS += "<br>COMタイプ：触手系<br>取得ソース：快Ｂ・露出・接触"
LOCALS += "<br>固有の取得経験：触手操作経験(あなた)、乳首開発(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM100
#DIM 快楽基礎値

IF MODE_確認("触手乳首責め", TARGET)
	CALL MODE_DELETE("触手乳首責め")
ELSE
	CALL MODE_CHANGE("触手乳首責め")
ENDIF
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------



RCVAR:乳首開発フラグ = 3
RCVAR:触手コマンドフラグ = 1

快楽基礎値 = あなた特殊能力:触手変化術 * 3
CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 快楽基礎値 * 70)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 快楽基礎値 * 20)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 1
	EXP:PLAYER:同性愛経験 += 1
ENDIF

EXP:PLAYER:触手操作経験 ++
RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE100
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(100)
	RETURN RESULT
;ニプレスだと不可
SIF TEQUIP:上半身下着あり == 2
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM100

IF MODE_確認("触手乳首責め", TARGET)
	;メッセージ表示タイミングはモード付加後なのでモードがあれば＝吸い付いたタイミング
	PRINTFORM %CALLNAME:PLAYER%は自らの触手を%CALLNAME:TARGET%の
	IF TEQUIP:上半身鎧あり
		PRINT 鎧の隙間から
		IF TEQUIP:上タイツあり
			PRINT タイツの下に
		ENDIF
		PRINT 潜り込ませ、
	ELSEIF TEQUIP:上半身服あり
		PRINT 服の隙間から
		IF TEQUIP:上タイツあり
			PRINT タイツの下に
		ENDIF
		PRINT 潜り込ませ、
	ELSEIF TEQUIP:上半身下着あり == 1
			IF TEQUIP:下タイツあり
				PRINT タイツの下から
			ENDIF
			PRINT ブラに潜り込ませ、
	ENDIF
	IF TALENT:陥没乳首 && !TCVAR:陥没乳首露出フラグ
		PRINT まだ乳輪に隠れている
	ELSEIF PALAM:欲情 >= PALAMLV:3
		PRINT 硬くそそり勃った
	ENDIF
	PRINTL 乳首に吸い付かせた
ELSE
	;メッセージ表示タイミングはモード付加後なのでモードが無いなら＝解除したタイミング
	PRINTFORM %CALLNAME:PLAYER%は触手を%CALLNAME:TARGET%の乳首から離した…
ENDIF

