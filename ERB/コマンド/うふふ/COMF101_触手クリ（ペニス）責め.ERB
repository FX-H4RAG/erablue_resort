;-------------------------------------------------
;触手クリ責め
;触手系コマンド、レベル1
;-------------------------------------------------
@COMNAME101
#FUNCTIONS
IF GETBIT(TALENT:TARGET:性別, 1)
	TSTR:コマンド名受渡 = 触手ペニス責め
	IF MODE_確認("触手ペニス責め", TARGET)
		TSTR:コマンド名受渡 = 触手ペニス責め解除
	ENDIF
ELSE
	TSTR:コマンド名受渡 = 触手クリ責め
	IF MODE_確認("触手クリ責め", TARGET)
		TSTR:コマンド名受渡 = 触手クリ責め解除
	ENDIF
ENDIF
;複数モード_ダミーをリセット
複数モード_ダミー = 0

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_101
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("触手系")
;特殊な条件が必要なコマンドなので、弱点コマンドのランダム対象からは外す
;弱点コマンドカテゴリ = C
同時モード許可 = 1


@COM_TOOLTIP_101
LOCALS = <br>■触手クリ（ペニス）責め<br>
LOCALS += "クリ責めに特化した触手を出し、強い快楽を与えるコマンド。<br>相手に男性器がある場合、ペニス責めに変化"
LOCALS += "<br>COMタイプ：触手系<br>取得ソース：快Ｃ・露出・接触"
LOCALS += "<br>固有の取得経験：触手操作経験(あなた)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM101
#DIM 快楽基礎値

IF GETBIT(TALENT:TARGET:性別, 1)
	IF MODE_確認("触手ペニス責め", TARGET)
		CALL MODE_DELETE("触手ペニス責め")
	ELSE
		CALL MODE_CHANGE("触手ペニス責め")
	ENDIF
ELSE
	IF MODE_確認("触手クリ責め", TARGET)
		CALL MODE_DELETE("触手クリ責め")
	ELSE
		CALL MODE_CHANGE("触手クリ責め")
	ENDIF
ENDIF
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------



RCVAR:触手コマンドフラグ = 1

快楽基礎値 = あなた特殊能力:触手変化術 * 3
CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER, 快楽基礎値 * 100)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 101)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 快楽基礎値 * 30)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 1
	EXP:PLAYER:同性愛経験 += 1
ENDIF

EXP:PLAYER:触手操作経験 ++
RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE101
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(101)
	RETURN RESULT
;前張りだと不可
SIF TEQUIP:下半身下着あり == 2
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM101

IF GETBIT(TALENT:TARGET:性別, 1)
	LOCALS = ペニス
ELSE
	LOCALS = クリ
ENDIF


IF MODE_確認(@"触手%LOCALS%責め", TARGET)
	;メッセージ表示タイミングはモード付加後なのでモードがあれば＝吸い付いたタイミング
	PRINTFORM %CALLNAME:PLAYER%は自らの触手を%CALLNAME:TARGET%の
	IF TEQUIP:下腹部前鎧あり || TEQUIP:下腹部後鎧あり
		PRINT 鎧の隙間から
		IF TEQUIP:下タイツあり
			PRINT タイツの下に
		ENDIF
		PRINT 潜り込ませ、
	ELSEIF TEQUIP:下半身服あり
		PRINT 服の隙間から
		IF TEQUIP:下タイツあり
			PRINT タイツの下に
		ENDIF
		PRINT 潜り込ませ、
	ELSEIF TEQUIP:下半身下着あり
		IF TEQUIP:下タイツあり
			PRINT タイツの下から
		ENDIF
		PRINT 下着に潜り込ませ、
	ENDIF
	IF PALAM:欲情 >= PALAMLV:3
		PRINT 硬くそそり勃った
	ENDIF
	PRINTFORML %LOCALS%に吸い付かせた
ELSE
	;メッセージ表示タイミングはモード付加後なのでモードが無いなら＝解除したタイミング
	PRINTFORM %CALLNAME:PLAYER%は触手を%CALLNAME:TARGET%の%LOCALS%から離した…
ENDIF



