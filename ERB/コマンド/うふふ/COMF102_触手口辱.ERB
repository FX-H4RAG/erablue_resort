;-------------------------------------------------
;触手口辱
;触手系コマンド、レベル1
;-------------------------------------------------
@COMNAME102
#FUNCTIONS
TSTR:コマンド名受渡 = 触手口辱


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_102
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("触手系")
;特殊な条件が必要なコマンドなので、弱点コマンドのランダム対象からは外す
;弱点コマンドカテゴリ = B
同時モード許可 = 999


@COM_TOOLTIP_102
LOCALS = <br>■触手口辱<br>
LOCALS += "キャラの口に触手を入れ、口腔内を嬲るコマンド。"
LOCALS += "<br>COMタイプ：触手系<br>取得ソース：快Ｓ・接触"
LOCALS += "<br>固有の取得経験：触手操作経験(PLAYER)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM102
#DIM 快楽基礎値

CALL MODE_CHANGE("触手口辱", PLAYER, TARGET)
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
RCVAR:触手コマンドフラグ = 1

快楽基礎値 = あなた特殊能力:触手変化術 * 3
CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 快楽基礎値 * 40)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 快楽基礎値 * 20)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 1
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 1

EXP:PLAYER:触手操作経験 ++
RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE102
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(102)
	RETURN RESULT
SIF MODE_存在判定_ターゲット側("触手口辱", TARGET)
	RETURN 0
;口を何かに使ってるなら無理
SIF TEQUIP:TARGET:口
	RETURN 0
;将来的には許可する予定だけど（ユリウスとか）一旦あなた以外は封印
SIF PLAYER != MASTER
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM102
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%は

IF FLAG:モード管理 == 2
	PRINT 幾つもの触手を
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT 、
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT の口に潜り込ませ、腔内や舌を蹂躙した
	RETURN 0
ENDIF

PRINTFORML 自らの触手を%CALLNAME:TARGET%の口に潜り込ませ、腔内や舌を蹂躙した


