;-------------------------------------------------
;触手緊縛
;触手系コマンド、レベル2
;-------------------------------------------------
@COMNAME105
#FUNCTIONS
TSTR:コマンド名受渡 = 触手緊縛
IF MODE_確認_完全一致("触手緊縛", PLAYER, TARGET) > -1
	TSTR:コマンド名受渡 = 触手緊縛解除
ENDIF
;複数モード_ダミーをリセット
複数モード_ダミー = 0

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_105
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("触手系")
;特殊な条件が必要なコマンドなので、弱点コマンドのランダム対象からは外す
;弱点コマンドカテゴリ = S
同時モード許可 = 1


@COM_TOOLTIP_105
LOCALS = <br>■触手緊縛<br>
LOCALS += "キャラを触手で緊縛し、動きを制限するコマンド。<br>モード適用中はオートコマンドが反応しない。"
LOCALS += "<br>COMタイプ：触手系<br>取得ソース：快Ｓ・接触・露出"
LOCALS += "<br>固有の取得経験：触手操作経験(あなた)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM105
#DIM 快楽基礎値

IF MODE_確認_完全一致("触手緊縛", PLAYER, TARGET) > -1
	CALL MODE_DELETE_ターゲット側("触手緊縛", TARGET)
ELSE
	CALL MODE_CHANGE("触手緊縛", PLAYER, TARGET)
ENDIF
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------



RCVAR:触手コマンドフラグ = 1

快楽基礎値 = あなた特殊能力:触手変化術 * 3
CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 快楽基礎値 * 10)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 快楽基礎値 * 50)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 快楽基礎値 * 20)

EXP:PLAYER:触手操作経験 ++
RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE105
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(105)
	RETURN RESULT
;モード実行中なら問答無用で許可
SIF MODE_確認_完全一致("触手緊縛", PLAYER, TARGET) > -1
	RETURN 1
;操作術レベル不足
SIF あなた特殊能力:触手変化術 < 2
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM105

IF MODE_確認_完全一致("触手緊縛", PLAYER, TARGET) > -1
	;メッセージ表示タイミングはモード付加後なのでモードがあれば＝吸い付いたタイミング
	PRINTFORM %CALLNAME:PLAYER%は自らの触手を%CALLNAME:TARGET%の体に巻き付かせ、自由を封じた。
ELSE
	;メッセージ表示タイミングはモード付加後なのでモードが無いなら＝解除したタイミング
	PRINTFORM %CALLNAME:PLAYER%は触手を%CALLNAME:TARGET%から離した…
ENDIF

