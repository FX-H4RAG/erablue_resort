;-------------------------------------------------
;乳首吸い
;愛撫系コマンド、レベル1
;-------------------------------------------------
@COMNAME10
#FUNCTIONS
SELECTCASE COMMODE()
CASE 1
	TSTR:コマンド名受渡 = 乳首吸い
CASE 2
	TSTR:コマンド名受渡 = 同時に乳首吸い
CASE 3
	TSTR:コマンド名受渡 = 助手が乳首吸い
ENDSELECT

;複数モード_ダミーをリセット
複数モード_ダミー = 0

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_10
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ_被挿入中")
弱点コマンドカテゴリ = B
同時モード許可 = 1
助手モード許可 = 1

@COM10
;-------------------------------------------------
;モードの設定
;-------------------------------------------------

;助手モードなら助手乳首吸いに
IF FLAG:モード管理 % 8 == 2
	CALL MODE_CHANGE("助手乳首吸い")
ELSE
	CALL MODE_CHANGE("乳首吸い")
ENDIF

弱点コマンドカテゴリ = B
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 10
DOWNBASE:気力 += 35

TCVAR:乳首開発フラグ = 2

CALL SOURCE_CALC_露出(TARGET, PLAYER, 70)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 70)
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 15)
CALL SOURCE_CALC_奉仕(TARGET, PLAYER, 100)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 3
	EXP:PLAYER:同性愛経験 += 3
ENDIF

SIF TALENT:恋慕
	EXP:愛情経験 += 1

SIF TALENT:TARGET:猫舌
	EXP:嗜虐快楽経験 += 1

;口淫経験
EXP:PLAYER:口淫経験 ++


RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE10
;乳首吸い
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(10)
	RETURN RESULT
;上半身着衣
;鎧、差し込めないorはだけない服、タイツ、ブラ等があると無理
SIF TEQUIP:上半身鎧あり
	RETURN 0
SIF TEQUIP:上半身服あり && (!TEQUIP:手差し込み可 && !TEQUIP:はだけ可)
	RETURN 0
SIF TEQUIP:上タイツあり && !TEQUIP:上タイツ破れ
	RETURN 0
SIF TEQUIP:上半身下着あり
	RETURN 0
;ニプルキャップ使用中は不可
SIF TEQUIP:16
	RETURN 0
;搾乳機使用中は不可
SIF TEQUIP:17
	RETURN 0
;乳房電極装着中はダメ
SIF TEQUIP:24
	RETURN 0
;口を何かに使ってる・届かないなら無理
IF GETBIT(TEQUIP:PLAYER:口, 0) || (PLAYER == MASTER && GETBIT(TEQUIP:PLAYER:口, TARGET1OR2(TARGET)))
;ただし、PLAYERの口が使用中かつ助手モードではなく、ターゲットが乳首吸い状態でなく、助手に乳首吸いモードがある（＝同時乳首吸いの処理）ならスルー
	SIF !(GETBIT(TEQUIP:PLAYER:口, 0) && (PLAYER == MASTER && MODE_確認("乳首吸い", 助手は誰だ())) && !MODE_確認("乳首吸い", TARGET))
		RETURN 0
ENDIF

;助手モードでPLAYERの口が届かないなら無理
;ただしPLAYERのMODEに助手乳首吸いが存在するならスルー
SIF PLAYER != MASTER && GETBIT(TEQUIP:PLAYER:口, 2) && !(PLAYER != MASTER && MODE_確認("助手乳首吸い", PLAYER))
	RETURN 0

;胸に届かないなら無理
SIF PLAYER == MASTER && GETBIT(TEQUIP:TARGET:胸, 1) && !MODE_確認("助手乳首吸い", TARGET)
	RETURN 0
SIF PLAYER != MASTER && GETBIT(TEQUIP:TARGET:胸, 2) && !MODE_確認("乳首吸い", TARGET) && !MODE_確認("助手乳首吸い", PLAYER)
	RETURN 0

;陥没乳首は乳首露出させてからでないと不可
SIF TALENT:陥没乳首 && !TCVAR:陥没乳首露出フラグ
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
;授乳手コキや乳首責めとの調整は保留

@MESSAGE_COM10
IF 使ってるの誰や(TARGET, 5) == PLAYER || 使ってるの誰や(TARGET, 6) == PLAYER
	PRINTFORM %CALLNAME:TARGET%とつながったまま%OPPAI_DESCRIPTION(TARGET)%
ELSE
	PRINTFORM %CALLNAME:TARGET%の%OPPAI_DESCRIPTION(TARGET)%
ENDIF

;欲情のPALAMLV3以上
IF PALAM:欲情 >= PALAMLV:3
	PRINT の先端の、すっかり硬くなった突起に
ELSEIF PALAM:欲情 >= PALAMLV:2
	PRINT の先端の、硬さを帯び始めた突起に
ELSE
	PRINT の先端の、まだ柔らかい突起に
ENDIF
PRINTL 吸い付いた…

