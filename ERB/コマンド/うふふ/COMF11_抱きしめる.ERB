;-------------------------------------------------
;愛撫
;愛撫系コマンド　レベル2
;-------------------------------------------------
@COMNAME11
#FUNCTIONS
TSTR:コマンド名受渡 = 抱きしめる
SIF FLAG:モード管理 == 2
	TSTR:コマンド名受渡 = まとめて抱きしめる

;複数モード_ダミーをリセット
複数モード_ダミー = 0


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_11
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ_セクハラ")
同時モード許可 = 3
助手モード許可 = 1

@COM_TOOLTIP_11
LOCALS = <br>■愛撫<br>
LOCALS += "相手を強く抱きしめるコマンド。<br>Ｖ・Ａ挿入中の場合、好感度要素：恋心が増加する。"
LOCALS += "<br>COMタイプ：うふふorセクハラ<br>取得ソース：接触・露出"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM11
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------


CALL SOURCE_CALC_露出(TARGET, PLAYER, 80)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 110)

IF MODE_確認_完全一致("Ｖ挿入系", PLAYER, TARGET) > -1 || MODE_確認_完全一致("Ａ挿入系", PLAYER, TARGET) > -1 || MODE_確認_完全一致("Ｖ挿入系", TARGET, PLAYER) > -1 || MODE_確認_完全一致("Ａ挿入系", TARGET, PLAYER) > -1
	CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, 300)
ENDIF
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 5
	EXP:PLAYER:同性愛経験 += 5
ENDIF


RETURN 1

;-------------------------------------------------
;愛撫実行判定
;-------------------------------------------------
@COM_ABLE11
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(11)
	RETURN RESULT

RETURN 1




;-------------------------------------------------
;愛撫時メッセージ
;-------------------------------------------------
@MESSAGE_COM11
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%は
IF FLAG:モード管理 == 2
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT 、
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT を引き寄せ、強く抱きしめた
	RETURN 0
ENDIF

PRINTFORML %CALLNAME:TARGET%を引き寄せ、強く抱きしめた。
