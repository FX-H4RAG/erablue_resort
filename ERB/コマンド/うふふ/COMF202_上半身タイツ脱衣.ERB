;-------------------------------------------------
;上半身タイツ脱衣
;------------------------------------------------
@COMNAME202
#FUNCTIONS
IF TEQUIP:上下一体タイツ
	TSTR:コマンド名受渡 = タイツを脱がす
ELSE
	TSTR:コマンド名受渡 = インナーを脱がす
ENDIF

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_202
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
オートコマンド許可 = 3

@COM_TOOLTIP_202
LOCALS = <br>■インナーを脱がす<br>
LOCALS += "相手の上半身のインナーを脱がせるコマンド。<br>上下一体タイツの場合、[タイツを脱がす]に合流する。"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM202
;口上出力のため実際の脱衣処理はEVENTCOMENDで

;オートコマンドだけここでやる
IF TFLAG:オートコマンドフラグ
	RFLAG:オートコマンド脱衣用フラグ = 1
	TEQUIP:上タイツあり = 0
	TEQUIP:上タイツ破れ = 0
	;上下一体の場合ここで下も脱がす
	IF TEQUIP:上下一体タイツ
		TEQUIP:下タイツあり = 0
		TEQUIP:下タイツ破れ = 0
		TEQUIP:上下一体タイツ = 0
	ENDIF
ENDIF
;野外
IF BEDROOM(CFLAG:TARGET:現在マップ種別, CFLAG:TARGET:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1
		RFLAG:コマンド結果受渡し変数 = 1
		RCVAR:TARGET:露出コマンドフラグ = 1
	ENDIF
ENDIF

RETURN 1


@CAN_COM202(ARG)
SIF RFLAG:指示モード対象あなたフラグ
	RETURN 1

SKIPDISP ARG
PRINTFORML 上半身脱衣(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
A = 0
S = 0
;すべての命令に共通の要素を考慮

CALL COM_ORDER
;ABL:露出癖
IF ABL:露出癖
	SIF S
		PRINT  + 
	A += ABL:露出癖 * 10
	PRINT 露出癖
	PRINTV 'LV,ABL:露出癖,'(,ABL:露出癖 * 10,')
	S = 1
ENDIF
IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 1
	PRINT 快楽刻印
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 1,')
	S = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情,10)
	SIF S
		PRINT  + 
	A += GETPALAMLV(PALAM:欲情,10) * 10
	PRINT 欲情
	PRINTV 'LV,GETPALAMLV(PALAM:欲情,10),'(,GETPALAMLV(PALAM:欲情,10) * 10,')
	S = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	A -= 1
	PRINT 恥じらい
	PRINTV '(,1,')
	S = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER) && TALENT:性別嗜好 > 0
	PRINT  - 
	A -= 5
	PRINT 男嫌い
	PRINTV '(,5,')
	S = 5
ENDIF
;恋慕
IF TALENT:恋慕
	SIF S
		PRINT  + 
	A += 5
	PRINT 恋慕
	PRINTV '(,5,')
	S = 5
ENDIF
;セフレ
IF TALENT:セフレ
	SIF S
		PRINT  + 
	A += 3
	PRINT セフレ
	PRINTV '(,3,')
	S = 3
ENDIF
;野外
IF BEDROOM(CFLAG:現在マップ種別, CFLAG:現在位置) == 0 && GET_TARGETNUM() > 1
	LOCAL:1 = 0
	FOR LOCAL, 1, CHARANUM
		SIF TARGET:LOCAL < 1
			BREAK
		SIF TARGET:LOCAL == TARGET
			CONTINUE
		IF CFLAG:LOCAL:うふふ == 0
			LOCAL:1 = 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:1 
		PRINT  - 
		A -= 5
		PRINT 野外露出
		PRINTV '(,5,')
		S = 5
	ENDIF
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF S
		PRINT  + 
	A += 999
	PRINT あなたの言いなり
	PRINTV '(,999,')
	S = 999
ENDIF

;合計を表示(14以上で実行)
PRINT  = 
PRINTV A

V = 18
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT
SKIPDISP 0
;実行できない
SIF A < V
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE202
;上半身タイツ脱衣
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(202)
	RETURN RESULT
;脱衣不可
SIF TALENT:脱衣不可
	RETURN 0
;既に着ていない
SIF !TEQUIP:上タイツあり
	RETURN 0
;鎧があるとダメ
SIF TEQUIP:上半身鎧あり
	RETURN 0
;服があるとダメ
SIF TEQUIP:上半身服あり
	RETURN 0
;上下一体だと下の服や鎧も影響
IF TEQUIP:上下一体タイツ
	SIF TEQUIP:下腹部後鎧あり || TEQUIP:下腹部前鎧あり
		RETURN 0
	SIF TEQUIP:下半身服あり
		RETURN 0
ENDIF
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM202
IF TEQUIP:上下一体タイツ
	PRINTFORML %CALLNAME:TARGET%のタイツを脱がせた…
ELSE
	PRINTFORML %CALLNAME:TARGET%のインナーを脱がせた…
ENDIF

IF RFLAG:コマンド結果受渡し変数
	PRINTFORML 人目のあるところで脱いだことで、%CALLNAME:TARGET%はどこか落ち着かないようだ…
ENDIF


