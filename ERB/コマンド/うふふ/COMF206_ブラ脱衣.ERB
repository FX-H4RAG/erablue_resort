;-------------------------------------------------
;ブラ脱衣
;-------------------------------------------------
@COMNAME206
#FUNCTIONS
IF TEQUIP:上半身下着あり == 2
	TSTR:コマンド名受渡 = ニプレスを剥がす
ELSE
	TSTR:コマンド名受渡 = ブラを脱がす
ENDIF

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_206
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
オートコマンド許可 = 3

@COM_TOOLTIP_206
LOCALS = <br>■ブラを脱がす<br>
LOCALS += "相手の上半身の下着を脱がせるコマンド。<br>上半身の下着がニプレスの場合、[ニプレスを剥がす]に変化。<br>上半身下着抜き取り可の場合、上半身の服があっても実行出来る。"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM206
;口上出力のため実際の脱衣処理はEVENTCOMENDで

;オートコマンドだけここでやる
IF TFLAG:オートコマンドフラグ
	RFLAG:オートコマンド脱衣用フラグ = 1
	TEQUIP:上半身下着あり = 0
ENDIF

RETURN 1


@CAN_COM206(ARG)
SIF RFLAG:指示モード対象あなたフラグ
	RETURN 1

SKIPDISP ARG
PRINTFORML 上半身脱衣(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
A = 0
S = 0
;すべての命令に共通の要素を考慮

CALL COM_ORDER
;ABL:露出癖
IF ABL:露出癖
	SIF S
		PRINT  + 
	A += ABL:露出癖 * 10
	PRINT 露出癖
	PRINTV 'LV,ABL:露出癖,'(,ABL:露出癖 * 10,')
	S = 1
ENDIF
IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 1
	PRINT 快楽刻印
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 1,')
	S = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情,10)
	SIF S
		PRINT  + 
	A += GETPALAMLV(PALAM:欲情,10) * 5
	PRINT 欲情
	PRINTV 'LV,GETPALAMLV(PALAM:欲情,10),'(,GETPALAMLV(PALAM:欲情,10) * 5,')
	S = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	A -= 1
	PRINT 恥じらい
	PRINTV '(,1,')
	S = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER) && TALENT:性別嗜好 > 0
	PRINT  - 
	A -= 5
	PRINT 男嫌い
	PRINTV '(,5,')
	S = 5
ENDIF
;恋慕
IF TALENT:恋慕
	SIF S
		PRINT  + 
	A += 5
	PRINT 恋慕
	PRINTV '(,5,')
	S = 5
ENDIF
;セフレ
IF TALENT:セフレ
	SIF S
		PRINT  + 
	A += 3
	PRINT セフレ
	PRINTV '(,3,')
	S = 3
ENDIF

;合計を表示(14以上で実行)
PRINT  = 
PRINTV A

V = 20
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT
SKIPDISP 0
;実行できない
SIF A < V
	RETURN 0
RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE206
;上半身下着脱衣
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(206)
	RETURN RESULT
;脱衣不可
SIF TALENT:脱衣不可
	RETURN 0
;既に着ていない
SIF !TEQUIP:上半身下着あり
	RETURN 0
;鎧があるとダメ
SIF TEQUIP:上半身鎧あり
	RETURN 0
;抜き取れない服があるとダメ
SIF TEQUIP:上半身服あり && !TEQUIP:上半身下着抜き取り可
	RETURN 0
;タイツもダメ
SIF TEQUIP:上タイツあり
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------

@MESSAGE_COM206
IF TEQUIP:上半身下着あり == 2
	PRINTFORML %CALLNAME:TARGET%のニプレスを剥がした
ELSE
	PRINTFORML %CALLNAME:TARGET%のブラを脱がせた
ENDIF
IF TALENT:TARGET:バストサイズ == 3
	PRINT どたぷんっ
	PRINT_IMG "えっちハート"
	PRINT と、豊満な乳房が大きく揺れながらまろびでた
ELSEIF TALENT:TARGET:バストサイズ == 2
	PRINT たっぷん、と大きな乳房がまろびでた
ENDIF
