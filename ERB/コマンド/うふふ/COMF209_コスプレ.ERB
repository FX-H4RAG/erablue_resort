;-------------------------------------------------
;コスプレ
;-------------------------------------------------
@COMNAME209
#FUNCTIONS
TSTR:コマンド名受渡 = コスプレ

;複数モード_ダミーをリセット
複数モード_ダミー = 0
;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_209
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
同時モード許可 = 1

@COM_TOOLTIP_209
LOCALS = <br>■コスプレ<br>
LOCALS += "相手に普段と違う服を着てもらうコマンド。"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM209

CALL コスプレ実処理(TARGET)
RETURN RESULT


@CAN_コスプレ判定(ARG, 服難易度 = 15)
#DIM 服難易度

SKIPDISP ARG
PRINTL コスプレ
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
A = 0
S = 0
;すべての命令に共通の要素を考慮

CALL COM_ORDER
;ABL:露出癖
IF ABL:露出癖
	SIF S
		PRINT  + 
	A += ABL:露出癖 * 10
	PRINT 露出癖
	PRINTV 'LV,ABL:露出癖,'(,ABL:露出癖 * 10,')
	S = 1
ENDIF
IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 1
	PRINT 快楽刻印
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 1,')
	S = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情,10)
	SIF S
		PRINT  + 
	A += GETPALAMLV(PALAM:欲情,10) * 5
	PRINT 欲情
	PRINTV 'LV,GETPALAMLV(PALAM:欲情,10),'(,GETPALAMLV(PALAM:欲情,10) * 5,')
	S = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	A -= 1
	PRINT 恥じらい
	PRINTV '(,1,')
	S = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER) && TALENT:性別嗜好 > 0
	PRINT  - 
	A -= 5
	PRINT 男嫌い
	PRINTV '(,5,')
	S = 5
ENDIF
;恋慕
IF TALENT:恋慕
	SIF S
		PRINT  + 
	A += 5
	PRINT 恋慕
	PRINTV '(,5,')
	S = 5
ENDIF
;親愛
IF TALENT:恋慕 > 1
	SIF S
		PRINT  + 
	A += 5
	PRINT 親愛
	PRINTV '(,5,')
	S = 5
ENDIF

;合計を表示(20以上で実行)
PRINT  = 
PRINTV A

SIF A < 服難易度
	PRINT  < 
SIF A == 服難易度
	PRINT  = 
SIF A > 服難易度
	PRINT  > 
PRINT 実行値
PRINTV 服難易度

WAIT
SKIPDISP 0
;実行できない
SIF A < 服難易度
	RETURN 0
RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE209
;全部脱衣
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(209)
	RETURN RESULT

RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM209

PRINTFORML %CALLNAME:TARGET%に%RSTR:コマンド結果受渡し文字列%に着がえてもらった。




@コスプレ実処理(キャラ番号)
#DIM キャラ番号
#DIM ページ数
#DIM 実行難易度
#DIMS コスプレ一時配列, 200
#DIMS 分割文字列配列, 2
ページ数 = 0

DRAWLINE
PRINTL 着せたい服を選んでください
DRAWLINE
LOCALS = <div rect='13px,5px,935px,450px' border='5px' bcolor='#C0C0C0'></div>

;仕事の説明を表示
LOCALS += "<div rect='25px,25px,920px,450px'>"
コスプレ一時配列:0 = %所持制服文字列%%所持エロ衣装文字列%
SPLIT コスプレ一時配列:0, "_", コスプレ一時配列
FOR LOCAL, 0, 100
	IF コスプレ一時配列:LOCAL == ""
		BREAK
	ENDIF
	SPLIT コスプレ一時配列:LOCAL, "/", 分割文字列配列
	LOCALS += @"<button value='{LOCAL+1}'>[{LOCAL+1, 3}] %分割文字列配列:1, 40, LEFT%に変更　　</button>"
	SIF LOCAL % 2
		LOCALS += "<br>"
NEXT
LOCALS += "</div>"
;LOCALS += "<div rect='800px,420px,588px,50px'>"
LOCALS += "<div rect='640px,420px,588px,50px'>"
LOCALS += "<button value='0'>[0] 元の衣装に戻す</button>　"
LOCALS += "<button value='999'>[999] 戻る</button>"
LOCALS += "</div>"


HTML_PRINT LOCALS, 1
FOR LOCAL, 0, 25
	PRINTL
NEXT
DRAWLINE

DO
BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 999
		RETURN -1
	CASE 0
		CALL CLOTHES_RESET(キャラ番号)
		CALL CLOTHES_CHANGE(キャラ番号, CSTR:キャラ番号:コスプレ前の服)
		CSTR:キャラ番号:コスプレ前の服 '= ""
		RSTR:コマンド結果受渡し文字列 = 元の衣装
		RETURN 1
	CASEELSE
		LOCAL -= 1
		;一般衣装とエロ衣装で難易度を変える予定だが未実装、とりあえず15で固定
		SPLIT コスプレ一時配列:LOCAL, "/", 分割文字列配列
		IF 分割文字列配列:0 == "エロ"
			実行難易度 = 25
		ELSE
			実行難易度 = 15
		ENDIF
		CALL CAN_コスプレ判定(1, 実行難易度)
		IF RESULT == 0
			CALL CAN_コスプレ判定(0, 実行難易度)
			RETURN -1
		ENDIF
		SIF CSTR:キャラ番号:コスプレ前の服 == "" && CSTR:キャラ番号:表示グラフィックカテゴリ != ""
			CSTR:キャラ番号:コスプレ前の服 '= CSTR:キャラ番号:表示グラフィックカテゴリ
		LOCALS '= CSTR:キャラ番号:脱ぐ前の服
		SPLIT コスプレ一時配列:LOCAL, "/", 分割文字列配列
		CALL CLOTHES_CHANGE(キャラ番号, 分割文字列配列:1)
		CSTR:キャラ番号:脱ぐ前の服 '= LOCALS
		RSTR:コマンド結果受渡し文字列 = %分割文字列配列:1%
		RETURN 1
ENDSELECT
LOOP 1
