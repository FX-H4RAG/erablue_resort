
@COMNAME_S20_2
#FUNCTIONS
TSTR:コマンド名受渡 = 耳をふさぐ

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_20_2
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ_被挿入中_セクハラ")
弱点コマンドカテゴリ = S

@COM_TOOLTIP_S20_2
LOCALS = <br>■耳をふさぐ<br>
LOCALS += "キス系モード中、相手の耳を塞いでキスに集中させるコマンド。<br>[舌使い][猫舌]によって効果が増減。"
LOCALS += "<br>COMタイプ：うふふor被挿入中orセクハラ<br>取得ソース：露出・接触・快Ｓ・不潔・苦痛(一定条件時)"
LOCALS += "<br>固有の取得経験：キス経験(あなた・TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM_S20_2
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;奉仕コマンドフラグ
TCVAR:TARGET:奉仕コマンドフラグ = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 0
DOWNBASE:気力 += 20

CALL SOURCE_CALC_露出(TARGET, PLAYER, 100)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 250)
CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 200)
CALL SOURCE_CALC_好感度要素_恋心度UP(TARGET, PLAYER, 150)

CALL SOURCE_CALC_不潔(TARGET, PLAYER, 50)


;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------

;舌を使う調教では、調教者の舌使いによって、快感のソース増加
IF TALENT:PLAYER:舌使い
	CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
	CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 100)
ENDIF

;猫舌では減少
SIF TALENT:PLAYER:猫舌
	CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 20)


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 3
	EXP:PLAYER:同性愛経験 += 3
ENDIF


SIF TALENT:恋慕
	EXP:愛情経験 += 1

SIF TALENT:TARGET:猫舌
	EXP:嗜虐快楽経験 += 1

EXP:キス経験 ++
EXP:PLAYER:キス経験 ++
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S20_2
;耳をふさぐ実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE_S("20_2")
	RETURN RESULT
;キス系モードじゃない時はアウト
SIF MODE_確認("キス系", TARGET) == 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S20_2
PRINTFORML %CALLNAME:PLAYER%は唇を合わせながら%CALLNAME:TARGET%の耳をふさぎ、キスの水音だけに集中させた…
