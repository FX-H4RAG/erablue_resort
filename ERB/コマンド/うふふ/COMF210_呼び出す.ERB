;-------------------------------------------------
;呼び出す
;-------------------------------------------------
@COMNAME210
#FUNCTIONS
TSTR:コマンド名受渡 = 呼び出す

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_210
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("複数人プレイ")

@COM_TOOLTIP_210
LOCALS = <br>■呼び出す<br>
LOCALS += "性交渉中、他のキャラをこの場に呼び出すコマンド。<br>現在対象、及び呼び出す対象は[恋慕]か[セフレ]を所持していなければならない。"
LOCALS += "<br>COMタイプ：複数人プレイ<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM210
#DIM 表示候補, 1000
#DIM 現在ページ
#DIM 呼び出し人数

VARSET 表示候補, -1
現在ページ = 0
VARSET 呼び出し対象
呼び出し人数 = 0
CALL 呼び出す候補キャラ登録(表示候補)

$表示_LOOP
DRAWLINE
PRINTL 誰を呼び出しますか？
DRAWLINE
LOCAL:1 = 0
;キャラの表示（15人まで）
FOR LOCAL, (現在ページ * 15), ((現在ページ * 15) + 15)
	;登録が終わったら表示終了
	IF 表示候補:LOCAL == -1
		BREAK
	ENDIF
	SIF MATCH(呼び出し対象, 表示候補:LOCAL)
		SETCOLOR カラーパレット("黄")
	PRINTBUTTONLC @"[{LOCAL, 2}] %ADD_STR_SPACE(NAME:(表示候補:LOCAL), "]"), 41, LEFT%", LOCAL
	RESETCOLOR
	IF MAXBASE:(表示候補:LOCAL):体力 == BASE:(表示候補:LOCAL):体力
		SETCOLOR 0x50B050
	ELSEIF MAXBASE:(表示候補:LOCAL):体力 < BASE:(表示候補:LOCAL):体力 / 3
		SETCOLOR カラーパレット("赤")
	ENDIF
	PRINTFORM 体力：{BASE:(表示候補:LOCAL):体力, 4} / {MAXBASE:(表示候補:LOCAL):体力, 4}　
	RESETCOLOR
	IF BASE:(表示候補:LOCAL):性欲 > 2000
		SETCOLOR カラーパレット("ピンク")
	ELSEIF BASE:(表示候補:LOCAL):性欲 > 1000
		SETCOLOR カラーパレット("薄ピンク")
	ENDIF
	PRINTFORM 性欲：{BASE:(表示候補:LOCAL):性欲, 4}　
	RESETCOLOR
	IF CFLAG:(表示候補:LOCAL):発情期フラグ < 0
		SETCOLOR カラーパレット("えっちな色")
		PRINT 発情期
		PRINT_IMG "えっちハート"
		PRINT 　
		RESETCOLOR
	ELSE
		PRINT 　　　　　
	ENDIF
	IF TALENT:(表示候補:LOCAL):恋慕
		SETCOLOR カラーパレット("えっちな色")
		PRINT 恋慕
		RESETCOLOR
	ELSEIF TALENT:(表示候補:LOCAL):セフレ
		PRINT セフレ
	ENDIF
	PRINTL 
NEXT

DRAWLINE

;ページめくり表示
IF 現在ページ > 0
	PRINTBUTTONLC "[900] 前ページへ", 900
ELSE
	PRINTLC 　
ENDIF
PRINTFORM - {現在ページ + 1} -
IF 表示候補:((現在ページ * 15) + 15) > 0
	PRINTBUTTONC "[901] 次ページへ", 901
ENDIF
PRINTL 
IF 呼び出し人数
	PRINTBUTTON @"[998] 以上の{呼び出し人数}人を呼び出す", 998
ELSE
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAINFORM [998] 以上の{呼び出し人数}人を呼び出す
	RESETCOLOR
ENDIF
PRINT 　　　
PRINTBUTTON @"[997] 呼び出し可能キャラを全員呼び出す", 997
PRINTL 
PRINTBUTTON "[999] やめる", 999

BINPUT
SELECTCASE RESULT
	CASE 900
		現在ページ = MAX(現在ページ - 1, 0)
		GOTO 表示_LOOP
	CASE 901
		SIF LOCAL:1 == 0
			現在ページ += 1
		GOTO 表示_LOOP
	CASE 999
		RETURN -1
	CASE 998
		FOR LOCAL, 0, 500
			SIF 呼び出し対象:LOCAL < 1
				CONTINUE
			CFLAG:(呼び出し対象:LOCAL):現在位置 = CFLAG:PLAYER:現在位置
			CFLAG:(呼び出し対象:LOCAL):現在マップ種別 = CFLAG:PLAYER:現在マップ種別
			CALL うふふ開始キャラ処理(呼び出し対象:LOCAL)
			;野外オナニー時に備えて隠密を消す
			CALL 野外オナニー途中終了(呼び出し対象:LOCAL)
			CFLAG:(呼び出し対象:LOCAL):隠密 = 0
			;先に同室フラグを建てることで発覚口上をスキップする
			CFLAG:(呼び出し対象:LOCAL):同室 = 1
			SIF RFLAG:コマンド結果受渡し変数 < 1
				RFLAG:コマンド結果受渡し変数 = 呼び出し対象:LOCAL
		NEXT
		SIF 呼び出し人数 > 1
			RFLAG:コマンド結果受渡し変数２ = 1
		;モード管理フラグを初期化
		FLAG:モード管理 = 0
		RETURN 0
	CASE 997
		呼び出し人数 = 0
		FOR LOCAL, 0, VARSIZE("表示候補")
			;登録が終わったら表示終了
			IF 表示候補:LOCAL == -1
				BREAK
			ENDIF
			CFLAG:(表示候補:LOCAL):現在位置 = CFLAG:PLAYER:現在位置
			CFLAG:(表示候補:LOCAL):現在マップ種別 = CFLAG:PLAYER:現在マップ種別
			CALL うふふ開始キャラ処理(表示候補:LOCAL)
			;野外オナニー時に備えて隠密を消す
			CALL 野外オナニー途中終了(表示候補:LOCAL)
			CFLAG:(表示候補:LOCAL):隠密 = 0
			;先に同室フラグを建てることで発覚口上をスキップする
			CFLAG:(表示候補:LOCAL):同室 = 1
			SIF RFLAG:コマンド結果受渡し変数 < 1
				RFLAG:コマンド結果受渡し変数 = 表示候補:LOCAL
			呼び出し人数 ++
		NEXT
		SIF 呼び出し人数 > 1
			RFLAG:コマンド結果受渡し変数２ = 1
		;モード管理フラグを初期化
		FLAG:モード管理 = 0
		RETURN 0
	CASEELSE
		IF MATCH(呼び出し対象, 表示候補:RESULT)
			FOR LOCAL, 0, 500
				IF 呼び出し対象:LOCAL == 表示候補:RESULT
					呼び出し対象:LOCAL = 0
					呼び出し人数 --
					BREAK
				ENDIF
			NEXT
		ELSE
			FOR LOCAL, 0, 500
				IF 呼び出し対象:LOCAL < 1
					呼び出し対象:LOCAL = 表示候補:RESULT
					呼び出し人数 ++
					BREAK
				ENDIF
			NEXT
		ENDIF
		GOTO 表示_LOOP
ENDSELECT


@呼び出す候補キャラ登録(表示候補)
#DIM REF 表示候補
LOCAL:1 = 0
FOR LOCAL, 1, CHARANUM
	;滞在してないキャラは出さない
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	;セフレでも恋慕でもないキャラは出さない
	SIF !TALENT:LOCAL:恋慕 && !TALENT:LOCAL:セフレ
		CONTINUE
	;睡眠中はアウト
	SIF CFLAG:LOCAL:睡眠
		CONTINUE
	;既に同室者はアウト
	SIF MATCH(TARGET, LOCAL)
		CONTINUE
	表示候補:(LOCAL:1) = LOCAL
	LOCAL:1 += 1
NEXT

RETURN LOCAL:1

@CAN_COM210(ARG)
SKIPDISP ARG
PRINTFORML 他のキャラを呼び出す(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------

;体力限界時はオート許可
SIF CFLAG:体力限界
	RETURN 1

A = 0
S = 0
;すべての命令に共通の要素を考慮

CALL COM_ORDER
;ABL:露出癖
IF ABL:露出癖
	SIF S
		PRINT  + 
	A += ABL:露出癖 * 10
	PRINT 露出癖
	PRINTFORM LV{ABL:露出癖}({ABL:露出癖 * 10})
	S = 1
ENDIF
IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 1
	PRINT 快楽刻印
	PRINTFORM LV{MARK:快楽刻印}({MARK:快楽刻印 * 1})
	S = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情,10)
	SIF S
		PRINT  + 
	A += GETPALAMLV(PALAM:欲情,10) * 5
	PRINT 欲情
	PRINTFORM LV{GETPALAMLV(PALAM:欲情,10)}({GETPALAMLV(PALAM:欲情,10) * 5})
	S = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	A -= 5
	PRINT 恥じらい
	PRINT (5)
	S = 5
ENDIF
;恋慕
IF TALENT:恋慕
	SIF S
		PRINT  + 
	A += 5
	PRINT 恋慕
	PRINT (5)
	S = 5
ENDIF
;セフレ
IF TALENT:セフレ
	SIF S
		PRINT  + 
	A += 7
	PRINT セフレ
	PRINT (7)
	S = 7
ENDIF
;ニーア
IF TARGET == 64
	PRINT  - 
	A -= 999
	PRINT ニーア
	PRINT (999)
	S = 5
ENDIF

;合計を表示(14以上で実行)
PRINT  = 
PRINTV A

V = 40
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT
SKIPDISP 0
;実行できない
SIF A < V
	RETURN 0
RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE210
;呼び出す
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;複数人プレイに入るためのコマンドのため、一括管理はせずここで全部記述する
SIF TFLAG:調教モード != 2
	RETURN 0
SIF TARGET:1 <= 0 || CFLAG:睡眠 == 1 || CFLAG:隠密
	RETURN 0
;隠密プレイ時は除外
SIF CFLAG:PLAYER:隠密 || CFLAG:TARGET:隠密
	RETURN 0
;相手が恋慕、あるいはセフレでないときアウト
SIF !TALENT:恋慕 && !TALENT:セフレ
	RETURN 0
;指示モード時はだめ
SIF PLAYER != MASTER
	RETURN 0
;寝取らせ夜這い中はダメ
SIF TCVAR:言いなり
	RETURN 0

RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM210

PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:(RFLAG:コマンド結果受渡し変数)%
SIF RFLAG:コマンド結果受渡し変数２
	PRINT たち
PRINTL を呼び出した

