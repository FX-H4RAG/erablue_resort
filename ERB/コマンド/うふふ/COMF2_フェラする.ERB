;-------------------------------------------------
;フェラする
;愛撫系コマンド、レベル1
;-------------------------------------------------
@COMNAME2
#FUNCTIONS
TSTR:コマンド名受渡 = フェラする

SIF MODE_確認("フェラチオ系", TARGET)
	TSTR:コマンド名受渡 = シックスナイン
;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_2
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
弱点コマンドカテゴリ = C_ちんこ

@COM_TOOLTIP_2
LOCALS = <br>■フェラする<br>
LOCALS += "相手のペニスを中心に舌で愛撫を行うコマンド。<br>フェラチオ系のモード継続時、「シックスナイン」に変化する。<br>実行者が[舌使い][猫舌]を持っている場合、効果が増減する。"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：快Ｃ・露出・接触・征服"
LOCALS += "<br>固有の取得経験：口淫経験(あなた)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM2
;-------------------------------------------------
;モードの設定
;-------------------------------------------------
IF MODE_確認("フェラチオ系", TARGET)
	CALL MODE_CHANGE("シックスナイン")
	RFLAG:コマンド結果受渡し変数 = 1
ELSE
	CALL MODE_CHANGE("逆フェラ")
ENDIF
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力
DOWNBASE:体力 += 5

LOCAL = 160
;舌を使う調教では、調教者の舌使いによって、快感のソース増加
SIF TALENT:PLAYER:舌使い
	TIMES LOCAL , 1.50
;猫舌では減少
SIF TALENT:PLAYER:猫舌
	TIMES LOCAL , 0.75
CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER, LOCAL)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 25)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 100)
CALL SOURCE_CALC_征服(TARGET, PLAYER, 50)
CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 3
	EXP:PLAYER:同性愛経験 += 3
ENDIF
;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
SIF TALENT:PLAYER:猫舌
	EXP:PLAYER:嗜虐快楽経験 += 1
;口淫経験
EXP:PLAYER:口淫経験 ++

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE2
;フェラする実行判定
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(2)
	RETURN RESULT
;猫舌の調教者の場合は技巧Lv2以上必要
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 2
	RETURN 0
;オトコかふたなりじゃないとダメ
SIF !(TALENT:性別 & 2)
	RETURN 0

;下半身着衣
;前鎧、めくれない服、タイツ、前張りがあると無理
SIF TEQUIP:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0
SIF TEQUIP:下半身下着あり == 2
	RETURN 0
;オナホール使用中はダメ
SIF TEQUIP:12
	RETURN 0
IF MODE_確認("フェラチオ系", TARGET)
ELSE
	;口を何かに使ってる・届かないなら無理
	SIF TEQUIP:PLAYER:口
		RETURN 0
	;ちんこを何かに使ってる・届かないなら無理
	SIF TEQUIP:TARGET:ペニス
		RETURN 0
ENDIF
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------

@MESSAGE_COM2
IF RFLAG:コマンド結果受渡し変数 == 1
	PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%と頭の位置が互い違いになるように体勢を変えて、
ELSEIF MODE_確認("シックスナイン", TARGET)
	PRINTFORM %CALLNAME:PLAYER%は眼の前にある%CALLNAME:TARGET%の
ENDIF
PRINTFORM %CALLNAME:TARGET%の
;欲情のPALAMLV3以上
SIF PALAM:欲情 >= PALAMLV:3
	PRINT 硬くそそり勃った
PRINTL ペニスを口に咥えて舐めまわした…

