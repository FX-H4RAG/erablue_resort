;-------------------------------------------------
;クリキャップ
;道具使用コマンド、レベル2
;-------------------------------------------------
@COMNAME42
#FUNCTIONS
TSTR:コマンド名受渡 = クリキャップ

;弱点コマンド欄に専用の文字列を表示したい場合、これを設定すること
弱点コマンド欄表示名 = クリキャップ



;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_42
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("道具系")
弱点コマンドカテゴリ = C_女
同時モード許可 = 999



@COM_TOOLTIP_42
LOCALS = <br>■クリキャップ<br>
LOCALS += "クリトリスに自動で振動する性具を取りつけ、刺激するコマンド。<br>クリキャップ装備状態となり、外すか性行為を終了するまで持続的に快楽が発生する。"
LOCALS += "<br>COMタイプ：道具系<br>取得ソース：快Ｃ・露出・接触"
LOCALS += "<br>固有の取得経験：道具使用経験(PLAYER)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM42
#DIM 強さ初期値記録

IF OPTION変数:クリキャップ強さ初期値 == -1
	$選択LOOP
	PRINTL
	PRINTL
	PRINTL
	DRAWLINE
	PRINTL ■強さの初期値を選んでください
	PRINT 　　クリキャップ強さ：
	PRINTBUTTON "[-]", -1
	FOR LOCAL, 0, 6
		IF 強さ初期値記録 >= LOCAL
			PRINTBUTTON "■", LOCAL
		ELSE
			PRINTBUTTON "□", LOCAL
		ENDIF
	NEXT
	PRINTBUTTON "[+]", "6"
	PRINTL
	PRINTL
	PRINTBUTTONLC "[900] クリキャップを装着する", 900
	PRINTBUTTONLC "[999] 戻る", 999

	BINPUT
	LOCAL = RESULT
	SELECTCASE LOCAL
		CASE 0 TO 5
			強さ初期値記録 = LOCAL
			GOTO 選択LOOP
		CASE -1
			強さ初期値記録 = MAX(強さ初期値記録 - 1, 0)
			GOTO 選択LOOP
		CASE 6
			強さ初期値記録 = MIN(強さ初期値記録 + 1, 5)
			GOTO 選択LOOP
		CASE 900
			;着脱の処理
			CALL MODE_CHANGE("クリキャップ", PLAYER, TARGET, , , 強さ初期値記録)
		CASE 999
			RETURN -1
	ENDSELECT
ELSE
	;着脱の処理
	CALL MODE_CHANGE("クリキャップ", PLAYER, TARGET, , , OPTION変数:クリキャップ強さ初期値)
ENDIF


;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER,200)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 20)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 10)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 1
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 1

EXP:PLAYER:道具使用経験 ++

RCVAR:TARGET:道具コマンドフラグ = 1
RETURN 1

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_快Ｃ(TARGET, -1,100)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 10)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------

RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE42
;電動クリキャップ実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(42)
	RETURN RESULT
;すでにあるとだめ　(部位使ってる判定と被ってるのでいらない？)
;SIF MODE_存在判定_ターゲット側("クリキャップ", TARGET)
;	RETURN 0
;クリがない場合駄目
SIF TALENT:TARGET:Ｃ感度 == -2
	RETURN 0
;オトコやふたなりはダメ
SIF TALENT:性別 & 2
	RETURN 0
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF ITEM:2 == 0 && NOITEM == 0
	RETURN 0

;下半身着衣
;前鎧、めくれない服、タイツ、下着があると無理
SIF TEQUIP:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0
SIF TEQUIP:下半身下着あり
	RETURN 0

;装着者が指を何かに使ってるなら無理
SIF TEQUIP:PLAYER:指
	RETURN 0
;部位使ってると駄目
SIF TEQUIP:TARGET:クリトリス && !TFLAG:コマンド条件制御用
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM42
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%は
IF GETBIT(FLAG:モード管理, モードビット_同時)
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT 、
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT 全員に電動クリキャップを取り付けた
	RETURN 0
ENDIF
PRINTFORML %CALLNAME:TARGET%に電動クリキャップを取り付けた




