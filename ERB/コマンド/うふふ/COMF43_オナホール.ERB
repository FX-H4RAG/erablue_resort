;-------------------------------------------------
;オナホール
;道具使用コマンド、レベル2
;-------------------------------------------------
@COMNAME43
#FUNCTIONS
TSTR:コマンド名受渡 = オナホール

;弱点コマンド欄に専用の文字列を表示したい場合、これを設定すること
弱点コマンド欄表示名 = オナホール


;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_43
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("道具系")
弱点コマンドカテゴリ = C_ちんこ
同時モード許可 = 999


@COM_TOOLTIP_43
LOCALS = <br>■オナホール<br>
LOCALS += "ペニスに自動で振動する性具を取りつけ、刺激するコマンド。<br>オナホール装備状態となり、外すか性行為を終了するまで持続的に快楽が発生する。"
LOCALS += "<br>COMタイプ：道具系<br>取得ソース：快Ｃ・露出・接触"
LOCALS += "<br>固有の取得経験：道具使用経験(PLAYER)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM43
;着脱の処理
CALL MODE_CHANGE("オナホール", PLAYER, TARGET)
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER,200)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 20)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 10)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 1
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 1

EXP:PLAYER:道具使用経験 ++
RCVAR:TARGET:道具コマンドフラグ = 1


RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE43
;オナホール実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(43)
	RETURN RESULT
;使用済みだとだめ
SIF MODE_存在判定_ターゲット側("オナホール", TARGET)
	RETURN 0
;ちんこがない場合駄目
SIF TALENT:TARGET:Ｃ感度 == -2
	RETURN 0
SIF GETBIT(TALENT:TARGET:性別, 1) == 0
	RETURN 0
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF ITEM:3 == 0 && NOITEM == 0
	RETURN 0

;下半身着衣
;前鎧、めくれない服、タイツ、下着があると無理
SIF TEQUIP:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0
SIF TEQUIP:下半身下着あり
	RETURN 0
	
;部位使ってると駄目
SIF TEQUIP:TARGET:ペニス
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM43
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%は
IF FLAG:モード管理 == 2
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT 、
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT 全員のペニスをそれぞれのオナホールに挿入した
	RETURN 0
ENDIF
PRINTFORML %CALLNAME:TARGET%のペニスをオナホールに挿入した



