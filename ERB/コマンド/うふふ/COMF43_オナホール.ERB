;-------------------------------------------------
;オナホール
;道具使用コマンド、レベル2
;-------------------------------------------------
@COMNAME43
#FUNCTIONS
TSTR:コマンド名受渡 = オナホール
SIF TEQUIP:オナホール
	TSTR:コマンド名受渡 = オナホール解除

;弱点コマンド欄に専用の文字列を表示したい場合、これを設定すること
弱点コマンド欄表示名 = オナホール

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_43
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("道具系")
弱点コマンドカテゴリ = C_ちんこ
SIF TEQUIP:オナホール
	弱点コマンドカテゴリ = 

@COM_TOOLTIP_43
LOCALS = <br>■オナホール<br>
LOCALS += "ペニスに自動で振動する性具を取りつけ、刺激するコマンド。<br>オナホール装備状態となり、外すか性行為を終了するまで持続的に快楽が発生する。"
LOCALS += "<br>COMタイプ：道具系<br>取得ソース：快Ｃ・露出・接触"
LOCALS += "<br>固有の取得経験：道具使用経験(あなた)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM43
;着脱の処理
TEQUIP:オナホール = !TEQUIP:オナホール

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 10
DOWNBASE:気力 += 80

CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER,200)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 20)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 10)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 1
	EXP:PLAYER:同性愛経験 += 1
ENDIF

EXP:PLAYER:道具使用経験 ++

;-------------------------------------------------
;モードの設定
;-------------------------------------------------
CALL MODE_DELETE("逆アナル系")

RETURN 1
;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM43(ARG)
PRINTL ＜オナホール装備中＞
TEQUIP:TARGET:ペニス = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
DOWNBASE:体力 += 5
DOWNBASE:気力 += 20

CALL SOURCE_CALC_快Ｃ(TARGET, -1,100)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 10)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------

RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE43
;オナホール実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(43)
	RETURN RESULT
;解除はいつでも可能
SIF TEQUIP:12
	RETURN 1
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF ITEM:3 == 0 && NOITEM == 0
	RETURN 0
;オトコやふたなりじゃないとダメ
SIF !(TALENT:性別 & 2)
	RETURN 0

;下半身着衣
;前鎧、めくれない服、タイツ、下着があると無理
SIF TEQUIP:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0
SIF TEQUIP:下半身下着あり
	RETURN 0
	
;新妻プレイ中はダメ
SIF TEQUIP:33
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM43
;解除時
IF !TEQUIP:オナホール
	PRINTFORML %CALLNAME:TARGET%からオナホールを外した
ELSE
	PRINTFORML %CALLNAME:TARGET%のペニスをオナホールに挿入した
ENDIF


