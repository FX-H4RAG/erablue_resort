;-------------------------------------------------
;アナルバイブ
;道具使用コマンド、レベル2
;-------------------------------------------------
@COMNAME45
#FUNCTIONS
TSTR:コマンド名受渡 = アナルバイブ
SIF TEQUIP:アナルバイブ
	TSTR:コマンド名受渡 = アナルバイブ解除

;弱点コマンド欄に専用の文字列を表示したい場合、これを設定すること
弱点コマンド欄表示名 = アナルバイブ

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_45
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("道具系")
弱点コマンドカテゴリ = A
SIF TEQUIP:アナルバイブ
	弱点コマンドカテゴリ = 

@COM_TOOLTIP_45
LOCALS = <br>■アナルバイブ<br>
LOCALS += "電動のアナルバイブを挿入し、腸内を刺激するコマンド。<br>アナルバイブ装備状態となり、外すか性行為を終了するまで持続的に快楽が発生する。"
LOCALS += "<br>COMタイプ：道具系<br>取得ソース：快Ａ・露出・接触・苦痛"
LOCALS += "<br>固有の取得経験：道具使用経験(あなた)・Ａ経験(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM45
;着脱の処理
TEQUIP:アナルバイブ = !TEQUIP:アナルバイブ 
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 60
DOWNBASE:気力 += 150

CALL SOURCE_CALC_快Ａ(TARGET, PLAYER,250)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 20)

LOCAL = 250 * PAIN_CHECK_A(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;Ａ経験
EXP:Ａ経験 ++

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 1
	EXP:PLAYER:同性愛経験 += 1
ENDIF

EXP:PLAYER:道具使用経験 ++

RETURN 1
;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM45(ARG)
;アナルバイブ挿入中
PRINTL ＜アナルバイブ挿入中＞
TEQUIP:TARGET:アナル = 1

DOWNBASE:ARG:体力 += 30
DOWNBASE:ARG:気力 += 80

CALL SOURCE_CALC_快Ａ(TARGET, -1,125)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 25)
LOCAL = 100 * PAIN_CHECK_A(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;自慰実行時は経験＋１
SIF EXP_UP(22,TARGET)
	EXP:ARG:Ａ経験 ++

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE45
;アナルバイブ実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(45)
	RETURN RESULT
;解除はいつでも可能
SIF TEQUIP:14
	RETURN 1
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF ITEM:5 == 0 && NOITEM == 0
	RETURN 0

;下半身着衣
;後鎧、めくれない服、タイツ、前張りがあると無理
SIF TEQUIP:下腹部後鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0
SIF TEQUIP:下半身下着あり == 2
	RETURN 0
	
;浣腸＋アナルプラグ使用中はダメ
SIF TEQUIP:21
	RETURN 0
;アナルビーズ使用中は不可
SIF TEQUIP:15
	RETURN 0
;バルーン使用中は不可
SIF TEQUIP:22
	RETURN 0
;電極使用中は不可
SIF TEQUIP:23
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM45
;解除時
IF !TEQUIP:アナルバイブ
	PRINTFORML %CALLNAME:TARGET%のアヌスからアナルバイブを抜いた
	PRINTFORM %CALLNAME:TARGET%は
	;Ａ経験180以上＋淫乱か娼婦
	IF EXP:Ａ経験 > 180 && TALENT:淫乱
		PRINT 秘唇と口から涎をたらしながら、お尻を広げ続け
	;Ａ経験140以上＋淫乱か娼婦
	ELSEIF EXP:Ａ経験 > 140 && TALENT:淫乱
		PRINT 潤んだ瞳で懇願するようにお尻を振っ
	;Ａ経験100以上＋淫乱か娼婦
	ELSEIF EXP:Ａ経験 > 100 && TALENT:淫乱
		PRINT 物足りなさそうにお尻を突き出し
	;Ａ経験70以上＋淫乱か娼婦
	ELSEIF EXP:Ａ経験 > 70 && TALENT:淫乱
		PRINT 少し名残惜しそうにお尻を揺らし
	;Ａ経験40以上
	ELSEIF EXP:Ａ経験 > 40
		PRINT お尻をもじもじさせつつも、どこかほっとした表情をうかべ
	;Ａ経験20以上
	ELSEIF EXP:Ａ経験 > 20
		PRINT 強制的な排泄感に頬を染め
	;それ以外
	ELSE
		PRINT 刺激に辛そうな表情で耐え
	ENDIF
	PRINTFORML ている…
ELSE
	PRINTFORM %CALLNAME:TARGET%の
	SIF TEQUIP:下半身下着あり
		PRINTFORM 下着をずらし、
	PRINTFORML 菊穴にアナルバイブをあてがった…
	PRINTFORM %CALLNAME:TARGET%は
	;Ａ経験180以上＋淫乱か娼婦
	IF EXP:Ａ経験 > 180 && TALENT:淫乱
		PRINT とろけきった表情で、自らのお尻を広げ
	;Ａ経験140以上＋淫乱か娼婦
	ELSEIF EXP:Ａ経験 > 140 && TALENT:淫乱
		PRINT 挿入を待ち焦がれるように腰をくねらせ
	;Ａ経験100以上＋淫乱か娼婦
	ELSEIF EXP:Ａ経験 > 100 && TALENT:淫乱
		PRINT 頬を染め、期待にお尻を揺らめかせ
	;Ａ経験70以上＋淫乱か娼婦
	ELSEIF EXP:Ａ経験 > 70 && TALENT:淫乱
		PRINT すぼまりに与えられるバイブの刺激に身体を震わせ
	;Ａ経験40以上
	ELSEIF EXP:Ａ経験 > 40
		PRINT 期待と否定の入り混じったような複雑な表情をし
	;Ａ経験20以上
	ELSEIF EXP:Ａ経験 > 20
		PRINT 恥じらいに染まった顔を伏せ
	;それ以外
	ELSE
		PRINT 不浄の穴を犯される恥辱と屈辱感にまみれた表情をし
	ENDIF
	PRINTL ている…
	PRINTFORM %CALLNAME:TARGET%の
	;潤滑LV4以上
	SIF PALAM:潤滑 >= PALAMLV:4
		PRINT 粘液にまみれた
	PRINTFORML アヌスにバイブを挿し込んだ
ENDIF


