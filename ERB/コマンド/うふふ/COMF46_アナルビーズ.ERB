;-------------------------------------------------
;アナルビーズ
;道具使用コマンド、レベル3
;-------------------------------------------------
@COMNAME46
#FUNCTIONS
TSTR:コマンド名受渡 = アナルビーズ

;弱点コマンド欄に専用の文字列を表示したい場合、これを設定すること
弱点コマンド欄表示名 = アナルビーズ解除

;複数モード_ダミーをリセット
複数モード_ダミー = 0

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_46
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("道具系")
弱点コマンドカテゴリ = A
SIF MODE_確認_ターゲット側("アナルビーズ", TARGET) < 0 && 初期設定中 == 0
	弱点コマンドカテゴリ = 
同時モード許可 = 999
助手モード許可 = 1


@COM_TOOLTIP_46
LOCALS = <br>■アナルビーズ<br>
LOCALS += "アナルビーズを挿入し、腸内を刺激するコマンド。<br>アナルビーズ装備状態となり、外すか性行為を終了するまで持続的に快楽が発生する。"
LOCALS += "<br>COMタイプ：道具系<br>取得ソース：快Ａ・露出・接触・苦痛"
LOCALS += "<br>固有の取得経験：道具使用経験(あなた)・Ａ経験(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM46
;着脱の処理
CALL MODE_CHANGE("アナルビーズ", PLAYER, TARGET)

;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_接触(TARGET, PLAYER, 20)

LOCAL = 200 * PAIN_CHECK_A(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)

;装着時
CALL SOURCE_CALC_快Ａ(TARGET, PLAYER, 200)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 1
	EXP:PLAYER:同性愛経験 += 1
ENDIF

EXP:PLAYER:道具使用経験 ++

RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE46
;アナルビーズ実行判定
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(46)
	RETURN RESULT
;すでに使ってるとだめ
SIF MODE_確認_ターゲット側("アナルビーズ", TARGET) > -1
	RETURN 0
;Aがない場合駄目
SIF TALENT:TARGET:Ａ感度 == -2
	RETURN 0
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF ITEM:6 == 0 && NOITEM == 0
	RETURN 0


;下半身着衣
;後鎧、めくれない服、タイツがあると無理
SIF TEQUIP:下腹部後鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0
	
;部位使ってると駄目
SIF TEQUIP:TARGET:アナル
	RETURN 0
;新妻プレイ中はダメ
SIF TEQUIP:33
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM46
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%は
IF FLAG:モード管理 == 2
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT 、
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT 全員の菊穴にアナルビーズをひとつづつ飲み込ませていった…
	RETURN 0
ENDIF
PRINTFORML %CALLNAME:TARGET%の菊穴にアナルビーズをひとつづつ飲み込ませていった…
