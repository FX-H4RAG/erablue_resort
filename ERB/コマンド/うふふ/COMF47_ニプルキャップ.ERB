;-------------------------------------------------
;ニプルキャップ
;道具使用コマンド、レベル2
;-------------------------------------------------
@COMNAME47
#FUNCTIONS
TSTR:コマンド名受渡 = ニプルキャップ
SIF MODE_確認("ニプルキャップ", TARGET)
	TSTR:コマンド名受渡 = ニプルキャップ解除

;弱点コマンド欄に専用の文字列を表示したい場合、これを設定すること
弱点コマンド欄表示名 = ニプルキャップ

;複数モード_ダミーをリセット
複数モード_ダミー = 0
;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_47
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("道具系")
;弱点コマンドカテゴリ = B
SIF MODE_確認("ニプルキャップ", TARGET)
	弱点コマンドカテゴリ = 
同時モード許可 = 1
助手モード許可 = 1

@COM_TOOLTIP_47
LOCALS = <br>■ニプルキャップ<br>
LOCALS += "乳首に自動で振動する性具を取りつけ、刺激を与えるコマンド。<br>ニプルキャップ装備状態となり、外すか性行為を終了するまで持続的に快楽が発生する。<br>陥没乳首の場合、[乳首責め]コマンドで勃起させなければ実行できない。"
LOCALS += "<br>COMタイプ：道具系<br>取得ソース：快Ｂ・露出・接触"
LOCALS += "<br>固有の取得経験：道具使用経験(あなた)・乳首開発(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM47
;着脱の処理
IF MODE_確認("ニプルキャップ", TARGET)
	CALL MODE_DELETE("ニプルキャップ")
ELSE
	CALL MODE_CHANGE("ニプルキャップ")
	RCVAR:乳首開発フラグ = 1
ENDIF
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER,200)
SIF TALENT:陥没乳首
	CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 120)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 20)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 10)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 1
	EXP:PLAYER:同性愛経験 += 1
ENDIF

EXP:PLAYER:道具使用経験 ++

RETURN 1
;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM47(ARG)
;ニプルキャップ装備中
PRINTL ＜ニプルキャップ装備中＞
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
RCVAR:ARG:乳首開発フラグ = 1

CALL SOURCE_CALC_快Ｂ(TARGET, -1,100)
SIF TALENT:陥没乳首
	CALL SOURCE_CALC_快Ｂ(TARGET, -1, 70)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 10)

RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE47
;ニプルキャップ実行判定
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(47)
	RETURN RESULT
;解除はいつでも可能
SIF MODE_確認("ニプルキャップ", TARGET)
	RETURN 1
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF ITEM:10 == 0 && NOITEM == 0
	RETURN 0
;勃起してない陥没乳首はダメ
SIF TALENT:陥没乳首 && !TCVAR:陥没乳首露出フラグ
	RETURN 0
	
;上半身着衣
;鎧、差し込めないorはだけない服、タイツ、ブラ等があると無理
SIF TEQUIP:上半身鎧あり
	RETURN 0
SIF TEQUIP:上半身服あり && (!TEQUIP:手差し込み可 && !TEQUIP:はだけ可)
	RETURN 0
SIF TEQUIP:上タイツあり && !TEQUIP:上タイツ破れ
	RETURN 0
SIF TEQUIP:上半身下着あり
	RETURN 0
	
;搾乳器使用中は不可
SIF TEQUIP:17
	RETURN 0
;乳房電極装着中はダメ
SIF TEQUIP:24
	RETURN 0
;シャワー中はダメ
SIF TEQUIP:32
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM47
IF !MODE_確認("ニプルキャップ", TARGET)
	PRINTFORML %CALLNAME:TARGET%からクリップローターを取り外した
ELSE
	PRINTFORM %CALLNAME:TARGET%の乳首に
	SIF TEQUIP:上半身服あり && TEQUIP:手差し込み可
		PRINTL 服の隙間から
	PRINTL クリップローターを貼り付けた
ENDIF


