;-------------------------------------------------
;搾乳器
;道具使用コマンド、レベル3
;-------------------------------------------------
@COMNAME48
#FUNCTIONS
TSTR:コマンド名受渡 = 搾乳器
SIF MODE_確認("搾乳器", TARGET)
	TSTR:コマンド名受渡 = 搾乳器解除

;弱点コマンド欄に専用の文字列を表示したい場合、これを設定すること
弱点コマンド欄表示名 = 搾乳器

;複数モード_ダミーをリセット
複数モード_ダミー = 0

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_48
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("道具系")
;弱点コマンドカテゴリ = B
SIF MODE_確認("搾乳器", TARGET)
	弱点コマンドカテゴリ = 
同時モード許可 = 1
助手モード許可 = 1

@COM_TOOLTIP_48
LOCALS = <br>■搾乳器<br>
LOCALS += "乳首に母乳を絞る器具を取りつけ、刺激を与えるコマンド。<br>搾乳器装備状態となり、外すか性行為を終了するまで持続的に快楽が発生する。<br>[母乳体質]でなければ実行できない。<br>性癖素質[搾乳プレイ]所持でなければ実行できない。<br>陥没乳首の場合、[乳首責め]コマンドで勃起させなければ実行できない。"
LOCALS += "<br>COMタイプ：道具系<br>取得ソース：快Ｂ・露出・接触・苦痛"
LOCALS += "<br>固有の取得経験：道具使用経験(あなた)・乳首開発(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM48
;着脱の処理
IF MODE_確認("搾乳器", TARGET)
	CALL MODE_DELETE("搾乳器")
ELSE
	CALL MODE_CHANGE("搾乳器")
ENDIF

;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
RCVAR:乳首開発フラグ = 1

CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER,200)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 10)
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 100)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 1
	EXP:PLAYER:同性愛経験 += 1
ENDIF

EXP:PLAYER:道具使用経験 ++

RETURN 1
;-------------------------------------------------
;装着継続の処理
;-------------------------------------------------
@EQUIP_COM48(ARG)
;搾乳器装備中
PRINTL ＜搾乳器装備中＞
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
RCVAR:ARG:乳首開発フラグ = 1

CALL SOURCE_CALC_快Ｂ(TARGET, -1,100)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 25)
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 50)
RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE48
;搾乳器実行判定
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(48)
	RETURN RESULT
;解除はいつでも可能
SIF MODE_確認("搾乳器", TARGET)
	RETURN 1
;アイテムを持っているか、アイテムなし設定でないとダメ
SIF ITEM:11 == 0 && NOITEM == 0
	RETURN 0
;母乳体質でないと使えない
SIF TALENT:母乳体質 == 0
	RETURN 0
;性癖を持ってないと使えない
SIF 性癖素質:搾乳プレイ == 0
	RETURN 0
;勃起してない陥没乳首はダメ
SIF TALENT:陥没乳首 && !TCVAR:陥没乳首露出フラグ
	RETURN 0
	
;上半身着衣
;鎧、服、タイツ、ブラ等があると無理
SIF TEQUIP:上半身鎧あり
	RETURN 0
SIF TEQUIP:上半身服あり
	RETURN 0
SIF TEQUIP:上タイツあり && !TEQUIP:上タイツ破れ
	RETURN 0
SIF TEQUIP:上半身下着あり
	RETURN 0

;Ｂ感度2以下はダメ
SIF ABL:TARGET:Ｂ感覚 <= 2
	RETURN 0
;オトコだとダメ
SIF !(TALENT:性別 & 1)
	RETURN 0
;シャワー中はダメ
SIF TEQUIP:32
	RETURN 0
;新妻プレイ中はダメ
SIF TEQUIP:33
	RETURN 0
;ベッドルームでないとダメ
SIF !BEDROOM(CFLAG:MASTER:現在マップ種別, CFLAG:PLAYER:現在位置)
	RETURN 0
RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM48
;解除時
IF !MODE_確認("搾乳器", TARGET)
	PRINTFORML %CALLNAME:TARGET%から搾乳器を取り外した
ELSE
	PRINTFORML %CALLNAME:TARGET%に搾乳器を取り付けた
ENDIF

