;-------------------------------------------------
;アナル舐め
;愛撫系コマンド、レベル1
;-------------------------------------------------
@COMNAME4
#FUNCTIONS
SELECTCASE COMMODE()
CASE 1
	TSTR:コマンド名受渡 = アナル舐め
CASE 2
	TSTR:コマンド名受渡 = 同時にアナル舐め
CASE 3
	TSTR:コマンド名受渡 = 助手がアナル舐め
ENDSELECT

;複数モード_ダミーをリセット
複数モード_ダミー = 0

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_4
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("Ａ系")
弱点コマンドカテゴリ = A
同時モード許可 = 1
助手モード許可 = 1

@COM_TOOLTIP_4
LOCALS = <br>■アナル舐め<br>
LOCALS += "相手のアナルを舌で愛撫するコマンド。<br>実行者が[舌使い][猫舌]を持っている場合、効果が増減する。"
LOCALS += "<br>COMタイプ：Ａ系<br>取得ソース：快Ａ・露出・接触・液体"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM4
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 5
DOWNBASE:気力 += 50

LOCAL = 50
;調教者のTALENT:舌使いをみる
SIF TALENT:PLAYER:舌使い
	TIMES LOCAL , 1.50
;調教者のTALENT:猫舌をみる
SIF TALENT:PLAYER:猫舌
	TIMES LOCAL , 0.75
CALL SOURCE_CALC_快Ａ(TARGET, PLAYER, LOCAL)
CALL SOURCE_CALC_液体(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 180)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 150)



;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 3
	EXP:PLAYER:同性愛経験 += 3
ENDIF

;調教者が猫舌持ちの場合は、調教者に嗜虐快楽経験を追加
SIF TALENT:PLAYER:猫舌
	EXP:PLAYER:嗜虐快楽経験 += 1

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE4
;アナル舐め実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(4)
	RETURN RESULT

;猫舌の調教者の場合は技巧Lv2以上必要
SIF TALENT:PLAYER:猫舌 && ABL:PLAYER:技巧 < 2
	RETURN 0

;下半身着衣
;後鎧、めくれない服、タイツがあると無理
SIF TEQUIP:下腹部後鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0
;アナルバイブ使用中は不可
SIF TEQUIP:14
	RETURN 0
;アナルビーズ使用中は不可
SIF TEQUIP:15
	RETURN 0
;普通の浣腸使用中は不可
SIF TEQUIP:21
	RETURN 0
;バルーン使用中は不可
SIF TEQUIP:22
	RETURN 0
;電極使用中は不可
SIF TEQUIP:23
	RETURN 0
;口を何かに使ってる・届かないなら無理
SIF TEQUIP:PLAYER:口
	RETURN 0
;アナルを何かに使ってる・届かないなら無理
SIF TEQUIP:TARGET:アナル
	RETURN 0
RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM4
PRINTFORM %CALLNAME:TARGET%の
IF TCVAR:101 >= 0
	PRINTFORM 先程まで犯されてぼっかりと開いた
;Ａ経験10以下
ELSEIF EXP:Ａ経験 <= 10
	PRINT 硬く閉ざされた
ENDIF
PRINT アヌスを丁寧に舐めほぐし
;Ａ経験100以上＋欲望のPALAMLV3以上
SIF EXP:Ａ経験 >= 100 && PALAM:欲情 >= PALAMLV:3
	PRINT 、舌先を突き入れて穴の中までかきまわしてやっ
PRINTL た…

