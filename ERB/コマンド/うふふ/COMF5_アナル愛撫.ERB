;-------------------------------------------------
;アナル愛撫
;愛撫系コマンド、レベル2
;-------------------------------------------------
@COMNAME5
#FUNCTIONS
SELECTCASE COMMODE()
CASE 1
	TSTR:コマンド名受渡 = アナル愛撫
CASE 2
	TSTR:コマンド名受渡 = 同時にアナル愛撫
CASE 3
	TSTR:コマンド名受渡 = 助手がアナル愛撫
ENDSELECT

;複数モード_ダミーをリセット
複数モード_ダミー = 0

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_5
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("Ａ系")
弱点コマンドカテゴリ = A
同時モード許可 = 1
助手モード許可 = 1

@COM_TOOLTIP_5
LOCALS = <br>■アナル愛撫<br>
LOCALS += "相手のアナルを指や道具で愛撫するコマンド。"
LOCALS += "<br>COMタイプ：Ａ系<br>取得ソース：快Ａ・露出・接触・苦痛"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM5
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 40
DOWNBASE:気力 += 100

CALL SOURCE_CALC_快Ａ(TARGET, PLAYER, 100)
SIF TALENT:PLAYER:器用な指
	CALL SOURCE_CALC_快Ａ(TARGET, PLAYER, 50)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 150)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)

;苦痛
LOCAL = 120 * PAIN_CHECK_A(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 2
	EXP:PLAYER:同性愛経験 += 2
ENDIF

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE5
;アナル愛撫実行判定
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(5)
	RETURN RESULT

;下半身着衣
;後鎧、めくれない服、タイツあると無理
SIF TEQUIP:下腹部後鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0
;指を何かに使ってる・届かないなら無理
SIF TEQUIP:PLAYER:指
	RETURN 0
;アナルを何かに使ってる・届かないなら無理
SIF TEQUIP:TARGET:アナル
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM5
;アナルバイブ
IF TEQUIP:アナルバイブ
	PRINTFORML 小刻みに震えるアナルバイブを前後に抜き差しした…
;アナルビーズ
ELSEIF TEQUIP:アナルビーズ
	PRINTFORML ビーズの紐を引っ張って軽く出し入れしてやった…
;浣腸器＋プラグ
ELSEIF TEQUIP:浣腸
	PRINTFORML 排泄感に震える菊穴をプラグでグリグリとほじってやった…
;拡張バルーン
ELSEIF TEQUIP:拡張バルーン
	PRINTFORML アナルを限界まで広げているバルーンを前後に動かしてやった…
;アナル電極
ELSEIF TEQUIP:アナル電極
	PRINTFORML かすかに音を立てる電極でグリグリと菊穴をこねくりまわした…
ELSEIF 使ってるの誰や(TARGET, 5) == PLAYER
	PRINTFORML %CALLNAME:TARGET%を犯しながらアヌスにいたずらをした…
ELSE
	PRINTFORM %CALLNAME:TARGET%のアナルを弄っ
	;Ａ経験100以上＋欲望のPALAMLV3以上
	SIF PALAM:欲情 >= PALAMLV:3 && EXP:Ａ経験 >= EXPLV:3
		PRINT て、指先で直腸までかきまわし
PRINTL た…
ENDIF

