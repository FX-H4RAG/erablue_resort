
@COMNAME_S60_1
#FUNCTIONS
TSTR:コマンド名受渡 = Ｇスポ責め

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_60_1
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("Ｖ系")
弱点コマンドカテゴリ = V

@COM_TOOLTIP_S60_1
LOCALS = <br>■Ｇスポ責め<br>
LOCALS += "膣上側に存在する性感帯に、ペニスで強い刺激を与えるコマンド。<br>Ｖ挿入系モード時のみ実行可能。"
LOCALS += "<br>COMタイプ：Ｖ系<br>取得ソース：快Ｖ・接触・苦痛・不潔(一定条件時)"
LOCALS += "<br>固有の取得経験：Ｖ経験(TARGET)・Ｖ性交経験(TARGET)・Ｇスポ開発(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM_S60_1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 50
DOWNBASE:気力 += 100

TCVAR:Ｇスポ開発フラグ = 3

CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER, 500)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)

CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 500)

;苦痛
LOCAL = 200 * PAIN_CHECK_V(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)

;貞操観念
IF !TALENT:恋慕
	IF TALENT:貞操 == 1
		CALL SOURCE_CALC_不潔(TARGET, PLAYER, 500)
	ELSEIF TALENT:貞操 == 0
		CALL SOURCE_CALC_不潔(TARGET, PLAYER, 250)
	ENDIF
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:Ｖ経験 ++
EXP:Ｖ性交経験 ++

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 4
	EXP:PLAYER:同性愛経験 += 4
ENDIF

EXP:愛情経験 += TALENT:恋慕 * 2

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S60_1
;Ｇスポ責め実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE_S("60_1")
	RETURN RESULT
;Ｖ挿入系モードじゃない時はアウト
SIF MODE_確認("Ｖ挿入系", TARGET) == 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S60_1
PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%の膣内を前後しながら、腹側にある性感帯を擦り上げた


