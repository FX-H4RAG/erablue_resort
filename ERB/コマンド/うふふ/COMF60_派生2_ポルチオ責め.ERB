
@COMNAME_S60_2
#FUNCTIONS
TSTR:コマンド名受渡 = ポルチオ責め
LOCAL = MODE_確認("正常位", TARGET)
LOCAL += MODE_確認("屈曲位", TARGET)
SIF LOCAL && BASE:PLAYER:勃起 >= 1500 && MAX(PALAM:PLAYER:快Ｃ, PALAM:PLAYER:快Ｖ, PALAM:PLAYER:快Ａ, PALAM:PLAYER:快Ｂ, PALAM:PLAYER:快Ｓ) >= PALAMLV:3 + ((PALAMLV:4 - PALAMLV:3) * 5 / 10)
	TSTR:コマンド名受渡 = 種付けプレス

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_60_2
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("Ｖ系")
弱点コマンドカテゴリ = V
同時モード許可 = 1
助手モード許可 = 1

@COM_TOOLTIP_S60_2
CALLF COMNAME_S60_2()
LOCALS = <br>■%TSTR:コマンド名受渡%<br>
IF TSTR:コマンド名受渡 == "ポルチオ責め"
	LOCALS += "子宮口付近に存在する性感帯に、ペニスで強い刺激を与えるコマンド。<br>Ｖ挿入系モード時のみ実行可能。"
ELSE
	LOCALS += "相手を孕ませようと、体全体を用いてペニスを子宮口に強く深く押し付けるコマンド。<br>Ｖ挿入系モード時のみ実行可能。"
ENDIF
LOCALS += "<br>COMタイプ：Ｖ系<br>取得ソース：快Ｖ・接触・苦痛・不潔(一定条件時)"
LOCALS += "<br>固有の取得経験：Ｖ経験(TARGET)・Ｖ性交経験(TARGET)・ポルチオ開発(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM_S60_2
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
CALLF COMNAME_S60_2()
SIF TSTR:コマンド名受渡 == "種付けプレス"
	RFLAG:コマンド結果受渡し変数 = 1


RCVAR:ポルチオ開発フラグ = 3

CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER, 500)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)

CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 500)

;苦痛
LOCAL = 200 * PAIN_CHECK_V(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)

;貞操観念
IF !TALENT:恋慕
	IF TALENT:貞操 == 1
		CALL SOURCE_CALC_不潔(TARGET, PLAYER, 500)
	ELSEIF TALENT:貞操 == 0
		CALL SOURCE_CALC_不潔(TARGET, PLAYER, 250)
	ENDIF
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:Ｖ性交経験 ++

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 4
	EXP:PLAYER:同性愛経験 += 4
ENDIF

EXP:愛情経験 += TALENT:恋慕 * 2

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S60_2
;Ｇスポ責め実行判定
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE_S("60_2")
	RETURN RESULT
;Ｖ挿入系モードじゃない時はアウト
SIF MODE_確認("Ｖ挿入系", TARGET) == 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S60_2
IF RFLAG:コマンド結果受渡し変数
	PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%に覆いかぶさり、全身を密着させてペニスを子宮口へと押し込んだ
ELSE
	PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%の膣内を前後しながら、奥の子宮口付近にある性感帯を圧迫した
ENDIF


