
@COMNAME_S60_2
#FUNCTIONS
TSTR:コマンド名受渡 = ポルチオ責め
LOCAL = 0
SIF MODE_確認_完全一致("正常位", PLAYER, TARGET) > -1
	LOCAL += 1
SIF MODE_確認_完全一致("寝バック", PLAYER, TARGET) > -1
	LOCAL += 1
SIF MODE_確認_完全一致("屈曲位", PLAYER, TARGET) > -1
	LOCAL += 1
IF LOCAL && BASE:PLAYER:勃起 >= 1500 && MAX(PALAM:PLAYER:快Ｃ, PALAM:PLAYER:快Ｖ, PALAM:PLAYER:快Ａ, PALAM:PLAYER:快Ｂ, PALAM:PLAYER:快Ｓ) >= PALAMLV:3 + ((PALAMLV:4 - PALAMLV:3) * 5 / 10)
	SIF PLAYER == MASTER && あなた射精我慢フラグ
		RETURNF 
	TSTR:コマンド名受渡 = 種付けプレス
ENDIF
;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_60_2
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("Ｖ系")
SIF CFLAG:TARGET:睡眠
	TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ強")
睡眠中許可 = 1
弱点コマンドカテゴリ = V
同時モード許可 = 3
同時モード拡張許可 = 1
オートコマンド許可 = 1
連続オート許可 = 1

@COM_TOOLTIP_S60_2
CALLF COMNAME_S60_2()
LOCALS = <br>■%TSTR:コマンド名受渡%<br>
IF TSTR:コマンド名受渡 == "ポルチオ責め"
	LOCALS += "子宮口付近に存在する性感帯に、ペニスで強い刺激を与えるコマンド。<br>Ｖ挿入系モード時のみ実行可能。"
	SIF 子宮口挿入_封印 == 0
	LOCALS += "<br>ポルチオ開発経験が一定以上の時、確率で「子宮口挿入」コマンドが解放される。"
ELSE
	LOCALS += "相手を孕ませようと、体全体を用いてペニスを子宮口に強く深く押し付けるコマンド。<br>Ｖ挿入系モード時のみ実行可能。"
ENDIF
LOCALS += "<br>COMタイプ：Ｖ系<br>取得ソース：快Ｖ・接触・苦痛・不潔(一定条件時)"
LOCALS += "<br>固有の取得経験：Ｖ性交経験(TARGET)・ポルチオ開発(TARGET)・Ｃ性交経験_Ｖ挿入(PLAYER)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM_S60_2
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
CALLF COMNAME_S60_2()
SIF TSTR:コマンド名受渡 == "種付けプレス"
	RFLAG:コマンド結果受渡し変数 = 1


RCVAR:ポルチオ開発フラグ = 3

CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER, 500)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)
IF EXP:TARGET:ポルチオ開発 > 200 && ABL:TARGET:Ｖ感覚 > 3 && 子宮口挿入_封印 == 0 && CFLAG:TARGET:子宮口許可フラグ == 0 && RAND:2 && RFLAG:コマンド結果受渡し変数 == 0
	CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER, 500)
	CFLAG:TARGET:子宮口許可フラグ = 1
	RFLAG:コマンド結果受渡し変数２ = 1
ENDIF

CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 500)

;苦痛
LOCAL = 200 * PAIN_CHECK_V(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)

;貞操観念
IF !TALENT:恋慕
	IF TALENT:貞操 == 1
		CALL SOURCE_CALC_不潔(TARGET, PLAYER, 500)
	ELSEIF TALENT:貞操 == 0
		CALL SOURCE_CALC_不潔(TARGET, PLAYER, 250)
	ENDIF
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:Ｖ性交経験 ++
EXP:PLAYER:Ｃ性交経験_Ｖ挿入 ++

;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 4
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 4

EXP:愛情経験 += TALENT:恋慕 * 2

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S60_2
;Ｇスポ責め実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE_S("60_2")
	RETURN RESULT
;Ｖ挿入系モードじゃない時はアウト
SIF MODE_確認_完全一致("Ｖ挿入系", PLAYER, TARGET) < 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S60_2
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%は

IF FLAG:モード管理 == 2
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT 、
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT の膣内を前後しながら、奥の子宮口付近にある性感帯を圧迫した
	RETURN 0
ENDIF

IF RFLAG:コマンド結果受渡し変数
	PRINTFORML %CALLNAME:TARGET%に覆いかぶさり、全身を密着させてペニスを子宮口へと押し込んだ
ELSE
	PRINTFORML %CALLNAME:TARGET%の膣内を前後しながら、奥の子宮口付近にある性感帯を圧迫した
	IF RFLAG:コマンド結果受渡し変数２
		PRINTL
		PRINTFORML 柔らかく熟れた%CALLNAME:TARGET%の子宮はポルチオ快楽で口を開き、%CALLNAME:PLAYER%のペニスを咥えこんでしまった！
		PRINTFORML %CALLNAME:TARGET%は不意の強い快楽に、目を白黒させて戸惑っている…
	ENDIF
ENDIF

