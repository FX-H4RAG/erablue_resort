
@COMNAME_S62_1
#FUNCTIONS
TSTR:コマンド名受渡 = 入口を責める
;男・ふたなり相手だと前立腺責めになる
SIF GETBIT(TALENT:TARGET:性別, 2)
	TSTR:コマンド名受渡 = 前立腺責め

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_62_1
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("Ａ系")
SIF CFLAG:TARGET:睡眠
	TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ強")
睡眠中許可 = 1
弱点コマンドカテゴリ = A
同時モード許可 = 3
同時モード拡張許可 = 1
オートコマンド許可 = 1
連続オート許可 = 1

@COM_TOOLTIP_S62_1
LOCALS = <br>■入口を責める<br>
LOCALS += "腸内の入口付近に、ペニスで強い刺激を与えるコマンド。<br>相手が男・ふたなりの時、[前立腺責め]に変化する。<br>Ａ挿入系モード時のみ実行可能。"
LOCALS += "<br>COMタイプ：Ａ系<br>取得ソース：快Ａ・接触・苦痛"
LOCALS += "<br>固有の取得経験：Ａ性交経験(TARGET)・前立腺開発(TARGET・一定条件時)・Ｃ性交経験_Ａ挿入(PLAYER)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM_S62_1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

;ペニスがあるなら前立腺もある
IF GETBIT(TALENT:TARGET:性別, 1)
	RCVAR:前立腺開発フラグ = 3
ENDIF
CALL SOURCE_CALC_快Ａ(TARGET, PLAYER, 500)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)
CALL SOURCE_CALC_快Ｃ(PLAYER, TARGET, 500)

;苦痛
LOCAL = 200 * PAIN_CHECK_A(TARGET) / 1000
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, LOCAL)


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:PLAYER:Ｃ性交経験_Ａ挿入 ++
EXP:Ａ性交経験 ++

;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 4
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 4

EXP:愛情経験 += TALENT:恋慕 * 2

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S62_1
;入口を責める実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE_S("62_1")
	RETURN RESULT
;Ａ挿入系モードじゃない時はアウト
SIF !MODE_存在判定_完全一致("Ａ挿入系", PLAYER, TARGET)
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S62_1
#DIM 同時キャラ番号
PRINTFORM %CALLNAME:PLAYER%は

IF FLAG:モード管理 == 2
	FOR 同時キャラ番号, 0, 同時モード_選択数
		SIF 同時キャラ番号 > 0
			PRINT 、
		PRINTFORM %CALLNAME:(同時モード_選択キャラ:同時キャラ番号)%
	NEXT
	PRINT の入口付近を念入りに擦りあげた
	RETURN 0
ENDIF

IF GETBIT(TALENT:TARGET:性別, 1)
	PRINTFORM %CALLNAME:TARGET%のこりこりとした前立腺を執拗に責めたてた
ELSE
	PRINTFORM %CALLNAME:TARGET%の入口付近を念入りに擦りあげた
ENDIF

