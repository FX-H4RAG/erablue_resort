;-------------------------------------------------
;胸愛撫
;愛撫系コマンド、レベル2
;-------------------------------------------------
@COMNAME6
#FUNCTIONS
TSTR:コマンド名受渡 = 胸愛撫

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_6
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ_被挿入中")
弱点コマンドカテゴリ = B

@COM_TOOLTIP_6
LOCALS = <br>■胸愛撫<br>
LOCALS += "相手の乳房を優しく愛撫するコマンド。"
LOCALS += "<br>COMタイプ：うふふor被挿入中<br>取得ソース：快Ｂ・露出・接触"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM6
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 5
DOWNBASE:気力 += 50

CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 80)
CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 100)
SIF TALENT:PLAYER:器用な指
	CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 50)


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 5
	EXP:PLAYER:同性愛経験 += 5
ENDIF

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE6
;胸愛撫実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(6)
	RETURN RESULT
;鎧があると無理
SIF TEQUIP:上半身鎧あり
	RETURN 0
;指を何かに使ってる・届かないなら無理
SIF TEQUIP:PLAYER:指
	RETURN 0
;胸を何かに使ってる・届かないなら無理
SIF TEQUIP:TARGET:胸 == 2
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM6
IF TEQUIP:50 == PLAYER || TEQUIP:51 == PLAYER
	PRINTFORM %CALLNAME:TARGET%とつながったまま%OPPAI_DESCRIPTION(TARGET)%
ELSE
	PRINTFORM %CALLNAME:TARGET%の%OPPAI_DESCRIPTION(TARGET)%
ENDIF
;欲望のPALAMLV3以上＋
IF PALAM:欲情 >= PALAMLV:3 && !TEQUIP:ニプルキャップ && TEQUIP:搾乳機
	PRINTL の先端の、すっかり硬くなった突起をつまみあげた…
;小柄体型
ELSEIF TALENT:種族 < 0
	PRINTL を優しく撫でるように触れた…
ELSE
	PRINTL を揉みしだいた…
ENDIF

