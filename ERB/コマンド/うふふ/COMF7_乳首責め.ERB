;-------------------------------------------------
;乳首責め
;愛撫系コマンド、レベル1
;-------------------------------------------------
@COMNAME7
#FUNCTIONS
IF TALENT:陥没乳首 && !TCVAR:陥没乳首露出フラグ
	SELECTCASE COMMODE() 
	CASE 1
		TSTR:コマンド名受渡 = 乳首ほじり
	CASE 2
		TSTR:コマンド名受渡 = 同時に乳首ほじり
	CASE 3
		TSTR:コマンド名受渡 = 助手が乳首ほじり
	ENDSELECT
ELSE
	SELECTCASE COMMODE() 
	CASE 1
		TSTR:コマンド名受渡 = 乳首責め
	CASE 2
		TSTR:コマンド名受渡 = 同時に乳首責め
	CASE 3
		TSTR:コマンド名受渡 = 助手が乳首責め
	ENDSELECT
ENDIF

;複数モード_ダミーをリセット
複数モード_ダミー = 0

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_7
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ_被挿入中")
弱点コマンドカテゴリ = B
同時モード許可 = 1
助手モード許可 = 1

@COM_TOOLTIP_7
LOCALS = <br>■乳首責め<br>
LOCALS += "相手の乳首を責め、愛撫するコマンド。"
LOCALS += "<br>COMタイプ：うふふor被挿入中<br>取得ソース：快Ｂ・露出・接触・苦痛"
LOCALS += "<br>固有の取得経験：乳首開発(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM7
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

RCVAR:乳首開発フラグ = 2

CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 120)

SIF TALENT:PLAYER:器用な指
	CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 50)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 60)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 60)
CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 20)
;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 5
	EXP:PLAYER:同性愛経験 += 5
ENDIF

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE7
;乳首責め
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(7)
	RETURN RESULT
;Bがない場合駄目
SIF TALENT:TARGET:Ｂ感度 == -2
	RETURN 0
	
;上半身着衣
;鎧、差し込めないorはだけない服、タイツ、ブラ等があると無理
SIF TEQUIP:上半身鎧あり
	RETURN 0
SIF TEQUIP:上半身服あり && (!TEQUIP:手差し込み可 && !TEQUIP:はだけ可)
	RETURN 0
SIF TEQUIP:上タイツあり && !TEQUIP:上タイツ破れ
	RETURN 0
SIF TEQUIP:上半身下着あり
	RETURN 0
;ニプルキャップ使用中は不可
SIF TEQUIP:16
	RETURN 0
;搾乳機使用中は不可
SIF TEQUIP:17
	RETURN 0
;乳房電極装着中はダメ
SIF TEQUIP:24
	RETURN 0
;指を何かに使ってるなら無理
SIF TEQUIP:PLAYER:指 && MODE_確認_完全一致("授乳手コキ", TARGET, PLAYER) < 0 && MODE_確認_完全一致("乳首吸い", PLAYER, TARGET) < 0
	RETURN 0
;胸に届かないなら無理
CALL MODE_確認_部位届くかチェック(部位_胸, PLAYER, TARGET)
SIF RESULT == 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM7
IF MODE_確認_完全一致("Ｖ挿入系", PLAYER, TARGET) > -1 || MODE_確認_完全一致("Ａ挿入系", PLAYER, TARGET) > -1 || MODE_確認_完全一致("Ｖ挿入系", TARGET, PLAYER) > -1 || MODE_確認_完全一致("Ａ挿入系", TARGET, PLAYER) > -1
	PRINTFORM %CALLNAME:TARGET%とつながったまま%OPPAI_DESCRIPTION(TARGET)%
ELSE
	PRINTFORM %CALLNAME:TARGET%の%OPPAI_DESCRIPTION(TARGET)%
ENDIF
SELECTCASE TALENT:陥没乳首
	CASE 0
		;欲情のPALAMLV3以上
		IF PALAM:欲情 >= PALAMLV:3
			PRINT の先端の、すっかり硬くなった突起を
			IF MODE_確認_完全一致("授乳手コキ", TARGET, PLAYER) > -1 || MODE_確認_完全一致("乳首吸い", PLAYER, TARGET) > -1
				PRINTL 舌で転がした…
			ELSE
				PRINTL つまみあげた…
			ENDIF
		ELSE
			PRINT の先端の、まだ柔らかい突起を
			IF MODE_確認_完全一致("授乳手コキ", TARGET, PLAYER) > -1 || MODE_確認_完全一致("乳首吸い", PLAYER, TARGET) > -1
				PRINTL 舌で転がした…
			ELSE
				PRINTL つまみあげた…
			ENDIF
		ENDIF
	CASE 1
		IF !TCVAR:陥没乳首露出フラグ
			PRINT の凹みに指を入れ、隠れた乳首を
			SIF MODE_確認_完全一致("授乳手コキ", TARGET, PLAYER) > -1 || MODE_確認_完全一致("乳首吸い", PLAYER, TARGET) > -1
				PRINT 舌で
			PRINTL ほじり、弄った…
		ELSEIF TCVAR:陥没乳首露出フラグ == 10
			;フラグの数値が10になるのは勃起直後のみのはず
			PRINT の凹みに
			IF MODE_確認_完全一致("授乳手コキ", TARGET, PLAYER) > -1 || MODE_確認_完全一致("乳首吸い", PLAYER, TARGET) > -1
				PRINT 舌
			ELSE
				PRINT 指
			ENDIF
			PRINTL を入れてほじくると、
			PRINTFORML こりこりに硬くなった%CALLNAME:TARGET%の乳首が外に顔を出した
		ELSE
			PRINT の先端の、すっかり硬くなった突起を
			IF MODE_確認_完全一致("授乳手コキ", TARGET, PLAYER) > -1 || MODE_確認_完全一致("乳首吸い", PLAYER, TARGET) > -1
				PRINTL 舌で転がした…
			ELSE
				PRINTL つまみあげた…
			ENDIF
		ENDIF
ENDSELECT

