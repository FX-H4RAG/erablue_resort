
@COMNAME_S81_3
#FUNCTIONS
TSTR:コマンド名受渡 = バキュームフェラ

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_81_3
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
弱点コマンドカテゴリ = C_ちんこ
オートコマンド許可 = 1
連続オート許可 = 1

@COM_TOOLTIP_S81_3
LOCALS = <br>■バキュームフェラ<br>
LOCALS += "フェラチオ中に陰茎を強く吸い上げて刺激を与えるコマンド。"
LOCALS += "<br>実行者が[舌使い][猫舌]を持っている場合、効果が増減する。"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：快Ｃ・露出・接触・奉仕・不潔・苦痛(一定条件時)"
LOCALS += "<br>固有の取得経験：口淫経験(PLAYER)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM_S81_3
;奉仕快楽
RCVAR:TARGET:奉仕コマンドフラグ = 1
RCVAR:TARGET:汎用喘ぎ_文節低減フラグ = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)
CALL SOURCE_CALC_接触(PLAYER, TARGET, 150)
CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER, 200)
CALL SOURCE_CALC_奉仕(PLAYER, TARGET, 250)
CALL SOURCE_CALC_不潔(PLAYER, TARGET, 250)
;-------------------------------------------------
;特殊ソース処理
;-------------------------------------------------

;舌使いによって、快感のソース増加
IF TALENT:PLAYER:舌使い
	CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER, 150)
ENDIF

;猫舌では苦痛ちょいある
SIF TALENT:PLAYER:猫舌
	CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 20)


;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;フェラ経験
EXP:PLAYER:口淫経験 += 1

;同性愛
SIF 同性愛性癖判定(TARGET,PLAYER)
	EXP:TARGET:同性愛経験 += 4
SIF 同性愛性癖判定(PLAYER,TARGET)
	EXP:PLAYER:同性愛経験 += 4

EXP:愛情経験 += TALENT:恋慕 * 2

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S81_3
;実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE_S("81_3")
	RETURN RESULT
;フェラチオモードじゃない時はアウト
SIF !MODE_存在判定_完全一致("フェラチオ", PLAYER, TARGET)
	RETURN 0

;下半身着衣
;前鎧、めくれない服、タイツ、前張りがあると無理
SIF TEQUIP:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0
SIF TEQUIP:下半身下着あり == 2
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S81_3
PRINTFORML %CALLNAME:PLAYER%は口をすぼめ、%CALLNAME:TARGET%のペニスを強く吸い上げた…


