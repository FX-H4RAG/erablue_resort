;-------------------------------------------------
;パイズリ
;奉仕系コマンド、レベル3
;-------------------------------------------------
@COMNAME82
#FUNCTIONS
TSTR:コマンド名受渡 = パイズリ
SIF MODE_確認("フェラチオ", TARGET)
	TSTR:コマンド名受渡 = パイズリフェラ

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_82
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")

@COM_TOOLTIP_82
LOCALS = <br>■パイズリ<br>
LOCALS += "相手に胸で自分のペニスを愛撫してもらうコマンド。<br>パイズリモードとなり、モードを変更するか性行為を終了するまで持続的に特定のソースが発生する。<br>フェラチオモード時、[パイズリフェラ]に変化。"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：快Ｂ・露出・接触・奉仕・不潔"
LOCALS += "<br>固有の取得経験：パイズリ経験(TARGET)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM82
;-------------------------------------------------
;モードの設定
;-------------------------------------------------
IF MODE_確認("フェラチオ", TARGET) == 0
	CALL MODE_CHANGE("パイズリ")
ELSE
	CALL MODE_CHANGE("パイズリフェラ")
ENDIF
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------

;奉仕快楽
TFLAG:30 = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 10
DOWNBASE:気力 += 100

CALL SOURCE_CALC_快Ｂ(TARGET, PLAYER, 80)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)
CALL SOURCE_CALC_奉仕(TARGET, PLAYER, 400)

CALL SOURCE_CALC_不潔(TARGET, PLAYER, 200)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
EXP:パイズリ経験 ++

;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 7
	EXP:PLAYER:同性愛経験 += 7
ENDIF

;愛情経験
SIF TALENT:恋慕 == 1
	EXP:愛情経験 ++

RETURN 1

@CAN_COM82(ARG)
SKIPDISP ARG
PRINTL パイズリ
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
A = 0
S = 0

;すべての命令に共通の要素を考慮

CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	SIF S
		PRINT  + 
	A += ABL:欲望 * 1
	PRINT 欲望
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 1,')
	S = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF S
		PRINT  + 
	A += ABL:奉仕精神 * 4
	PRINT 奉仕精神
	PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
	S = 1
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF S
		PRINT  + 
	A += ABL:精液中毒 * 3
	PRINT 精液中毒
	PRINTV 'LV,ABL:精液中毒,'(,ABL:精液中毒 * 3,')
	S = 1
ENDIF

IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 1
	PRINT 快楽刻印
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 1,')
	S = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情,5)
	SIF S
		PRINT  + 
	A += GETPALAMLV(PALAM:欲情,5) * 1
	PRINT 欲情
	PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 1,')
	S = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	A -= 3
	PRINT 恥じらい
	PRINTV '(,3,')
	S = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭耐性 > 0
	SIF S
		PRINT  + 
	A += 1
	PRINT 汚臭鈍感
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭耐性 < 0
	PRINT  - 
	A -= 3
	PRINT 汚臭敏感
	PRINTV '(,3,')
	S = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF S
		PRINT  + 
	A += 6
	PRINT 献身的
	PRINTV '(,6,')
	S = 1
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感応答 < 0 && !TALENT:淫乱
	PRINT  - 
	A -= 3
	PRINT 快感の否定
	PRINTV '(,3,')
	S = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER) && TALENT:性別嗜好 > 0
	PRINT  - 
	A -= 12
	PRINT 男嫌い
	PRINTV '(,12,')
	S = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF S
		PRINT  + 
	A += 5
	PRINT 恋慕
	PRINTV '(,5,')
	S = 5
ENDIF
;親愛
IF TALENT:恋慕 > 1
	SIF S
		PRINT  + 
	A += 5
	PRINT 親愛
	PRINTV '(,5,')
	S = 5
ENDIF

;調教者がふたなり
IF (TALENT:PLAYER:性別 & 3) == 3
	SIF S
		PRINT  + 
	A += 8
	PRINT ふたなり
	PRINTV '(,8,')
	S = 1
ENDIF


;合計を表示(30以上で実行)
PRINT  = 
PRINTV A

V = 30
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT
SKIPDISP 0
;実行できない
SIF A < V
	RETURN 0
RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE82
;パイズリ実行判定
SIF !TFLAG:100
	RETURN 0
SIF GLOBAL_COMABLE(82)
	RETURN RESULT
;オトコだとダメ
SIF !(TALENT:性別 & 1)
	RETURN 0
;[絶壁]だと不可
SIF TALENT:バストサイズ == -2
	RETURN 0
;[爆乳]なら技巧LV1以上、[巨乳]でなければ技巧3以上、[貧乳]だと技巧4以上必要
SIF TALENT:バストサイズ + ABL:技巧 < 3
	RETURN 0
;調教者がオトコか半人半妖かふたなりじゃないとダメ
SIF !(TALENT:PLAYER:性別 & 2)
	RETURN 0
;縄使用中は不可
SIF TEQUIP:縄
	RETURN 0
;上半身着衣
;鎧、差し込めないorはだけない服、タイツがあると無理
SIF TEQUIP:上半身鎧あり
	RETURN 0
SIF TEQUIP:上半身服あり && (!TEQUIP:はだけ可 && !TEQUIP:パイズリ可)
	RETURN 0
SIF TEQUIP:上タイツあり && !TEQUIP:上タイツ破れ
	RETURN 0
;胸、ペニスを何かに使ってる・届かないなら無理
IF MODE_確認("フェラチオ", TARGET) == 0
	SIF TEQUIP:PLAYER:ペニス
		RETURN 0
	SIF TEQUIP:TARGET:胸
		RETURN 0
ENDIF
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM82
IF MODE_確認("パイズリフェラ", TARGET)
	PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%のペニスを加えたまま
	IF TALENT:バストサイズ == -1
		PRINT 薄い胸で懸命にペニスを
	;巨乳
	ELSEIF TALENT:バストサイズ == 1
		PRINT 豊満な胸でペニスを包み込んで
	;爆乳
	ELSEIF TALENT:バストサイズ == 2
		PRINT 豊満すぎる程の胸でペニスを包み隠すようにして
	ELSE
		PRINT 胸でペニスをはさんで
	ENDIF
	PRINTL 擦りあげ始めた…
ELSE
	PRINTFORM %CALLNAME:TARGET%は
	;恋慕か親愛
	SIF TEQUIP:上半身服あり
		PRINT 服を着たまま、
	;貧乳
	IF TALENT:バストサイズ == -1
		PRINT 薄い胸で懸命にペニスを
	;巨乳
	ELSEIF TALENT:バストサイズ == 1
		PRINT 豊満な胸でペニスを包み込んで
	;爆乳
	ELSEIF TALENT:バストサイズ == 2
		PRINT 豊満すぎる程の胸でペニスを包み隠すようにして
	ELSE
		PRINT 胸でペニスをはさんで
	ENDIF
	PRINTL 擦りあげている…
ENDIF


