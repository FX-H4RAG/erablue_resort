;-------------------------------------------------
;素股
;奉仕系コマンド、レベル1
;-------------------------------------------------
@COMNAME83
#FUNCTIONS
SELECTCASE COMMODE()
CASE 1
	TSTR:コマンド名受渡 = 素股
CASE 3
	TSTR:コマンド名受渡 = 助手に素股
ENDSELECT

;複数モード_ダミーをリセット
複数モード_ダミー = 0

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_83
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")
助手モード許可 = 1

@COM_TOOLTIP_83
LOCALS = <br>■素股<br>
LOCALS += "相手の股間にペニスを挟み、前後して刺激を与えるコマンド。<br>※モード化検討中。現状は単発コマンド。"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：快Ｃ・露出・接触・奉仕・不潔"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM83
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------

;奉仕快楽
TFLAG:30 = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;消費体力、気力
DOWNBASE:体力 += 40
DOWNBASE:気力 += 130

CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER, 150)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 30)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)
CALL SOURCE_CALC_奉仕(TARGET, PLAYER, 250)

CALL SOURCE_CALC_不潔(TARGET, PLAYER, 150)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 7
	EXP:PLAYER:同性愛経験 += 7
ENDIF

RETURN 1


@CAN_COM83(ARG)
SKIPDISP ARG
PRINTL 素股
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
A = 0
S = 0

;すべての命令に共通の要素を考慮

CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	SIF S
		PRINT  + 
	A += ABL:欲望 * 2
	PRINT 欲望
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 2,')
	S = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF S
		PRINT  + 
	A += ABL:奉仕精神 * 4
	PRINT 奉仕精神
	PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
	S = 1
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF S
		PRINT  + 
	A += ABL:精液中毒 * 1
	PRINT 精液中毒
	PRINTV 'LV,ABL:精液中毒,'(,ABL:精液中毒 * 1,')
	S = 1
ENDIF

IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 2
	PRINT 快楽刻印
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 2,')
	S = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情,5)
	SIF S
		PRINT  + 
	A += GETPALAMLV(PALAM:欲情,5) * 1
	PRINT 欲情
	PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 1,')
	S = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	A -= 1
	PRINT 恥じらい
	PRINTV '(,1,')
	S = 1
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感応答 < 0 && !TALENT:淫乱
	PRINT  - 
	A -= 3
	PRINT 快感の否定
	PRINTV '(,3,')
	S = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER) && TALENT:性別嗜好 > 0
	PRINT  - 
	A -= 7
	PRINT 男嫌い
	PRINTV '(,7,')
	S = 1
ENDIF
;恋慕
IF TALENT:恋慕
	SIF S
		PRINT  + 
	A += 3
	PRINT 恋慕
	PRINTV '(,3,')
	S = 3
ENDIF
;親愛
IF TALENT:恋慕 > 1
	SIF S
		PRINT  + 
	A += 3
	PRINT 親愛
	PRINTV '(,3,')
	S = 3
ENDIF

;調教者がふたなり
IF (TALENT:PLAYER:性別 & 3) == 3
	SIF S
		PRINT  + 
	A += 8
	PRINT ふたなり
	PRINTV '(,8,')
	S = 1
ENDIF

;媚薬
IF TEQUIP:媚薬
	SIF S
		PRINT  + 
	A += 6
	PRINT 媚薬
	PRINTV '(,6,')
	S = 1
ENDIF


;合計を表示(20以上で実行)
PRINT  = 
PRINTV A

V = 20
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT
SKIPDISP 0
;実行できない
SIF A < V
	RETURN 0
RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------


@COM_ABLE83
;素股実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(83)
	RETURN RESULT
;潤滑が高くないとダメ
SIF PALAM:潤滑 < 2000
	RETURN 0
;調教者がオトコか半人半妖かふたなりじゃないとダメ
SIF !(TALENT:PLAYER:性別 & 2)
	RETURN 0
;バイブ挿入中はダメ
SIF TEQUIP:13
	RETURN 0
;縄使用中は不可
SIF TEQUIP:縄
	RETURN 0

;下半身着衣
;鎧、めくれない服、タイツ、前張りがあると無理
SIF TEQUIP:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:下腹部後鎧あり
	RETURN 0
SIF TEQUIP:下半身服あり && (!TEQUIP:スカートめくり可 && !TEQUIP:ずらし可)
	RETURN 0
SIF TEQUIP:下タイツあり && !TEQUIP:下タイツ破れ
	RETURN 0
SIF TEQUIP:下半身下着あり
	RETURN 0
	
;お風呂場プレイ中はプレイマットが無いとダメ
IF TEQUIP:31
	SIF ITEM:20 == 0 && NOITEM == 0
		RETURN 0
ENDIF
;膣、ペニスを何かに使ってる・届かないなら無理
SIF TEQUIP:PLAYER:ペニス
	RETURN 0
SIF TEQUIP:TARGET:膣
	RETURN 0
RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM83
PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%のペニスを股にはさみ、
;淫乱か娼婦＋欲情のPALAMLV5以上
IF TALENT:淫乱 && PALAM:欲情 >= PALAMLV:5
	PRINTFORM 快楽に溺れきった表情で%CALLNAME:PLAYER%を見つめながら
;恋慕か親愛＋欲情のPALAMLV5以上
ELSEIF TALENT:恋慕 && PALAM:欲情 >= PALAMLV:5 && PLAYER == MASTER
	PRINT 今にもとろけそうな表情を浮かべながら
;欲情のPALAMLV4以上
ELSEIF PALAM:欲情 >= PALAMLV:4
	PRINT 口の端からよだれをこぼしながら
;恋慕か親愛
ELSEIF TALENT:恋慕 && PLAYER == MASTER
	PRINTFORM 陶酔しきった瞳で%CALLNAME:PLAYER%を見つめながら
;淫乱か娼婦
ELSEIF TALENT:淫乱
	PRINTFORM 淫猥な目つきで挑発しながら
;恥薄い
ELSEIF TALENT:羞恥心 < 0
	PRINTFORM 大胆に腰を動かしながら
;感情乏しい
ELSEIF TALENT:感情乏しい
	PRINTFORM うっすらと上気した顔で
ELSE
	PRINT 恥ずかしそうに
ENDIF
;小柄体型
IF TALENT:種族 < 0
	PRINT 幼いワレメを
ELSE
	PRINT 陰唇を
ENDIF
PRINTL ペニスに擦りつけている…

