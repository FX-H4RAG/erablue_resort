;-------------------------------------------------
;足扱き
;奉仕系コマンド、レベル4
;-------------------------------------------------

@COMNAME85
#FUNCTIONS
TSTR:コマンド名受渡 = 足扱き

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_85
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("うふふ")

@COM_TOOLTIP_85
LOCALS = <br>■足扱き<br>
LOCALS += "相手の足でペニスを愛撫してもらうコマンド。<br>※モード化検討中。現状は単発コマンド。"
LOCALS += "<br>COMタイプ：うふふ<br>取得ソース：征服・露出・接触・奉仕・不潔"
LOCALS += "<br>固有の取得経験：嗜虐快楽経験(TARGET・一定条件時)<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM85
;-------------------------------------------------
;COM別FLAGの設定
;-------------------------------------------------

;奉仕快楽
RCVAR:TARGET:奉仕コマンドフラグ = 1
RCVAR:TARGET:汎用喘ぎ_文節低減フラグ = 1
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

CALL SOURCE_CALC_征服(TARGET, PLAYER, 350)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 50)
CALL SOURCE_CALC_奉仕(TARGET, PLAYER, 80)

CALL SOURCE_CALC_不潔(TARGET, PLAYER, 100)

;-------------------------------------------------
;経験の処理
;-------------------------------------------------
;同性愛　レズなら戻り値1ゲイなら戻り値2
IF HETEROSEX(TARGET,PLAYER)
	EXP:同性愛経験 += 4
	EXP:PLAYER:同性愛経験 += 4
ENDIF
;サドは嗜虐快楽
SIF ABL:サド性感
EXP:嗜虐快楽経験 ++

RETURN 1

@CAN_COM85(ARG)
SKIPDISP ARG
PRINTFORML 足扱き(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
A = 0
S = 0

;すべての命令に共通の要素を考慮

CALL COM_ORDER

;ABL:欲望
IF ABL:欲望
	SIF S
		PRINT  + 
	A += ABL:欲望 * 1
	PRINT 欲望
	PRINTV 'LV,ABL:欲望,'(,ABL:欲望 * 1,')
	S = 1
ENDIF
;ABL:奉仕精神
IF ABL:奉仕精神
	SIF S
		PRINT  + 
	A += ABL:奉仕精神 * 4
	PRINT 奉仕精神
	PRINTV 'LV,ABL:奉仕精神,'(,ABL:奉仕精神 * 4,')
	S = 1
ENDIF
;ABL:精液中毒
IF ABL:精液中毒
	SIF S
		PRINT  + 
	A += ABL:精液中毒 * 1
	PRINT 精液中毒
	PRINTV 'LV,ABL:精液中毒,'(,ABL:精液中毒 * 1,')
	S = 1
ENDIF

IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 1
	PRINT 快楽刻印
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 1,')
	S = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情,5)
	SIF S
		PRINT  + 
	A += GETPALAMLV(PALAM:欲情,5) * 1
	PRINT 欲情
	PRINTV 'LV,GETPALAMLV(PALAM:欲情,5),'(,GETPALAMLV(PALAM:欲情,5) * 1,')
	S = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	A -= 3
	PRINT 恥じらい
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭鈍感
IF TALENT:汚臭耐性 > 0
	SIF S
		PRINT  + 
	A += 1
	PRINT 汚臭鈍感
	PRINTV '(,1,')
	S = 1
ENDIF
;汚臭敏感
IF TALENT:汚臭耐性 < 0
	PRINT  - 
	A -= 1
	PRINT 汚臭敏感
	PRINTV '(,1,')
	S = 1
ENDIF
;献身的
IF TALENT:献身的
	SIF S
		PRINT  + 
	A += 6
	PRINT 献身的
	PRINTV '(,6,')
	S = 1
ENDIF
;快感の否定（淫乱、娼婦で無効）
IF TALENT:快感応答 < 0 && !TALENT:淫乱
	PRINT  - 
	A -= 3
	PRINT 快感の否定
	PRINTV '(,1,')
	S = 1
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER) && TALENT:性別嗜好 > 0
	PRINT  - 
	A -= 8
	PRINT 男嫌い
	PRINTV '(,5,')
	S = 5
ENDIF
;恋慕
IF TALENT:恋慕
	SIF S
		PRINT  + 
	A += 5
	PRINT 恋慕
	PRINTV '(,5,')
	S = 5
ENDIF
;セフレ
IF TALENT:セフレ
	SIF S
		PRINT  + 
	A += 3
	PRINT セフレ
	PRINTV '(,3,')
	S = 3
ENDIF

;倒錯的
IF TALENT:倒錯的
	SIF S
		PRINT  + 
	A += 4
	PRINT 倒錯的
	PRINTV '(,4,')
	S = 4
ENDIF
;調教者がふたなり
IF (TALENT:PLAYER:性別 & 3) == 3
	SIF S
		PRINT  + 
	A += 8
	PRINT ふたなり
	PRINTV '(,8,')
	S = 1
ENDIF


;合計を表示(22以上で実行)
PRINT  = 
PRINTV A

V = 22
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT
SKIPDISP 0
;実行できない
SIF A < V
	RETURN 0
RETURN 1
;-------------------------------------------------
;実行判定
;-------------------------------------------------

@COM_ABLE85
;足扱き実行判定
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(85)
	RETURN RESULT
;ちんこがない場合駄目
SIF TALENT:PLAYER:Ｃ感度 == -2
	RETURN 0
;調教者がオトコか半人半妖かふたなりじゃないとダメ
SIF !(TALENT:PLAYER:性別 & 2) || BASE:PLAYER:勃起 < 1000
	RETURN 0
;縄使用中は不可
; SIF TEQUIP:縄
; 	RETURN 0
;下半身着衣
;前鎧、めくれない服、タイツ、前張りがあると無理
SIF TEQUIP:PLAYER:下腹部前鎧あり
	RETURN 0
SIF TEQUIP:PLAYER:下半身服あり && (!TEQUIP:PLAYER:スカートめくり可 && !TEQUIP:PLAYER:ずらし可)
	RETURN 0
SIF TEQUIP:PLAYER:下タイツあり && !TEQUIP:PLAYER:下タイツ破れ
	RETURN 0
;ペニスを何かに使ってる・届かないなら無理
SIF TEQUIP:PLAYER:ペニス
	RETURN 0
RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM85
PRINTFORM %CALLNAME:TARGET%は%CALLNAME:PLAYER%のペニスを足裏で
;恋慕か親愛
SIF TALENT:恋慕 && PLAYER == MASTER
	PRINT 丹念に
PRINTFORML 擦りあげている…

