@MODETYPE_シックスナイン(部位番号, 処理モード, プレイヤー番号, ターゲット番号)
#DIM 部位番号
#DIM プレイヤー番号
#DIM ターゲット番号
#DIMS 処理モード

IF 処理モード == "写真用文字列_プレイヤー"
	;モード固有の「～～をする」の文章を入れ込む
	詳細文文字列受け渡し変数 = %CALLNAME:ターゲット番号%と互いの性器を舐っている
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ELSEIF 処理モード == "写真用文字列_ターゲット"
	;モード固有の「～～をする」の文章を入れ込む
	詳細文文字列受け渡し変数 = %CALLNAME:プレイヤー番号%と互いの性器を舐っている
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ENDIF

;0=ペニス 1=クリ 2=指（手） 3=口 4=胸 5=膣 6=アナル
;キスは両者口のみ専有
SELECTCASE 部位番号
	CASE 部位_ペニス
		RETURN 部位状況_占有
	CASE 部位_クリトリス
		RETURN 部位状況_占有
	CASE 部位_口
		RETURN 部位状況_占有
	CASE 部位_胸
		RETURN 部位状況_届かない
ENDSELECT


@MODE_持続快楽_シックスナイン(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

CALL SOURCE_CALC_快Ｃ(対象キャラ番号, 実行キャラ番号, 150)
CALL SOURCE_CALC_快Ｃ(実行キャラ番号, 対象キャラ番号, 150)
CALL SOURCE_CALC_接触(対象キャラ番号, 実行キャラ番号, 80)
CALL SOURCE_CALC_接触(実行キャラ番号, 対象キャラ番号, 80)
CALL SOURCE_CALC_奉仕(対象キャラ番号, 実行キャラ番号, 100)
CALL SOURCE_CALC_奉仕(実行キャラ番号, 対象キャラ番号, 100)
EXP:対象キャラ番号:口淫経験 ++
EXP:実行キャラ番号:口淫経験 ++
RCVAR:対象キャラ番号:奉仕コマンドフラグ = 1
RCVAR:実行キャラ番号:奉仕コマンドフラグ = 1

RETURN 1


@MESSAGE_MODE_シックスナイン解除(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

PRINTFORML %CALLNAME:実行キャラ番号%と%CALLNAME:対象キャラ番号%は互いの性器から舌を離した…



@MODETYPE_ボテ腹ズリ(部位番号, 処理モード, プレイヤー番号, ターゲット番号)
#DIM 部位番号
#DIM プレイヤー番号
#DIM ターゲット番号
#DIMS 処理モード

IF 処理モード == "写真用文字列_プレイヤー"
	;モード固有の「～～をする」の文章を入れ込む
	詳細文文字列受け渡し変数 = %TEXTR("大きく膨らん/子供を孕ん")%だお腹で%CALLNAME:ターゲット番号%の%TEXTR("ペニス/チンポ")%を扱いている
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ELSEIF 処理モード == "写真用文字列_ターゲット"
	;モード固有の「～～をする」の文章を入れ込む
	詳細文文字列受け渡し変数 = %TEXTR("大きく膨らん/子供を孕ん")%だ%CALLNAME:プレイヤー番号%のお腹で%TEXTR("ペニス/チンポ/おちんぽ")%を扱いている
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ENDIF

;0=ペニス 1=クリ 2=指（手） 3=口 4=胸 5=膣 6=アナル
SELECTCASE 部位番号
	CASE 部位_ペニス
		SIF 処理モード == "ターゲット"
			RETURN 部位状況_占有
	CASE 部位_クリトリス
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
	CASE 部位_膣
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
	CASE 部位_アナル
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
ENDSELECT


@MODE_持続快楽_ボテ腹ズリ(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

CALL SOURCE_CALC_快Ｃ(対象キャラ番号, 実行キャラ番号, 150)
CALL SOURCE_CALC_接触(対象キャラ番号, 実行キャラ番号, 80)
CALL SOURCE_CALC_接触(実行キャラ番号, 対象キャラ番号, 80)
CALL SOURCE_CALC_奉仕(実行キャラ番号, 対象キャラ番号, 100)
RCVAR:実行キャラ番号:奉仕コマンドフラグ = 1

RETURN 1


@MESSAGE_MODE_ボテ腹ズリ解除(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

IF 対象キャラ番号 == MASTER
	PRINTFORML %CALLNAME:対象キャラ番号%は%CALLNAME:実行キャラ番号%に腹部での奉仕を止めさせた…
ELSE
	PRINTFORML %CALLNAME:実行キャラ番号%は%CALLNAME:対象キャラ番号%の陰茎から腹部を離した…
ENDIF



@MODETYPE_全裸散歩(部位番号, 処理モード, プレイヤー番号, ターゲット番号)
#DIM 部位番号
#DIM プレイヤー番号
#DIM ターゲット番号
#DIMS 処理モード

IF 処理モード == "写真用文字列_プレイヤー"
	;モード固有の「～～をする」の文章を入れ込む
	;詳細文文字列受け渡し変数 = 
	;基本あなた専用なのでここに来ることはないはず
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ELSEIF 処理モード == "写真用文字列_ターゲット"
	;モード固有の「～～をする」の文章を入れ込む
	IF 知識素質:ターゲット番号:性知識 < -1
		詳細文文字列受け渡し変数 = 身体を隠すことも無く堂々と歩く
	ELSE
		詳細文文字列受け渡し変数 = 不安そうに周囲を伺いながら歩く
	ENDIF
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ENDIF

;0=ペニス 1=クリ 2=指（手） 3=口 4=胸 5=膣 6=アナル
;部位専有なし


@MODE_持続快楽_全裸散歩(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

CALL SOURCE_CALC_露出(対象キャラ番号, 実行キャラ番号, 100)
RCVAR:対象キャラ番号:露出コマンドフラグ = 1

RETURN 1
