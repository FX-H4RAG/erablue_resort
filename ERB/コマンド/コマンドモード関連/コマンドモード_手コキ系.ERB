@系統リスト_手コキ系(モード名)
#FUNCTION
#DIMS モード名
IF GROUPMATCH(モード名, "手コキ", "授乳手コキ")
	モード確認結果 = 1
	RETURNF 1
ENDIF



@MODE_持続快楽_手コキ(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

CALL SOURCE_CALC_快Ｃ(対象キャラ番号, 実行キャラ番号, 100)
CALL SOURCE_CALC_接触(実行キャラ番号, 対象キャラ番号, 70)
CALL SOURCE_CALC_接触(対象キャラ番号, 実行キャラ番号, 80)
CALL SOURCE_CALC_奉仕(実行キャラ番号, 対象キャラ番号, 90)
EXP:実行キャラ番号:手淫経験 ++
;奉仕快楽
RCVAR:実行キャラ番号:奉仕コマンドフラグ = 1

RETURN 1


@MODE_持続快楽_授乳手コキ(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

CALL SOURCE_CALC_快Ｃ(対象キャラ番号, 実行キャラ番号, 100)
CALL SOURCE_CALC_快Ｂ(実行キャラ番号, 対象キャラ番号, 100)
CALL SOURCE_CALC_接触(実行キャラ番号, 対象キャラ番号, 100)
CALL SOURCE_CALC_接触(対象キャラ番号, 実行キャラ番号, 80)
CALL SOURCE_CALC_奉仕(実行キャラ番号, 対象キャラ番号, 120)
EXP:実行キャラ番号:手淫経験 ++
RCVAR:実行キャラ番号:乳首開発フラグ = 2
;奉仕快楽
RCVAR:実行キャラ番号:奉仕コマンドフラグ = 1

RETURN 1



@MODETYPE_手コキ(部位番号, 処理モード, プレイヤー番号, ターゲット番号)
#DIM 部位番号
#DIM プレイヤー番号
#DIM ターゲット番号
#DIMS 処理モード

IF 処理モード == "写真用文字列_プレイヤー"
	;モード固有の「～～をする」の文章を入れ込む
	詳細文文字列受け渡し変数 = %CALLNAME:ターゲット番号%の%TEXTR("ペニス/チンポ")%を%TEXTR("手指で責め/指の輪っかで扱く/指の輪っかでしこしこと愛撫する/手コキする/手でしこしこする/手で愛撫する")%
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ELSEIF 処理モード == "写真用文字列_ターゲット"
	;モード固有の「～～をする」の文章を入れ込む
	SELECTCASE RAND:4
	CASE 0
		詳細文文字列受け渡し変数 = %CALLNAME:プレイヤー番号%の手指で%TEXTR("ペニス/チンポ/おちんぽ")%を扱かれている
	CASE 1
		詳細文文字列受け渡し変数 = %CALLNAME:プレイヤー番号%が指で作った輪っかで%TEXTR("ペニス/チンポ/おちんぽ")%を%TEXTR("扱かれている/しこしこされている")%
	CASE 2
		詳細文文字列受け渡し変数 = %CALLNAME:プレイヤー番号%に%TEXTR("ペニス/チンポ/おちんぽ")%を手で%TEXTR("愛撫されている/しこしこされている")%
	CASE 3
		詳細文文字列受け渡し変数 = %CALLNAME:プレイヤー番号%に%TEXTR("ペニス/チンポ/おちんぽ")%を手コキされている
	ENDSELECT
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ENDIF

;0=ペニス 1=クリ 2=指（手） 3=口 4=胸 5=膣 6=アナル
SELECTCASE 部位番号
	CASE 部位_ペニス
		SIF 処理モード == "ターゲット"
			RETURN 部位状況_占有
	CASE 部位_指
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_占有
ENDSELECT


@MODETYPE_授乳手コキ(部位番号, 処理モード, プレイヤー番号, ターゲット番号)
#DIM 部位番号
#DIM プレイヤー番号
#DIM ターゲット番号
#DIMS 処理モード

IF 処理モード == "写真用文字列_プレイヤー"
	;モード固有の「～～をする」の文章を入れ込む
	SELECTCASE RAND:4
	CASE 0 TO 2
		詳細文文字列受け渡し変数 = %CALLNAME:ターゲット番号%に%TEXTR("乳/乳首")%を吸われながら、%TEXTR("ペニス/チンポ")%を%TEXTR("手指で責め/指の輪っかで扱く/指の輪っかでしこしこと愛撫する/手コキする/手でしこしこする/手で愛撫する")%
	CASE 3
		詳細文文字列受け渡し変数 = 母乳を与える体勢で%CALLNAME:ターゲット番号%の%TEXTR("ペニス/チンポ")%を%TEXTR("手コキ/手でしこしこ/手で愛撫")%する
	ENDSELECT
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ELSEIF 処理モード == "写真用文字列_ターゲット"
	;モード固有の「～～をする」の文章を入れ込む
	SELECTCASE RAND:4
	CASE 0 TO 2
		詳細文文字列受け渡し変数 = %CALLNAME:プレイヤー番号%の%TEXTR("乳/乳首")%を%TEXTR("吸い/しゃぶり")%ながら、%TEXTR("ペニス/チンポ/おちんぽ")%を%TEXTR("手/手指/指の輪っか")%で%TEXTR("愛撫されている/扱かれている/しこしこされている/しこしこと愛撫されている")%
	CASE 3
		詳細文文字列受け渡し変数 = 母乳を与える体勢で%CALLNAME:プレイヤー番号%に%TEXTR("ペニス/チンポ/おちんぽ")%を%TEXTR("手/手指/指の輪っか")%で%TEXTR("愛撫されている/扱かれている/しこしこされている/しこしこと愛撫されている")%
	ENDSELECT
	;下の部位専有判定と別の値を返す
	RETURN 部位状況_写真用
ENDIF

;0=ペニス 1=クリ 2=指（手） 3=口 4=胸 5=膣 6=アナル
;キスは両者口のみ専有
SELECTCASE 部位番号
	CASE 部位_ペニス
		SIF 処理モード == "ターゲット"
			RETURN 部位状況_占有
	CASE 部位_クリトリス
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
	CASE 部位_指
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_占有
	CASE 部位_口
		SIF 処理モード == "ターゲット"
			RETURN 部位状況_占有
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
	CASE 部位_胸
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_占有
	CASE 部位_膣
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
	CASE 部位_アナル
		SIF 処理モード == "プレイヤー"
			RETURN 部位状況_届かない
ENDSELECT



@MESSAGE_MODE_手コキ解除(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

IF 対象キャラ番号 == MASTER
	PRINTFORML %CALLNAME:対象キャラ番号%は%CALLNAME:実行キャラ番号%を止め、陰茎から手を離させた
ELSE
	PRINTFORML %CALLNAME:実行キャラ番号%は%CALLNAME:対象キャラ番号%の陰茎から手を離した
ENDIF


@MESSAGE_MODE_授乳手コキ解除(実行キャラ番号, 対象キャラ番号)
#DIM 実行キャラ番号
#DIM 対象キャラ番号

IF 対象キャラ番号 == MASTER
	PRINTFORML %CALLNAME:対象キャラ番号%は%CALLNAME:実行キャラ番号%の乳から口を離し、陰茎から手を離させた
ELSE
	PRINTFORML %CALLNAME:実行キャラ番号%は%CALLNAME:対象キャラ番号%の陰茎から手を離した
ENDIF
