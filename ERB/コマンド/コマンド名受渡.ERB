
@TRAIN_COMNAME(ARG)
#FUNCTIONS

SIF ARG < 0
	RETURNF

TSTR:コマンド名受渡 = 
TSTR:コマンドカラー受渡 = 
SELECTCASE SAVESTR:ゲームフェイズ管理
	CASE "むりやりうふふモード"
		TRYCALLFORMF RAPE_COMNAME{ARG}
		VARSET RESULTS
		SPLIT TSTR:コマンド名受渡, "_", RESULTS
		TSTR:コマンド名受渡 = 
		FOR LOCAL, 0, RESULT
			IF RESULTS:LOCAL == "H"
				TSTR:コマンド名受渡 += "<img src='えっちハート'>"
			ELSE
				TSTR:コマンド名受渡 += @"%RESULTS:LOCAL%"
			ENDIF
		NEXT
	CASE "マッサージモード"
		TRYCALLFORMF MASSAGE_COMNAME{ARG}
		VARSET RESULTS
		SPLIT TSTR:コマンド名受渡, "_", RESULTS
		TSTR:コマンド名受渡 = 
		FOR LOCAL, 0, RESULT
			IF RESULTS:LOCAL == "H"
				TSTR:コマンド名受渡 += "<img src='えっちハート'>"
			ELSE
				TSTR:コマンド名受渡 += @"%RESULTS:LOCAL%"
			ENDIF
		NEXT
	CASE "通常モード"
		TRYCALLFORMF COMNAME{ARG}
ENDSELECT

RETURNF TSTR:コマンド名受渡


@TRAIN_COMNAME_S(ARGS)
;派生コマンド用
#FUNCTIONS

SIF ARGS == ""
	RETURNF

TSTR:コマンド名受渡 = 
TSTR:コマンドカラー受渡 = 
SELECTCASE SAVESTR:ゲームフェイズ管理
	CASE "むりやりうふふモード"
		TRYCALLFORMF RAPE_COMNAME_S%ARGS%
	CASE "マッサージモード"
		TRYCALLFORMF MASSAGE_COMNAME_S%ARGS%
	CASE "通常モード"
		TRYCALLFORMF COMNAME_S%ARGS%
ENDSELECT

RETURNF TSTR:コマンド名受渡


@同時モード設定()
#DIM 同室番号
#DIM 選択キャラカウント
#DIM ページ数
#DIM 次ページフラグ
#DIM 選択キャラ一時保存, 10
;同室者から、同時コマンドのターゲットを指定する
;まず候補を並べる

選択キャラカウント = 0
次ページフラグ = 0
DRAWLINE
FOR 同室番号, 1, CHARANUM
	SIF TARGET:同室番号 < 1
		BREAK
	IF GETBIT(選択キャラ一時保存:(同室番号 / 64), 同室番号 % 64)
		選択キャラカウント += 1
	ENDIF
	SIF 同室番号 < ページ数 * 30
		CONTINUE
	SIF 同室番号 == ページ数 * 30 + 30
		CONTINUE
	IF 同室番号 > ページ数 * 30 + 30
		次ページフラグ = 1
		CONTINUE
	ENDIF
	SIF CFLAG:(TARGET:同室番号):うふふ == 0
		CONTINUE
	;うふふ同室者は全部並べる
	IF GETBIT(選択キャラ一時保存:(同室番号 / 64), 同室番号 % 64)
		SETCOLOR カラーパレット("えっちな色")
	ENDIF
	IF CFLAG:(TARGET:同室番号):体力限界
		SETCOLOR カラーパレット("グレーアウト")
		PRINTPLAINFORM [{同室番号}] %NAME:(TARGET:同室番号)%
	ELSE
		PRINTBUTTON @"[{同室番号}] %NAME:(TARGET:同室番号)%", 同室番号
	ENDIF
	RESETCOLOR
	PRINTL
NEXT
PRINTL
IF 選択キャラカウント < 2
	SETCOLOR カラーパレット("グレーアウト")
	PRINTPLAIN [998] これで決定
	RESETCOLOR
ELSE
	PRINTBUTTON "[998] これで決定", 998
ENDIF
PRINTL
PRINTL
IF ページ数 > 0
	PRINTBUTTONLC "[996] 前ページへ", 996
ELSE
	PRINTLC 　
ENDIF
IF 次ページフラグ
	PRINTBUTTONLC "[997] 次ページへ", 997
ENDIF
PRINTBUTTON "[999] 戻る", 999
PRINTL
DRAWLINE

BINPUT
SELECTCASE RESULT
	CASE 999
		VARSET 選択キャラ一時保存
		RETURN 0
	CASE 998
		VARSET 同時モード_選択キャラ, -1
		同時モード_選択数 = 選択キャラカウント
		選択キャラカウント = 0
		FOR 同室番号, 1, CHARANUM
			SIF TARGET:同室番号 < 1
				BREAK
			IF GETBIT(選択キャラ一時保存:(同室番号 / 64), 同室番号 % 64)
				同時モード_選択キャラ:選択キャラカウント = TARGET:同室番号
				選択キャラカウント += 1
			ENDIF
		NEXT
		TFLAG:TARGET変更用 = 同時モード_選択キャラ:0
		VARSET 選択キャラ一時保存
		RETURN 1
	CASE 996
		ページ数 --
		RESTART
	CASE 997
		ページ数 ++
		RESTART
	CASEELSE
		INVERTBIT 選択キャラ一時保存:(RESULT / 64), RESULT % 64
		RESTART
ENDSELECT
