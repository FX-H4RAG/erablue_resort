
@COMNAME_S306_12
#FUNCTIONS
TSTR:コマンド名受渡 = 角ブラシ

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_S_306_12
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ弱")


@COM_TOOLTIP_S306_12
LOCALS = <br>■角ブラシ<br>
LOCALS += "相手の角をブラシで磨くコマンド。"
LOCALS += "<br>COMタイプ：セクハラ<br>取得ソース：接触・露出・快Ｓ・歓楽(一定条件時)・不快(一定条件時)・反感(一定条件時)"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM_S306_12
CALL セクハラ反応タイプ判定(TARGET)
;RFLAG:コマンド結果受渡し変数
;1:拒絶  2:受け入れ  3:喜び
;RFLAG:コマンド結果受渡し変数２
;1:無知  2:無反応  3:嫌悪  4:拒否  5:余裕  6:羞恥  7:愉悦  8:汎用

SELECTCASE RFLAG:コマンド結果受渡し変数２
	CASE 2
		;無反応
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1 TO 2
				;拒絶と受け入れ時は反応なし
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
			CASE 3
				;喜び時はちょっと喜ぶ
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 10)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 10)
		ENDSELECT
	CASE 3 TO 4
		;嫌悪と拒否
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				CALL SOURCE_CALC_不快(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_反感(TARGET, PLAYER, 200)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
			CASE 2
				;受け入れ
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 10)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 10)
			CASE 3
				;喜び
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 150)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
		ENDSELECT
	CASE 6
		;羞恥
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				CALL SOURCE_CALC_不快(TARGET, PLAYER, 10)
				CALL SOURCE_CALC_反感(TARGET, PLAYER, 10)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 100)
			CASE 2
				;受け入れ
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 20)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 20)
			CASE 3
				;喜び
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 150)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 100)
		ENDSELECT
	CASEELSE
		;上記以外は同じソースを発生
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
			CASE 2
				;受け入れ
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 10)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 10)
			CASE 3
				;喜び
				CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 150)
				CALL SOURCE_CALC_接触(TARGET, PLAYER, 100)
				CALL SOURCE_CALC_露出(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_友情度UP(TARGET, PLAYER, 50)
				CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
		ENDSELECT
ENDSELECT

RCVAR:TARGET:角開発フラグ = 2
;角開発時は露出追加
SIF EXP:TARGET:角開発 >= 100 && GET_TARGETNUM() > 1 && !CFLAG:TARGET:うふふ
	CALL SOURCE_CALC_露出(TARGET, PLAYER, 250)

;発生する快楽は受け手の状態に依存しない
CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 20)
SIF TALENT:PLAYER:器用な指
	CALL SOURCE_CALC_快Ｓ(TARGET, PLAYER, 30)
TIME += 10

RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE_S306_12
;選択から飛んでくるのでABLEは常に0
RETURN 0

@身体に触れる_ABLE_角ブラシ
;関数実行のため、派生番号を返す
RETURN 8

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM_S306_12

PRINTFORML %CALLNAME:PLAYER%はブラシを取り出し、%CALLNAME:TARGET%の角を磨いた。
IF CUP:TARGET:快Ｓ > 1000
	;一定以上の快楽が発生した時にちょっと声が出る
	PRINTFORML 不意の快楽に、%CALLNAME:TARGET%は思わず少し声を漏らしてしまった…
	PRINTL
ENDIF

@MESSAGE_COM_S306_12_非口上時
SELECTCASE RFLAG:コマンド結果受渡し変数２
	CASE 1
		;無知
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1 TO 2
				;拒絶と受け入れに違い無し
				PRINTFORML %CALLNAME:TARGET%は何か用だろうか、と首を傾げている
			CASE 3
				;喜び時は喜ぶ
				PRINTFORML %CALLNAME:TARGET%は角を磨いてもらって嬉しそうにしている
		ENDSELECT
	CASE 2
		;無反応
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1 TO 2
				;拒絶と受け入れ時は反応なし
				PRINTFORML %CALLNAME:TARGET%は何も反応を示さず、平然としている
			CASE 3
				;喜び時はちょっと喜ぶ
				PRINTFORML %CALLNAME:TARGET%は平然としているが、どことなく嬉しそうな雰囲気を感じる
		ENDSELECT
	CASE 3
		;嫌悪
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				PRINTFORML %CALLNAME:TARGET%は嫌そうな顔で%CALLNAME:PLAYER%の手を払い除けた…
			CASE 2
				;受け入れ
				PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%を無視し、好きに整えさせている
			CASE 3
				;喜び
				PRINTFORML %CALLNAME:TARGET%は無視している風だが、%CALLNAME:PLAYER%が角を磨きやすいように少し身体を寄せてきた…
		ENDSELECT
	CASE 4
		;拒否
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				PRINTFORML %CALLNAME:TARGET%は気安く触れるべきでない、と%CALLNAME:PLAYER%の手を拒んだ
			CASE 2
				;受け入れ
				PRINTFORML %CALLNAME:TARGET%は不満そうな顔をしているが、%CALLNAME:PLAYER%の手を拒むことはしないようだ…
			CASE 3
				;喜び
				PRINTFORML %CALLNAME:TARGET%は悪い気はしない様子で、%CALLNAME:PLAYER%の好きにさせている…
		ENDSELECT
	CASE 5
		;余裕
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%を窘め、他人の角に気安く触れるべきではないと諭した
			CASE 2
				;受け入れ
				PRINTFORML %CALLNAME:TARGET%は困ったように笑いながら、口だけの注意を%CALLNAME:PLAYER%にしている
			CASE 3
				;喜び
				PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%が角を磨きやすいよう、身体を寄せてきた…
		ENDSELECT
	CASE 6
		;羞恥
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				PRINTFORML %CALLNAME:TARGET%は真っ赤になって、%CALLNAME:PLAYER%から少し距離を取ってしまった…
			CASE 2
				;受け入れ
				PRINTFORML %CALLNAME:TARGET%は顔を赤くしながら、%CALLNAME:PLAYER%にされるがままになっている
			CASE 3
				;喜び
				PRINTFORML %CALLNAME:TARGET%は赤くなった顔で%CALLNAME:PLAYER%を見つめ、小さな声でお礼を言ってきた
		ENDSELECT
	CASE 7
		;愉悦
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				PRINTFORML %CALLNAME:TARGET%は面白そうに微笑み、%CALLNAME:PLAYER%の整え方がなっていないとダメ出しをしてきた
			CASE 2
				;受け入れ
				PRINTFORML %CALLNAME:TARGET%は角だけでいいのかと、冗談交じりで%CALLNAME:PLAYER%をからかっている…
			CASE 3
				;喜び
				PRINTFORML %CALLNAME:TARGET%は角以外も触れていいのにと、色っぽい表情で%CALLNAME:PLAYER%を挑発している…
		ENDSELECT
	CASE 8
		;汎用
		SELECTCASE RFLAG:コマンド結果受渡し変数
			CASE 1
				;拒絶
				PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%に触れられることを嫌がっている…
			CASE 2
				;受け入れ
				PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%の手付きを嫌がらず、受け入れている
			CASE 3
				;喜び
				PRINTFORML %CALLNAME:TARGET%は%CALLNAME:PLAYER%に角を磨いてもらうことを少し嬉しく思っているようだ…
		ENDSELECT
ENDSELECT

