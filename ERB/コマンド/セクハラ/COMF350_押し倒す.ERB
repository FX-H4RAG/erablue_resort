
@COMNAME350
#FUNCTIONS
TSTR:コマンド名受渡 = 押し倒す
SIF !BEDROOM(CFLAG:PLAYER:現在マップ種別, CFLAG:PLAYER:現在位置)
	TSTR:コマンド名受渡 = 抱き寄せる
SIF GET_TARGETNUM() > 1 && !CFLAG:MASTER:隠密
	TSTR:コマンド名受渡 = まとめて%TSTR:コマンド名受渡%

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_350
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ")

@COM_TOOLTIP_350
LOCALS = <br>■押し倒す<br>
LOCALS += "あるいは抱き寄せる。すでに合意を得ている相手と性交渉を行うコマンド。<br>通常二人きり時のみだが、一緒に隠密時は同じ場所に他のキャラがいても二人きり扱いになる。"
LOCALS += "<br>COMタイプ：セクハラ<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM350
;押し倒す
IF CFLAG:MASTER:隠密 && CFLAG:TARGET:隠密
	CFLAG:TARGET:うふふ = 1
ELSE
	FOR LOCAL,1,CHARANUM
		SIF TARGET:LOCAL <= 0
			BREAK
		CFLAG:(TARGET:LOCAL):うふふ = 1
	NEXT
ENDIF
CFLAG:MASTER:うふふ = 1

IF 初体験日:TARGET:初うふふ == 0
	初体験日:TARGET:初うふふ = DAY
	TFLAG:初うふふフラグ = 1
ENDIF

;3Pモード管理フラグを初期化
FLAG:モード管理 = 0

TIME += 5

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE350
;押し倒す実行判定
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(350)
	RETURN RESULT
;飲み会時はだめ
SIF TFLAG:調教モード == 3
	RETURN 0
;馴れ合い強度2
SIF TCVAR:MASTER:馴れ合い強度 < 2
	RETURN 0
;隠密中は本人の合意素質のみでOK
IF CFLAG:MASTER:隠密 && CFLAG:TARGET:隠密
	SIF !GETBIT(CFLAG:TARGET:1 , 0)
		RETURN 0
;合意がないとダメ
ELSE
	;非隠密時は二人きりでないとだめ
	SIF GET_TARGETNUM() > 1
		RETURN 0
	SIF !GETBIT(CFLAG:TARGET:1 , 0)
		RETURN 0
ENDIF
SIF GET_TARGETNUM() > 1 && TCVAR:MASTER:馴れ合い強度 < 3
	RETURN 0
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
RETURN 1
;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM350
IF GET_TARGETNUM() > 1
	PRINTFORML %CALLNAME:PLAYER%はあたりに人がいないことを確認すると%CALLNAME:TARGET%達を抱き寄せた…
ELSEIF CFLAG:TARGET:隠密 && CFLAG:MASTER:隠密
	PRINTFORML %CALLNAME:PLAYER%は上手く物陰に隠れられていることを確認すると%CALLNAME:TARGET%を抱き寄せた…
ELSE
	PRINTFORML %CALLNAME:PLAYER%はあたりに人がいないことを確認すると%CALLNAME:TARGET%を抱き寄せた…
ENDIF
SIF TFLAG:初うふふフラグ
	PRINTFORML %CALLNAME:TARGET%は覚悟を決めたような目で%CALLNAME:PLAYER%を見つめている…

