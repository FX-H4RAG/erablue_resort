;-------------------------------------------------
;定住の誘い
;-------------------------------------------------
@COMNAME315
#FUNCTIONS
TSTR:コマンド名受渡 = 性処理業務交渉

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_315
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_315
LOCALS = <br>■性処理業務交渉<br>
LOCALS += "従業員相手に、性処理業務をしてくれないか交渉するコマンド。<br>"
LOCALS += "何をお願いするかによって成功率や支払う報酬が変化する。<br>"
LOCALS += "<br>COMタイプ：日常<br>取得ソース：選択肢によって変動"
LOCALS += "<br>固有の取得経験：選択肢によって変動<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM315
#DIMS CONST 選択先順番制御 = "手コキ処理", "パイズリ処理", "フェラチオ処理", "手マン処理", "クンニ処理"
#DIM 選択先保存
#DIMS DYNAMIC 性処理選択肢, 100
#DIM DYNAMIC 性処理数

DRAWLINE
PRINTFORML %CALLNAME:TARGET%にどの性処理を依頼しますか？
PRINTL

FOR LOCAL, 0, VARSIZE("選択先順番制御")
	CALLFORM 性処理_ABLE_%選択先順番制御:LOCAL%
	IF RESULT == -1
		CALLFORMF 性処理NAME_%選択先順番制御:LOCAL%
		IF ツールチップ切り替えOPTION記憶 == 0
			TSTR:ツールチップ受渡 = 
			TRYCALLFORM 性処理_TOOLTIP_%選択先順番制御:LOCAL%
			HTML_PRINT @"<nonbutton title='%TSTR:ツールチップ受渡%'><font color='#%カラーパレット_HTML("グレーアウト")%'>[{LOCAL}] %TSTR:コマンド名受渡%</font></nonbutton>"
		ELSE
			SETCOLOR カラーパレット("グレーアウト")
			PRINTPLAINFORM [{LOCAL}] %TSTR:コマンド名受渡%
			RESETCOLOR
			PRINTL 
		ENDIF
	ELSEIF RESULT == 1
		CALLFORMF 性処理NAME_%選択先順番制御:LOCAL%
		IF ツールチップ切り替えOPTION記憶 == 0
			TSTR:ツールチップ受渡 = 
			TRYCALLFORM 性処理_TOOLTIP_%選択先順番制御:LOCAL%
			HTML_PRINT @"<button value='{LOCAL}' title='%TSTR:ツールチップ受渡%'>[{LOCAL}] %TSTR:コマンド名受渡%</button>"
		ELSE
			PRINTBUTTON @"[{LOCAL}] %TSTR:コマンド名受渡%", LOCAL
			PRINTL 
		ENDIF
	ENDIF
NEXT

VARSET RESULTS
VARSET 性処理選択肢
ENUMFUNCBEGINSWITH "性処理_ABLE_"
性処理数 = RESULT
ARRAYCOPY "RESULTS", "性処理選択肢"
FOR LOCAL, 0, 性処理数
	性処理選択肢:LOCAL = %REPLACE(性処理選択肢:LOCAL, "性処理_ABLE_", "")%
	SIF MATCH(選択先順番制御, 性処理選択肢:LOCAL)
		CONTINUE
	CALLFORM %RESULTS:LOCAL%
	IF RESULT == -1
		CALLFORMF 性処理NAME_%性処理選択肢:LOCAL%
		IF ツールチップ切り替えOPTION記憶 == 0
			TSTR:ツールチップ受渡 = 
			TRYCALLFORM 性処理_TOOLTIP_%性処理選択肢:LOCAL%
			HTML_PRINT @"<nonbutton title='%TSTR:ツールチップ受渡%'><font color='#%カラーパレット_HTML("グレーアウト")%'>[{LOCAL + 100}] %TSTR:コマンド名受渡%</font></nonbutton>"
		ELSE
			SETCOLOR カラーパレット("グレーアウト")
			PRINTPLAINFORM [{LOCAL + 100}] %TSTR:コマンド名受渡%
			RESETCOLOR
			PRINTL 
		ENDIF
	ELSEIF RESULT == 1
		CALLFORMF 性処理NAME_%性処理選択肢:LOCAL%
		IF ツールチップ切り替えOPTION記憶 == 0
			TSTR:ツールチップ受渡 = 
			TRYCALLFORM 性処理_TOOLTIP_%性処理選択肢:LOCAL%
			HTML_PRINT @"<button value='{LOCAL + 100}' title='%TSTR:ツールチップ受渡%'>[{LOCAL + 100}] %TSTR:コマンド名受渡%</button>"
		ELSE
			PRINTBUTTON @"[{LOCAL + 100}] %TSTR:コマンド名受渡%", LOCAL + 100
			PRINTL 
		ENDIF
		PRINTL 
	ENDIF
NEXT


PRINTL
PRINTBUTTON "[999] 戻る", 999
PRINTL

DRAWLINE
BINPUT

LOCAL = RESULT
SELECTCASE LOCAL
	CASE 999
		RETURN -1
	CASE IS < 100
		CALL 性処理業務交渉処理(TARGET, 選択先順番制御:LOCAL)
		SIF RESULT == -1
			RESTART
	CASEELSE
		CALL 性処理業務交渉処理(TARGET, 性処理選択肢:(LOCAL - 100))
		SIF RESULT == -1
			RESTART
ENDSELECT

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE315

;移動実行判定
SIF SAVESTR:ゲームフェイズ管理 != "通常モード"
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(315)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;従業員である
SIF TALENT:従業員 == 0
	RETURN 0
;移動中でない

RETURN 1


;-------------------------------------------------
;メッセージ
;-------------------------------------------------

@MESSAGE_COM315

PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%に%RSTR:コマンド結果受渡し文字列%をお願いした。
CALLFORM MESSAGE_性処理_%RSTR:コマンド結果受渡し文字列%


@MESSAGE_COM315_非口上時

TRYCALLFORM MESSAGE_性処理_%RSTR:コマンド結果受渡し文字列%_非口上時



@性処理業務交渉処理(対象キャラ, 実行性処理)
#DIM 対象キャラ
#DIM 値段計算
#DIMS 実行性処理

CALLFORM CAN_性処理_%実行性処理%(対象キャラ)
PRINTL

IF RESULT == -1
	値段計算 = 0
	RFLAG:コマンド結果受渡し変数 = 1
ELSE
	値段計算 = RESULT
ENDIF

IF MONEY < 値段計算
	PRINTW 所持ルピが足りません。
	RETURN -1
ENDIF

PRINTFORML 所持ルピ：%NUM_FORMAT(MONEY)%ルピ
PRINTL
PRINTFORML %実行性処理%を%CALLNAME:対象キャラ%にお願いしますか？
PRINTBUTTON "[0] はい", 0
PRINTL
PRINTBUTTON "[1] いいえ", 1
PRINTL

BINPUT
SELECTCASE RESULT
	CASE 0
		RSTR:コマンド結果受渡し文字列 = %実行性処理%
		RFLAG:コマンド結果受渡し変数２ = 値段計算
		MONEY -= 値段計算
		CALLFORM 性処理_%実行性処理%()
		RETURN RESULT
	CASE 1
		RETURN -1
ENDSELECT


@補正後好感度算出(対象キャラ)
#FUNCTION
#DIM 対象キャラ
#DIM 補正後好感度

補正後好感度 = CFLAG:対象キャラ:好感度レベル

補正後好感度 += TALENT:対象キャラ:自制心 * 7
補正後好感度 += TALENT:対象キャラ:一線越えない * 10
補正後好感度 += TALENT:対象キャラ:貞操 * 10
補正後好感度 += TALENT:対象キャラ:羞恥心 * 5
補正後好感度 += TALENT:対象キャラ:抵抗 * 15
補正後好感度 += TALENT:対象キャラ:態度 * 5

IF HETEROSEX(対象キャラ, PLAYER)
	SELECTCASE 性癖素質:対象キャラ:同性愛
		CASE 1
			;補正なし
		CASE 2
			補正後好感度 -= 5
		CASE 3
			補正後好感度 -= 15
		CASEELSE
			補正後好感度 += 15
	ENDSELECT
ENDIF

SELECTCASE TALENT:対象キャラ:性別嗜好
	CASE 1
		SIF TALENT:PLAYER:性別 == 2
			補正後好感度 += 20
	CASE 2
		SIF TALENT:PLAYER:性別 == 1
			補正後好感度 += 20
	CASE 3
		補正後好感度 += 20
ENDSELECT

補正後好感度 -= TALENT:対象キャラ:性的興味 * 5
補正後好感度 -= TALENT:対象キャラ:経験豊富 * 5
補正後好感度 -= TALENT:対象キャラ:ギャル * 5
補正後好感度 -= TALENT:対象キャラ:弱味 * 20
SIF TALENT:対象キャラ:マイペース < 0
	補正後好感度 -= 10
SIF 初体験日:対象キャラ:初うふふ
	補正後好感度 -= 10

SIF TALENT:対象キャラ:セフレ
	補正後好感度 = 999

RETURNF 補正後好感度
