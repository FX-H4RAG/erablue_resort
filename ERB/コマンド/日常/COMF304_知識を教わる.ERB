
@COMNAME304
#FUNCTIONS
TSTR:コマンド名受渡 = 知識を教わる

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_304
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_304
LOCALS = <br>■知識を教わる<br>
LOCALS += "自身よりも高い知識を持つ相手から、該当知識を教わるコマンド。一定確率で大成功、成功、失敗に分岐。<br>短時間に実行しすぎると会話のネタが無くなり失敗する。素質：恋慕を所持している相手の場合、この制限は無効となる。<br>相手の性知識が０以下の場合、効果が低くなる。"
LOCALS += "<br>COMタイプ：日常<br>取得ソース：歓楽・征服"
LOCALS += "<br>固有の取得経験：会話経験(あなた)<br>"
TSTR:ツールチップ受渡 = %LOCALS%


@COM304
;知識を教わる
;知識種類決定
DRAWLINE
PRINTL 何を教わりますか？
DRAWLINE
CALL 知識種類(TARGET, PLAYER, 1)
PRINTL
PRINTBUTTON "[999]戻る", 999
PRINTL
DRAWLINE
$INPUT_LOOP
INPUT

LOCAL = RESULT
SELECTCASE LOCAL
	CASE 1 TO 100
		IF ERDNAME(知識素質, LOCAL) == ""
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		IF 知識素質:PLAYER:調合知識 >= 知識素質:TARGET:調合知識 && 知識素質:PLAYER:調合知識 < 0
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		;知識種類を格納
		TFLAG:コマンド結果受渡し変数２ = LOCAL
	CASE 999
		RETURN -1
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT

LOCAL:2 = RAND:100
LOCAL:1 = 教える上手さ(TARGET, PLAYER, TFLAG:コマンド結果受渡し変数２)
IF LOCAL:2 <= LOCAL:1 / 2
	;上手さ / 2以下で超成功
	TFLAG:コマンド結果受渡し変数 = 1
	EXP:MASTER:会話経験 ++
	LOCAL:3 = RAND:20 + 25
	知識素質:PLAYER:(LOCAL + 100) += LOCAL:3
ELSEIF LOCAL:2 <= LOCAL:1
	;上手さ以下で成功
	TFLAG:コマンド結果受渡し変数 = 0
	LOCAL:3 = RAND:10 + 15
	知識素質:PLAYER:(LOCAL + 100) += LOCAL:3
ELSE
	;残りは失敗
	TFLAG:コマンド結果受渡し変数 = -1
	LOCAL:3 = RAND:5
	知識素質:PLAYER:(LOCAL + 100) += LOCAL:3
ENDIF

;歓楽強度
LOCAL:0 = 100
;征服強度
LOCAL:1 = 200

IF TFLAG:コマンド結果受渡し変数 == -1
	TIMES LOCAL:0 , 0.10
	TIMES LOCAL:1 , 0.50
ELSEIF TFLAG:コマンド結果受渡し変数 == 0
	TIMES LOCAL:0 , 1.00
	TIMES LOCAL:1 , 1.00
ELSE
	TIMES LOCAL:0 , 2.00
	TIMES LOCAL:1 , 2.00
ENDIF

DOWNBASE:気力 += 100
DOWNBASE:MASTER:気力 += 100
CALL SOURCE_CALC_歓楽(TARGET, PLAYER, LOCAL:0)
CALL SOURCE_CALC_征服(TARGET, PLAYER, LOCAL:1)

TCVAR:TARGET:学習フラグ += 80

TIME += 60
EXP:MASTER:会話経験 ++
RETURN 1
;-------------------------------------------------
;知識種類
;-------------------------------------------------
@知識種類(ARG, ARG:1, 表示フラグ)
#DIM 表示フラグ
;ARG=教えるキャラ番号
;ARG:1=教えてもらうキャラ番号
LOCAL = 0

;自身がマイナス知識の場合は教われない

IF 知識素質:ARG:調合知識 > 知識素質:(ARG:1):調合知識 && 知識素質:(ARG:1):調合知識 >= 0
	LOCAL += 1
	SIF 表示フラグ
		PRINTBUTTONLC "[1]調合知識", 1
ENDIF
IF 知識素質:ARG:魔法知識 > 知識素質:(ARG:1):魔法知識 && 知識素質:(ARG:1):魔法知識 >= 0
	LOCAL += 1
	SIF 表示フラグ
		PRINTBUTTONLC "[2]魔法知識", 2
ENDIF
IF 知識素質:ARG:機械知識 > 知識素質:(ARG:1):機械知識 && 知識素質:(ARG:1):機械知識 >= 0
	LOCAL += 1
	SIF 表示フラグ
		PRINTBUTTONLC "[3]機械知識", 3
ENDIF
IF 知識素質:ARG:錬金知識 > 知識素質:(ARG:1):錬金知識 && 知識素質:(ARG:1):錬金知識 >= 0
	LOCAL += 1
	SIF 表示フラグ
		PRINTBUTTONLC "[4]錬金知識", 4
ENDIF
IF 知識素質:ARG:魔物知識 > 知識素質:(ARG:1):魔物知識 && 知識素質:(ARG:1):魔物知識 >= 0
	LOCAL += 1
	SIF 表示フラグ
		PRINTBUTTONLC "[5]魔物知識", 5
ENDIF
IF 知識素質:ARG:絵師 > 知識素質:(ARG:1):絵師 && 知識素質:(ARG:1):絵師 >= 0
	LOCAL += 1
	SIF 表示フラグ
		PRINTBUTTONLC "[6]絵の心得", 6
ENDIF
IF 知識素質:ARG:高貴 > 知識素質:(ARG:1):高貴 && 知識素質:(ARG:1):高貴 >= 0
	LOCAL += 1
	SIF 表示フラグ
		PRINTBUTTONLC "[7]マナー作法", 7
ENDIF
IF 知識素質:ARG:釣り好き > 知識素質:(ARG:1):釣り好き && 知識素質:(ARG:1):釣り好き >= 0
	LOCAL += 1
	SIF 表示フラグ
		PRINTBUTTONLC "[8]釣り知識", 8
ENDIF
IF 知識素質:ARG:踊り子 > 知識素質:(ARG:1):踊り子 && 知識素質:(ARG:1):踊り子 >= 0
	LOCAL += 1
	SIF 表示フラグ
		PRINTBUTTONLC "[9]踊り技能", 9
ENDIF
IF 知識素質:ARG:歌唱技能 > 知識素質:(ARG:1):歌唱技能 && 知識素質:(ARG:1):歌唱技能 >= 0
	LOCAL += 1
	SIF 表示フラグ
		PRINTBUTTONLC "[10]歌唱技能", 10
ENDIF

RETURN LOCAL
;-------------------------------------------------
;学習効率
;-------------------------------------------------
@教える上手さ(ARG, ARG:1, 知識番号)
#FUNCTION
#DIM 知識番号
;ARG=教えるキャラ番号
;ARG:1=教えてもらうキャラ番号

;教える側と教えられる側の知識
LOCAL = 知識素質:ARG:知識番号 * 40
LOCAL += 知識素質:(ARG:1):知識番号 * 10

;教える側の影響素質
LOCAL += TALENT:ARG:会話資質 * 10
LOCAL += TALENT:ARG:精神的余裕 * 10
IF TALENT:ARG:性別嗜好 == 3
	LOCAL += -20
ELSEIF TALENT:ARG:性別嗜好 == 1 && TALENT:(ARG:1):性別 == 2
	LOCAL += -20
ELSEIF TALENT:ARG:性別嗜好 == 2 && TALENT:(ARG:1):性別 == 1
	LOCAL += -20
ENDIF

;教えられる側の影響素質
LOCAL += TALENT:(ARG:1):態度 * -10
LOCAL += TALENT:(ARG:1):プライド * -5
;あなたの場合は素質じゃなく技能で増減
IF ARG:1 == PLAYER
	LOCAL += (ABL:PLAYER:話術技能 - 2) * 5
ELSE
	LOCAL += TALENT:(ARG:1):会話資質 * 10
ENDIF
IF TALENT:(ARG:1):性別嗜好 == 3
	LOCAL += -20
ELSEIF TALENT:(ARG:1):性別嗜好 == 1 && TALENT:ARG:性別 == 2
	LOCAL += -20
ELSEIF TALENT:(ARG:1):性別嗜好 == 2 && TALENT:ARG:性別 == 1
	LOCAL += -20
ENDIF

RETURNF LOCAL

@知識系素質の取得(ARG)
;話術技能もここで成長
IF ARG == PLAYER
	SELECTCASE EXP:PLAYER:会話経験
		CASE IS >= 5000
			IF ABL:PLAYER:話術技能 <= 9
				PRINTFORML %CALLNAME:PLAYER%の話術技能がLv10になった！
				ABL:PLAYER:話術技能 = 10
			ENDIF
		CASE IS >= 2500
			IF ABL:PLAYER:話術技能 <= 8
				PRINTFORML %CALLNAME:PLAYER%の話術技能がLv9になった！
				ABL:PLAYER:話術技能 = 9
			ENDIF
		CASE IS >= 1500
			IF ABL:PLAYER:話術技能 <= 7
				PRINTFORML %CALLNAME:PLAYER%の話術技能がLv8になった！
				ABL:PLAYER:話術技能 = 8
			ENDIF
		CASE IS >= 1000
			IF ABL:PLAYER:話術技能 <= 6
				PRINTFORML %CALLNAME:PLAYER%の話術技能がLv7になった！
				ABL:PLAYER:話術技能 = 7
			ENDIF
		CASE IS >= 750
			IF ABL:PLAYER:話術技能 <= 5
				PRINTFORML %CALLNAME:PLAYER%の話術技能がLv6になった！
				ABL:PLAYER:話術技能 = 6
			ENDIF
		CASE IS >= 500
			IF ABL:PLAYER:話術技能 <= 4
				PRINTFORML %CALLNAME:PLAYER%の話術技能がLv5になった！
				ABL:PLAYER:話術技能 = 5
			ENDIF
		CASE IS >= 300
			IF ABL:PLAYER:話術技能 <= 3
				PRINTFORML %CALLNAME:PLAYER%の話術技能がLv4になった！
				ABL:PLAYER:話術技能 = 4
			ENDIF
		CASE IS >= 100
			IF ABL:PLAYER:話術技能 <= 2
				PRINTFORML %CALLNAME:PLAYER%の話術技能がLv3になった！
				ABL:PLAYER:話術技能 = 3
			ENDIF
		CASE IS >= 30
			IF ABL:PLAYER:話術技能 <= 1
				PRINTFORML %CALLNAME:PLAYER%の話術技能がLv2になった！
				ABL:PLAYER:話術技能 = 2
			ENDIF
		CASE IS >= 5
			IF ABL:PLAYER:話術技能 <= 0
				PRINTFORML %CALLNAME:PLAYER%の話術技能がLv1になった！
				ABL:PLAYER:話術技能 = 1
			ENDIF
	ENDSELECT
ENDIF
;ここから知識系素質
FOR LOCAL, 1, 100
	;知識マイナスの場合は通常成長しない
	SIF 知識素質:ARG:LOCAL < 0
		CONTINUE
	LOCAL:1 = POWER(10, 知識素質:ARG:LOCAL) * 100
	IF 知識素質:ARG:(LOCAL + 100) >= LOCAL:1
		知識素質:ARG:LOCAL += 1
		PRINTFORMW %CALLNAME:ARG%は[%ERDNAME(知識素質, LOCAL)%Lv{知識素質:ARG:LOCAL}]を習得した！
		;知識成長口上
		TRYCALLFORM KOJO_SKILLUP_知識素質_{NO:ARG}(LOCAL)
	ENDIF
NEXT

;ハーレム素質成長処理
LOCAL:1 = 0
FOR LOCAL, 0, CHARANUM
	IF TALENT:LOCAL:恋慕
		LOCAL:1 += 1
	ENDIF
NEXT

IF LOCAL:1 >= 10 && あなた特殊能力:ハーレム < 4
	DRAWLINE
	PRINTFORML %CALLNAME:PLAYER%はハーレムの極意を悟った！
	PRINTW 能力[ハーレムの主]が成長し、[ハーレムキング]に目覚めた！
	あなた特殊能力:ハーレム = 4
ELSEIF LOCAL:1 >= 5 && あなた特殊能力:ハーレム < 3
	DRAWLINE
	PRINTFORML %CALLNAME:PLAYER%はハーレムの管理に熟達した！
	PRINTW 能力[ハーレム一人前]が成長し、[ハーレムの主]に目覚めた！
	あなた特殊能力:ハーレム = 3
ELSEIF LOCAL:1 >= 3 && あなた特殊能力:ハーレム < 2
	DRAWLINE
	PRINTFORML %CALLNAME:PLAYER%はハーレムのコツを掴んだ！
	PRINTW 能力[ハーレム入門]が成長し、[ハーレム一人前]に目覚めた！
	あなた特殊能力:ハーレム = 2
ENDIF

@性知識段階表示(ARG, ARG:1, ツールチップフラグ = 0)
#FUNCTIONS
#DIM ツールチップフラグ
;ARGは性知識レベル、ARG:1はキャラ番号
;普段キャラ番号は使わないが、耳年増算出のために引っ張ってくる

SELECTCASE ARG
	CASE -2
		IF ツールチップフラグ
			RETURNF "<br>知識レベル：マイナス<br>性行為そのものを知らない、理解していない状態。いわゆる無知シチュ。<br>特別な手段を用いない限り、レベルは上昇しない。<br>"
		ENDIF
		RETURNF "無知"
	CASE -1
		IF ツールチップフラグ
			RETURNF "<br>知識レベル：マイナス<br>性行為＝交尾のような認識をもつ状態。恋愛などは存在は知っていても、理解はしていない。<br>特別な手段を用いない限り、レベルは上昇しない。<br>"
		ENDIF
		RETURNF "獣並み"
	CASE 0
		IF TALENT:(ARG:1):性的興味 > 0
			IF ツールチップフラグ
				RETURNF "<br>知識レベル：０<br>性的興味が強く、聞きかじりの知識は多いが正確な知識は少ない。<br>"
			ENDIF
			RETURNF "耳年増"
		ELSE
			IF ツールチップフラグ
				RETURNF "<br>知識レベル：０<br>性的な知識が少なく、経験も乏しい状態。<br>処女、あるいは未成年者などが多く該当する。<br>"
			ENDIF
			RETURNF "疎い"
		ENDIF
	CASE 1
		IF ツールチップフラグ
			RETURNF "<br>知識レベル：１<br>正しく性知識があり、恋愛を楽しむ下地がある状態。<br>非処女、あるいは成人などが多く該当する。<br>"
		ENDIF
		RETURNF "必要十分"
	CASE 2
		IF ツールチップフラグ
			RETURNF "<br>知識レベル：２<br>マニアックな性知識にも精通しており、詳しく説明する能力がある状態。<br>"
		ENDIF
		RETURNF "有識者"
	CASE 3
		IF ツールチップフラグ
			RETURNF "<br>知識レベル：３<br>性についで殆どのことを知り尽くしている状態。<br>"
		ENDIF
		RETURNF "百戦錬磨"
ENDSELECT

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE304
;会話実行判定
SIF !TFLAG:100
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(304)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
;口を何かに使ってるなら無理
SIF TEQUIP:PLAYER:口 == 1
	RETURN 0
SIF TEQUIP:TARGET:口 == 1
	RETURN 0
;ある程度好感度が必要
SIF CFLAG:TARGET:好感度レベル <= 50
	RETURN 0
;隠密中はダメ
SIF CFLAG:PLAYER:隠密
	RETURN 0
;友人は一日一回まで、親友になると２回まで教えてくれる
SIF CFLAG:TARGET:好感度レベル <= TCVAR:TARGET:学習フラグ
	RETURN 0
;相手が知識持ち必須
CALL 知識種類(TARGET, PLAYER, 0)
SIF !RESULT
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM304
;TFLAG:コマンド結果受渡し変数=１大成功　０成功　-１失敗
;TFLAG:コマンド結果受渡し変数２=知識種類

PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%から%ERDNAME(知識素質,TFLAG:コマンド結果受渡し変数２)%について教えてもらった
SELECTCASE TFLAG:コマンド結果受渡し変数
	CASE 1
		PRINTFORML %CALLNAME:TARGET%の教え方は非常にわかりやすく、大きく理解を進めることが出来た！
	CASE 0
		PRINTFORML いくつかの疑問点について、%CALLNAME:PLAYER%は正しい知識を得ることが出来た
	CASE -1
		PRINTFORML あまり集中できず、%CALLNAME:PLAYER%は殆ど勉強が進まなかった…
ENDSELECT


