@サイド領域描画
#DIMS 表示文字列
#DIMS 追加表示文字列

サイド描画行数保存 = LINECOUNT
LOCAL = 5525
LOCAL:1 = 500
LOCAL:2 = 4360
;基礎部分
表示文字列 = <div rect='{LOCAL}, 0, 4375, {LOCAL:2}' padding='50,25,50' color='#101010' border='31' bcolor='#C0C0C0'><nobr>
追加表示文字列 = 

TRYCALLFORM サイド描画表示種類_%サイド描画表示種類%(表示文字列, 追加表示文字列)
表示文字列 += "</nobr></div>"
;下部分表示
表示文字列 += @"<div rect='{LOCAL}, {LOCAL:2 - 15}, {LOCAL:1}, 200' padding='25,15,0' color='#101010' border='31' bcolor='#C0C0C0'><button value='サイド領域_サイド切り替え_表示なし'>表示なし</button></div>"
SIF サイド描画表示種類 == "表示なし"
	表示文字列 += @"<div rect='{LOCAL + 25}, {LOCAL:2 - 35}, {LOCAL:1 - 40}, 60' color='#101010'></div>"
LOCAL += LOCAL:1 + 50
LOCAL:1 = 700
表示文字列 += @"<div rect='{LOCAL}, {LOCAL:2 - 15}, {LOCAL:1}, 200' padding='25,0,0' color='#101010' border='31' bcolor='#C0C0C0'><button value='サイド領域_サイド切り替え_クイック移動'>クイック移動</button></div>"
SIF サイド描画表示種類 == "クイック移動"
	表示文字列 += @"<div rect='{LOCAL + 25}, {LOCAL:2 - 35}, {LOCAL:1 - 40}, 60' color='#101010'></div>"
LOCAL += LOCAL:1 + 50
LOCAL:1 = 500
表示文字列 += @"<div rect='{LOCAL}, {LOCAL:2 - 15}, {LOCAL:1}, 200' padding='25,15,0' color='#101010' border='31' bcolor='#C0C0C0'><button value='サイド領域_サイド切り替え_能力表示'>能力表示</button></div>"
SIF サイド描画表示種類 == "能力表示"
	表示文字列 += @"<div rect='{LOCAL + 25}, {LOCAL:2 - 35}, {LOCAL:1 - 40}, 60' color='#101010'></div>"
LOCAL += LOCAL:1 + 50
LOCAL:1 = 400
表示文字列 += @"<div rect='{LOCAL}, {LOCAL:2 - 15}, {LOCAL:1}, 200' padding='25,15,0' color='#101010' border='31' bcolor='#C0C0C0'><button value='サイド領域_サイド切り替え_同室者'>同室者</button></div>"
SIF サイド描画表示種類 == "同室者"
	表示文字列 += @"<div rect='{LOCAL + 25}, {LOCAL:2 - 35}, {LOCAL:1 - 40}, 60' color='#101010'></div>"
; IF CFLAG:PLAYER:うふふ && CFLAG:TARGET:うふふ
; 	LOCAL += LOCAL:1 + 50
; 	LOCAL:1 = 700
; 	表示文字列 += @"<div rect='{LOCAL}, {LOCAL:2 - 15}, {LOCAL:1}, 200' padding='25,15,0' color='#101010' border='31' bcolor='#C0C0C0'><button value='サイド領域_サイド切り替え_クイック自慰'>クイック自慰</button></div>"
; 	SIF サイド描画表示種類 == "クイック自慰"
; 		表示文字列 += @"<div rect='{LOCAL + 25}, {LOCAL:2 - 35}, {LOCAL:1 - 40}, 60' color='#101010'></div>"
; ELSE
	LOCAL += LOCAL:1 + 50
	LOCAL:1 = 900
	表示文字列 += @"<div rect='{LOCAL}, {LOCAL:2 - 15}, {LOCAL:1}, 200' padding='25,15,0' color='#101010' border='31' bcolor='#C0C0C0'><button value='サイド領域_サイド切り替え_クイック触れ合い'>クイック触れ合い</button></div>"
	SIF サイド描画表示種類 == "クイック触れ合い"
		表示文字列 += @"<div rect='{LOCAL + 25}, {LOCAL:2 - 35}, {LOCAL:1 - 40}, 60' color='#101010'></div>"
; ENDIF

SIF 追加表示文字列 != ""
	表示文字列 += @"%追加表示文字列%"
HTML_PRINT 表示文字列, 1

@サイド領域_エロステータス把握(ARG)
#DIMS L_STR, 2
VARSET L_STR
IF CFLAG:ARG:ゲージ起動分類 == 1
	L_STR = (恋慕対象)
	L_STR:1 = color='#FF3399'
ELSEIF CFLAG:ARG:発情期フラグ < 0
	L_STR = (発情期)
	L_STR:1 = color='#FF3399'
ELSEIF PREGNANCY_PROGRESS(ARG) == 5
	L_STR = (臨月)
	L_STR:1 = color='#FF3399'
ELSEIF TALENT:ARG:妊娠
	L_STR = (妊娠中)
	L_STR:1 = color='#FF99CF'
ELSEIF RISKY_DAY(ARG) && あなた特殊能力:危険日感知
	L_STR = (危険日)
	L_STR:1 = color='#FFCCE7'
ELSEIF RISKY_DAY(ARG) == -1 && あなた特殊能力:危険日感知
	L_STR = （安全日）
	L_STR:1 = color='#ccffff'
ENDIF
RESULTS '= L_STR
RESULTS:1 '= L_STR:1


@サイド領域_改行処理

FOR LOCAL, 0, 40 - (LINECOUNT - サイド描画行数保存)
	PRINTL
NEXT


@サイド領域_実行処理
#DIM 入力数値

IF STRCOUNT(RESULTS, "サイド領域_サイド切り替え_")
	サイド描画表示種類 = %REPLACE(RESULTS, "サイド領域_サイド切り替え_", "")%
	RETURN 1
ENDIF

SELECTCASE サイド描画表示種類
	CASE "クイック移動"
		IF STRCOUNT(RESULTS, "サイド領域_スクロール_")
			IF RESULTS == "サイド領域_スクロール_左"
				サイド描画横スクロール保存 = MAX(0, サイド描画横スクロール保存 - 42)
			ELSEIF RESULTS == "サイド領域_スクロール_右"
				サイド描画横スクロール保存 = MIN(サイド描画_クイック移動マップ横幅保存 - 42, サイド描画横スクロール保存 + 42)
			ELSEIF RESULTS == "サイド領域_スクロール_上"
				サイド描画縦スクロール保存 = MAX(0, サイド描画縦スクロール保存 - 36)
			ELSEIF RESULTS == "サイド領域_スクロール_下"
				;2行の余裕を持たせる
				サイド描画縦スクロール保存 = MIN(サイド描画_クイック移動マップ縦幅保存 - 34, サイド描画縦スクロール保存 + 36)
			ENDIF
			RETURN 1
		ENDIF
		;うふふ中は移動なし
		SIF CFLAG:PLAYER:うふふ
			RETURN 1
		;飲み会中は移動なし
		SIF CFLAG:PLAYER:飲み会
			RETURN 1
		入力数値 = 0
		入力数値 = TOINT(REPLACE(RESULTS, "サイド領域_", ""))
		SIF 入力数値 == 0
			RETURN 1

		CALL 表示マップ切り替え判定(入力数値)
		LOCAL = RESULT
		IF 入力数値 == 99
			;エリア移動選択
			CALL エリア移動選択画面表示
			IF RESULT == -1
				;移動しない場合
				RETURN 1
			ELSEIF RESULT == 1
				;移動処理へ進む
				サイド描画横スクロール保存 = 0
				CALL COM400
				SELECTCOM = 400
				TFLAG:SELECTCOM受け渡し = 400
				RETURN 0
			ENDIF
		ELSEIF CFLAG:MASTER:現在位置 == 入力数値
			RETURN 1
		ELSEIF LOCAL
			サイド描画横スクロール保存 = 0
			RETURN 1
		ELSEIF 存在しない移動先(CFLAG:PLAYER:現在マップ種別, 入力数値)
			RETURN 1
		ELSE
			;目的地が離れているので、最終目的地として設定
			TFLAG:長距離移動時_目的地格納 = 入力数値
			CALLFORM SKIP_MOVE_{CFLAG:PLAYER:現在マップ種別}(入力数値)
			;現在位置を更新し、次も移動の扱いにする
			CFLAG:MASTER:現在位置 = RESULT
			TIME += 1
			TFLAG:SELECTCOM受け渡し = 400
			SELECTCOM = 400
			CALL COM400
			RETURN 0
		ENDIF
		;移動処理
		TIME += 1
		TFLAG:SELECTCOM受け渡し = 400
		SELECTCOM = 400
	CASE "クイック触れ合い"
		CALLFORM 身体に触れる_ABLE_%REPLACE(RESULTS, "サイド領域_", "")%
		RFLAG:サイドボタン受渡し変数 = RESULT
		RFLAG:サイドボタン操作 = 0
		RESULT = 306
		CALL USERCOM
		RETURN RESULT
ENDSELECT

RETURN 0


@サイド描画表示種類_クイック移動(表示文字列, 追加表示文字列)
#DIMS REF 表示文字列
#DIMS REF 追加表示文字列

IF CFLAG:PLAYER:現在マップ種別 == 999
ELSE
	CALL DRAW_MAP(42, サイド描画横スクロール保存, サイド描画縦スクロール保存)
	サイド描画_クイック移動マップ横幅保存 = RESULT / 2
	REPLACE RESULTS, "value='", "value='サイド領域_"
	表示文字列 += @" %RESULTS%"

	IF CFLAG:PLAYER:現在マップ種別 != 999
		追加表示文字列 += "<div rect='5600, 4125, 4225, 100'>"
		追加表示文字列 += "<p align='center'><button value='サイド領域_99'>[エリア移動] </button></p></div>"
	ENDIF
	;マップが横幅よりデカいならスクロールキー表示
	IF サイド描画_クイック移動マップ横幅保存 > 42
		追加表示文字列 += "<div rect='5600, 4125, 4225, 100'>"
		IF サイド描画横スクロール保存 > 0
			追加表示文字列 += "<button value='サイド領域_スクロール_左'>[←]</button>"
		ELSE
			追加表示文字列 += "　　"
		ENDIF
		IF サイド描画縦スクロール保存 > 0
			追加表示文字列 += "<button value='サイド領域_スクロール_上'>[↑]</button>"
		ELSE
			追加表示文字列 += "　　"
		ENDIF
		IF サイド描画縦スクロール保存 + 36 < サイド描画_クイック移動マップ縦幅保存
			追加表示文字列 += "<button value='サイド領域_スクロール_下'>[↓]</button>"
		ELSE
			追加表示文字列 += "　　"
		ENDIF
		IF サイド描画横スクロール保存 + 42 < サイド描画_クイック移動マップ横幅保存
			追加表示文字列 += "<button value='サイド領域_スクロール_右'>[→] </button>"
		ELSE
			追加表示文字列 += "　　"
		ENDIF
		追加表示文字列 += "</div>"
	ENDIF
ENDIF

@サイド描画表示種類_能力表示(表示文字列, 追加表示文字列)
#DIMS REF 表示文字列
#DIMS REF 追加表示文字列

IF TARGET > 0
	表示文字列 += @" ■%NAME:TARGET%<br>"
	CALL PRINT_STATE_BASE(TARGET)
	表示文字列 += @" %RESULTS%<br>"
	CALL PRINT_STATE_TALENT(TARGET)
	表示文字列 += @" %RESULTS%<br>"
	CALL PRINT_STATE_CLOTH(TARGET)
	表示文字列 += @" %RESULTS%<br>"
	CALL PRINT_STATE_弱点コマンド(TARGET)
	表示文字列 += @" %RESULTS%<br>"
	REPLACE RESULTS, "value='", "value='サイド領域_"
ENDIF


@サイド描画表示種類_同室者(表示文字列, 追加表示文字列)
#DIMS REF 表示文字列
#DIMS REF 追加表示文字列
#DIMS ボタン文字列
#DIM L_CNT
#DIM 助手人数
#DIM 助手候補, 1000
#DIM 表示可能人数
#DIM 横幅

VARSET LOCAL
VARSET 助手候補
ボタン文字列 '= ""
;横に何人入るか
表示可能人数 = ((1600 / GETCONFIG("フォントサイズ")) - 55 ) / 8
IF 表示可能人数 > 0 && (GET_TARGETNUM() == 1 || 二人目画像表示用())
	;対象が一人、または複数プレイ時はなるべく大きく短冊を表示
	VARSET LOCALS
	;ターゲット
	助手人数 = 二人目画像表示用(-1)
	IF GET_TARGETNUM() == 1 || 表示可能人数 == 1
		;対象が一人の場合
		LOCAL:1 = 1
	ELSE
		;助手も含めて全員横一列に表示
		LOCAL:1 = 1 + MIN(助手人数, 表示可能人数)
	ENDIF
	横幅 =  LIMIT(800 * 表示可能人数 / LOCAL:1, 800, 2000)
	CALL サイド領域_エロステータス把握(TARGET:LOCAL)
	LOCALS '= RESULTS
	LOCALS:1 '= RESULTS:1
	SIF STRLENS(CALLNAME:TARGET + LOCALS) + 2 > (横幅 * 2 / 100)
		LOCALS '= ""
	LOCALS:2 '= 短冊グラ表示用文字列関数(TARGET, 1, 横幅)
	表示文字列 += @"<button value='{TARGET + 2000}' title='%CALLNAME:TARGET + LOCALS%&#13;&#10;%RESULTS:3%'>%LOCALS:2%</button>　"
	ボタン文字列 += @"<button value='{TARGET + 2000}' title='%CALLNAME:TARGET + LOCALS%'><font face='' %LOCALS:1%>%"[" + CALLNAME:TARGET + "]" + LOCALS, MIN((横幅 * 2 / 100), 40), LEFT%</font></button>"
	L_CNT = 0
	WHILE L_CNT < 助手人数
		助手候補:L_CNT = 二人目画像表示用(1 + L_CNT)
		L_CNT ++
	WEND
	IF 表示可能人数 * 2 < 助手人数
		;2段使っても表示しきれない場合、表示キャラをランダムで抽出する
		FOR L_CNT, 助手人数-1, 1, -1
			SWAP 助手候補:L_CNT, 助手候補:(RAND:L_CNT)
		NEXT
	ENDIF
	IF 表示可能人数 > 1
		;助手を横一列に全員表示可能
		FOR L_CNT, 0, MIN(表示可能人数*2,助手人数)
			IF L_CNT == 表示可能人数 - 1
				;1度だけ折り返す
				表示文字列 += @"%"<br>" * (1 + 横幅 * GETCONFIG("フォントサイズ") / (GETCONFIG("一行の高さ") * 50))%%ボタン文字列%<br>"
				ボタン文字列 '= ""
			ENDIF
			LOCAL = 助手候補:L_CNT
			CALL サイド領域_エロステータス把握(LOCAL)
			LOCALS '= RESULTS
			LOCALS:1 '= RESULTS:1
			SIF STRLENS(CALLNAME:LOCAL + LOCALS) + 2 > (横幅 * 2 / 100)
				LOCALS '= ""
			LOCALS:2 '= 短冊グラ表示用文字列関数(LOCAL, 2 + L_CNT, 横幅)
			表示文字列 += @"<button value='{LOCAL + 2000}' title='%CALLNAME:LOCAL + LOCALS%&#13;&#10;%RESULTS:(3+L_CNT)%'>%LOCALS:2%</button>"
			ボタン文字列 += @"<button value='{LOCAL + 2000}' title='%CALLNAME:LOCAL + LOCALS%'><font face='' %LOCALS:1%>%"[" + CALLNAME:LOCAL + "]" + LOCALS, MIN((横幅 * 2 / 100), 40), LEFT%</font></button>"
		NEXT
	ELSEIF 二人目画像表示用() && 表示可能人数 > 1
		;助手をランダムで表示
		LOCAL = 二人目画像表示用()
		CALL サイド領域_エロステータス把握(LOCAL)
		LOCALS '= RESULTS
		LOCALS:1 '= RESULTS:1
		LOCALS:2 '= 短冊グラ表示用文字列関数(LOCAL, 2, MIN(800 * 表示可能人数 / 2, 2000))
		表示文字列 += @"<button value='{LOCAL + 2000}' title='%CALLNAME:LOCAL + LOCALS%&#13;&#10;%RESULTS:4%'>%LOCALS:2%</button>"
		ボタン文字列 += @"<button value='{LOCAL + 2000}' title='%CALLNAME:LOCAL + LOCALS%'><font face='' %LOCALS:1%>%"[" + CALLNAME:LOCAL + "]" + LOCALS, 16, LEFT%</font></button>"
	ENDIF
	表示文字列 += @"%"<br>" * (1 + 横幅 * GETCONFIG("フォントサイズ") / (GETCONFIG("一行の高さ") * 50))%%ボタン文字列%<br>"
ELSE
	表示文字列 += "<br>"
	FOR LOCAL, 1, CHARANUM
		;表示可能人数が0なら表示終了
		SIF 表示可能人数 < 1
			BREAK
		IF TARGET:LOCAL == TARGET
		ELSEIF TARGET:LOCAL > 0 && LOCAL:2 < 4
			VARSET LOCALS
			CALL サイド領域_エロステータス把握(TARGET:LOCAL)
			LOCALS '= RESULTS
			LOCALS:1 '= RESULTS:1
			LOCALS:2 '= 顔グラ表示用文字列関数(TARGET:LOCAL, 1+LOCAL:1, LOCAL:1 / 表示可能人数, 800)
			表示文字列 += @"<button value='{TARGET:LOCAL + 2000}' title='%CALLNAME:(TARGET:LOCAL) + LOCALS%&#13;&#10;%RESULTS:(3+LOCAL:1)%'>%LOCALS:2%</button>"
			ボタン文字列 += @"<button value='{TARGET:LOCAL + 2000}' title='%CALLNAME:(TARGET:LOCAL) + LOCALS%'><font face='' %LOCALS:1%>%"[" + CALLNAME:(TARGET:LOCAL) + "]", 16, LEFT%</font></button>"
			LOCAL:1 ++
			IF LOCAL:1 / 表示可能人数 > LOCAL:2
				表示文字列 += @"%"<br>" * 8%%ボタン文字列%<br>"
				ボタン文字列 '= ""
				LOCAL:2 ++
			ENDIF
		ELSE
			IF ボタン文字列 != ""
				表示文字列 += @"%"<br>" * 8%%ボタン文字列%<br>"
				ボタン文字列 '= ""
			ENDIF
			BREAK
		ENDIF
	NEXT
ENDIF


@サイド描画表示種類_クイック触れ合い(表示文字列, 追加表示文字列)
#DIMS REF 表示文字列
#DIMS REF 追加表示文字列

; IF CFLAG:PLAYER:うふふ && CFLAG:TARGET:うふふ
; 	サイド描画表示種類 = クイック自慰
; 	CALL サイド描画表示種類_クイック自慰(表示文字列, 追加表示文字列)
; 	RETURN 0
; ENDIF

IF TARGET
	CALL COM_ABLE306
	IF RESULT
		表示文字列 += "配置先整理中のため、単純に並べて表示<br>"

		VARSET RESULTS
		ENUMFUNCBEGINSWITH "身体に触れる_ABLE_"
		FOR LOCAL, 0, RESULT
			CALLFORM %RESULTS:LOCAL%
			IF RESULT
				LOCALS = %REPLACE(RESULTS:LOCAL, "身体に触れる_ABLE_", "")%
				表示文字列 += @"<button value='サイド領域_%LOCALS%'>[%LOCALS, 16, LEFT%]</button><br>"
			ENDIF
		NEXT
		; 表示文字列 += "</nobr></div>"
		; 表示文字列 += "<div rect='5600, 1000, 1000, 3000'><nobr><img src='汎用人型' height='2000' width='500'>"
	ELSE
		表示文字列 += "セクハラ不能状態のためコマンド非表示<br>"
	ENDIF
ENDIF

; @サイド描画表示種類_クイック自慰(表示文字列, 追加表示文字列)
; #DIMS REF 表示文字列
; #DIMS REF 追加表示文字列

; IF !CFLAG:PLAYER:うふふ || !CFLAG:TARGET:うふふ
; 	サイド描画表示種類 = クイック触れ合い
; 	CALL サイド描画表示種類_クイック触れ合い(表示文字列, 追加表示文字列)
; 	RETURN 0
; ENDIF

; IF TARGET
; 	CALL COM_ABLE306
; 	IF RESULT
; 		表示文字列 += "配置先整理中のため、単純に並べて表示<br>"

; 		VARSET RESULTS
; 		ENUMFUNCBEGINSWITH "身体に触れる_ABLE_"
; 		FOR LOCAL, 0, RESULT
; 			CALLFORM %RESULTS:LOCAL%
; 			IF RESULT
; 				LOCALS = %REPLACE(RESULTS:LOCAL, "身体に触れる_ABLE_", "")%
; 				表示文字列 += @"<button value='サイド領域_%LOCALS%'>[%LOCALS, 16, LEFT%]</button><br>"
; 			ENDIF
; 		NEXT
; 		; 表示文字列 += "</nobr></div>"
; 		; 表示文字列 += "<div rect='5600, 1000, 1000, 3000'><nobr><img src='汎用人型' height='2000' width='500'>"
; 	ELSE
; 		表示文字列 += "セクハラ不能状態のためコマンド非表示<br>"
; 	ENDIF
; ENDIF
