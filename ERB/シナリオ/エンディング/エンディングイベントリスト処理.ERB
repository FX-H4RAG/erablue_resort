
@エンディングイベントリスト
#DIMS 表示用リスト, 100
#DIMS NEWフラグ用リスト, 100
#DIM 表示候補数
#DIM 表示キー
#DIM DYNAMIC 初回起動
VARSET LOCAL
VARSET LOCALS
VARSET NEWフラグ用リスト

IF 初回起動 == 0
	表示キー = -1
	初回起動 = 1
ENDIF

表示候補数 = DT_ROW_LENGTH("エンディング用変数配列")
SPLIT TSTR:NEWイベントフラグ, "_", NEWフラグ用リスト

DRAWLINE
IF FLAG:クリア済み視聴フラグ
	PRINTL 再視聴するクリア済みエンディングイベントを選んでください。
	PRINTL ※基本的に再視聴では報酬などが取得出来ません。イベント中に「◯◯を入手した」と出ても取得されません。
	PRINTL ※再視聴ではエンディング処理はされません。強くてニューゲームの条件も満たしません。
	PRINTBUTTON "[9998] イベントリストへ戻る", 9998
ELSE
	PRINTL 開始するイベントを選んでください。
	PRINTBUTTON "[9998] クリア済みイベントを再視聴する", 9998
ENDIF
PRINTL
DRAWLINE
IF FLAG:クリア済み視聴フラグ
	FOR LOCAL, 0, 表示候補数
		;発生してないものは出さない
		SIF DT_CELL_GET("エンディング用変数配列", LOCAL, "イベント発生フラグ") == 0
			CONTINUE
		IF DT_CELL_GET("エンディング用変数配列", LOCAL, "イベント完了フラグ") == 1
			IF 表示キー == LOCAL
				LOCALS:1 += @"<font color='#%カラーパレット_HTML("黄")%'><button value='{LOCAL}'>[{LOCAL}]%DT_CELL_GETS("エンディング用変数配列", LOCAL, "イベント名")%</button></font><br>"
			ELSE
				LOCALS:1 += @"<button value='{LOCAL}'>[{LOCAL}]%DT_CELL_GETS("エンディング用変数配列", LOCAL, "イベント名")%</button><br>"
			ENDIF
		ENDIF
	NEXT
ELSE
	IF FLAG:エンディング後フラグ
		LOCALS:1 += "エンディング到達済"
	ELSE
		FOR LOCAL, 0, 表示候補数
			SIF DT_CELL_GET("エンディング用変数配列", LOCAL, "イベント発生フラグ") == 0
				CONTINUE
			LOCALS:10 = 
			LOCALS:11 = 
			IF DT_CELL_GET("エンディング用変数配列", LOCAL, "イベント完了フラグ") == 1
				LOCALS:11 += "　視聴済"
			ELSEIF MATCH(NEWフラグ用リスト, @"ENDHING%TOSTR(LOCAL)%")
				LOCALS:10 += @"<font color='#%カラーパレット_HTML("黄")%'>"
				LOCALS:11 += @"</font><font color='#FF5555'> NEW!!</font>"
			ENDIF
			IF 表示キー == LOCAL
				LOCALS:1 += @"<font color='#%カラーパレット_HTML("黄")%'><button value='{LOCAL}'>[{LOCAL}]%DT_CELL_GETS("エンディング用変数配列", LOCAL, "イベント名")%</button>%LOCALS:11%</font><br>"
			ELSE
				LOCALS:1 += @"%LOCALS:10%<button value='{LOCAL}'>[{LOCAL}]%DT_CELL_GETS("エンディング用変数配列", LOCAL, "イベント名")%</button>%LOCALS:11%<br>"
			ENDIF
		NEXT
	ENDIF
ENDIF

LOCALS = <div rect='81,31,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='156,156,1837,2712'><nobr>%LOCALS:1%</nobr></div>
LOCALS += "<div width='1050' height='125' xpos='337' ypos='0' color='#101010'>エンディングイベント</div>"
LOCALS += "<div rect='2081,31,3937,2812' border='31' bcolor='#C0C0C0' padding='50,25,50'>"
IF FLAG:エンディング後フラグ && !FLAG:クリア済み視聴フラグ
	LOCALS += "この周回では既にエンディングに到達しました。<br>"
	LOCALS += "他の結末を望む場合、「強くてNEWGAME」から周回してください。<br>"
ELSEIF 表示キー == -1
	LOCALS += "左のリストから任意のエンディングを選択してください。<br>"
	LOCALS += "この欄にはエンディングの詳細と、エンディングを視聴するかどうかの確認ボタンが表示されます。<br>"
ELSE
	CALLFORM イベント関数_エンディングイベント_%DT_CELL_GETS("エンディング用変数配列", 表示キー, "イベント名")%("設定取得", 表示キー)
	LOCALS += TSTR:ツールチップ受渡
	LOCALS += "<br>"
	IF FLAG:クリア済み視聴フラグ
		LOCALS += "<button value='10000'>[10000]このエンディングを再視聴する</button><br>"
	ELSE
		RESULT = 0
		CALLFORM イベント関数_エンディングイベント_%DT_CELL_GETS("エンディング用変数配列", 表示キー, "イベント名")%("実行条件", 表示キー)
		IF RESULT
			LOCALS += "<button value='10000'>[10000]このエンディングに決定する</button><br>"
		ELSE
			LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[10000]このエンディングに決定する　　※実行条件未達</font><br>"
		ENDIF
	ENDIF
ENDIF
LOCALS += "</div>"

HTML_PRINT LOCALS

FOR LOCAL, 0, 25
	PRINTL
NEXT
; PRINTBUTTON "[9997]システムの説明", 9997
; PRINTL
PRINTBUTTON "[9999]戻る", 9999
PRINTL

IF チュートリアルフラグ:エンディングリスト == 0
	LOCAL = 9997
ELSE
	BINPUT
	LOCAL = RESULT
ENDIF
SELECTCASE LOCAL
	CASE IS < 表示候補数
		IF MATCH(NEWフラグ用リスト, @"ENDHING%TOSTR(LOCAL)%")
			REPLACE TSTR:NEWイベントフラグ, @"ENDHING%TOSTR(LOCAL)%_", ""
			TSTR:NEWイベントフラグ = %RESULTS%
		ENDIF
		表示キー = LOCAL
		RESTART
	CASE 9997
		CALL エンディングリストチュートリアル()
		RESTART
	CASE 9998
		INVERTBIT FLAG:クリア済み視聴フラグ, 0
		表示キー = -1
		RESTART
	CASE 9999
		FLAG:クリア済み視聴フラグ = 0
		表示キー = -1
		RETURN 0
	CASE 10000
		DRAWLINE
		IF !FLAG:クリア済み視聴フラグ
			PRINTFORML 『%DT_CELL_GETS("エンディング用変数配列", 表示キー, "イベント名")%』のエンディングを迎えます。
			PRINTL よろしいですか？
			PRINTBUTTONLC "[0] はい", 0
			PRINTBUTTONLC "[1] いいえ", 1
			PRINTL
			BINPUT
			SIF RESULT != 0
				RESTART
		ENDIF
		CALLFORM イベント関数_エンディングイベント_%DT_CELL_GETS("エンディング用変数配列", 表示キー, "イベント名")%("実処理", 表示キー)
		IF !FLAG:クリア済み視聴フラグ
			FLAG:エンディング後フラグ = 1
			CALLFORM イベント関数_エンディングイベント_%DT_CELL_GETS("エンディング用変数配列", 表示キー, "イベント名")%("設定取得", 表示キー)
			STR:エンド後_エンディング番号 '= TSTR:コマンド名受渡
			DT_CELL_SET "エンディング用変数配列", 表示キー, "イベント完了フラグ", 1
		ENDIF
ENDSELECT

@エンディングイベント完了フラグ汎用処理(ARGS)
#DIM 行番号

SIF FLAG:クリア済み視聴フラグ
	RETURN 0

FOR LOCAL, 0, DT_ROW_LENGTH("エンディング用変数配列")
	LOCALS = %DT_CELL_GETS("エンディング用変数配列", LOCAL, "イベント名")%
	IF LOCALS == ARGS
		行番号 = LOCAL
		BREAK
	ENDIF
NEXT
DT_CELL_SET "エンディング用変数配列", 行番号, "イベント完了フラグ", 1


@エンディングイベント完了フラグ確認(ARGS)
#FUNCTION
#DIM 行番号
VARSET LOCAL

FOR LOCAL, 0, DT_ROW_LENGTH("エンディング用変数配列")
	LOCALS = %DT_CELL_GETS("エンディング用変数配列", LOCAL, "イベント名")%
	IF LOCALS == ARGS
		行番号 = LOCAL
		LOCAL:1 = 1
		BREAK
	ENDIF
NEXT
IF LOCAL:1
	RETURNF DT_CELL_GET("エンディング用変数配列", 行番号, "イベント完了フラグ")
ELSE
	RETURNF -1
ENDIF



