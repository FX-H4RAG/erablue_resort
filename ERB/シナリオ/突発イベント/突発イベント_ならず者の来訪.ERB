
@イベント発生条件_突発イベント_ならず者の来訪
;開放条件を満たしたら1を返すこと

イベント消滅日数渡し = 3
イベント消滅確率渡し = 30
イベント再発生日数渡し = 20
イベント再発生確率渡し = 10

;開放条件を満たしたら1を返すこと
IF 空艇港初来訪イベントフラグ
	RETURN 1
ENDIF
RETURN 0

@イベント関数_突発イベント_ならず者の来訪(ARG)
LOCAL = DT_CELL_GET("各イベント用変数配列", ARG, "イベント再発生CT")
IF LOCAL == 0
	;初回
	DRAWLINE
	PRINTL ［ならず者の来訪］
	DRAWLINE
	PRINTW
	PRINTFORMW どうやら山中の小さな開けた場所にならず者たちの船が降りてきたらしい。
	PRINTFORMW 放置すれば泥棒や資源の乱獲をして逃げるだろう。
	PRINTFORMW そうなればリゾートの人気が落ちてしまうのは火を見るより明らかなのは確かだ。
	PRINTFORMW 被害が出る前に倒してしまおう。
	PRINTL
	PRINTL 山中に降りてきたならず者たちと戦います。準備はよろしいですか？（２連戦あります！）
	PRINTBUTTON "[0]はい", 0
	PRINTL
	PRINTBUTTON "[1]いいえ", 1
	PRINTL
	BINPUT
	SELECTCASE RESULT
		CASE 0
			PRINTFORMW %CALLNAME:PLAYER%たちは山中に降りてきたならず者たちを襲撃した！

			CALL ならず者の来訪イベント_戦闘部分
		CASE 1
			PRINTW いや、油断は良くない。ここはしっかりと準備をしてからにしよう。
	ENDSELECT
ELSE
	;二回目以降
	DRAWLINE
	PRINTL ［ならず者の来訪］
	DRAWLINE
	PRINTW
	PRINTFORMW またならず者たちの船が降りてきたらしい。
	PRINTFORMW 被害が出る前にさっさと倒してしまおう。
	PRINTL
	PRINTL 山中に降りてきたならず者たちと戦います。準備はよろしいですか？（２連戦あります！）
	PRINTBUTTON "[0]はい", 0
	PRINTL
	PRINTBUTTON "[1]いいえ", 1
	PRINTL
	BINPUT
	SELECTCASE RESULT
		CASE 0
			PRINTFORMW %CALLNAME:PLAYER%たちは山中に降りてきたならず者たちを襲撃した！

			CALL ならず者の来訪イベント_戦闘部分
		CASE 1
			PRINTW いや、油断は良くない。ここはしっかりと準備をしてからにしよう。
	ENDSELECT
ENDIF


@ならず者の来訪イベント_戦闘部分
CALL イベント戦闘初期処理()
敵BATTLE_STATE_STR:0:エネミー名 = Lv10_ならず者の構成員
敵BATTLE_STATE_STR:2:エネミー名 = Lv10_ならず者の構成員
敵BATTLE_STATE_STR:4:エネミー名 = Lv10_ならず者の構成員
CALL イベント中戦闘用処理()
IF RESULT == 0
	敵BATTLE_STATE_STR:0:エネミー名 = Lv10_ならず者の構成員
	敵BATTLE_STATE_STR:1:エネミー名 = Lv10_ならず者の構成員
	敵BATTLE_STATE_STR:2:エネミー名 = Lv15_ならず者の頭領
	敵BATTLE_STATE_STR:3:エネミー名 = Lv10_ならず者の構成員
	敵BATTLE_STATE_STR:4:エネミー名 = Lv10_ならず者の構成員
	CALL イベント中戦闘用処理()
	IF RESULT == 0
		;勝利
		PRINTFORMW %CALLNAME:PLAYER%たちは無事にならず者たちを掃討した。
		IF RAND:100 < 5
			;5%だけ起こる処理
			PRINTW 残された船の残骸からそこそこの品があったが、ならず者には価値が分からなかったようで無造作に置かれていた。
			PRINTW ありがたく有効活用させてもらうとしよう。
			STRDATA LOCALS
				DATA 古ぼけた指輪・銀
				DATA 白銀の古紋
				DATA 古ぼけた耳飾り・銀
				DATA 宝晶原石
			ENDDATA
			SIF !FLAG:クリア済み視聴フラグ
				CALL 素材入手処理(LOCALS, 1)
			PRINTFORMW 「%LOCALS%」を手に入れた。
		ELSEIF !乗機所持チェック("試作大型走艇") && !RAND:4
			;設計図未所持時、25％で発見
			PRINTW 残された船の残骸に珍しい設計の走艇の図面があったが、ならず者には価値が分からなかったようで無造作に置かれていた。
			PRINTW ありがたく有効活用させてもらうとしよう。
			PRINTW 「試作大型走艇の設計図」を手に入れた。
			SIF !FLAG:クリア済み視聴フラグ
				CALL 乗機取得処理_設計図("試作大型走艇")
		ELSE
			SELECTCASE RAND(10)
				CASE 0
					LOCAL = RAND(2500, 7501)
					SIF !FLAG:クリア済み視聴フラグ
						MONEY += LOCAL
					PRINTFORMW 残された船の残骸から{LOCAL}ルピが出てきたので押収した。
				CASE 1
					PRINTW 残された船の残骸はもぬけの殻で、ならず者の頭領が残したと思しき走り書きのメモだけが残されていた。
					IF FLAG:クリア済み視聴フラグ
						PRINTW どうやら狙っていたお宝の情報のようだが、字が非常に汚く、読み取ることができなかった……
					ELSE
						PRINTW どうやら狙っていたお宝の情報のようだが、字が非常に汚く、読み取れるのは一部分だけだ……
						SELECTCASE RAND(4)
							CASE 0
								PRINTL 「ワイバーンは体内で────を結晶化────
								PRINTW 　希少な────────ダストとして─────価値で取引───」
							CASE 1
								PRINTL 「デンキンナギが生み出した──────珪砂に─────
								PRINTW 　呑み込んだ───運良く──────、────高値で─────」
							CASE 2
								PRINTL 「プロミネンスリアクター─────炎────高める────
								PRINTW 　鋼鉄の巨人─────星晶─────」
							CASE 3
								PRINTL 「転がる獣は───攻撃で自らの────けてしまう
								PRINTW 　一度も──────狩る───────、───高値がつく」
						ENDSELECT
					ENDIF
				CASE 2
					LOCALS = %ランダム素材名("ダンジョン素材", 0)%
					SIF !FLAG:クリア済み視聴フラグ
						CALL 素材入手処理(LOCALS, 1)
					PRINTFORMW 残された船の残骸から「%LOCALS%」があったので押収した。
				CASE 3
					LOCALS = %ランダム素材名("食物素材", 1)%
					SIF !FLAG:クリア済み視聴フラグ
						CALL 素材入手処理(LOCALS, 1)
					PRINTFORMW 残された船の残骸から手つかずの 「%LOCALS%」が残されていたので押収した。
				CASE 4
					LOCAL = RAND(1000, 5001)
					SIF !FLAG:クリア済み視聴フラグ
						MONEY += LOCAL
					PRINTFORMW 残された船はもぬけの殻だったが、その残骸から{LOCAL}ルピになるパーツが出てきたので売却した。
				CASEELSE
					LOCAL = RAND(500, 2501)
					SIF !FLAG:クリア済み視聴フラグ
						MONEY += LOCAL
					PRINTFORMW 残された船の残骸から{LOCAL}ルピが出てきたので押収した。
			ENDSELECT
		ENDIF
		CALL イベント完了フラグ汎用処理("ならず者の来訪")
	ELSEIF RESULT == 1
		;全滅
		PRINTW 不覚を取ってしまった。ここは一旦撤退しよう。
	ELSEIF RESULT == -1
		;逃走
		PRINTW 思ったよりも手強いようだ。ここは一旦撤退しよう。
	ENDIF
ELSEIF RESULT == 1
	;全滅
	PRINTW 不覚を取ってしまった。ここは一旦撤退しよう。
ELSEIF RESULT == -1
	;逃走
	PRINTW 思ったよりも手強いようだ。ここは一旦撤退しよう。
ENDIF
CALL ダンジョン終了時処理
