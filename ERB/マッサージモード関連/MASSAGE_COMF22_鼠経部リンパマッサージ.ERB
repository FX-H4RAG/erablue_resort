;-------------------------------------------------
;鼠経部リンパマッサージ
;-------------------------------------------------
@MASSAGE_COMNAME22
#FUNCTIONS
TSTR:コマンド名受渡 = 鼠経部リンパマッサージ


;===================================================
;コマンドタイプ
;===================================================
@MASSAGE_COMTYPE_22
#FUNCTION
TFLAG:コマンドタイプ受渡 = MASSAGE_COMTYPE("エロマッサージ")

@MASSAGE_COM_TOOLTIP_22
LOCALS = <br>■鼠経部リンパマッサージ<br>
LOCALS += "鼠径部からＶラインにかけてを指圧・掌圧で揉みほぐし、リンパ節を刺激する。"
LOCALS += "かなり際どい場所に触れるため、事前に対象の警戒を解かなければいけない。"
LOCALS += "<br>COMタイプ：エロマッサージ<br>取得ソース：快Ｃ・快Ｖ・露出・接触・歓楽"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@MASSAGE_COM22
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------


;鼠径部からビキニラインにかけて素肌の上からの刺激なので快Ｖはほんの気持ち程度
SIF TALENT:TARGET:Ｖ感度 >= -1
	CALL SOURCE_CALC_快Ｖ(TARGET, PLAYER, 30)
	
;対象者がオトコ、半人半妖、ふたなりの場合はＣ感。外性器なのでＶより刺激が強い
SIF GETBIT(TALENT:TARGET:性別, 1) && TALENT:TARGET:Ｃ感度 != -2
	CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER, 50)

CALL SOURCE_CALC_露出(TARGET, PLAYER, 75)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 75)
CALL SOURCE_CALC_歓楽(TARGET, PLAYER, 150)


IF BASE:TARGET:理性 <= 1
;理性がおしまいになってしまった場合、快楽に流されて警戒が下がっていく。
	BASE:TARGET:性欲 += 50
	BASE:TARGET:警戒 -= 40
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
ELSEIF 知識素質:TARGET:性知識 <= -1
;性知識がない場合、そもそも警戒することができない。そういうもんかって受け入れるため理性は下がり続けるし、快感を伴うから警戒も下がり続ける。
;……シヴァお前……
	BASE:TARGET:理性 -= 50
	BASE:TARGET:性欲 += 50
	BASE:TARGET:警戒 -= 50
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
ELSE
;局部に直接触れるわけではないので、警戒は少し上がるが理性は削れる。性欲も煽られる。なまごろし～！
	BASE:TARGET:理性 -= 50
	BASE:TARGET:性欲 += 50
	BASE:TARGET:警戒 += 20
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 50)
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@MASSAGE_COM_ABLE22
SIF SAVESTR:ゲームフェイズ管理 != "マッサージモード"
	RETURN 0

;うつ伏せだとダメ
SIF TCVAR:TARGET:マッサージ_うつ伏せフラグ == 1
	RETURN 0

;実行するだけならそこそこ緩い。警戒はされるけど。
SIF (BASE:TARGET:理性 + BASE:TARGET:警戒) > 1500
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MASSAGE_MESSAGE_COM22
PRINTFORM %CALLNAME:PLAYER%はマッサージオイルをつけた手のひらで、%CALLNAME:TARGET%の
	IF TALENT:TARGET:体型 >= 1
	;肉感的以上
		PRINTFORM むっちりとした肉付きの
	ELSEIF TALENT:TARGET:体型 <= -2
	;細身以下
		PRINTFORM 肉付きの薄い華奢な
	ENDIF
PRINTFORML %TEXTR("鼠径部/ビキニライン")%を%TEXTR("丁寧に/優しく/ゆっくりと")%揉みほぐした。

