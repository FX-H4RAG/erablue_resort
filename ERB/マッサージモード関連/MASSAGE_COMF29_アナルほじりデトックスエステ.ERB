;-------------------------------------------------
;アナルほじりデトックスエステ
;-------------------------------------------------
@MASSAGE_COMNAME29
#FUNCTIONS
TSTR:コマンド名受渡 = アナルほじりデトックスエステ


;===================================================
;コマンドタイプ
;===================================================
@MASSAGE_COMTYPE_29
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("エロマッサージ")

@MASSAGE_COM_TOOLTIP_29
LOCALS = <br>■アナルほじりデトックスエステ<br>
LOCALS += "お尻の穴に指を入れ、直腸から刺激を与えるマッサージ。"
LOCALS += "直腸刺激はデトックス効果があり、ハリのある美尻になる_-_ということにしている_。"
LOCALS += "<br>COMタイプ：エロマッサージ<br>取得ソース：快Ａ・露出・接触・歓楽・液体"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@MASSAGE_COM29
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

;基盤ができたあとで理性を削る処理を入れる


CALL SOURCE_CALC_快Ａ(TARGET, PLAYER, 170)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 150)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 150)

;ケツ穴スボスボなのでＡ感1以下は苦痛。
SIF ABL:TARGET:Ａ感覚 <= 1
	CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 40)

;Ａ感0はさらに苦痛。
SIF ABL:TARGET:Ａ感覚 == 0
	CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 30)

;ローションは別のコマンドになるっぽいので気持ち程度
CALL SOURCE_CALC_液体(TARGET, PLAYER, 1000)

;下半身下着がある場合は脱がす。
IF TEQUIP:下半身下着あり == 1
	TEQUIP:下半身下着あり = 0
	RFLAG:コマンド結果受渡し変数 = 1
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@MASSAGE_COM_ABLE29
SIF SAVESTR:ゲームフェイズ管理 != "マッサージモード"
	RETURN 0

;Aがない場合駄目
SIF TALENT:TARGET:Ａ感度 == -2
	RETURN 0

;仰向けだとダメ
SIF TCVAR:TARGET:マッサージ_うつ伏せフラグ == 0
	RETURN 0


RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MASSAGE_MESSAGE_COM29

PRINTFORM %CALLNAME:PLAYER%は、

	IF RFLAG:コマンド結果受渡し変数 == 1
		PRINTFORM %CALLNAME:TARGET%の
			;Ｖがあり欲情している場合
			SIF TALENT:TARGET:Ｖ感度 >= -1 && PALAM:欲情  >= PALAMLV:3
				PRINTFORM ぐっしょりと愛液を吸った
		PRINTFORM 施術着を脱がし、
	ENDIF
PRINTFORM オイルでたっぷり濡らした指を%CALLNAME:TARGET%の
	IF ABL:TARGET:Ａ感覚 >= 3
		PRINTFORM %TEXTR("こなれた/よくほぐれた")%%TEXTR("アナル/アヌス/ケツ穴/ケツまんこ")%
	ELSE
		PRINTFORM %TEXTR("締め付けの強い/キツキツの")%%TEXTR("アナル/尻の穴/肛門")%
	ENDIF
	
	PRINTFORM に挿し入れて、
	IF TALENT:TARGET:性別 == 2 && RAND:3 == 0
	;男かつ33％。前立腺は男にしか存在しないので、これはこの上なく男らしい行為のはずである。
		PRINTFORML 直腸の内壁から前立腺を%TEXTR("捏ねるようにマッサージした/指圧でぐりぐりと刺激した")%。
	ELSE
		PRINTFORML %TEXTR("内側から揉みほぐすようにねっとりとかき混ぜた/ぐちゃぐちゃと水音を立てながらかき回した/直腸の内壁を指の腹で押し込むように刺激した")%
	ENDIF
