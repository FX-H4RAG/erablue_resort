;-------------------------------------------------
;勃起おしゃぶりエステ
;-------------------------------------------------
@MASSAGE_COMNAME31
#FUNCTIONS
TSTR:コマンド名受渡 = 勃起おしゃぶりエステ


;===================================================
;コマンドタイプ
;===================================================
@MASSAGE_COMTYPE_31
#FUNCTION
TFLAG:コマンドタイプ受渡 = MASSAGE_COMTYPE("エロマッサージ")

@MASSAGE_COM_TOOLTIP_31
LOCALS = <br>■勃起おしゃぶりエステ<br>
LOCALS += "フェラモードには突入しない単発コマンド。"
LOCALS += "マッサージ中にカチカチに『凝って』しまったペニスを、おくちでマッサージしてあげる。"
LOCALS += "<br>COMタイプ：エロマッサージ<br>取得ソース：快Ｃ・露出・接触・奉仕(PLAYER)・不潔(PLAYER)"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@MASSAGE_COM31
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------

;基盤ができたあとで理性を削る処理を入れる

;フェラからだいたいそのまま持ってきた
CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER, 200)
CALL SOURCE_CALC_露出(TARGET, PLAYER, 30)
CALL SOURCE_CALC_接触(TARGET, PLAYER, 120)

;舌使いによって、快感のソース増加
IF TALENT:PLAYER:舌使い
	CALL SOURCE_CALC_快Ｃ(TARGET, PLAYER, 150)
ENDIF

;猫舌では苦痛ちょいある
SIF TALENT:PLAYER:猫舌
	CALL SOURCE_CALC_苦痛(TARGET, PLAYER, 20)

CALL SOURCE_CALC_奉仕(PLAYER, TARGET, 300)
CALL SOURCE_CALC_不潔(PLAYER, TARGET, 250)


;下半身下着がある場合は脱がす。
IF TEQUIP:下半身下着あり == 1
	TEQUIP:下半身下着あり = 0
	RFLAG:コマンド結果受渡し変数 = 1
ENDIF
IF BASE:TARGET:理性 <= 1
;理性がおしまいになってしまった場合、快楽に流されて警戒が下がっていく。
	BASE:TARGET:性欲 += 300
	BASE:TARGET:警戒 -= 80
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 150)
ELSEIF 知識素質:TARGET:性知識 <= -1
;性知識がない場合でも「流石にこれはダメだろ！」と思う。が、時既に遅し。
;ようやく100上げることができた警戒で理性が50上がるが、気持ちよすぎてそこから理性が500下がるイメージでこの数値。シヴァ……
	BASE:TARGET:理性 -= 450
	BASE:TARGET:性欲 += 400
	BASE:TARGET:警戒 += 100
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 150)
ELSE
;実行に際し十分な理性・警戒削りがなされている場合。
;警戒300で理性が150回復するも、煽られる性欲と与えられる刺激で理性が300削られて理性変動は-150。
;……というのが平常時における挙動だが、ちんぽをしゃぶられる快感に警戒心が塗りつぶされてしまう。
;フェラを許しちゃった時点で『終わり』。ここまで来ちゃったらそらそうよ。
	BASE:TARGET:理性 -= 240
	BASE:TARGET:性欲 += 300
	BASE:TARGET:警戒 += 10
	CALL SOURCE_CALC_好感度要素_性愛度UP(TARGET, PLAYER, 200)
ENDIF

;-------------------------------------------------
;経験の処理
;-------------------------------------------------

RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------

@MASSAGE_COM_ABLE31
SIF SAVESTR:ゲームフェイズ管理 != "マッサージモード"
	RETURN 0

;相手がオトコか半人半妖かふたなりじゃないとダメ
SIF !(TALENT:TARGET:性別 & 2)
	RETURN 0
;相手にちんこがない場合駄目
SIF TALENT:TARGET:Ｃ感度 == -2
	RETURN 0

;相手のちんぽが『凝って』ないとダメ
SIF BASE:TARGET:勃起 < 1000
	RETURN 0

;うつ伏せだとダメ
SIF TCVAR:TARGET:マッサージ_うつ伏せフラグ == 1
	RETURN 0

;理性警戒の合計が400以下じゃないとダメ
;実行できる時点で、十分な理性・警戒削りがなされている状態。
SIF (BASE:TARGET:理性 + BASE:TARGET:警戒) > 400
	RETURN 0

RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MASSAGE_MESSAGE_COM31

PRINTFORM %CALLNAME:PLAYER%は、

	IF RFLAG:コマンド結果受渡し変数 == 1
		PRINTFORM %CALLNAME:TARGET%の
			;Ｖがあり欲情している場合
			SIF TALENT:TARGET:Ｖ感度 >= -1 && PALAM:欲情  >= PALAMLV:3
			 	PRINTFORM ぐっしょりと愛液を吸った
		PRINTFORM 施術着を脱がし、
	ENDIF
	
PRINTFORM %CALLNAME:TARGET%の

	;巨根以上
	IF TALENT:TARGET:男性器サイズ >= 1
			PRINTFORM バキバキに勃起した%TEXTR("デカちんぽ/巨根")%
		ELSEIF TALENT:TARGET:男性器サイズ <= -1
		;短小以下
			PRINTFORM カチカチに硬くなったひとくちサイズの%TEXTR("ちんちん/おちんちん/ペニス")%
		ELSE
		;普通サイズ
			PRINTFORM ガチガチに勃起した%TEXTR("肉棒/ちんぽ")%
		ENDIF
		PRINTFORML を、%TEXTR("唾液たっぷりのおくち/舌で唾液をたっぷり塗りこん")%で『エステ』してあげた。

