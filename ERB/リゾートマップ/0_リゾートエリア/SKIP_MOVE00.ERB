
;-----------------------------------------------------------
;遠距離移動処理
;-----------------------------------------------------------

@SKIP_MOVE_0(ARG, ARG:1)
;ARG　目的地
;ARG:1　移動キャラID

#DIM スタート地点
#DIM スタートマップ
#DIM 目的マップ
#DIM 移動キャラ

;現在位置取得
移動キャラ = MASTER
スタート地点 = CFLAG:MASTER:現在位置
IF ARG:1
	移動キャラ = ARG:1
	スタート地点 = CFLAG:移動キャラ:現在位置
ENDIF

$LOOP
;マップ間を移動する際の処理
;（下のパターン分けでやると爆発的に量が増えていくため追加）
;マップ種別取得
スタートマップ = 派生マップ番号取得(0, スタート地点)
目的マップ = 派生マップ番号取得(0, ARG)
IF スタートマップ != 目的マップ
	;スタート種別ごとに場合分け
	SELECTCASE スタートマップ
		CASE 0
			SELECTCASE 目的マップ
				CASE 3
					ARG = 3
				CASE 5
					ARG = 5
				CASE 7
					;覗き場の場合は入れる方から入る
					IF GETBIT(TALENT:移動キャラ:性別, 0) == 1
						;女部分があれば女子
						ARG =  8
					ELSE
						ARG =  7
					ENDIF
				CASE 8
					;覗き場の場合は入れる方から入る
					IF GETBIT(TALENT:移動キャラ:性別, 0) == 0
						;女部分がないなら男子
						ARG =  7
					ELSE
						ARG =  8
					ENDIF
				CASE 15
					ARG = 15
			ENDSELECT
		CASE 3
			ARG = 2
		CASE 5
			ARG = 2
		CASE 7
			SIF 目的マップ != 8
				ARG = 6
		CASE 8
			SIF 目的マップ != 7
				ARG = 6
		CASE 15
			IF (TALENT:移動キャラ:性別 & 1 == 1)
				;女部分があれば女子
				ARG = 14
			ELSE
				ARG = 13
			ENDIF
	ENDSELECT
ENDIF
;暫定目的地に直接移動できる場合
IF CAN_MOVE(CFLAG:移動キャラ:現在マップ種別, スタート地点, ARG) & 1
	;そこに移動
	RETURN ARG
ENDIF

;袋小路から行ける場所は固定なので、そこを目指す
SELECTCASE スタート地点
	CASE 1
		RETURN 2
	CASE 3
		RETURN 2
	CASE 4
		RETURN 2
	CASE 5
		RETURN 2
	CASE 10
		RETURN 9
	CASE 12
		RETURN 11
	CASE 20
		RETURN 6
	CASE 302 TO 308
		RETURN 302
	CASE 504 TO 506
		RETURN 503
	CASE 711
		RETURN 702
	CASE 811
		RETURN 802
	CASE 1503 TO 1507
		IF TALENT:移動キャラ:性別 == 2
			RETURN 1513
		ELSE
			RETURN 1514
		ENDIF
ENDSELECT


;直接移動できない場合、1つ近い位置を暫定目的地に設定し直してループ
;地点追加時要フォロー（全ケース修正すること）
SELECTCASE ARG
CASE 1
;玄関
	;玄関へはロビー経由必須
	RETURN 2
CASE 2
;ロビー
	;7と8（脱衣所）からは廊下を目指す
	IF (スタート地点 == 7) || (スタート地点 == 8)
		RETURN 6
	;9と11（男湯女湯）からはそれぞれ脱衣所を経由する
	ELSEIF (スタート地点 == 9)
		RETURN 7
	ELSEIF (スタート地点 == 11)
		RETURN 8
	;15（プール）からは入れる方の更衣室を経由する
	ELSEIF (スタート地点 == 15)
		IF GETBIT(TALENT:移動キャラ:性別, 0) == 1
			;女部分があれば女子
			RETURN 14
		ELSE
			RETURN 13
		ENDIF
	ENDIF
CASE 3
;庭
	;庭へはロビー経由必須
	RETURN 2
CASE 4
;食堂
	;食堂へはロビー経由必須
	RETURN 2
CASE 5
;遊戯場
	;遊戯場へはロビー経由必須
	RETURN 2
CASE 6
;廊下
	;1.3.4.5.13.14からは2を目指す
	IF GROUPMATCH(スタート地点,1,3,4,5,13,14)
		RETURN 2
	;9と11（男湯女湯）からはそれぞれ脱衣所を経由する
	ELSEIF (スタート地点 == 9)
		RETURN 7
	ELSEIF (スタート地点 == 11)
		RETURN 8
	ELSEIF スタート地点 >= 703 && スタート地点 <= 711
		RETURN 702
	ELSEIF スタート地点 >= 803 && スタート地点 <= 811
		RETURN 802
	;15（プール）からは入れる方の更衣室を経由する
	ELSEIF (スタート地点 == 15)
		IF GETBIT(TALENT:移動キャラ:性別, 0) == 1
			;女部分があれば女子
			RETURN 14
		ELSE
			RETURN 13
		ENDIF
	ENDIF
CASE 7
;男子脱衣所
	;内部からは袋小路処理や直接移動処理でOKなので外から目指す場合
	;男子脱衣所へは廊下経由必須
	RETURN 6
CASE 8
;女子脱衣所
	;内部からは袋小路処理や直接移動処理でOKなので外から目指す場合
	;女子脱衣所へは廊下経由必須
	RETURN 6
CASE 9
;男湯
	;男湯へは脱衣所経由必須
	RETURN 7
CASE 10
;男露天
	;男露天へは男湯経由必須
	RETURN 9
CASE 11
;女湯
	;女湯へは脱衣所経由必須
	RETURN 8
CASE 12
;女露天
	;女露天へは女湯経由必須
	RETURN 11
CASE 13
;男更衣室
	;更衣室へはロビー経由必須
	RETURN 2
CASE 14
;女更衣室
	;更衣室へはロビー経由必須
	RETURN 2
CASE 15
;プール
	;入れる方の更衣室を目指す
	IF GETBIT(TALENT:移動キャラ:性別, 0) == 1
		;女部分があれば女子
		RETURN 14
	ELSE
		RETURN 13
	ENDIF
CASE 20
;自室
	;自室へはロビー経由必須
	RETURN 6

;バーエリアは現状全エリアが相互通行可能ではないため、CASE分けを一つだけ追加
CASE 504 TO 506
;ボドゲ・卓球
	;バーエリアを目指す
	RETURN 503

;本来入り口への誘導は必要ないが、男湯女湯だけは必要なので残す
CASE 702
	;入れる方なら6を目指す、入れないなら隠し通路を目指す
	IF GETBIT(TALENT:移動キャラ:性別, 0) == 1
		;女部分があれば女子
		RETURN 710
	ELSE
		RETURN 6
	ENDIF
CASE 802
	;入れる方なら6を目指す、入れないなら隠し通路を目指す
	IF GETBIT(TALENT:移動キャラ:性別, 0) == 0
		;女部分がないなら男子
		RETURN 810
	ELSE
		RETURN 6
	ENDIF
CASE 710
	;入れる方の隠し通路を目指す
	IF GETBIT(TALENT:移動キャラ:性別, 0) == 1
		;女部分があれば女子
		RETURN 810
	ELSE
		RETURN 702
	ENDIF
CASE 810
	;入れる方の隠し通路を目指す
	IF GETBIT(TALENT:移動キャラ:性別, 0) == 0
		;女部分がないなら男子
		RETURN 710
	ELSE
		RETURN 802
	ENDIF
;温泉は現状全エリアが相互通行可能ではないため、CASE分けを追加
CASE 703 TO 709
	;入れる方の隠し通路を目指す
	IF GETBIT(TALENT:移動キャラ:性別, 0) == 1
		;女部分があれば女子
		RETURN 710
	ELSE
		RETURN 702
	ENDIF
CASE 803 TO 809
	;入れる方の隠し通路を目指す
	IF GETBIT(TALENT:移動キャラ:性別, 0) == 0
		;女部分がないなら男子
		RETURN 810
	ELSE
		RETURN 802
	ENDIF
CASE 711
	;脱衣所を目指す
	RETURN 702
CASE 811
	;脱衣所を目指す
	RETURN 802
ENDSELECT
PRINTFORMW 移動エラーです。移動キャラ{移動キャラ}：{スタート地点}>{ARG}
GOTO LOOP


@CAN_MOVE_0(ARG, ARG:1)
;部屋の構造
;ARG 移動元
;ARG:1 移動先
;戻り値はビット管理 & 1で移動可能、 & 2で見通し可能
;RETURNF 1 移動可能
;RETURNF 2 移動不可だが見通せる
;RETURNF 3 直通（移動可能＆見通せる）

#FUNCTION
SELECTCASE ARG
	;玄関
	CASE 1
		;ロビー直通
		SIF ARG:1 == 2
			RETURNF 3
	;ロビー
	CASE 2
		;玄関直通
		SIF ARG:1 == 1
			RETURNF 3
		;庭直通
		SIF ARG:1 == 3
			RETURNF 3
		;食堂移動可
		SIF ARG:1 == 4
			RETURNF 1
		;遊戯場移動可
		SIF ARG:1 == 5
			RETURNF 1
		;廊下直通
		SIF ARG:1 == 6
			RETURNF 3
		;男子更衣室移動可
		SIF ARG:1 == 13
			RETURNF 1
		;女子更衣室移動可
		SIF ARG:1 == 14
			RETURNF 1
	;庭
	CASE 3
		;ロビー直通
		SIF ARG:1 == 2
			RETURNF 3
	;食堂
	CASE 4
		;ロビー移動可
		SIF ARG:1 == 2
			RETURNF 1
	;遊戯場
	CASE 5
		;ロビー移動可
		SIF ARG:1 == 2
			RETURNF 1
	;廊下
	CASE 6
		;ロビー直通
		SIF ARG:1 == 2
			RETURNF 3
		;男子脱衣所移動可
		SIF ARG:1 == 7
			RETURNF 1
		;女子脱衣所移動可
		SIF ARG:1 == 8
			RETURNF 1
		;自室移動可
		SIF ARG:1 == 20
			RETURNF 1
	;男子脱衣所
	CASE 7
		;廊下移動可
		SIF ARG:1 == 6
			RETURNF 1
		;男湯移動可
		SIF ARG:1 == 9
			RETURNF 1
	;女子脱衣所
	CASE 8
		;廊下移動可
		SIF ARG:1 == 6
			RETURNF 1
		;女湯移動可
		SIF ARG:1 == 11
			RETURNF 1
	;男湯
	CASE 9
		;男子脱衣所移動可
		SIF ARG:1 == 7
			RETURNF 1
		;男露天風呂直通
		SIF ARG:1 == 10
			RETURNF 3
	;男露天風呂
	CASE 10
		;男湯直通
		SIF ARG:1 == 9
			RETURNF 3
	;女湯
	CASE 11
		;女子脱衣所移動可
		SIF ARG:1 == 8
			RETURNF 1
		;女露天風呂直通
		SIF ARG:1 == 12
			RETURNF 3
	;女露天風呂
	CASE 12
		;女湯直通
		SIF ARG:1 == 11
			RETURNF 3
	;男更衣室
	CASE 13
		;ロビー移動可
		SIF ARG:1 == 2
			RETURNF 1
		;プール移動可
		SIF ARG:1 == 15
			RETURNF 1
	;女更衣室
	CASE 14
		;ロビー移動可
		SIF ARG:1 == 2
			RETURNF 1
		;プール移動可
		SIF ARG:1 == 15
			RETURNF 1
	;プール
	CASE 15
		;男更衣室移動可
		SIF ARG:1 == 13
			RETURNF 1
		;女更衣室移動可
		SIF ARG:1 == 14
			RETURNF 1
	;自室
	CASE 20
		;廊下移動可
		SIF ARG:1 == 6
			RETURNF 1
	;キャンプ場の場合、どこからでもどこへでも行ける
	;ただし遠いので見通せない
	CASE 303 TO 308
		SIF ARG:1 >= 303 && ARG:1 <= 307
			RETURNF 1
		SIF 施設改造度:5:6 && ARG:1 == 308
			RETURNF 1
		SIF ARG:1 == 2
			RETURNF 1
	;遊戯場内部
	;バーエリア、どのエリアにも行ける
	CASE 503
		SIF ARG:1 == 2 || ARG:1 == 504 || ARG:1 == 505 
			RETURNF 1
		SIF 施設改造度:5:6 && ARG:1 == 506
			RETURNF 1
	;卓球・ボドゲからはバーエリアのみ
	CASE 504 TO 506
		SIF ARG:1 == 503
			RETURNF 1
	;温泉施設内部
	;脱衣所からはどのエリアにも行ける
	CASE 702
		SIF ARG:1 == 6 || (ARG:1 >= 703 && ARG:1 <= 711)
			RETURNF 1
	;温泉内部からは脱衣所＋内部ならどこへでも、反対側の隠し通路もOK
	CASE 703 TO 709
		SIF ARG:1 == 702
			RETURNF 1
		SIF ARG:1 >= 703 && ARG:1 <= 710
			RETURNF 3
	;隠し通路は相互に移動可能
	CASE 710
		SIF ARG:1 == 702
			RETURNF 1
		SIF ARG:1 >= 703 && ARG:1 <= 710
			RETURNF 3
		SIF ARG:1 >= 809 && ARG:1 <= 810
			RETURNF 1
	;マッサージ施設は脱衣所と外
	CASE 711
		SIF ARG:1 == 6 || ARG:1 == 702
			RETURNF 1
	;脱衣所からはどのエリアにも行ける
	CASE 802
		SIF ARG:1 == 6 || (ARG:1 >= 803 && ARG:1 <= 811)
			RETURNF 1
	;温泉内部からは脱衣所＋内部ならどこへでも
	CASE 803 TO 809
		SIF ARG:1 == 802
			RETURNF 1
		SIF ARG:1 >= 803 && ARG:1 <= 810
			RETURNF 3
	;隠し通路は相互に移動可能
	CASE 810
		SIF ARG:1 == 802
			RETURNF 1
		SIF ARG:1 >= 803 && ARG:1 <= 810
			RETURNF 3
		SIF ARG:1 >= 709 && ARG:1 <= 710
			RETURNF 1
	;マッサージ施設は脱衣所と外
	CASE 811
		SIF ARG:1 == 6 || ARG:1 == 802
			RETURNF 1
	;プール内部の場合、どこへでも行ける
	CASE 1503 TO 1515
		SIF ARG:1 >= 1503 && ARG:1 <= 1515
			RETURNF 1
		SIF ARG:1 == 13 || ARG:1 == 14
			RETURNF 1
ENDSELECT
RETURNF 0

@存在しない移動先_0(場所番号)
#FUNCTION
#DIM 場所番号

IF 場所番号 < 50
	IF 施設改造度:場所番号:0 > 0
		;改装済みの場合、改装前の部屋は存在しない
		RETURNF 1
	ENDIF
	IF GROUPMATCH(場所番号, 9, 10, 11, 12) && 施設改造度:7:0
		;温泉改装済みの場合、改装前の風呂場は存在しない
		RETURNF 1
	ENDIF
ENDIF
IF (場所番号 >= 1 && 場所番号 <= 15) || 場所番号 == 20
	;基本マップ
	RETURNF 0
ELSEIF 場所番号 >= 303 && 場所番号 <= 307 && 施設改造度:3:0
	;キャンプ場
	RETURNF 0
ELSEIF 場所番号 == 308 && 施設改造度:3:8
	;訓練場
	RETURNF 0
ELSEIF 場所番号 >= 503 && 場所番号 <= 505 && 施設改造度:5:0
	;遊戯室
	RETURNF 0
ELSEIF 場所番号 == 506 && 施設改造度:5:6
	;遊戯室
	RETURNF 0
ELSEIF 場所番号 >= 702 && 場所番号 <= 711 && 施設改造度:7:0
	;温泉男湯
	RETURNF 0
ELSEIF 場所番号 >= 802 && 場所番号 <= 811 && 施設改造度:8:0
	;温泉女湯
	RETURNF 0
ELSEIF 場所番号 >= 1503 && 場所番号 <= 1506 && 施設改造度:15:0
	;プール
	RETURNF 0
ENDIF
RETURNF 1
