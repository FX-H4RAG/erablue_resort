

;-------------------------------------------------
;男水着チャレンジ
;-------------------------------------------------
@COMNAME_PLACE_381_0_15
#FUNCTION
TSTR:コマンド名受渡 = 男水着チャレンジ
IF 施設改造度:15:0
	TSTR:コマンド名受渡 = 
	RETURNF 0
ENDIF
RETURNF 1

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_381_0_15
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("セクハラ")

@COM_TOOLTIP_381_0_15
LOCALS = <br>■男水着チャレンジ<br>
LOCALS += "女性、あるいはふたなりキャラに男用の水着を着せてバレないかチャレンジしてもらうコマンド。<br>一定確率で成功、失敗に分岐。"
LOCALS += "<br>COMタイプ：セクハラ<br>取得ソース：露出"
LOCALS += "<br>固有の取得経験：露出快楽経験<br>"
TSTR:ツールチップ受渡 = %LOCALS%
		
@COM381_0_15
#DIM 成功閾値

;成功度の判定
LOCAL = RAND:100
成功閾値 = 0
IF TALENT:TARGET:バストサイズ == -2
	成功閾値 += 50
ELSEIF TALENT:TARGET:バストサイズ == -1
	成功閾値 += 25
ENDIF
SIF TALENT:TARGET:度胸 > 0
	成功閾値 += 5
SIF TALENT:TARGET:感情乏しい > 0
	成功閾値 += 10
SIF TALENT:TARGET:目立ちたがり > 0
	成功閾値 += 5
成功閾値 -= TALENT:TARGET:羞恥心 * 5
成功閾値 += TALENT:TARGET:豪快 * 5
成功閾値 += ABL:TARGET:露出癖 * 3

IF LOCAL >= 成功閾値
	;失敗
	RFLAG:コマンド結果受渡し変数 = -1
	SIF 知識素質:TARGET:性知識 > -1
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 800)
ELSE
	;成功
	RFLAG:コマンド結果受渡し変数 = 1
	SIF 知識素質:TARGET:性知識 > -1
		CALL SOURCE_CALC_露出(TARGET, PLAYER, 500)
ENDIF

TIME += 30
RETURN 1


@CAN_COM381_0_15(ARG)
SIF RFLAG:指示モード対象あなたフラグ
	RETURN 1

SKIPDISP ARG
PRINTFORML 男水着チャレンジ(%CALLNAME:TARGET%)
;-------------------------------------------------
;実行判定処理
;-------------------------------------------------
A = 0
S = 0
;すべての命令に共通の要素を考慮

CALL COM_ORDER
;ABL:露出癖
IF ABL:露出癖
	SIF S
		PRINT  + 
	A += ABL:露出癖 * 10
	PRINT 露出癖
	PRINTV 'LV,ABL:露出癖,'(,ABL:露出癖 * 10,')
	S = 1
ENDIF
IF MARK:快楽刻印
	SIF S
		PRINT  + 
	A += MARK:快楽刻印 * 1
	PRINT 快楽刻印
	PRINTV 'LV,MARK:快楽刻印,'(,MARK:快楽刻印 * 1,')
	S = 1
ENDIF
;PALAM:欲情
IF GETPALAMLV(PALAM:欲情,10)
	SIF S
		PRINT  + 
	A += GETPALAMLV(PALAM:欲情,10) * 5
	PRINT 欲情
	PRINTV 'LV,GETPALAMLV(PALAM:欲情,10),'(,GETPALAMLV(PALAM:欲情,10) * 5,')
	S = 1
ENDIF
;恥じらい
IF TALENT:羞恥心 > 0
	PRINT  - 
	A -= 10
	PRINT 恥じらい
	PRINTV '(,10,')
	S = 10
ENDIF
;男嫌い（恋慕、親愛で無効）
IF TALENT:性別嗜好 & 1 && !(TALENT:PLAYER:性別 & 1) && (!TALENT:恋慕 || PLAYER != MASTER) && TALENT:性別嗜好 > 0
	PRINT  - 
	A -= 5
	PRINT 男嫌い
	PRINTV '(,5,')
	S = 5
ENDIF
;恋慕
IF TALENT:恋慕
	SIF S
		PRINT  + 
	A += 5
	PRINT 恋慕
	PRINTV '(,5,')
	S = 5
ENDIF
;セフレ
IF TALENT:セフレ
	SIF S
		PRINT  + 
	A += 3
	PRINT セフレ
	PRINTV '(,3,')
	S = 3
ENDIF
;性知識不足
IF 知識素質:TARGET:性知識 > -1
	SIF S
		PRINT  + 
	A += 15
	PRINT 性知識不足
	PRINTV '(,15,')
	S = 15
ENDIF

;言いなり
IF TCVAR:言いなり
	SIF S
		PRINT  + 
	A += 999
	PRINT あなたの言いなり
	PRINTV '(,999,')
	S = 999
ENDIF

;合計を表示(60以上で実行)
PRINT  = 
PRINTV A

V = 60
SIF A < V
	PRINT  < 
SIF A == V
	PRINT  = 
SIF A > V
	PRINT  > 
PRINT 実行値
PRINTV V

WAIT
SKIPDISP 0
;実行できない
SIF A < V
	RETURN 0
RETURN 1


;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE381_0_15
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(381)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;隠密中はダメ
SIF CFLAG:PLAYER:隠密
	RETURN 0
;男はダメ
SIF GETBIT(TALENT:TARGET:性別, 0) == 0
	RETURN 0
;貧乳以下
SIF TALENT:TARGET:バストサイズ > -1
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM381_0_15

PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%に男物の水着を着せて、辺りを一周してくるプレイをさせた。


@MESSAGE_COM381_0_15_非口上時

SELECTCASE RFLAG:コマンド結果受渡し変数
	CASE -1
		;失敗時
		PRINTL 
		PRINTL ……周囲にいるモブ観光客たちが怪しんでいる。
		PRINTFORML ひそひそと、%CALLNAME:TARGET%が女性じゃないかという囁き声が聞こえてきた……
		IF 知識素質:TARGET:性知識 < 0
			PRINTFORML %CALLNAME:TARGET%はその視線を意に介さず、堂々と%CALLNAME:PLAYER%の元へと戻ってきた。
		ELSE
			PRINTFORML %CALLNAME:TARGET%は好機と疑念の視線に顔を赤くしながら耐え、なんとか%CALLNAME:PLAYER%の元へと戻ってきた。
		ENDIF
	CASE 1
		;成功時
		PRINTL 
		PRINTL 
		PRINTL ……どうやら誰にもバレずに戻ってくることが出来たようだ。
		IF 知識素質:TARGET:性知識 < 0
			PRINTFORML %CALLNAME:TARGET%は何の意味があったのだろうか、と首をかしげている。
		ELSE
			PRINTFORML %CALLNAME:TARGET%はバレていないかと不安になりながら、急いで元の水着を着直した。
		ENDIF
ENDSELECT


