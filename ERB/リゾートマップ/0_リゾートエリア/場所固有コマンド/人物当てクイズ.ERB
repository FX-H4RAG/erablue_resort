
@人物当てクイズ(ARG, カード枚数)
#DIM 入力アイコンID
#DIM CONST レイヤー番号 = 50
#DIMS キャラ名,10
#DIM 有効キャラリスト,200
#DIM 有効キャラ数
#DIM カード枚数

VARSET 有効キャラリスト
FOR LOCAL, 0, CHARANUM
	SIF CSTR:LOCAL:画像フォルダ == ""
		CSTR:LOCAL:画像フォルダ = {NO:LOCAL}%CALLNAME:LOCAL%
	IF EXISTFILE("resources/" + CSTR:LOCAL:画像フォルダ + "/顔_デフォルト.png")
		ARRAYSHIFT 有効キャラリスト, 1, LOCAL
	ENDIF
NEXT
FOR LOCAL,0,200
	IF !有効キャラリスト:LOCAL
		IF !LOCAL
			RFLAG:コマンド結果受渡し変数 = RAND:2
			RFLAG:コマンド結果受渡し変数３ = 1 
			RETURN ;ゲームできないのでランダムの結果を与える
		ENDIF
		有効キャラ数 = LOCAL
		PRINTFORML {LOCAL} 人から出題するよ
		BREAK
	ENDIF
	RESULT:LOCAL = 有効キャラリスト:LOCAL
NEXT
GDISPOSE レイヤー番号 + 0 
GCREATE レイヤー番号 + 0 , 1600, 900
GSETBRUSH レイヤー番号 + 0 , 0xFF004000
GFILLRECTANGLE レイヤー番号 + 0 , 0, 0, 900, 800
CBGSETG レイヤー番号 + 0 , 0, 0, -1
$LOOP
CBGREMOVERANGE -2, -2
VARSET RESULT
FOR LOCAL, 0 , 有効キャラ数
	SWAP 有効キャラリスト:(RAND:(有効キャラ数 - LOCAL)),有効キャラリスト:(有効キャラ数 - LOCAL - 1)
NEXT
LOCAL:1 = MIN(有効キャラ数, カード枚数) 
FOR LOCAL, 0, LOCAL:1
	LOCALS:LOCAL '= CSTR:(有効キャラリスト:LOCAL):画像フォルダ
	GDISPOSE レイヤー番号 + 10 + LOCAL
	GCREATEFROMFILE レイヤー番号 + 10 + LOCAL, LOCALS:LOCAL + "/顔_デフォルト.png"
NEXT
GDISPOSE レイヤー番号 + 1
GCREATE レイヤー番号 + 1 , 1600, 900
FOR LOCAL, 0, LOCAL:1
	LOCAL:2 = RAND(0, 600)
	LOCAL:3 = RAND(-600, -200)
	CBGSETG レイヤー番号 + 10 + LOCAL, LOCAL:2, LOCAL:3, -2
	GSETBRUSH レイヤー番号 + 1 , 0xFF000000 + LOCAL * 0x00100000
	GFILLRECTANGLE レイヤー番号 + 1 , LOCAL:2, 900 - GHEIGHT(レイヤー番号 + 10 + LOCAL) + LOCAL:3, GWIDTH(レイヤー番号 + 10 + LOCAL), GHEIGHT(レイヤー番号 + 10 + LOCAL)
NEXT
CBGREMOVERANGE -3, -3
GDISPOSE レイヤー番号 + 2 
GCREATE レイヤー番号 + 2 , 1600, 900
;正解を右に表示する
;GDRAWG レイヤー番号 + 2 , レイヤー番号 + 10, 800, 360, 360, 720, 0, 0, 180, 360
GSETFONT レイヤー番号 + 2 , GETFONT(), 18, 0
GSETPEN レイヤー番号 + 2 , 0xFFC0C0C0, 0
GDRAWTEXT レイヤー番号 + 2 , CALLNAME:(有効キャラリスト:0) + " を探してね", 20, 760
GDRAWTEXT レイヤー番号 + 2 , "キャラ以外をクリックすることでシャッフルできます", 20, 780
CBGSETG レイヤー番号 + 2 , 0, 0, -3

CBGSETBMAPG レイヤー番号 + 1

INPUTMOUSEKEY

;PRINTFORML DEBUG MOUSEKEY {RESULT}, {RESULT:1} 
;PRINTFORML DEBUG PRINTPOINT X {RESULT:2}, Y {RESULT:3},
;PRINTFORML DEBUG PRINTCOLOR {RESULT:4}

入力アイコンID = 100
IF RESULT == 1
	SELECTCASE RESULT:4
		CASE 0x000000
			入力アイコンID = 0
		CASE 0x100000
			入力アイコンID = 1
		CASE 0x200000
			入力アイコンID = 2
		CASE 0x300000
			入力アイコンID = 3
		CASE 0x400000
			入力アイコンID = 4
		CASE 0x500000
			入力アイコンID = 5
		CASE 0x600000
			入力アイコンID = 6
		CASE 0x700000
			入力アイコンID = 7
		CASE 0x800000
			入力アイコンID = 8
		CASE 0x900000
			入力アイコンID = 9
	ENDSELECT
	ELSE
		PRINTL リタイアしますか？
		PRINTBUTTON "[0] はい",0
		PRINTBUTTON "[1] いいえ",1
		BINPUT
		SIF RESULT
			GOTO LOOP
		RETURN 0
ENDIF

SIF 入力アイコンID == 100
	GOTO LOOP

CBGREMOVERANGE -3, -3
GDISPOSE レイヤー番号 + 2 
GCREATE レイヤー番号 + 2 , 1600, 900
GSETBRUSH レイヤー番号 + 2 , 0x80101010
GFILLRECTANGLE レイヤー番号 + 2 , 0, 0, 900, 800
GDRAWG レイヤー番号 + 2 , レイヤー番号 + 10 + 入力アイコンID, 20, 360, GWIDTH(レイヤー番号 + 10 + 入力アイコンID) * 2, GHEIGHT(レイヤー番号 + 10 + 入力アイコンID) * 2, 0, 0, GWIDTH(レイヤー番号 + 10 + 入力アイコンID), GHEIGHT(レイヤー番号 + 10 + 入力アイコンID)
GSETBRUSH レイヤー番号 + 2 , 0xFFF0F0F0
GSETFONT レイヤー番号 + 2 , GETFONT(), 36, 0
GSETPEN レイヤー番号 + 2 , 0xFFF0F0F0, 0

IF 入力アイコンID
	IF ARG == 有効キャラリスト:0 ;出題者が正解だった
		GDRAWTEXT レイヤー番号 + 2 , "ばか・・・", 400, 500
	ELSE
		GDRAWTEXT レイヤー番号 + 2 , "はずれ・・・", 400, 500
	ENDIF
	RFLAG:コマンド結果受渡し変数 = 0
ELSE
	SIF ARG == 有効キャラリスト:0 ;出題者が正解だった
		RFLAG:コマンド結果受渡し変数２ = 1
	GDRAWTEXT レイヤー番号 + 2 , "あたり・・・", 400, 500
	RFLAG:コマンド結果受渡し変数 = 1
	EXP:MASTER:人物当て正解 += 1
	CALL 履歴登録チェック(MASTER, "人物当て正解")
ENDIF
GDRAWTEXT レイヤー番号 + 2 , "私の名前は " + CALLNAME:(有効キャラリスト:入力アイコンID) + " です", 400, 540
CBGSETG レイヤー番号 + 2 , 0, 0, -3
WAIT
CBGCLEAR
