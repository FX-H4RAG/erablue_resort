
;-------------------------------------------------
;ポーカー処理関連
;-------------------------------------------------
;あなた勝利時は１
;代入番号キャラ勝利時は-１
;引き分け時は０を返り値とする
;-------------------------------------------------
		
@簡易ポーカー(キャラ番号)
#DIM 手持ちカード, 5
#DIM 交換カード, 5
#DIM 相手カード強さ
#DIM 自己カード強さ
#DIM キャラ番号
#DIM ループ用
#DIM 被りチェック
#DIM カード交換, 5
#DIM フラッシュ？
#DIM ペア系？
#DIM ストレート？

VARSET 手持ちカード
VARSET 交換カード
VARSET 相手カード強さ
VARSET 自己カード強さ
VARSET ループ用
VARSET 被りチェック
VARSET カード交換
VARSET フラッシュ？
VARSET ペア系？
VARSET ストレート？

DRAWLINE

;簡易版のため相手の役は単純な確率で算出
;ロイヤルストレートフラッシュ・ファイブカード・ストレートフラッシュは確率が0.1%未満のため省略

LOCAL = RAND:1000

;ギャンブラーは強いので交換アリ
SIF TALENT:キャラ番号:ギャンブラー > 0
	LOCAL = MAX(LOCAL, RAND:1000)

SELECTCASE LOCAL
	CASE IS < 400
		;ブタ：４０％
		相手カード強さ = 0
	CASE IS < 840
		;ワンペア：４４％
		相手カード強さ = 1
	CASE IS < 920
		;ツーペア：８％
		相手カード強さ = 2
	CASE IS < 970
		;スリーカード：５％
		相手カード強さ = 3
	CASE IS < 990
		;ストレート：２％
		相手カード強さ = 4
	CASE IS < 993
		;フラッシュ：０．３％
		相手カード強さ = 5
	CASE IS < 998
		;フルハウス：０．５％
		相手カード強さ = 6
	CASEELSE
		;フォーカード：０．２％
		相手カード強さ = 7
ENDSELECT


;自分の手持ちカード配布
FOR ループ用, 0, 5
	$被りループ
	;53種
	手持ちカード:ループ用 = RAND:53
	;カード被りが無いか
	FOR 被りチェック, 0, ループ用
		SIF 手持ちカード:被りチェック == 手持ちカード:ループ用
			GOTO 被りループ
	NEXT
NEXT

;手札表示
$表示ループ
FOR ループ用, 0, 5
	SELECTCASE 手持ちカード:ループ用
		CASE 52
			PRINT [JOKER] 
		CASE 0 TO 12
			PRINT [
			PRINT_IMG "スペード"
			PRINTPLAINFORM {手持ちカード:ループ用 + 1}]  
			SIF 手持ちカード:ループ用 < 10
				PRINT  
		CASE 13 TO 25
			PRINT [
			PRINT_IMG "クローバー"
			PRINTPLAINFORM {手持ちカード:ループ用 % 13 + 1}]  
			SIF 手持ちカード:ループ用 % 13 < 10
				PRINT  
		CASE 26 TO 38
			PRINT [
			PRINT_IMG "ハート"
			PRINTPLAINFORM {手持ちカード:ループ用 % 13 + 1}]  
			SIF 手持ちカード:ループ用 % 13 < 10
				PRINT  
		CASE 39 TO 51
			PRINT [
			PRINT_IMG "ダイヤ"
			PRINTPLAINFORM {手持ちカード:ループ用 % 13 + 1}]  
			SIF 手持ちカード:ループ用 % 13 < 10
				PRINT  
	ENDSELECT
NEXT
PRINTL 

;交換フェイズ
FOR ループ用, 0, 5
	IF カード交換:ループ用 == 0
		PRINTBUTTON "[交換]", ループ用
	ELSEIF カード交換:ループ用 == 1
		SETCOLOR 0xFFD400
		PRINTBUTTON "[保持]", ループ用
		RESETCOLOR
	ENDIF
	PRINT   
NEXT
PRINTL 

PRINTBUTTON "[999]交換する", 999
PRINTL
PRINTBUTTON "[説明]", 998

INPUT

SELECTCASE RESULT
	CASE 0 TO 4
		INVERTBIT カード交換:RESULT, 0
		CLEARLINE 5
		GOTO 表示ループ
	CASE 999
	CASE 998
		DRAWLINE
		PRINTL 配られた手札を一回交換し、出来た役で勝敗を決めるポーカーのミニゲームです。
		PRINTL 役はワンペア、ツーペア、スリーカード、ストレート、フラッシュ
		PRINTL フルハウス、 フォーカード、ストレートフラッシュ、ファイブカード
		PRINTL ロイヤルストレートフラッシュの順で強くなります。
		PRINTL 
		PRINTW たまに相手がギャンブルを挑んでくることがあり、ルピや罰ゲームを賭けることが出来ます。
		GOTO 表示ループ
	CASEELSE
		GOTO 表示ループ
ENDSELECT

;交換用カードが手持ちとも他の交換とも被りがないかチェック
FOR ループ用, 0, 5
	IF カード交換:ループ用 == 0
		$被りループ２
		;53種
		交換カード:ループ用 = RAND:53
		;カード被りが無いか
		FOR 被りチェック, 0, 5
			SIF 手持ちカード:被りチェック == 交換カード:ループ用
				GOTO 被りループ２
		NEXT
		FOR 被りチェック, 0, ループ用
			SIF 交換カード:被りチェック == 交換カード:ループ用
				GOTO 被りループ２
		NEXT
	ELSEIF カード交換:ループ用 == 1
		交換カード:ループ用 = 0
	ENDIF
NEXT

;被りチェックを突破したら手持ちと交換
FOR ループ用, 0, 5
	IF カード交換:ループ用 == 0
		手持ちカード:ループ用 = 交換カード:ループ用
	ELSEIF カード交換:ループ用 == 1
	ENDIF
NEXT

DRAWLINE

;交換後再表示
FOR ループ用, 0, 5
	SELECTCASE 手持ちカード:ループ用
		CASE 52
			PRINT [JOKER] 
		CASE 0 TO 12
			PRINT [
			PRINT_IMG "スペード"
			PRINTPLAINFORM {手持ちカード:ループ用 + 1}]  
			SIF 手持ちカード:ループ用 < 10
				PRINT  
		CASE 13 TO 25
			PRINT [
			PRINT_IMG "クローバー"
			PRINTPLAINFORM {手持ちカード:ループ用 % 13 + 1}]  
			SIF 手持ちカード:ループ用 % 13 < 10
				PRINT  
		CASE 26 TO 38
			PRINT [
			PRINT_IMG "ハート"
			PRINTPLAINFORM {手持ちカード:ループ用 % 13 + 1}]  
			SIF 手持ちカード:ループ用 % 13 < 10
				PRINT  
		CASE 39 TO 51
			PRINT [
			PRINT_IMG "ダイヤ"
			PRINTPLAINFORM {手持ちカード:ループ用 % 13 + 1}]  
			SIF 手持ちカード:ループ用 % 13 < 10
				PRINT  
	ENDSELECT
NEXT
PRINTL 

;役判定
;まずはフラッシュ系の判定
FOR ループ用, 1, 5
	IF 手持ちカード:0 == 52
		;初手ジョーカーの場合２枚目で判定する
		SIF (手持ちカード:1 / 13) == (手持ちカード:ループ用 / 13)
			フラッシュ？ += 1
	ELSE
		;通常一枚目で照合する
		IF (手持ちカード:0 / 13) == (手持ちカード:ループ用 / 13)
			フラッシュ？ += 1
		ELSEIF 手持ちカード:ループ用 == 52
			フラッシュ？ += 1
		ENDIF
	ENDIF
NEXT

;ペア系の判定
;まずは手札を全て数字化
FOR ループ用, 0, 5
	SIF 手持ちカード:ループ用 == 52
		;ジョーカーが計算後-1になるように
		手持ちカード:ループ用 = -2
	手持ちカード:ループ用 = 手持ちカード:ループ用 % 13 + 1
NEXT

;フラッシュ成立ならフラッシュ？＝４になっているはず
;その場合ペア系はありえないのでストレートまでGOTO
SIF フラッシュ？ == 4
	GOTO ストレート判定

;一枚目照合
FOR ループ用, 1, 5
	SIF 手持ちカード:0 == 手持ちカード:ループ用
		ペア系？ += 1
NEXT
;二枚目照合
FOR ループ用, 2, 5
	SIF 手持ちカード:1 == 手持ちカード:ループ用
		ペア系？ += 1
NEXT
;三枚目照合
FOR ループ用, 3, 5
	SIF 手持ちカード:2 == 手持ちカード:ループ用
		ペア系？ += 1
NEXT
;四枚目照合
SIF 手持ちカード:3 == 手持ちカード:4
	ペア系？ += 1

SELECTCASE ペア系？
	CASE 1
		;ワンペア
		ペア系？ = 1
	CASE 2
		;ツーペア
		ペア系？ = 2
	CASE 3
		;スリーカード
		ペア系？ = 3
	CASE 4
		;フルハウス
		ペア系？ = 6
	CASE 6
		;フォーカード
		ペア系？ = 7
ENDSELECT

;ジョーカーの処理
IF MIN(手持ちカード:0, 手持ちカード:1, 手持ちカード:2, 手持ちカード:3, 手持ちカード:4) == -1
	SELECTCASE ペア系？
		CASE 0
			;ブタ＞ワンペア
			ペア系？ = 1
		CASE 1
			;ワンペア＞スリーカード
			ペア系？ = 3
		CASE 2
			;ツーペア＞フルハウス
			ペア系？ = 6
		CASE 3
			;スリーカード＞フォーカード
			ペア系？ = 7
		CASE 7
			;フォーカード＞ファイブカード
			ペア系？ = 9
	ENDSELECT
ENDIF

$ストレート判定

;手持ちを小さい順にソート
ARRAYSORT 手持ちカード, FORWARD

;２番目～５番目のカードが１差であることを判定
FOR ループ用, 1, 4
	SELECTCASE 手持ちカード:(ループ用 + 1) - 手持ちカード:ループ用
		CASE 1
			ストレート？ += 1
		CASE 2
			;ジョーカー処理
			IF 手持ちカード:0 == -1
				ストレート？ += 2
				;二重処理されないように数変更
				手持ちカード:0 = -10
			ENDIF
		
	ENDSELECT
NEXT

IF ストレート？ == 3
	IF 手持ちカード:0 > 0
		SIF 手持ちカード:1 - 手持ちカード:0 == 1
			ストレート？ += 1
	ELSEIF 手持ちカード:0 == -1
		;端っこジョーカーのフォロー
		ストレート？ += 1
	ENDIF
ENDIF

;ロイヤル系のストレートのフォロー
;１を１４にしてから再判定
IF ストレート？ != 4 && (手持ちカード:0 == 1 || 手持ちカード:1 == 1)
	FOR ループ用, 0, 5
		SIF 手持ちカード:ループ用 == 1
			手持ちカード:ループ用 = 14
	NEXT
	ストレート？ = 0
	GOTO ストレート判定
ENDIF



;役の強さに変換
IF ストレート？ == 4
	自己カード強さ = 4
	;ストフラ
	IF フラッシュ？ == 4
		自己カード強さ += 4
		;ロイフラ？
		SIF 手持ちカード:4 == 14
			自己カード強さ += 2
		SIF 手持ちカード:1 == 10 && 手持ちカード:0 < 0
			自己カード強さ += 2
	ENDIF
ELSEIF フラッシュ？ == 4
	自己カード強さ = 5
ELSEIF ペア系？
	自己カード強さ = ペア系？
ENDIF

自己カード強さ = MIN(自己カード強さ, 10)

;役名表示
PRINT 自分の役：
CALL 役名表示(自己カード強さ)
PRINTL 
PRINT 相手の役：
CALL 役名表示(相手カード強さ)
PRINTL 
SIF 自己カード強さ == 10
	CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"ロイヤルストレートフラッシュを完成させた！！","実績")
SIF 相手カード強さ == 10
	CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"ロイヤルストレートフラッシュを完成させた！！","実績")

IF 自己カード強さ > 相手カード強さ
	PRINTFORMW %CALLNAME:MASTER%勝利！
	EXP:MASTER:ポーカー勝利 += 1
	CALL 履歴登録チェック(MASTER, "ポーカー勝利")
	RETURN 1
ELSEIF 自己カード強さ < 相手カード強さ
	PRINTFORMW %CALLNAME:キャラ番号%勝利！
	EXP:キャラ番号:ポーカー勝利 += 1
	CALL 履歴登録チェック(キャラ番号, "ポーカー勝利")
	RETURN -1
ELSE
	PRINTW 引き分け
	RETURN 0
ENDIF


@役名表示(ARG)

SELECTCASE ARG
	CASE 0
		PRINT ブタ
	CASE 1
		PRINT ワンペア
	CASE 2
		PRINT ツーペア
	CASE 3
		PRINT スリーカード
	CASE 4
		PRINT ストレート
	CASE 5
		PRINT フラッシュ
	CASE 6
		PRINT フルハウス
	CASE 7
		PRINT フォーカード
	CASE 8
		PRINT ストレートフラッシュ
	CASE 9
		PRINT ファイブカード
	CASE 10
		PRINT ロイヤルストレートフラッシュ
ENDSELECT



