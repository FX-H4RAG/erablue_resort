
;-------------------------------------------------
;店舗を改装する
;-------------------------------------------------
@COMNAME_PLACE_380_1_2
#FUNCTION
TSTR:コマンド名受渡 = 日替わり店舗
IF GET_DAY() == "日"
	TSTR:コマンド名受渡 = おせんべ屋さん
ELSEIF GET_DAY() == "水"
	TSTR:コマンド名受渡 = バーガー屋さん
ENDIF
RETURNF 1

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_380_1_2
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("単独可能")

@COM_TOOLTIP_380_1_2
IF GET_DAY() == "日"
	LOCALS = <br>■おせんべ屋さん<br>
	LOCALS += "日曜日にだけ売りに来てくれるおせんべ屋さんに寄るコマンド。"
	LOCALS += "<br>営業時間は７時～１８時"
ELSEIF GET_DAY() == "水"
	LOCALS = <br>■バーガー屋さん<br>
	LOCALS += "水曜日にだけ売りに来てくれるバーガー屋さんに寄るコマンド。"
	LOCALS += "<br>営業時間は５時～２０時"
ENDIF
LOCALS += "<br>COMタイプ：単独可能<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

@COM380_1_2
#DIM 品番号

品番号 = 0
IF GET_DAY() == "日"
	STRDATA 汎用アイテム購入用_序文
		DATA ■おせんべ屋さん<br>　ロジーヌ「いらっしゃい、おいしいおせんべはいかが？」
		DATA ■おせんべ屋さん<br>　ロジーヌ「おいしいおせんべ、焼き立てだよぉ」
		DATA ■おせんべ屋さん<br>　ロジーヌ「あらこんにちは。おせんべ、焼けてるよぉ」
	ENDDATA
	汎用アイテム購入用_品揃え登録:(品番号++) = 醤油おせんべ
	汎用アイテム購入用_品揃え登録:(品番号++) = 海苔おせんべ
	汎用アイテム購入用_品揃え登録:(品番号++) = 醤油おせんべセット
	汎用アイテム購入用_品揃え登録:(品番号++) = 海苔おせんべセット
	汎用アイテム購入用_品揃え登録:(品番号++) = 贈答用おせんべセット,6000,ルピ
ELSEIF GET_DAY() == "水"
	STRDATA 汎用アイテム購入用_序文
		DATA ■バーガー屋さん<br>　バーガー店員「いらっしゃいませ！　ご一緒にポテトフライはいかがですか？」
		DATA ■バーガー屋さん<br>　バーガー店員「いらっしゃいませ！　期間限定でスマイル無料になっております！」
		DATA ■バーガー屋さん<br>　バーガー店員「いらっしゃいませ！　なにはなくともバーガーですよ！」
	ENDDATA
	汎用アイテム購入用_品揃え登録:(品番号++) = チーズバーガー
	汎用アイテム購入用_品揃え登録:(品番号++) = ダブルバーガー
	汎用アイテム購入用_品揃え登録:(品番号++) = ビッグバーガー
	汎用アイテム購入用_品揃え登録:(品番号++) = ポテトフライＬ
ENDIF
CALL 汎用アイテム購入画面処理(0)
RETURN RESULT

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE380_1_2
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(380)
	RETURN RESULT
;隠密中はダメ
SIF CFLAG:PLAYER:隠密
	RETURN 0
;特定曜日だけ
IF GET_DAY() == "日"
	;おせんべ
ELSEIF GET_DAY() == "水" && イベント完了フラグ確認("大拡張！空艇港") > 0
	;バーガー
ELSE
	;以上を満たさないならダメ
	RETURN 0
ENDIF
;営業時間
SIF TIME < 420 || TIME > 1080
	RETURN 0
;前提イベント
SIF イベント完了フラグ確認("大拡張！空艇港") < 1
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM380_1_2

IF GET_DAY() == "日"
	;おせんべ
	PRINTL ロジーヌ：
	PRINTL 「どうもありがとうねぇ、またいらっしゃい。体に気をつけてねぇ」
ELSEIF GET_DAY() == "水"
	PRINTL バーガー店員：
	PRINTL 「ありがとうございました！　またのお越しをお待ちしておりまーす！」
ENDIF

