@よろず屋輸入品店処理
#DIM 値段一時登録
#DIM 表示アビ番号
#DIM 関数総数

LOCALS = 
DRAWLINE
PRINTL 島外から運んできた珍しい品物を販売しています
DRAWLINE
LOCALS += "<div rect='62,0,4375,3000' border='31' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='4469,0,1562,625' border='31' bcolor='#C0C0C0'></div>"
LOCALS += @"<div rect='4531,62,1562,625'>所持ルピ：%NUM_FORMAT(MONEY)%ルピ<br>所持ZP　：%ZP所持量全文字列()%</div>"

;ランダムで５つの商品を並べる
FOR LOCAL, 0, 5
	SIF 輸入品店_品揃え登録文字列:LOCAL == ""
		CONTINUE
	LOCALS += @"<div rect='{200 + LOCAL % 2 * 2100},{100 + LOCAL / 2 * 600},2000,550' border='20' bcolor='#C0C0C0' padding='31'><nobr>"
	IF 輸入品店_品揃え登録:LOCAL:品目種別 == 4
		CALLFORM 素材性能記述_%輸入品店_品揃え登録文字列:LOCAL%
		LOCALS += @"■ランク%TOFULL(TOSTR(素材性能_素材ランク))%：<font color='#%カラーパレット_HTML(@"%素材性能_属性%属性")%'>"
	ELSE
		LOCALS += "■"
	ENDIF
	LOCALS += @"%輸入品店_品揃え登録文字列:LOCAL%"
	IF 輸入品店_品揃え登録:LOCAL:品目種別 == 4
		LOCALS += "</font>"
		IF 素材アイテム_数値データ取得("累計入手素材数", 輸入品店_品揃え登録文字列:LOCAL) == 0
			LOCALS += @"　<font color='#%カラーパレット_HTML("真っ赤")%'>未入手！</font>"
		ENDIF
	ENDIF
	値段一時登録 = 輸入品店_品揃え登録:LOCAL:値段
	LOCALS:1 = 
	IF 値段一時登録 < 0
		値段一時登録 = 値段一時登録 * -1
		LOCALS:1 = 　<font color='#%カラーパレット_HTML("黄")%'>SALE！</font>
	ENDIF
	IF 輸入品店_品揃え登録:LOCAL:品目種別 == 2
		LOCALS += @"<br>　オプション数：{輸入品店_品揃え登録:LOCAL:品目性能, 2}<br>　値段：{値段一時登録}ルピ%LOCALS:1%<br>"
	ELSE
		LOCALS += @"<br>　値段：{値段一時登録}ルピ%LOCALS:1%<br><br>"
	ENDIF
	LOCALS += @"　　　　　　　　<button value='{LOCAL + 100}'>[詳細]</button>　　<button value='{LOCAL}'>[購入する]</button>"
	LOCALS += "</div>"
NEXT


LOCALS += "<div rect='550,2812,5125,281'>"
IF ITEM:輸入品リロールチケット > 0
	LOCALS += "<button value='997'>[997] 輸入品リロールチケットを使用</button>　　"
ELSE
	LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[997] 輸入品リロールチケットを使用</font>　　"
ENDIF
LOCALS += "<button value='998'>[998] システムの説明</button>　　"
LOCALS += "<button value='999'>[999] 戻る</button>"
LOCALS += "</div>"

HTML_PRINT LOCALS, 1
FOR LOCAL, 0, 27
	PRINTL
NEXT

IF チュートリアルフラグ:輸入品店 == 0
	RESULT = 998
ELSE
	BINPUT
ENDIF
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 997
		CALL 輸入品店品揃え変動処理
		ITEM:輸入品リロールチケット -= 1
		RESTART
	CASE 999
		RETURN -1
	CASE 998
		DRAWLINE
		PRINTL ［輸入品店：チュートリアル］
		DRAWLINE
		HTML_PRINT "<img src='顔エラ' height='900' width='900'>"
		PRINTL 
		PRINTL 
		PRINTL 
		PRINTL 
		PRINTL 
		PRINTL 
		PRINTL 
		CALL 輸入品店システムチュートリアル
		DRAWLINE
		RESTART
	CASE IS >= 100
		;詳細表示
		DRAWLINE
		PRINTFORML ■%輸入品店_品揃え登録文字列:(LOCAL - 100)%
		SELECTCASE 輸入品店_品揃え登録:(LOCAL - 100):品目種別
			CASE 1
				;証
				TRYCALLFORM 証関数_%輸入品店_品揃え登録文字列:(LOCAL - 100)%("フレーバー")
				LOCALS = %詳細文文字列受け渡し変数%<br>
				LOCALS += @"・追加アビ<br>"
				VARSET RESULTS
				関数総数 = ENUMFUNCBEGINSWITH(@"証アビ関数_%輸入品店_品揃え登録文字列:(LOCAL - 100)%_")
				FOR 表示アビ番号, 0, 関数総数
					TSTR:コマンド名受渡 = 
					TRYCALLFORM %RESULTS:表示アビ番号%("アビ名")
					SIF TSTR:コマンド名受渡 == ""
						CONTINUE
					詳細文文字列受け渡し変数 = 
					TRYCALLFORM %RESULTS:表示アビ番号%("アビ説明文")
					LOCALS += @"%詳細文文字列受け渡し変数%<br>"
				NEXT
				LOCALS += "<br>"
				HTML_PRINT LOCALS
			CASE 2
				;指輪
				CALLFORM 指輪基礎効果文章表示_%輸入品店_品揃え登録文字列:(LOCAL - 100)%(0, 輸入品店_品揃え登録:(LOCAL - 100):品目性能２)
				LOCALS = %詳細文文字列受け渡し変数%<br>
				LOCALS += @"【オプション効果】<br>%オプション効果表示文字列作成(輸入品店_オプション固定登録:(LOCAL - 100))%"
				HTML_PRINT LOCALS
			CASE 3
				;耳飾り
				CALLFORM 耳飾り基礎効果文章表示_%輸入品店_品揃え登録文字列:(LOCAL - 100)%(0, 輸入品店_品揃え登録:(LOCAL - 100):品目性能２)
				LOCALS = %詳細文文字列受け渡し変数%
				HTML_PRINT LOCALS
			CASE 4
				;食材
				CALLFORM 素材性能記述_%輸入品店_品揃え登録文字列:(LOCAL - 100)%
				PRINTFORM カテゴリ：%素材性能_カテゴリ, 12, LEFT%
				IF 素材性能_カテゴリ == "食物素材"
					PRINTFORML 　　分類：%素材性能_食材分類%
				ELSEIF 素材性能_カテゴリ == "贈答アイテム"
					PRINTFORML 　　分類：%素材性能_プレゼント%
				ELSE
					PRINTL
				ENDIF
				PRINTFORM 　ランク：%TOFULL(TOSTR(素材性能_素材ランク)), 12, LEFT%　　属性：
				SETCOLOR カラーパレット(@"%素材性能_属性%属性")
				PRINTFORML %素材性能_属性%属性
				RESETCOLOR
		ENDSELECT
		PRINTFORML
		WAIT
		RESTART
	CASEELSE
		;購入
		IF 輸入品店_品揃え登録:LOCAL:値段 > MONEY
			PRINTW ルピが足りません。
			RESTART
		ENDIF
		IF 輸入品店_品揃え登録:LOCAL:値段 < 0
			MONEY += 輸入品店_品揃え登録:LOCAL:値段
		ELSE
			MONEY -= 輸入品店_品揃え登録:LOCAL:値段
		ENDIF
		SELECTCASE 輸入品店_品揃え登録:LOCAL:品目種別
			CASE 1
				;証
				CALL 汎用証取得処理(輸入品店_品揃え登録文字列:LOCAL)
			CASE 2
				;指輪
				CALL 汎用指輪取得処理(輸入品店_品揃え登録文字列:LOCAL, 0, 輸入品店_品揃え登録:LOCAL:品目性能２ - 1)
				IF RESULT > -1
					;オプション固定のため一旦オプション無しで登録してから手動で入れ込む
					DT_CELL_SET "所持指輪データベース", DT_ROW_LENGTH("所持指輪データベース") - 1, "オプションステータス補正", 輸入品店_オプション固定登録:LOCAL
				ENDIF
			CASE 3
				;耳飾り
				CALL 汎用耳飾り取得処理(輸入品店_品揃え登録文字列:LOCAL, 輸入品店_品揃え登録:LOCAL:品目性能２)
			CASE 4
				;食材
				CALL 素材入手処理(輸入品店_品揃え登録文字列:LOCAL, 1)
		ENDSELECT
		IF RESULT > -1
			PRINTFORMW %輸入品店_品揃え登録文字列:LOCAL%を購入しました。
			輸入品店_品揃え登録文字列:LOCAL =
		ENDIF
		RESTART
ENDSELECT





@輸入品店品揃え変動処理
#DIM 登録品目上限
#DIM 登録品目番号
#DIM 証品目数
#DIM 装飾品品目数
#DIM 食材品目数
#DIMS 候補保存配列, 200
#DIM 候補食材数
#DIM オプションランク一時保存

;輸入品店_品揃え登録:LOCAL:品目種別
;	0:登録無し 1:証 2:指輪 3:耳飾り 4:素材
VARSET 輸入品店_品揃え登録
VARSET 輸入品店_品揃え登録文字列
VARSET 輸入品店_オプション固定登録
VARSET 候補保存配列
候補食材数 = 0

;現状５品目
登録品目上限 = 5
登録品目番号 = 0
証品目数 = 0

;まず証が出現するかどうかを見る
;1/6で持ってない証からランダムで出現
IF RAND:6 == 0
	CALL ランダム証("輸入")
	IF RESULTS != ""
		輸入品店_品揃え登録:登録品目番号:品目種別 = 1
		輸入品店_品揃え登録文字列:登録品目番号 '= RESULTS
		輸入品店_品揃え登録:登録品目番号:値段 = 50000
		証品目数 = 1
		登録品目番号 += 1
	ENDIF
ENDIF

;残りの枠のうち、１～３枠を指輪・耳飾りで埋める
;証なしの場合は２～３枠
IF 証品目数
	装飾品品目数 = RAND:3 + 1
ELSE
	装飾品品目数 = RAND:2 + 2
ENDIF

FOR LOCAL, 0, 装飾品品目数
	輸入品店_品揃え登録:登録品目番号:品目種別 = RAND:2 + 2
	IF 輸入品店_品揃え登録:登録品目番号:品目種別 == 2
		CALL ランダム指輪("輸入")
		IF RESULTS == ""
			THROW 輸入品店に並べる指輪の候補がありません。開発者に報告してください。
		ENDIF
		輸入品店_品揃え登録文字列:登録品目番号 '= RESULTS

		;基礎販売額の取得
		基礎販売額 = 0
		CALLFORM 指輪個別文章表示_%輸入品店_品揃え登録文字列:登録品目番号%()
		輸入品店_品揃え登録:登録品目番号:値段 = 基礎販売額

		;オプションを２個～４個つける
		輸入品店_品揃え登録:登録品目番号:品目性能 = RAND:3 + 2
		CALLFORM 指輪ステータス決定_%輸入品店_品揃え登録文字列:登録品目番号%(0, 1)

		;オプションランクがRESULTに入っているので、それに応じた数のオプションを記録
		オプションランク一時保存 = RESULT
		オプション補正文字列 = 
		FOR LOCAL:1, 0, 輸入品店_品揃え登録:登録品目番号:品目性能
			CALL オプション効果決定(オプションランク一時保存)
		NEXT
		輸入品店_オプション固定登録:登録品目番号 = %オプション補正文字列%

		;ランダム効果を決定
		CALLFORM 指輪基礎効果文章表示_%輸入品店_品揃え登録文字列:登録品目番号%
		SIF RESULT > 1
			輸入品店_品揃え登録:登録品目番号:品目性能２ = RAND:RESULT + 1

		;値段決定(オプション数１個につき50%値段UP)
		輸入品店_品揃え登録:登録品目番号:値段 = (輸入品店_品揃え登録:登録品目番号:値段 * (10 + 輸入品店_品揃え登録:登録品目番号:品目性能 * 5) / 10) * RAND(80, 120) / 100
		;10%の確率でセール品
		SIF RAND:10 == 0
			輸入品店_品揃え登録:登録品目番号:値段 = 輸入品店_品揃え登録:登録品目番号:値段 / -2

	ELSEIF 輸入品店_品揃え登録:登録品目番号:品目種別 == 3
		CALL ランダム耳飾り("輸入")
		IF RESULTS == ""
			THROW 輸入品店に並べる耳飾りの候補がありません。開発者に報告してください。
		ENDIF
		輸入品店_品揃え登録文字列:登録品目番号 '= RESULTS

		;基礎販売額の取得
		基礎販売額 = 0
		CALLFORM 耳飾り個別文章表示_%輸入品店_品揃え登録文字列:登録品目番号%()
		輸入品店_品揃え登録:登録品目番号:値段 = 基礎販売額

		;ランダム効果を決定
		CALLFORM 耳飾り基礎効果文章表示_%輸入品店_品揃え登録文字列:登録品目番号%
		SIF RESULT > 1
			輸入品店_品揃え登録:登録品目番号:品目性能２ = RAND:RESULT + 1

		;値段決定
		輸入品店_品揃え登録:登録品目番号:値段 = 輸入品店_品揃え登録:登録品目番号:値段 * RAND(80, 120) / 100
		;10%の確率でセール品
		SIF RAND:10 == 0
			輸入品店_品揃え登録:登録品目番号:値段 = 輸入品店_品揃え登録:登録品目番号:値段 / -2
	ENDIF
	登録品目番号 += 1
NEXT

;残りは食材・プレゼントを登録
;まずは候補食材を羅列
FOR LOCAL, 0, DT_ROW_LENGTH("所持素材データベース")
	;登録されてないアイテム判定のため比較用配列に保存
	;食べ物のみ
	SIF DT_CELL_GETS("所持素材データベース", LOCAL, "素材カテゴリ") != "食物素材" && DT_CELL_GETS("所持素材データベース", LOCAL, "素材カテゴリ") != "贈答アイテム"
		CONTINUE
	;ランク２以上
	SIF DT_CELL_GET("所持素材データベース", LOCAL, "素材ランク") < 2
		CONTINUE
	候補保存配列:候補食材数 = %DT_CELL_GETS("所持素材データベース", LOCAL, "素材アイテム名")%
	候補食材数 += 1
NEXT
食材品目数 = 候補食材数

FOR LOCAL, 登録品目番号, 登録品目上限
	輸入品店_品揃え登録:登録品目番号:品目種別 = 4
	輸入品店_品揃え登録文字列:登録品目番号 = %候補保存配列:(RAND:候補食材数)%
	
	CALLFORM 素材性能記述_%輸入品店_品揃え登録文字列:登録品目番号%
	;値段決定
	IF 素材性能_カテゴリ == "食物素材"
		輸入品店_品揃え登録:登録品目番号:値段 = 素材性能_素材ランク * 素材性能_素材ランク * 500 * RAND(80, 120) / 100
	ELSE
		輸入品店_品揃え登録:登録品目番号:値段 = 素材性能_素材ランク * 素材性能_素材ランク * 1000 * RAND(80, 120) / 100
	ENDIF
	;10%の確率でセール品
	SIF RAND:10 == 0
		輸入品店_品揃え登録:登録品目番号:値段 = 輸入品店_品揃え登録:登録品目番号:値段 / -2

	登録品目番号 += 1
NEXT
