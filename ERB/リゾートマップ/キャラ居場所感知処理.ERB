@キャラ居場所感知処理
#DIM 表示マップ種別
#DIMS 滞在者保存, 500
#DIM 滞在部屋番号保存, 500
#DIM 配列番号
#DIM ページ数
#DIM 対象キャラ
#DIM 入室禁止フラグ
#DIM TARGET登録

表示マップ種別 = CFLAG:MASTER:現在マップ種別
入室禁止フラグ = 0
ページ数 = 0
個別移動枠切り替え = 1

$RE_LOOP
;全員の居場所を見て配列に突っ込む
VARSET 滞在者保存
VARSET 滞在部屋番号保存
配列番号 = 0
FOR 対象キャラ, 1, CHARANUM
	SIF CFLAG:対象キャラ:滞在期間 < 0
		CONTINUE
	SIF CFLAG:対象キャラ:現在マップ種別 != 表示マップ種別
		CONTINUE
	SIF CFLAG:対象キャラ:現在位置 == 999
		CONTINUE
	IF FINDELEMENT(滞在部屋番号保存, CFLAG:対象キャラ:現在位置 + 1000) > -1
		滞在者保存:(FINDELEMENT(滞在部屋番号保存, CFLAG:対象キャラ:現在位置 + 1000)) += @"{対象キャラ}_"
	ELSE
		滞在者保存:配列番号 = {対象キャラ}_
		滞在部屋番号保存:配列番号 = CFLAG:対象キャラ:現在位置 + 1000
		配列番号 += 1
	ENDIF
NEXT
;部屋番号順にソート
ARRAYMSORT 滞在部屋番号保存, 滞在者保存

;マップ種別表示
$表示_LOOP
VARSET LOCALS
DRAWLINE
;個別追跡キャラ表示
FOR LOCAL, 0, 3
	LOCALS += @"<div rect='{30 + LOCAL * 2030}, 30, 2000, 600' padding='50,25,50'"
	IF 個別移動枠切り替え
		LOCALS += " color='#101010' border='31' bcolor='#C0C0C0'><nobr>"
	ELSE
		LOCALS += " color='#101010' border='31' bcolor='#666666'><nobr><font color='#666666'><clearbutton>"
	ENDIF
	IF 個別移動管理キャラ:LOCAL > 0
		LOCALS += @"■%NAME:(個別移動管理キャラ:LOCAL)%<br>"
		CALLFORM エリア情報登録_{CFLAG:(個別移動管理キャラ:LOCAL):現在マップ種別}("エリア名")
		IF MODE_確認_完全一致("ハーヴィンオナホ系", PLAYER, 個別移動管理キャラ:LOCAL) > -1
			LOCALS += @"<font color='#%カラーパレット_HTML("赤ピンク")%'><img src='えっちハート'>%DT_CELL_GETS("体位モードデータベース", MODE_確認_完全一致("ハーヴィンオナホ系", PLAYER, 個別移動管理キャラ:LOCAL), "モード名")%中<img src='えっちハート'></font><br><br>"
		ELSEIF CFLAG:(個別移動管理キャラ:LOCAL):現在マップ種別 == 0 && CFLAG:(個別移動管理キャラ:LOCAL):現在位置 == 999
			LOCALS += @"現在リゾートの外にいます<br><br>"
		ELSEIF CFLAG:MASTER:飲み会 || CFLAG:MASTER:うふふ
			;モード中は移動禁止
			LOCALS += @"現在マップ：%TSTR:場所名受渡%<br>"
			LOCALS += @"現在位置：%GETPLACENAME(CFLAG:(個別移動管理キャラ:LOCAL):現在マップ種別, CFLAG:(個別移動管理キャラ:LOCAL):現在位置)%<br>"
		ELSE
			LOCALS += @"<button value='追跡移動_{LOCAL}'>現在マップ：%TSTR:場所名受渡%<br>"
			LOCALS += @"現在位置：%GETPLACENAME(CFLAG:(個別移動管理キャラ:LOCAL):現在マップ種別, CFLAG:(個別移動管理キャラ:LOCAL):現在位置)%</button><br>"
		ENDIF
		LOCALS += @"<button value='外す_{LOCAL}'>[個別追跡から外す]</button>"
		LOCALS += @"　　<button value='リスト選択_{LOCAL}'>[リストから選択]</button>"
	ELSE
		LOCALS += @"<br><br><br>　　　　　　　　　　　<button value='リスト選択_{LOCAL}'>[リストから選択]</button>"
	ENDIF
	IF 個別移動枠切り替え
		LOCALS += "</nobr></div>"
	ELSE
		LOCALS += "</nobr></font></clearbutton></div>"
	ENDIF
NEXT
FOR LOCAL, 3, 6
	LOCALS:1 += @"<div rect='{130 + (LOCAL - 3) * 2030}, 130, 2000, 600' padding='50,25,50'"
	IF 個別移動枠切り替え
		LOCALS:1 += " color='#101010' border='31' bcolor='#666666'><nobr><font color='#666666'><clearbutton>"
	ELSE
		LOCALS:1 += " color='#101010' border='31' bcolor='#C0C0C0'><nobr>"
	ENDIF
	IF 個別移動管理キャラ:LOCAL > 0
		LOCALS:1 += @"■%NAME:(個別移動管理キャラ:LOCAL)%<br>"
		CALLFORM エリア情報登録_{CFLAG:(個別移動管理キャラ:LOCAL):現在マップ種別}("エリア名")
		IF MODE_確認_完全一致("ハーヴィンオナホ系", PLAYER, 個別移動管理キャラ:LOCAL) > -1
			LOCALS:1 += @"<font color='#%カラーパレット_HTML("赤ピンク")%'><img src='えっちハート'>%DT_CELL_GETS("体位モードデータベース", MODE_確認_完全一致("ハーヴィンオナホ系", PLAYER, 個別移動管理キャラ:LOCAL), "モード名")%中<img src='えっちハート'></font><br><br>"
		ELSEIF CFLAG:(個別移動管理キャラ:LOCAL):現在マップ種別 == 0 && CFLAG:(個別移動管理キャラ:LOCAL):現在位置 == 999
			LOCALS:1 += @"現在リゾートの外にいます<br><br>"
		ELSE
			LOCALS:1 += @"<button value='追跡移動_{LOCAL}'>現在マップ：%TSTR:場所名受渡%<br>"
			LOCALS:1 += @"現在位置：%GETPLACENAME(CFLAG:(個別移動管理キャラ:LOCAL):現在マップ種別, CFLAG:(個別移動管理キャラ:LOCAL):現在位置)%</button><br>"
		ENDIF
		LOCALS:1 += @"<button value='外す_{LOCAL}'>[個別追跡から外す]</button>"
		LOCALS:1 += @"　　<button value='リスト選択_{LOCAL}'>[リストから選択]</button>"
	ELSE
		LOCALS:1 += @"<br><br><br>　　　　　　　　　　　<button value='リスト選択_{LOCAL}'>[リストから選択]</button>"
	ENDIF
	IF 個別移動枠切り替え
		LOCALS:1 += "</nobr></font></clearbutton></div>"
	ELSE
		LOCALS:1 += "</nobr></div>"
	ENDIF
NEXT
PRINTBUTTON "[枠切り替え]", "枠切り替え"
PRINTL
IF 個別移動枠切り替え
	HTML_PRINT LOCALS:1 + LOCALS, 1
ELSE
	HTML_PRINT LOCALS + LOCALS:1, 1
ENDIF
FOR LOCAL, 0, 7
	PRINTL
NEXT
DRAWLINE
FOR LOCAL, 0, 10
	TSTR:場所名受渡 = 
	TRYCCALLFORM エリア情報登録_{LOCAL}("エリア名")
		IF TSTR:場所名受渡 != ""
			SIF ENUMFUNCENDSWITH(@"大規模エリア建設条件_%TSTR:場所名受渡%") > 0 && MATCH(開拓エリア開発番号, LOCAL) < 1
				CONTINUE
			IF 表示マップ種別 == LOCAL
				SETCOLOR カラーパレット("黄")
				PRINTPLAINFORM [%TSTR:場所名受渡%]
				RESETCOLOR
			ELSE
				PRINTBUTTON @"[%TSTR:場所名受渡%]", @"マップ切り替え_{LOCAL}"
			ENDIF
			PRINT 　　
		ENDIF
	CATCH
		BREAK
	ENDCATCH
NEXT
IF 表示マップ種別 == 999
	SETCOLOR カラーパレット("黄")
	PRINTPLAINFORM [宿泊エリア]
	RESETCOLOR
ELSE
	PRINTBUTTON "[宿泊エリア]", "マップ切り替え_999"
ENDIF
PRINTL
DRAWLINE
FOR LOCAL, 0, 500
	;本来-1になることはないはずだけどなんかそのパターンが存在するのでとりあえず応急措置
	SIF 滞在部屋番号保存:LOCAL == 999
		CONTINUE
	SIF 滞在部屋番号保存:LOCAL < 1
		BREAK
	IF LOCAL / 8 != ページ数
		CONTINUE
	ENDIF
	
	;うふふ中・飲み会中は移動なし
	IF CFLAG:MASTER:飲み会 || CFLAG:MASTER:うふふ
		PRINTFORML ■%GETPLACENAME(表示マップ種別, 滞在部屋番号保存:LOCAL - 1000)%
	ELSE
		PRINTBUTTON @"■%GETPLACENAME(表示マップ種別, 滞在部屋番号保存:LOCAL - 1000)%", 滞在部屋番号保存:LOCAL - 1000
		PRINTL
	ENDIF
	VARSET RESULTS
	SPLIT 滞在者保存:LOCAL, "_", RESULTS
	FOR 配列番号, 0, CHARANUM
		SIF RESULTS:配列番号 == ""
			BREAK
		SIF 配列番号 > 0
			PRINT 、
		IF MATCH(個別移動管理キャラ, TOINT(RESULTS:配列番号))
			PRINTFORM %CALLNAME:TOINT(RESULTS:配列番号)%
			;（{TCVAR:TOINT(RESULTS:配列番号):行動時間ゆらぎ}）
		ELSE
			PRINTBUTTON @"%CALLNAME:TOINT(RESULTS:配列番号)%", @"追跡_%RESULTS:配列番号%"
		ENDIF
	NEXT
	PRINTL
	PRINTL
NEXT
IF ページ数 > 0
	PRINTBUTTONLC "[998] 前のページへ", 998
ELSE
	PRINTLC 　
ENDIF
IF 滞在部屋番号保存:((ページ数 + 1) * 8) > 0
	PRINTBUTTONLC "[997] 次のページへ", 997
ELSE
	PRINTLC 　
ENDIF
PRINTBUTTON "[999] 戻る", 999
TARGET登録 = 0
BINPUTS
IF ISNUMERIC(RESULTS) == 0
	IF STRFIND(RESULTS, "追跡_") > -1
		;追跡キャラ設定
		対象キャラ = TOINT(SUBSTRING(RESULTS, 5))
		DRAWLINE
		LOCAL:1 = 0
		FOR LOCAL, 0, 6
			IF 個別移動管理キャラ:LOCAL < 1
				個別移動管理キャラ:LOCAL = 対象キャラ
				LOCAL:1 = 1
				BREAK
			ENDIF
		NEXT
		IF LOCAL:1
			PRINTFORMW 個別に居場所感知するキャラに%CALLNAME:対象キャラ%を追加しました。
			GOTO 表示_LOOP
		ELSE
			PRINTFORML %CALLNAME:対象キャラ%を個別に居場所感知するキャラに設定します。
			PRINTL 誰と入れ替えますか？
			DRAWLINE
			FOR LOCAL, 0, 6
				IF 個別移動管理キャラ:LOCAL > 0
					PRINTBUTTON @"[{LOCAL}] %NAME:(個別移動管理キャラ:LOCAL)%", LOCAL
					PRINTL
				ENDIF
			NEXT
			PRINTL
			PRINTBUTTON "[999] 戻る", 999
			PRINTL
			BINPUT
			SELECTCASE RESULT
				CASE 999
					GOTO 表示_LOOP
				CASE 0 TO 5
					個別移動管理キャラ:RESULT = 対象キャラ
					GOTO 表示_LOOP
			ENDSELECT
		ENDIF
	ELSEIF STRFIND(RESULTS, "外す_") > -1
		対象キャラ = TOINT(SUBSTRING(RESULTS, 5))
		個別移動管理キャラ:対象キャラ = 0
		GOTO 表示_LOOP
	ELSEIF STRFIND(RESULTS, "リスト選択_") > -1
		LOCAL = TOINT(REPLACE(RESULTS, "リスト選択_", ""))
		CALL 枠選択_リスト式()
		IF RESULT
			個別移動管理キャラ:LOCAL = RESULT
			PRINTFORMW 個別に居場所感知するキャラに%CALLNAME:RESULT%を追加しました。
		ENDIF
		GOTO 表示_LOOP
	ELSEIF STRFIND(RESULTS, "マップ切り替え_") > -1
		表示マップ種別 = TOINT(SUBSTRING(RESULTS, 15))
		ページ数 = 0
		GOTO RE_LOOP
	ELSEIF STRFIND(RESULTS, "追跡移動_") > -1
		対象キャラ = 個別移動管理キャラ:(TOINT(SUBSTRING(RESULTS, 9)))
		RESULTS = {CFLAG:対象キャラ:現在位置}
		表示マップ種別 = CFLAG:対象キャラ:現在マップ種別
		TARGET登録 = 対象キャラ
	ELSEIF RESULTS == "枠切り替え"
		INVERTBIT 個別移動枠切り替え, 0
		GOTO 表示_LOOP
	ENDIF
ENDIF
LOCAL = TOINT(RESULTS)
SELECTCASE LOCAL
	CASE 999
		RETURN 0
	CASE 998
		ページ数 -= 1
		GOTO 表示_LOOP
	CASE 997
		ページ数 += 1
		GOTO 表示_LOOP
	CASEELSE
		;クイック移動処理
		SIF 表示マップ種別 == CFLAG:MASTER:現在マップ種別 && LOCAL == CFLAG:MASTER:現在位置
			RETURN 0
		;まずはエリア移動から
		TFLAG:SELECTCOM受け渡し = 400
		SELECTCOM = 400
		IF 表示マップ種別 != CFLAG:MASTER:現在マップ種別
			IF 表示マップ種別 == 999
				IF CFLAG:MASTER:現在マップ種別 != 0
					TFLAG:エリア切り替え時_目的地格納 = 100
					CALL COM400
				ENDIF
			ELSEIF CFLAG:MASTER:現在マップ種別 == 999
				CFLAG:MASTER:現在マップ種別 = 0
				IF 施設改造度:20:0
					CFLAG:MASTER:現在位置 = 2001
				ELSE
					CFLAG:MASTER:現在位置 = 20
				ENDIF
				IF 表示マップ種別 != 0
					TFLAG:エリア切り替え時_目的地格納 = 表示マップ種別 + 100
					CALL COM400
				ENDIF
			ELSE
				TFLAG:エリア切り替え時_目的地格納 = 表示マップ種別 + 100
				CALL COM400
			ENDIF
		ENDIF
		;どっかで立ち止まったなら以降の処理キャンセル
		SIF RFLAG:立ち止まらないフラグ < 0
			RETURN 0
		;次にエリア内移動
		入室禁止フラグ = 0
		IF 表示マップ種別 == 999
			SELECTCASE LOCAL
				CASE IS >= 2000
					VARSET RESULTS
					SPLIT スイートルーム部屋割り配列:(LOCAL - 2000), "_", RESULTS
					FOR 対象キャラ, 0, RESULT
						IF CFLAG:(TOINT(RESULTS:対象キャラ)):現在マップ種別 == 999 && CFLAG:(TOINT(RESULTS:対象キャラ)):現在位置 == LOCAL && CFLAG:(TOINT(RESULTS:対象キャラ)):睡眠 && 陥落チェック((TOINT(RESULTS:対象キャラ))) == 0
							入室禁止フラグ = 1
						ENDIF
					NEXT
				CASE IS >= 1000
					IF CFLAG:(従業員部屋割り配列:(LOCAL - 1000)):現在マップ種別 == 999 && CFLAG:(従業員部屋割り配列:(LOCAL - 1000)):睡眠 && CFLAG:(従業員部屋割り配列:(LOCAL - 1000)):現在位置 == LOCAL - 1000 && 陥落チェック(従業員部屋割り配列:(LOCAL - 1000)) == 0
						入室禁止フラグ = 1
					ENDIF
				CASEELSE
					IF CFLAG:(滞在者部屋割り配列:LOCAL):現在マップ種別 == 999 && CFLAG:(滞在者部屋割り配列:LOCAL):睡眠 && CFLAG:(滞在者部屋割り配列:LOCAL):現在位置 == LOCAL && 陥落チェック(滞在者部屋割り配列:LOCAL) == 0
						入室禁止フラグ = 1
					ENDIF
			ENDSELECT
			IF 入室禁止フラグ
				PRINTL 部屋には鍵が掛かっている。
				PRINTW どうやら眠っているようだ…
				CFLAG:MASTER:現在マップ種別 = 0
				IF 施設改造度:20:0
					CFLAG:MASTER:現在位置 = 2001
				ELSE
					CFLAG:MASTER:現在位置 = 20
				ENDIF
			ELSE
				CFLAG:MASTER:現在マップ種別 = 999
				CFLAG:MASTER:現在位置 = LOCAL
				SIF TARGET登録
					TFLAG:TARGET変更用 = TARGET登録
			ENDIF
		ELSEIF LOCAL != CFLAG:MASTER:現在位置
			IF CFLAG:MASTER:現在マップ種別 == 2 && LOCAL >= 1000
				;そのまま目的地に移動させる
				;の前に入室禁止処理
				VARSET RESULTS
				SPLIT 定住者部屋割り配列:(LOCAL - 1000), "_", RESULTS
				FOR 対象キャラ, 0, RESULT
					IF CFLAG:(TOINT(RESULTS:対象キャラ)):現在マップ種別 == 2 && CFLAG:(TOINT(RESULTS:対象キャラ)):現在位置 == LOCAL && CFLAG:(TOINT(RESULTS:対象キャラ)):睡眠 && 陥落チェック((TOINT(RESULTS:対象キャラ))) == 0
						入室禁止フラグ = 1
					ENDIF
				NEXT
				IF 入室禁止フラグ
					PRINTL 家には鍵が掛かっている。
					PRINTW どうやら眠っているようだ…
					RETURN 0
				ELSE
					;全員外出時
					入室禁止フラグ = 1
					SPLIT 定住者部屋割り配列:(LOCAL - 1000), "_", RESULTS
					FOR 対象キャラ, 0, RESULT
						IF CFLAG:(TOINT(RESULTS:対象キャラ)):現在マップ種別 == 2 && CFLAG:(TOINT(RESULTS:対象キャラ)):現在位置 == LOCAL
							入室禁止フラグ = 0
						ELSEIF 追従中キャラ(TOINT(RESULTS:対象キャラ))
							入室禁止フラグ = 0
						ENDIF
					NEXT
					IF 入室禁止フラグ
						PRINTL 家には鍵が掛かっている。
						PRINTW どうやら家に誰もいないようだ…
						RETURN 0
					ENDIF
				ENDIF
				CFLAG:MASTER:現在位置 = LOCAL
			ELSEIF CFLAG:MASTER:現在マップ種別 == 1 && LOCAL >= 1000
				;そのまま目的地に移動させる
				;の前に入室禁止処理
				VARSET RESULTS
				入室禁止フラグ = 1
				SPLIT 商業区店割り配列:(LOCAL - 1000):店員配列, "_", RESULTS
				FOR 対象キャラ, 0, RESULT
					IF TOINT(RESULTS:対象キャラ) > 0
						IF CFLAG:(TOINT(RESULTS:対象キャラ)):現在マップ種別 == 1 && CFLAG:(TOINT(RESULTS:対象キャラ)):現在位置 == LOCAL
							入室禁止フラグ = 0
						ENDIF
					ELSEIF TOINT(RESULTS:対象キャラ) < 0
						IF TIME >= 540 && TIME <= 1200
							入室禁止フラグ = 0
						ENDIF
					ENDIF
				NEXT
				IF 入室禁止フラグ
					PRINTL 店には鍵が掛かっている。
					PRINTW どうやら誰もいないようだ…
					RETURN 0
				ENDIF
				CFLAG:MASTER:現在位置 = LOCAL
			ELSE
				TFLAG:長距離移動時_目的地格納 = LOCAL
				CALL SKIP_MOVE(CFLAG:MASTER:現在マップ種別, LOCAL)
				;現在位置を更新し、次も移動の扱いにする
				CFLAG:MASTER:現在位置 = RESULT
				;マップ間をまたいで移動時の処理
				CALL マップ遷移判定処理(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置)
				IF RESULT
					CFLAG:MASTER:現在位置 = RESULT
				ENDIF
				TIME += 1
				TFLAG:SELECTCOM受け渡し = 400
				SELECTCOM = 400
				CALL COM400
				CALL 入室不可処理
				SIF TARGET登録
					TFLAG:TARGET変更用 = TARGET登録
			ENDIF
		ENDIF
ENDSELECT

@枠選択_リスト式()
#DIM 表示候補, 500
#DIM 現在ページ
#DIM 候補人数

;表示候補選定＆ソート処理
VARSET 表示候補
VARSET LOCALS
候補人数 = 0
;フィルタ処理・絞り込み処理
FOR LOCAL, 1, CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	表示候補:(候補人数) = LOCAL
	候補人数 += 1
NEXT

$表示_LOOP
DRAWLINE
VARSET LOCAL
VARSET LOCALS
;表示処理
FOR LOCAL, 現在ページ * 30, (現在ページ * 30) + 30
	SIF 表示候補:LOCAL < 1
		BREAK
	IF CFLAG:(表示候補:LOCAL):ゲージ起動分類 == 1
		LOCALS += "<font color='#FF3399'>"
	ENDIF
	LOCALS += @"<button value = '{表示候補:LOCAL}'>[{表示候補:LOCAL,3}] %ADD_STR_SPACE(NAME:(表示候補:LOCAL), "]", 1),41,LEFT%</button>"
	SIF CFLAG:(表示候補:LOCAL):ゲージ起動分類 == 1
		LOCALS += "</font>"
	IF CFLAG:(表示候補:LOCAL):滞在期間 == 999
		LOCALS += "　　　常時滞在　"
	ELSEIF CFLAG:(表示候補:LOCAL):滞在期間 > 0
		LOCALS += @"　残滞在：{CFLAG:(表示候補:LOCAL):滞在期間, 2}日　"
	ENDIF

	LOCAL:1 += 1
	IF LOCAL:1 % 2 == 0
		HTML_PRINT LOCALS
		LOCALS = 
	ENDIF
NEXT
IF LOCALS != ""
	HTML_PRINT LOCALS
	LOCALS = 
ENDIF
PRINTL 
IF 現在ページ > 0
	PRINTLC  [998]前のページへ
ELSE
	PRINTLC  
ENDIF
IF 表示候補:((現在ページ * 30) + 30) > 0
	PRINTLC  [997]次のページへ
ENDIF
PRINTBUTTON "[999] 戻る", 999
PRINTL
DRAWLINE

BINPUT
SELECTCASE RESULT
	CASE 999
		RETURN 0
	CASE 998
		現在ページ -= 1
		GOTO 表示_LOOP
	CASE 997
		現在ページ += 1
		GOTO 表示_LOOP
	CASEELSE
		RETURN RESULT
ENDSELECT
