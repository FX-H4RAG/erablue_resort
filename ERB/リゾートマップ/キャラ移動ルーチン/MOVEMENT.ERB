;時間経過処理
@CHARA_MOVEMENT
#DIM キャラ登録番号
#DIM 追い出し先
#DIM モード実行中フラグ
#DIM モード番号

FOR LOCAL,0,CHARANUM
	;滞在してないキャラはスルー
	SIF CFLAG:LOCAL:滞在期間 == -1
		CONTINUE
	;マップ間移動処理
	CALL マップ遷移判定処理(CFLAG:LOCAL:現在マップ種別, CFLAG:LOCAL:現在位置)
	IF RESULT
		CFLAG:LOCAL:現在位置 = RESULT
	ENDIF
	; SIF LOCAL == MASTER
	; 	CALL マップ種別変更(LOCAL)
	
;	;睡眠中、MASTERと同室なら追い出す
;	IF LOCAL > 0 && CFLAG:LOCAL:睡眠 && CFLAG:LOCAL:現在位置 == CFLAG:MASTER:現在位置 && !TALENT:LOCAL:恋慕 && FLAG:(300 + CFLAG:MASTER:現在位置)
;		IF CFLAG:LOCAL:衰弱
;			PRINTFORML %CALLNAME:LOCAL%は疲れて眠ってしまった…
;		ELSE
;			PRINTFORML %CALLNAME:LOCAL%に追い出された…
;		ENDIF
;		;追い出し先
;		FOR 追い出し先,0,20
;			;移動可能な部屋が見つかるとそこに移動
;			IF CAN_MOVE_1(CFLAG:MASTER:現在位置, 追い出し先) & 1
;				CFLAG:MASTER:現在位置 = 追い出し先
;				BREAK
;			ENDIF
;		NEXT
;		;うふふリセット
;		CVARSET CFLAG, 317
;		TARGET = 0
;	ENDIF
	
	;BASEの自然変動
	;MASTERと同じ場所にいないとき
	IF CFLAG:LOCAL:現在位置 != CFLAG:MASTER:現在位置 || CFLAG:LOCAL:現在マップ種別 != CFLAG:PLAYER:現在マップ種別
		BASE:LOCAL:ムード = 0
		BASE:LOCAL:理性 = BASE:LOCAL:理性 + TIME_PROGRESS(TFLAG:行動前時刻) * (MAXBASE:LOCAL:理性 + 1000) / ((BASE:LOCAL:理性 + 1000) * 2)
		BASE:LOCAL:理性 = MIN(理性キャップ算出(LOCAL), BASE:LOCAL:理性)
	ENDIF
	;怒りは常に減少する
	BASE:LOCAL:怒り = BASE:LOCAL:怒り - TIME_PROGRESS(TFLAG:行動前時刻) * 5 / (CFLAG:LOCAL:怒り + 1)
	SIF BASE:LOCAL:怒り <= 0
		CFLAG:LOCAL:怒り = 0
	BASE:LOCAL:怒り = MAX(0,BASE:LOCAL:怒り)
	;不機嫌
	SIF CFLAG:LOCAL:不機嫌
		BASE:LOCAL:怒り = MAXBASE:LOCAL:怒り
	;勃起度の自然減少・増加
	IF !TCVAR:LOCAL:イきそう && !CFLAG:LOCAL:うふふ
		BASE:LOCAL:勃起 = MAX(0,BASE:LOCAL:勃起 - TIME_PROGRESS(TFLAG:行動前時刻) * 20)
	ELSE
		BASE:LOCAL:勃起 = MIN(MAXBASE:LOCAL:勃起, BASE:LOCAL:勃起 + TIME_PROGRESS(TFLAG:行動前時刻) * 20)
	ENDIF
	;陥没乳首勃起の自然減少
	SIF TCVAR:LOCAL:陥没乳首露出フラグ && !CFLAG:LOCAL:うふふ
		TCVAR:LOCAL:陥没乳首露出フラグ = MAX(0, TCVAR:LOCAL:陥没乳首露出フラグ - TIME_PROGRESS(TFLAG:行動前時刻))
	;酩酊の処理
	IF BASE:LOCAL:酩酊 > 0
		BASE:LOCAL:酩酊 = BASE:LOCAL:酩酊 - TIME_PROGRESS(TFLAG:行動前時刻) * 5
		BASE:LOCAL:酩酊 = MAX(0, BASE:LOCAL:酩酊)
	ELSEIF BASE:LOCAL:酩酊 < 0
		BASE:LOCAL:酩酊 = BASE:LOCAL:酩酊 + TIME_PROGRESS(TFLAG:行動前時刻)
		BASE:LOCAL:酩酊 = MIN(0, BASE:LOCAL:酩酊)
	ENDIF
	SIF BASE:LOCAL:酩酊 == 0
		CFLAG:LOCAL:酔いすぎ = 0

	;移動インターバルの減少
	TCVAR:LOCAL:移動インターバル時間 = MAX(TCVAR:LOCAL:移動インターバル時間 - TIME_PROGRESS(TFLAG:行動前時刻), 0)

	;ショッピングインターバルの減少
	TCVAR:LOCAL:移動ルーチンインターバル時間 = MAX(TCVAR:LOCAL:移動ルーチンインターバル時間 - TIME_PROGRESS(TFLAG:行動前時刻), 0)

	;会話累積値　100を超えると会話不能フラグ
	SIF TCVAR:LOCAL:会話累積値 > 100
		TCVAR:LOCAL:話題無しフラグ = 1
	;減衰
	TCVAR:LOCAL:会話累積値 = MAX(TCVAR:LOCAL:会話累積値 - TIME_PROGRESS(TFLAG:行動前時刻) , 0)
	;0になると会話不能解除
	SIF TCVAR:LOCAL:会話累積値 == 0
		TCVAR:LOCAL:話題無しフラグ = 0
	
	;身体の清潔度
	LOCAL:1 = CFLAG:LOCAL:風呂
	CFLAG:LOCAL:風呂 += TIME_PROGRESS(TFLAG:行動前時刻)
	IF BATHROOM(CFLAG:LOCAL:現在マップ種別, CFLAG:LOCAL:現在位置)
		SIF LOCAL == 0 && LOCAL:1
			PRINTFORMW %CALLNAME:MASTER%は風呂に浸かった
		CFLAG:LOCAL:風呂 = 0
	ENDIF
	
	SIF LOCAL == MASTER
		CONTINUE
		
	;同行フラグ（1時間持続）
	;うふふ中・隠密中は同行フラグ消えない
	IF CFLAG:LOCAL:うふふ || (CFLAG:LOCAL:隠密 && CFLAG:LOCAL:同行)
		CFLAG:LOCAL:同行 = MAX(CFLAG:LOCAL:同行 - TIME_PROGRESS(TFLAG:行動前時刻),0)
		SIF !CFLAG:LOCAL:同行
			CFLAG:LOCAL:同行 = 1
	ELSE
		CFLAG:LOCAL:同行 = MAX(CFLAG:LOCAL:同行 - TIME_PROGRESS(TFLAG:行動前時刻),0)
		SIF CFLAG:LOCAL:同行 < 1
			CFLAG:LOCAL:デート = 0
	ENDIF
	IF CFLAG:LOCAL:同行 || MODE_確認_完全一致("ハーヴィンオナホ系", PLAYER, LOCAL) > -1
		CFLAG:LOCAL:現在位置 = CFLAG:MASTER:現在位置
		CFLAG:LOCAL:現在マップ種別 = CFLAG:PLAYER:現在マップ種別
	ENDIF

	;うふふ中ではない、かつ同行終わったら隠密消去
	SIF !CFLAG:LOCAL:同行 && !CFLAG:LOCAL:うふふ && LOCAL > 0
		CFLAG:LOCAL:隠密 = 0

	;性欲
	SIF !CFLAG:LOCAL:睡眠
		CALL 性欲ゲージ増加(LOCAL)

	;現在何らかのモードに入っているか
	モード実行中フラグ = 0
	FOR モード番号, 0, DT_ROW_LENGTH("体位モードデータベース")
		IF DT_CELL_GET("体位モードデータベース", モード番号, "実行キャラ") == LOCAL || DT_CELL_GET("体位モードデータベース", モード番号, "対象キャラ") == LOCAL
			モード実行中フラグ = 1
			BREAK
		ENDIF
	NEXT

	;遊びに誘ったフラグ（１時間持続）
	IF CFLAG:LOCAL:一緒に遊ぶ
		CFLAG:LOCAL:一緒に遊ぶ = MAX(CFLAG:LOCAL:一緒に遊ぶ - TIME_PROGRESS(TFLAG:行動前時刻),0)
		SIF CFLAG:LOCAL:うふふ || モード実行中フラグ
			;うふふ中、あるいは何らかのモード実行中は遊ぶが途切れない
			CFLAG:LOCAL:一緒に遊ぶ = MAX(CFLAG:LOCAL:一緒に遊ぶ, 1)
		IF !CFLAG:LOCAL:一緒に遊ぶ
			PRINTFORML %CALLNAME:LOCAL%は遊びに満足したようだ。
		ENDIF
	ENDIF

	;野外オナニー処理
	IF 性癖素質:LOCAL:野外オナニー && !CFLAG:LOCAL:うふふ
		CALL 野外オナニー判定(LOCAL)
		;オナニー中は移動しない
		SIF IS_野外オナニー中(LOCAL)
			CONTINUE
	ENDIF

	SIF CFLAG:LOCAL:うふふ
		CONTINUE
	SIF CFLAG:LOCAL:同行
		CONTINUE
	SIF CFLAG:LOCAL:飲み会
		CONTINUE
	SIF CFLAG:LOCAL:一緒に遊ぶ
		CONTINUE
	SIF モード実行中フラグ
		CONTINUE
	;部屋移動
	CALL CHARA_MOVEMENT_T(LOCAL)
NEXT

LOCAL:1 = 滞在キャラ数算出関数(2)

FOR LOCAL, 1, マップ部屋番号上限
	SIF LOCAL == 20
		CONTINUE
	IF 存在しない移動先(0, LOCAL) == 0
		雑務ゲージ:LOCAL = MIN(雑務ゲージ:LOCAL + (TIME_PROGRESS(TFLAG:行動前時刻) * LOCAL:1 / 120), 10000)
	ENDIF
NEXT

@CHARA_MOVEMENT_ACTIVETIME(ARG)
#DIM 寝る
#DIM 起きる

CALL CHARA_ROUTINE_STATUS(ARG, "就寝時刻")
寝る = RESULT
CALL CHARA_ROUTINE_STATUS(ARG, "起床時刻")
起きる = RESULT

;時間設定ない場合は22時寝の8時起き
SIF !寝る
	寝る = 1320
SIF !起きる
	起きる = 480

;泥酔時は常時寝る
SIF CFLAG:ARG:酔いすぎ == 1
	寝る = 0
;体力限界時は眠る
SIF CFLAG:ARG:体力限界 == 1
	寝る = 0

;PLAYERがうふふ、同じ部屋にいる、隠密状態が同じ場合は寝ない
SIF CFLAG:MASTER:うふふ && CFLAG:MASTER:隠密 == CFLAG:ARG:隠密 && CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置 && CFLAG:MASTER:現在マップ種別 == CFLAG:ARG:現在マップ種別
	RETURN 0

RETURN TIME < 起きる || TIME > 寝る


;@CHARA_MOVEMENT_MOVERATE(ARG)
;;キャラごとの移動率補正呼び出し
;RETURN 90 + CFLAG:ARG:移動率補正


@CHARA_MOVEMENT_KOUHO(ARG,ARG:1)
#DIM 移動先候補
#DIM 移動キャラ

移動先候補 = ARG
移動キャラ = ARG:1

;よく行く場所はあらゆる条件を無視して確率上昇

SIF CFLAG:移動キャラ:よく行く場所 == 移動先候補
	RETURN 200

SELECTCASE 移動先候補
	CASE 7,9,10,13
		;女部分があるなら男子には入らない
		IF TALENT:移動キャラ:性別 & 1 == 1
			RETURN 0
		ELSE
			RETURN 100
		ENDIF
	CASE 8,11,12,14
		;女部分がないなら女子には入らない
		IF TALENT:移動キャラ:性別 & 1 == 1
			RETURN 100
		ELSE
			RETURN 0
		ENDIF
	CASE 20
		;基本自室には来ない（後々恋慕状態だと来るように再設定すること）
		RETURN 0
	;ほかはまんべんなく移動する
	CASEELSE
		RETURN 100
ENDSELECT

;ゲーム内現在時刻をゲーム開始時からの経過した分で返す
@NOW()
#FUNCTION
RETURNF TIME + DAY * 24 * 60

@TIME_PROGRESS(ARG)
#FUNCTION
RETURNF TIME + DAY * 1440 - ARG

@CHARA_SLEEP, ARG
#DIM 睡眠場所指定
#DIM 睡眠マップ
#DIM 睡眠場所
#DIM 部屋割
VARSET LOCAL

;現在、キャラごとの個室がないのでまるごとオミット、寝る時にどっか行くだけ

;IF (CAN_MOVE_1(CFLAG:MASTER:現在位置, CFLAG:ARG:現在位置) == 2 || CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置) && !CFLAG:MASTER:睡眠
;	IF CFLAG:ARG:現在位置 == CFLAG:ARG:311
;		IF TALENT:ARG:恋慕 == 1
;			PRINTFORMW %CALLNAME:ARG%は眠っ\@CFLAG:ARG:睡眠 ? ている # た\@
;			LOCAL = 2
;		ELSE
;			PRINTFORMW %CALLNAME:ARG%は眠そうにしている
;			LOCAL = 1
;		ENDIF
;	ELSE
;		IF CFLAG:ARG:衰弱 && TALENT:ARG:恋慕 && CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置
;			PRINTFORMW %CALLNAME:ARG%を%GETPLACENAME(CFLAG:ARG:現在位置)%から%GETPLACENAME(CFLAG:ARG:311)%へ運びました
;			LOCAL = 3
;		ELSE
;			PRINTFORMW %CALLNAME:ARG%は%GETPLACENAME(CFLAG:ARG:現在位置)%から%GETPLACENAME(CFLAG:ARG:311)%に戻りました
;			LOCAL = 0
;		ENDIF
;	ENDIF
;ENDIF

IF !CFLAG:ARG:睡眠
	CALL CHARA_ROUTINE_STATUS(ARG, "就寝場所")
	睡眠場所指定 = RESULT
	IF 睡眠場所指定
		睡眠マップ = RESULT:1
		睡眠場所 = RESULT:2
	ENDIF
	CFLAG:ARG:同行 = 0
	CFLAG:ARG:デート = 0
	CFLAG:ARG:睡眠 = 1
	CFLAG:ARG:うふふ = 0
	CALL 性欲ゲージ減少処理(ARG)
	TCVAR:ARG:初うふふフラグ = 0

	IF CFLAG:ARG:酔いすぎ == 1
		IF CFLAG:MASTER:酔いすぎ != 1 && CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置
			;あなたがいる場合は部屋へと運ぶ
			CFLAG:ARG:現在位置 = キャラクター部屋検索(ARG)
			CFLAG:ARG:現在マップ種別 = 部屋検索_マップ種別
			PRINTFORMW %CALLNAME:MASTER%は酔いつぶれてしまった%CALLNAME:ARG%を部屋へと運びました。
		ENDIF
	ELSEIF CFLAG:ARG:体力限界
		IF CFLAG:MASTER:酔いすぎ != 1 && CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置
			;あなたがいる場合は部屋へと運ぶ
			CFLAG:ARG:現在位置 = キャラクター部屋検索(ARG)
			CFLAG:ARG:現在マップ種別 = 部屋検索_マップ種別
			PRINTFORMW %CALLNAME:MASTER%は疲れ果ててしまった%CALLNAME:ARG%を部屋へと運びました。
		ENDIF
	ELSEIF CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置 && CFLAG:MASTER:現在マップ種別 == CFLAG:ARG:現在マップ種別
		IF 睡眠場所指定
			IF CFLAG:MASTER:現在位置 == 睡眠場所 && CFLAG:MASTER:現在マップ種別 == 睡眠マップ
				PRINTFORMW %CALLNAME:ARG%はこのまま%GETPLACENAME(睡眠マップ, 睡眠場所)%で眠るようだ。
				TRYCALLFORM KOJO_EVENT_この場おやすみ_{NO:ARG}
			ELSE
				PRINTFORMW %CALLNAME:ARG%は%GETPLACENAME(睡眠マップ, 睡眠場所)%で眠るようだ。
				TRYCALLFORM KOJO_EVENT_移動おやすみ_{NO:ARG}
			ENDIF
		ELSE
			部屋割 = キャラクター部屋検索(ARG)
			IF CFLAG:MASTER:現在位置 == 部屋割 && CFLAG:MASTER:現在マップ種別 == 部屋検索_マップ種別
				PRINTFORMW %CALLNAME:ARG%はこのまま部屋で眠るようだ。
				TRYCALLFORM KOJO_EVENT_この場おやすみ_{NO:ARG}
			ELSE
				PRINTFORMW %CALLNAME:ARG%は部屋に戻って眠るようだ。
				TRYCALLFORM KOJO_EVENT_移動おやすみ_{NO:ARG}
			ENDIF
		ENDIF
	ENDIF
	
	IF CFLAG:ARG:酔いすぎ == 1 || CFLAG:ARG:体力限界
		;泥酔時は基本そのまま
		;体力限界時はあなたが同じ部屋にいるはずなので運んだ後のはず
	ELSEIF 睡眠場所指定
		;寝る場所指定がある場合はそこへ
		CFLAG:ARG:現在位置 = 睡眠場所
		CFLAG:ARG:現在マップ種別 = 睡眠マップ
	ELSE
		;指定なしの場合、場所を宿泊エリアの個室に設定する
		CFLAG:ARG:現在位置 = キャラクター部屋検索(ARG)
		CFLAG:ARG:現在マップ種別 = 部屋検索_マップ種別
	ENDIF

	IF CFLAG:MASTER:現在位置 == CFLAG:ARG:現在位置 && CFLAG:MASTER:現在マップ種別 == CFLAG:ARG:現在マップ種別
		;もし同じ部屋にあなたがいるなら追い出される
		IF 陥落チェック(ARG) == 0
			PRINTFORMW %CALLNAME:MASTER%は追い出されてしまった……
			LOCAL = キャラクター部屋検索(ARG)
			IF CFLAG:ARG:現在位置 == 303
				CFLAG:ARG:現在位置 = 307
			ELSEIF 部屋検索_マップ種別 == 999
				CFLAG:PLAYER:現在マップ種別 = 0
				CFLAG:MASTER:現在位置 = 20
				宿泊エリア表示種別 = 0
				CALL 宿泊エリア処理
			ELSEIF 部屋検索_マップ種別 == 2
				CFLAG:PLAYER:現在マップ種別 = 2
				CFLAG:MASTER:現在位置 = 1
				宿泊エリア表示種別 = 2
				CALL 宿泊エリア処理
			ENDIF
		ENDIF
	ENDIF

	;服をリセット
	CALL CLOTHES_RESET(ARG)
	CALL CLOTHES_RESET_TRAIN(ARG)
	;二日酔い処理のため、酩酊が残ってたら絶対値を負に
	BASE:ARG:酩酊 = BASE:ARG:酩酊 * -1
ENDIF

CALL 特定キャラモードリセット(ARG)

CFLAG:ARG:同行 = 0
CFLAG:ARG:デート = 0
CFLAG:ARG:睡眠 = 1
CFLAG:ARG:飲み会 = 0
CFLAG:ARG:うふふ = 0
CALL 性欲ゲージ減少処理(ARG)
TCVAR:ARG:初うふふフラグ = 0


;SIF LOCAL == 3
;	CFLAG:MASTER:現在位置 = CFLAG:ARG:311
;施錠
;FLAG:(300 + CFLAG:ARG:現在位置) = 1

@CHARA_AWAKE, ARG


IF CFLAG:ARG:睡眠 && CFLAG:ARG:現在位置 == CFLAG:MASTER:現在位置
	SIF ARG > 0
	PRINTFORMW %CALLNAME:ARG%が目を覚ました
	;おはよう口上
	TRYCALLFORM KOJO_EVENT_おはよう_{NO:ARG}
ENDIF

IF CFLAG:ARG:睡眠
	CALL CHARA_MOVEMENT_ACTIVETIME(ARG)
	;睡眠時間
	IF RESULT
		CALL CHARA_SLEEP, ARG
		RETURN 0
	ENDIF
	PRINTFORML %CALLNAME:ARG%が目を覚ましたようだ。
	CALL CHARA_ROUTINE_STATUS(ARG, "初期位置")
	;起きても酩酊が残ってるなら二日酔い
	SIF BASE:ARG:酩酊
		CFLAG:ARG:酔いすぎ = 2
ENDIF
CFLAG:ARG:睡眠 = 0
RETURN 1



;ARG 移動させるキャラ番号
@CHARA_MOVEMENT_T(ARG)
;活動時間　睡眠時間ならRESULTに真、活動時間ならRESULTに偽を代入
CALL CHARA_MOVEMENT_ACTIVETIME(ARG)

;睡眠時間
IF RESULT
	CALL CHARA_SLEEP, ARG
	RETURN 0
;活動時間、CFLAG:睡眠が立っていれば起床処理
ELSE
	CALL CHARA_AWAKE, ARG
	SIF RESULT == 0
		RETURN 0
ENDIF

;スケジュールに基づいた移動処理
IF TCVAR:ARG:移動インターバル時間 == 0
	CALL MOVE_ON_SCHEDULE(ARG)
	IF RESULT > 0
		;RETURNが１＝移動したという意味なので3分のインターバル
		TCVAR:ARG:移動インターバル時間 = 3
	ENDIF
ENDIF

RETURN 1
