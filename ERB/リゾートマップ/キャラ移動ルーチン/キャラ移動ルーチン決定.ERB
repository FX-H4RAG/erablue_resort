;キャラが一日どう過ごすかをここで決定し、それに準ずる行動範囲を設定する。
@移動ルーチンセット決定(ARG)
#DIM ループ用
#DIM ルーチン候補配列, 10
#DIM ショッピング候補配列, 10

移動ルーチン:ARG:ルーチンID = 
移動ルーチン:ARG:マップ内ルーチンID = 
移動ルーチン:ARG:PMルーチンID = 
移動ルーチン:ARG:PMマップ内ルーチンID = 

;日付限定のイベントなどがあるかどうかをチェック
;イベント番号順に関数が存在するかを見る
VARSET RESULTS
VARSET RESULT
IF 開催予定日取り == DAY
	TRYCALLFORM MOVE_ROUTINE_%開催予定祭り名%(ARG, "参加判定")
	;1が帰ってきたら参加なのでIDに入力
	IF RESULT
		移動ルーチン:ARG:ルーチンID = %開催予定祭り名%
	ENDIF
ENDIF

;イベントでルーチンが入ったなら関数を抜ける
SIF 移動ルーチン:ARG:ルーチンID != ""
	RETURN 0


;従業員の業務で居場所指定があるかどうかをチェック
;イベント番号順に関数が存在するかを見る
VARSET RESULTS
VARSET RESULT
TRYCALLFORM MOVE_ROUTINE_業務_%現在仕事数字文字列変換(現在仕事:ARG:0)%(ARG, "参加判定")
;1が帰ってきたら仕事なのでIDに入力して関数を抜ける
IF RESULT
	移動ルーチン:ARG:ルーチンID = 業務_%現在仕事数字文字列変換(現在仕事:ARG:0)%
	RETURN 0
ENDIF

;キャラ個別移動関数があるかをチェック（未実装））
;TRYCCALLFORM ～～～
;	SIF RESULT
;		RETURN 0
;CATCH
;ENDCATCH
;
;キャラ個別でルーチンが入ったなら関数を抜ける
;SIF 移動ルーチン:ARG:ルーチンID != ""
;	RETURN 0

;午前と午後で違うルーチンのキャラも居ていいよね
DO
;1/3の確率（実際はもう少し高い）で午前も午後も同じ行動をする
IF 移動ルーチン:ARG:PMルーチンID != "" && !RAND:3
	SWAP 移動ルーチン:ARG:ルーチンID, 移動ルーチン:ARG:PMルーチンID
	SWAP 移動ルーチン:ARG:マップ内ルーチンID, 移動ルーチン:ARG:PMマップ内ルーチンID
	BREAK
ENDIF

;最後に汎用移動ルーチンの選択
;1:プールで遊ぶ、2:外で遊ぶ、3:遊戯室で遊ぶは常時発生
;他は条件満たした時出現
LOCAL = 3
ルーチン候補配列:0 = 1
ルーチン候補配列:1 = 2
ルーチン候補配列:2 = 3
LOCAL:2 = 0
IF ダンジョンクリアフラグ_沿岸部の森
	FOR LOCAL:1, 3, 9
		IF 空艇港店舗状況:(LOCAL:1):0 > 0 && 空艇港店舗状況:(LOCAL:1):0 != 3
			LOCAL:2 += 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:2 > 0
		ルーチン候補配列:LOCAL = 4
		LOCAL += 1
	ENDIF
ENDIF


移動ルーチン:ARG:ルーチンID = %TOSTR(ルーチン候補配列:(RAND:LOCAL))%


;選択箇所が改造済みならば更に分岐
SELECTCASE 移動ルーチン:ARG:ルーチンID
	CASE "1"
		IF 施設改造度:15:0
			;プールサイド、メインプール、ボールプール、流れるプールの４択
			移動ルーチン:ARG:マップ内ルーチンID = %TOSTR(RAND:4 + 3)%
			;ハーヴィンはメインプールに行かない
			SIF TALENT:ARG:種族 == 4 && 移動ルーチン:ARG:マップ内ルーチンID == "4"
				移動ルーチン:ARG:マップ内ルーチンID = 5
		ENDIF
	CASE "2"
		IF 施設改造度:3:0
			;メインテント、バーベキューエリア、渓流エリア、サウナテントの４択
			LOCAL = 4
			;訓練場増築済みだと5択
			SIF 施設改造度:3:8
				LOCAL += 1
			移動ルーチン:ARG:マップ内ルーチンID = %TOSTR(RAND:LOCAL + 3)%
			;釣り好きはキャンプ場にいると絶対渓流に行く
			SIF 知識素質:ARG:釣り好き
				移動ルーチン:ARG:マップ内ルーチンID = 5
		ENDIF
	CASE "3"
		IF 施設改造度:5:0
			;バーエリア、ボードゲームエリア、卓球場の３択
			LOCAL = 3
			;図書館増築済みだと4択
			SIF 施設改造度:5:6
				LOCAL += 1
			移動ルーチン:ARG:マップ内ルーチンID = %TOSTR(RAND:LOCAL + 3)%
		ENDIF
ENDSELECT

SWAP 移動ルーチン:ARG:ルーチンID, 移動ルーチン:ARG:PMルーチンID
SWAP 移動ルーチン:ARG:マップ内ルーチンID, 移動ルーチン:ARG:PMマップ内ルーチンID

LOOP 移動ルーチン:ARG:ルーチンID == ""



@脱衣所場所算出(キャラ番号, マップ種別取得フラグ)
#FUNCTION
#DIM マップ種別取得フラグ
#DIM キャラ番号

SIF マップ種別取得フラグ
	RETURNF 0

IF 施設改造度:7:0
	;温泉改造済時
	;他のルーチンと風呂場ルーチンが干渉しないよう、脱衣所に入る時点でインターバル時間をリセット
	TCVAR:キャラ番号:移動ルーチンインターバル時間 = 0
	IF TALENT:キャラ番号:性別 == 2
		RETURNF 702
	ELSE
		RETURNF 802
	ENDIF
ELSE
	IF TALENT:キャラ番号:性別 == 2
		RETURNF 7
	ELSE
		RETURNF 8
	ENDIF
ENDIF

@風呂場算出(キャラ番号, マップ種別取得フラグ)
#FUNCTION
#DIM マップ種別取得フラグ
#DIM キャラ番号

SIF マップ種別取得フラグ
	RETURNF 0

IF 施設改造度:7:0
	;温泉改造済時
	IF TCVAR:キャラ番号:移動ルーチンインターバル時間 > 0
		RETURNF TOINT(移動ルーチン:キャラ番号:マップ内ルーチンID)
	ENDIF

	TCVAR:キャラ番号:移動ルーチンインターバル時間 = RAND(30, 60)
	IF TALENT:キャラ番号:性別 == 2
		移動ルーチン:キャラ番号:マップ内ルーチンID = %TOSTR(703 + RAND:6)%
	ELSE
		移動ルーチン:キャラ番号:マップ内ルーチンID = %TOSTR(803 + RAND:6)%
	ENDIF
	RETURNF TOINT(移動ルーチン:キャラ番号:マップ内ルーチンID)
ELSE
	IF TALENT:キャラ番号:性別 == 2
		RETURNF 9 + RAND:2
	ELSE
		RETURNF 11 + RAND:2
	ENDIF
ENDIF
