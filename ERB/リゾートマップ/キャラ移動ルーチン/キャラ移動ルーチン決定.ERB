;キャラが一日どう過ごすかをここで決定し、それに準ずる行動範囲を設定する。
@移動ルーチンセット決定(ARG)
#DIM ループ用

;日付限定のイベントなどがあるかどうかをチェック
;イベント番号順に関数が存在するかを見る
FOR ループ用, 100, 10
	TRYCALLFORM EVENT_FESTIVAL_{ループ用}
	;数字の若いイベントが開催中ならその場でループ中断
	SIF RESULT
		BREAK
NEXT

;イベントでルーチンが入ったなら関数を抜ける
SIF TCVAR:ARG:ルーチン番号
	RETURN 0


;キャラ個別移動関数があるかをチェック（未実装））
;TRYCCALLFORM ～～～
;	SIF RESULT
;		RETURN 0
;CATCH
;ENDCATCH
;
;キャラ個別でルーチンが入ったなら関数を抜ける
;SIF TCVAR:ARG:ルーチン番号
;	RETURN 0


;最後に汎用移動ルーチンの選択
;とりあえずは1:プールで遊ぶ、2:外で遊ぶ、3:遊戯室で遊ぶの三択
TCVAR:ARG:ルーチン番号 = RAND:3 + 1


;選択箇所が改造済みならば更に分岐
SELECTCASE TCVAR:ARG:ルーチン番号
	CASE 1
		IF 施設改造度:15:0
			;プールサイド、メインプール、ボールプール、流れるプールの４択
			TCVAR:ARG:マップ内ルーチン番号 = RAND:4 + 3
			;ハーヴィンはメインプールに行かない
			SIF TALENT:ARG:種族 == 4 && TCVAR:ARG:マップ内ルーチン番号 == 4
				TCVAR:ARG:マップ内ルーチン番号 += 1
		ENDIF
	CASE 2
		IF 施設改造度:3:0
			;メインテント、バーベキューエリア、渓流エリア、サウナテントの４択
			TCVAR:ARG:マップ内ルーチン番号 = RAND:4 + 3
			;釣り好きはキャンプ場にいると絶対渓流に行く
			SIF 知識素質:ARG:釣り好き
				TCVAR:ARG:マップ内ルーチン番号 = 5
		ENDIF
	CASE 3
		IF 施設改造度:5:0
			;バーエリア、ボードゲームエリア、卓球場の３択
			LOCAL = 3
			;図書館増築済みだと4択
			SIF 施設改造度:5:1
				LOCAL += 1
			TCVAR:ARG:マップ内ルーチン番号 = RAND:LOCAL + 3
		ENDIF
ENDSELECT


