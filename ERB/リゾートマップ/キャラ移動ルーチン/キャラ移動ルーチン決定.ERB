;キャラが一日どう過ごすかをここで決定し、それに準ずる行動範囲を設定する。
@移動ルーチンセット決定(ARG)

移動ルーチン:ARG:ルーチンID = 
移動ルーチン:ARG:PMルーチンID = 

;日付限定のイベントなどがあるかどうかをチェック
;イベント番号順に関数が存在するかを見る
IF 開催予定日取り == DAY
	TRYCCALLFORM MOVE_ROUTINE_%開催予定祭り名%(ARG, "参加判定")
		;1が帰ってきたら参加なのでIDに入力
		IF RESULT
			移動ルーチン:ARG:ルーチンID = %開催予定祭り名%
			CALLFORM ROUTINE_SCHEDULE_%開催予定祭り名%(ARG)
			RETURN 0
		ENDIF
	CATCH
	ENDCATCH
ENDIF

;従業員の業務で居場所指定があるかどうかをチェック
;イベント番号順に関数が存在するかを見る
TRYCCALLFORM MOVE_ROUTINE_業務_%現在仕事数字文字列変換(現在仕事:ARG:0)%(ARG, "参加判定")
	;1が帰ってきたら仕事なのでIDに入力して関数を抜ける
	IF RESULT
		移動ルーチン:ARG:ルーチンID = 業務_%現在仕事数字文字列変換(現在仕事:ARG:0)%
		CALLFORM ROUTINE_SCHEDULE_業務_%現在仕事数字文字列変換(現在仕事:ARG:0)%(ARG)
		RETURN 0
	ENDIF
CATCH
ENDCATCH

;キャラ個別移動関数があるかをチェック（未実装）
;TRYCCALLFORM ～～～
;	IF RESULT
;		移動ルーチン:ARG:ルーチンID = ～～～
;		CALLFORM ROUTINE_SCHEDULE_～～～(ARG)
;		RETURN 0
;	ENDIF
;CATCH
;ENDCATCH
;
;午前と午後で違うルーチンのキャラも居ていいよね
CALL 汎用移動ルーチン決定(ARG, GETNUM(移動ルーチン, "ルーチンID"))

;1/3の確率（実際はもう少し高い）で午前も午後も同じ行動をする
IF !RAND:3
	CALL 汎用移動ルーチン決定(ARG, GETNUM(移動ルーチン, "PMルーチンID"))
ENDIF

;スケジュールの設定
IF 移動ルーチン:ARG:PMルーチンID != ""
	CALLFORM ROUTINE_SCHEDULE_AM_%移動ルーチン:ARG:ルーチンID%(ARG)
	CALLFORM ROUTINE_SCHEDULE_PM_%移動ルーチン:ARG:PMルーチンID%(ARG)
ELSE
	CALLFORM ROUTINE_SCHEDULE_%移動ルーチン:ARG:ルーチンID%(ARG)
ENDIF

@汎用移動ルーチン決定(ARG, ルーチンID格納先)
#DIM ルーチンID格納先
#DIMS ルーチン候補配列, 10

;プールで遊ぶ、外で遊ぶ、遊戯室で遊ぶは常時発生
;他は条件満たした時出現
LOCAL = 3
ルーチン候補配列:0 '= "プールで遊ぶ"
ルーチン候補配列:1 '= "外で遊ぶ"
ルーチン候補配列:2 '= "遊戯室で遊ぶ"
LOCAL:2 = 0
IF ダンジョンクリアフラグ_沿岸部の森
	FOR LOCAL:1, 3, 9
		IF 空艇港店舗状況:(LOCAL:1):0 > 0 && 空艇港店舗状況:(LOCAL:1):0 != 3
			LOCAL:2 += 1
			BREAK
		ENDIF
	NEXT
	IF LOCAL:2 > 0
		ルーチン候補配列:LOCAL '= "空艇港でショッピング"
		LOCAL += 1
	ENDIF
ENDIF

移動ルーチン:ARG:ルーチンID格納先 '= ルーチン候補配列:(RAND:LOCAL)

@キャラ就寝場所セット(ARG)
SIF ARG == MASTER
	RETURN
;現在位置が自室なら泥酔や体力限界で自室に運ばれた可能性があるのでそのままにする
SIF CFLAG:ARG:現在位置 == キャラクター部屋検索(ARG) && CFLAG:ARG:現在マップ種別 == 999
	RETURN

CALL CHARA_ROUTINE_STATUS(ARG, "就寝場所")
IF RESULT
	CFLAG:ARG:現在マップ種別 = RESULT:1
	CFLAG:ARG:現在位置 = RESULT:2
ELSE
	CFLAG:ARG:現在マップ種別 = 999
	CFLAG:ARG:現在位置 = キャラクター部屋検索(ARG)
ENDIF

@CHARA_ROUTINE_STATUS(ARG, ARGS)
IF ARGS == "就寝時刻" || ARGS == "就寝場所"
	TRYCCALLFORM MOVE_ROUTINE_%移動ルーチン:ARG:PMルーチンID%(ARG, ARGS)
		RETURN RESULT
	CATCH
	ENDCATCH
ENDIF

TRYCCALLFORM MOVE_ROUTINE_%移動ルーチン:ARG:ルーチンID%(ARG, ARGS)
	RETURN RESULT
CATCH
ENDCATCH
RETURN 0
