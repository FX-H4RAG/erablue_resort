;------------------------------------------------------------------------------
;業務ルーチン 業務：販売、かつ店舗が指定されていること
;------------------------------------------------------------------------------
@MOVE_ROUTINE_業務_販売(キャラ番号, ARGS)
#DIM キャラ番号
#DIM 時間計算

SELECTCASE ARGS
	CASE "参加判定"
		;現在仕事:キャラ番号:1に数字が入っている＝店舗常駐である
		SIF 現在仕事:キャラ番号:1 > 0
			RETURN 1

	CASE "起床時刻"
		時間計算 = 450 - (TALENT:キャラ番号:早起き * 30)
		RETURN 時間計算
	
	CASE "就寝時刻"
		時間計算 = 1320 - (TALENT:キャラ番号:早寝 * 60)
		RETURN 時間計算
	
	CASE "初期位置"
		CFLAG:キャラ番号:現在位置 = 6
		CFLAG:キャラ番号:現在マップ種別 = 0
	
	CASE "移動処理"
		CALL MOVE_ROUTINE_移動処理(キャラ番号, "業務_販売")
		RETURN RESULT
ENDSELECT

@ROUTINE_PLAN_業務_販売(キャラ番号, マップ種別取得フラグ = 0)
#DIM マップ種別取得フラグ
#DIM キャラ番号

;時間ごとにどの場所にいるべきかを決定
SELECTCASE TIME
	;19時半より前は店舗にいる
	CASE IS < 1170
		SIF マップ種別取得フラグ
			RETURN 1
		RETURN 現在仕事:キャラ番号:1
	;風呂の前後１５分は脱衣所
	CASE IS < 1185
		SIF マップ種別取得フラグ
			RETURN 0
		IF TALENT:キャラ番号:性別 == 2
			RETURN 7
		ELSE
			RETURN 8
		ENDIF
	;20時半までは風呂にいる
	CASE IS < 1230
		SIF マップ種別取得フラグ
			RETURN 0
		IF TALENT:キャラ番号:性別 == 2
			RETURN 9
		ELSE
			RETURN 11
		ENDIF
	;風呂の前後１５分は脱衣所
	CASE IS < 1245
		SIF マップ種別取得フラグ
			RETURN 0
		IF TALENT:キャラ番号:性別 == 2
			RETURN 7
		ELSE
			RETURN 8
		ENDIF
	;寝るまでは自室にいる
	CASEELSE
		SIF マップ種別取得フラグ
			RETURN 999
		RETURN 999
ENDSELECT


