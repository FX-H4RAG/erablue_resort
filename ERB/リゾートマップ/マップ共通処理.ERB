
;---------------------------------------------------------------
;マップエリア名一覧
;---------------------------------------------------------------
@マップエリア名取得(ARG)
#FUNCTIONS

SELECTCASE ARG
	CASE 0
		RETURNF "リゾートエリア"
	CASE 1
		RETURNF "空艇港エリア"
	CASE 100
		RETURNF @"%SUBSTRING(開催予定祭り名, 9)%マップ"
	CASE 999
		RETURNF "宿泊エリア"
ENDSELECT

;---------------------------------------------------------------
;現在地によるマップ種別変更処理
;---------------------------------------------------------------
; @マップ種別変更(ARG)

; IF CFLAG:MASTER:現在位置 >= 303 && CFLAG:MASTER:現在位置 <= 307
; 	;キャンプ
; 	TFLAG:マップ種別 = 3
; ELSEIF CFLAG:MASTER:現在位置 >= 503 && CFLAG:MASTER:現在位置 <= 506
; 	;遊戯室
; 	TFLAG:マップ種別 = 5
; ELSEIF CFLAG:MASTER:現在位置 >= 702 && CFLAG:MASTER:現在位置 <= 711
; 	;男湯
; 	TFLAG:マップ種別 = 7
; ELSEIF CFLAG:MASTER:現在位置 >= 802 && CFLAG:MASTER:現在位置 <= 811
; 	;女湯
; 	TFLAG:マップ種別 = 8
; ELSEIF CFLAG:MASTER:現在位置 >= 1503 && CFLAG:MASTER:現在位置 <= 1506
; 	;プール
; 	TFLAG:マップ種別 = 15
; ELSEIF CFLAG:MASTER:現在位置 <= 20
; 	TFLAG:マップ種別 = 0
; ENDIF

;---------------------------------------------------------------
;キャラの居場所再設定
;---------------------------------------------------------------
@派生マップ番号取得(マップ番号, 現在地番号)
#FUNCTION
#DIM マップ番号
#DIM 現在地番号

SELECTCASE マップ番号
	CASE 0
		;リゾートマップ
		SELECTCASE 現在地番号
			CASE 1 TO 20
				RETURNF 0
			CASE 302 TO 308
				RETURNF 3
			CASE 502 TO 506
				RETURNF 5
			CASE 701 TO 711
				RETURNF 7
			CASE 801 TO 811
				RETURNF 8
			CASE 1503 TO 1515
				RETURNF 15
		ENDSELECT
ENDSELECT

@マップ遷移判定処理(マップ番号, 現在地番号)
#DIM マップ番号
#DIM 現在地番号

SELECTCASE マップ番号
	CASE 0
		;基本→キャンプ
		IF 現在地番号 == 3 && 施設改造度:3:0
			RETURN 303
		ENDIF
		;基本→遊戯
		IF 現在地番号 == 5 && 施設改造度:5:0
			RETURN 503
		ENDIF
		;基本→男湯
		IF 現在地番号 == 7 && 施設改造度:7:0
			RETURN 702
		ENDIF
		;基本→女湯
		IF 現在地番号 == 8 && 施設改造度:8:0
			RETURN 802
		ENDIF
		;基本→プール
		IF 現在地番号 == 15 && 施設改造度:15:0
			RETURN 1503
		ENDIF
		;キャンプ→基本
		IF 現在地番号 == 302
			RETURN 2
		ENDIF
		;遊戯→基本
		IF 現在地番号 == 502
			RETURN 2
		ENDIF
		;男湯→基本
		IF 現在地番号 == 701
			RETURN 6
		ENDIF
		;男湯→女湯
		IF 現在地番号 == 709
			RETURN 810
		ENDIF
		;女湯→基本
		IF 現在地番号 == 801
			RETURN 6
		ENDIF
		;女湯→男湯
		IF 現在地番号 == 809
			RETURN 710
		ENDIF
		;プール→基本
		IF 現在地番号 == 1513
			RETURN 13
		ENDIF
		IF 現在地番号 == 1514
			RETURN 14
		ENDIF
ENDSELECT

RETURN 0

;---------------------------------------------------------------
;マップ種別による移動先可否判定
;---------------------------------------------------------------
@存在しない移動先(ARG:1, ARG)
#FUNCTION

SELECTCASE ARG:1
	CASE 0
		IF ARG < 50
			IF 施設改造度:ARG:0 > 0
				;改装済みの場合、改装前の部屋は存在しない
				RETURNF 1
			ENDIF
			IF GROUPMATCH(ARG, 9, 10, 11, 12) && 施設改造度:7:0
				;温泉改装済みの場合、改装前の風呂場は存在しない
				RETURNF 1
			ENDIF
		ENDIF
		IF (ARG >= 1 && ARG <= 15) || ARG == 20
			;基本マップ
			RETURNF 0
		ELSEIF ARG >= 303 && ARG <= 307 && 施設改造度:3:0
			;キャンプ場
			RETURNF 0
		ELSEIF ARG == 308 && 施設改造度:3:8
			;訓練場
			RETURNF 0
		ELSEIF ARG >= 503 && ARG <= 505 && 施設改造度:5:0
			;遊戯室
			RETURNF 0
		ELSEIF ARG == 506 && 施設改造度:5:6
			;遊戯室
			RETURNF 0
		ELSEIF ARG >= 702 && ARG <= 711 && 施設改造度:7:0
			;温泉男湯
			RETURNF 0
		ELSEIF ARG >= 802 && ARG <= 811 && 施設改造度:8:0
			;温泉女湯
			RETURNF 0
		ELSEIF ARG >= 1503 && ARG <= 1506 && 施設改造度:15:0
			;プール
			RETURNF 0
		ENDIF
	CASE 1
		IF イベント完了フラグ確認("大拡張！空艇港") && ARG == 10
			RETURNF 0
		ENDIF
		IF ARG >= 1 && ARG <= 9
			RETURNF 0
		ENDIF
	CASE 100
		TRYCALLFORMF 存在しない移動先_%開催予定祭り名%(ARG)
		RETURNF RESULT
ENDSELECT

RETURNF 1

;---------------------------------------------------------------
;エリア移動を伴わないマップ切り替え構文
;---------------------------------------------------------------
@表示マップ切り替え判定(ARG)

CALL マップ遷移判定処理(CFLAG:PLAYER:現在マップ種別, ARG)
IF RESULT
	TFLAG:マップ種別 = 派生マップ番号取得(CFLAG:PLAYER:現在マップ種別, RESULT)
	RETURN 1
ENDIF

RETURN 0

;---------------------------------------------------------------
;部屋名出力（各マップERBファイルに受渡し
;---------------------------------------------------------------
@GETPLACENAME(ARG, ARG:1)
#FUNCTIONS
#DIMS ローカル文字列

TSTR:場所名受渡 = 

IF ARG == 999
	IF ARG:1 < 1000
		IF 滞在者部屋割り配列:(ARG:1) < 1
			LOCALS = 滞在者なし
		ELSE
			LOCALS = %CALLNAME:(滞在者部屋割り配列:(ARG:1))%
		ENDIF
		RETURNF @"客室{ARG:1}号室：%LOCALS%"
	ELSE
		IF 従業員部屋割り配列:(ARG:1 - 1000) < 1
			LOCALS = 使用者なし
		ELSE
			LOCALS = %CALLNAME:(従業員部屋割り配列:(ARG:1 - 1000))%
		ENDIF
		RETURNF @"従業員室{ARG:1}号室：%LOCALS%"
	ENDIF
ENDIF

CALLFORMF GETPLACENAME_{ARG}_{ARG:1 / 100}(ARG:1)

RETURNF TSTR:場所名受渡


;---------------------------------------------------------------
;同じ場所扱いになる部屋番号
;---------------------------------------------------------------
@同一場所判定(ARG, ARG:1)
#FUNCTION

IF GROUPMATCH(ARG, 2, 302, 502) && GROUPMATCH(ARG:1, 2, 302, 502)
	RETURNF 1
ELSEIF GROUPMATCH(ARG, 3, 303) && GROUPMATCH(ARG:1, 3, 303)
	RETURNF 1
ELSEIF GROUPMATCH(ARG, 5, 503) && GROUPMATCH(ARG:1, 5, 503)
	RETURNF 1
ELSEIF GROUPMATCH(ARG, 15, 1503) && GROUPMATCH(ARG:1, 15, 1503)
	RETURNF 1
ELSEIF GROUPMATCH(ARG, 13, 1513) && GROUPMATCH(ARG:1, 13, 1513)
	RETURNF 1
ELSEIF GROUPMATCH(ARG, 14, 1514) && GROUPMATCH(ARG:1, 14, 1514)
	RETURNF 1
ENDIF

RETURNF 0

;---------------------------------------------------------------
;数字の文字列化＆ニ桁化
;---------------------------------------------------------------
@TOSTR_ニ桁化(ARG)
#FUNCTIONS
IF ARG < 10 && ARG > -1
	RETURNF "0" + TOSTR(ARG)
ELSE
	RETURNF TOSTR(ARG)
ENDIF



