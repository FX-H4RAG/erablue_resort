
;-------------------------------------------------
;05_503_02バーエリアでお酒を飲む
;-------------------------------------------------
@COMNAME_PLACE_381_0_503
#FUNCTION
IF 施設改造度:5:0 == 0
	TSTR:コマンド名受渡 = 
	RETURNF 0
ENDIF
TSTR:コマンド名受渡 = 飲み会を始める
RETURNF 1
;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_381_0_503
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")
		
@COM381_0_503

VARSET LOCAL
IF 飲み会参加判定(TARGET) == 0
	TFLAG:コマンド結果受渡し変数 = 0
	RETURN 0
ENDIF

TFLAG:コマンド結果受渡し変数 = 1
;先にここでモードを変えておかないとTARGETが上手く格納されない
TFLAG:調教モード = 3
CFLAG:MASTER:飲み会 = 1
CFLAG:TARGET:飲み会 = 1

;同室者も同様に飲み会参加判定
FOR LOCAL, 1, CHARANUM
	SIF TARGET:LOCAL < 1
		BREAK
	SIF TARGET:LOCAL == TARGET
		CONTINUE
	IF 飲み会参加判定(TARGET:LOCAL) == 1
		CFLAG:(TARGET:LOCAL):飲み会 = 1
		TFLAG:コマンド結果受渡し変数２ += 1
	ENDIF
NEXT

;-------------------------------------------------
;追加処理
;-------------------------------------------------
@飲み会参加判定(ARG)
#FUNCTION

LOCAL = 0
LOCAL += CFLAG:ARG:好感度レベル - 10
LOCAL += CFLAG:ARG:恋慕レベル
SIF TALENT:ARG:マイペース < 0
	LOCAL += 10
SIF TALENT:ARG:精神的余裕 > 0
	LOCAL += 10
LOCAL += TALENT:ARG:自制心 * -10
LOCAL += TALENT:ARG:陽気／陰気 * 20
LOCAL += TALENT:ARG:飲兵衛 * 30
LOCAL += EXP:ARG:飲酒経験
SIF EXP:ARG:飲酒経験 < 10
	LOCAL += TALENT:ARG:性的興味 * 20

IF LOCAL > RAND:100
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE381_0_503
;条件に合う場合0（不許可）を返す
;どの条件にも引っかからないなら1（許可）を返す
;一括管理
SIF GLOBAL_COMABLE(381)
	RETURN RESULT
;睡眠中
SIF CFLAG:睡眠
	RETURN 0
;気力0
SIF BASE:MASTER:気力 <= 0
	RETURN 0
;二日酔い時はダメ
SIF BASE:MASTER:酩酊 < 0
	RETURN 0
RETURN 1

;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM381_0_503

IF TFLAG:コマンド結果受渡し変数 == 1
	PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%
	SIF TFLAG:コマンド結果受渡し変数２ > 0
		PRINT たち
	PRINTL と一緒に飲み会を始めた。
ELSE
	PRINTFORM %CALLNAME:PLAYER%は%CALLNAME:TARGET%を飲み会に誘ったが、断られてしまった…
ENDIF
