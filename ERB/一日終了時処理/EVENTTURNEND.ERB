@EVENTTURNEND

FOR LOCAL,1,CHARANUM
	;滞在してないキャラはスルー
	SIF CFLAG:LOCAL:滞在期間 == -1
		CONTINUE
	IF CFLAG:LOCAL:不機嫌
		CFLAG:LOCAL:不機嫌 --
		IF !CFLAG:LOCAL:不機嫌
			SIF MARK:LOCAL:反発刻印
				CFLAG:LOCAL:不機嫌 = -- MARK:LOCAL:反発刻印
		ENDIF
	ENDIF

	LOCAL:1 = MAXBASE:LOCAL:体力 / 3

	;回復早い、回復遅い
	LOCAL:1 = LOCAL:1 * 3 / (3 - TALENT:LOCAL:回復速度)

	BASE:LOCAL:体力 = MIN(MAXBASE:LOCAL:体力, BASE:LOCAL:体力 + LOCAL:1)
	CFLAG:LOCAL:衰弱 = 0
	
	;モードもリセット
	VARSET 体位モード格納:LOCAL:0
NEXT


;日付更新
IF CFLAG:MASTER:就寝時刻 % (DAY * 1440) > CFLAG:MASTER:起床予定時刻
	DAY ++
	TIME -= 1440
ENDIF

;お祭り終了
IF 開催予定日取り < DAY
	開催予定日取り = 0
	開催予定祭り名 = 
ENDIF

;素材相場の変動
FOR COUNT, 0, DT_ROW_LENGTH("所持素材データベース")
	LOCAL = DT_CELL_GET("所持素材データベース", COUNT, "素材値相場")
	IF LOCAL > 130
		LOCAL:1 = -10
	ELSEIF LOCAL < 80
		LOCAL:1 = 5
	ENDIF
	LOCAL = LIMIT(RAND(85 + LOCAL:1, 115 + LOCAL:1) * LOCAL / 100, 50, 200)
	DT_CELL_SET "所持素材データベース", COUNT, "素材値相場", LOCAL
NEXT

FLAG:0 = 0
VARSET RFLAG
VARSET RSTR
FOR LOCAL,0,CHARANUM
	VARSET RCVAR:LOCAL:0
NEXT

BEGIN SHOP



