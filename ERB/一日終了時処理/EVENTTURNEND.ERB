@EVENTTURNEND

FOR LOCAL,1,CHARANUM
	;体力限界を消去
	CFLAG:LOCAL:体力限界 = 0

	;滞在してないキャラは専用処理へ
	IF CFLAG:LOCAL:滞在期間 == -1
		CALL 一日終了時_非滞在キャラ処理(LOCAL)
		CONTINUE
	ENDIF
	IF CFLAG:LOCAL:不機嫌
		CFLAG:LOCAL:不機嫌 --
		IF !CFLAG:LOCAL:不機嫌
			SIF MARK:LOCAL:反発刻印
				CFLAG:LOCAL:不機嫌 = -- MARK:LOCAL:反発刻印
		ENDIF
	ENDIF
	
	;元気になる薬日数経過
	SIF CFLAG:LOCAL:元気になる薬フラグ > 0
		CFLAG:LOCAL:元気になる薬フラグ -= 1

	;素質変化による体力の向上
	CALL 体力変動素質適用処理(LOCAL)

	LOCAL:1 = 1000
	;回復早い、回復遅い
	LOCAL:1 = LOCAL:1 * 3 / (3 - TALENT:LOCAL:回復速度)
	BASE:LOCAL:体力 = MIN(MAXBASE:LOCAL:体力, BASE:LOCAL:体力 + LOCAL:1)

	;中毒があるなら欲求不満を加算
	SIF MAXARRAY(性癖素質:LOCAL:0, 1) >= 3
		CFLAG:LOCAL:中毒欲求不満フラグ += 1

	;服装リセット
	CSTR:LOCAL:今日の下着 = 
	CALL CLOTHES_RESET(LOCAL)
	CALL CLOTHES_RESET_TRAIN(LOCAL)
NEXT

;モードもリセット
DT_CLEAR "体位モードデータベース"

;フラグリセット
CALL うふふ終了処理
VARSET カットイン一日一回フラグ

;日付更新
IF CFLAG:MASTER:就寝時刻 % (DAY * 1440) > CFLAG:MASTER:起床予定時刻
	DAY ++
	TIME -= 1440
ENDIF

;お祭り終了
IF 開催予定日取り < DAY
	開催予定日取り = 0
	開催予定祭り名 = 
ENDIF

;フィールド時間経過
SIF フィールド展開:欲情フィールド
	フィールド展開:欲情フィールド -= 1

;素材相場の変動
FOR COUNT, 0, DT_ROW_LENGTH("所持素材データベース")
	LOCAL = DT_CELL_GET("所持素材データベース", COUNT, "素材値相場")
	IF LOCAL > 130
		LOCAL:1 = -10
	ELSEIF LOCAL < 80
		LOCAL:1 = 5
	ENDIF
	LOCAL = LIMIT(RAND(85 + LOCAL:1, 115 + LOCAL:1) * LOCAL / 100, 50, 200)
	DT_CELL_SET "所持素材データベース", COUNT, "素材値相場", LOCAL
NEXT

FLAG:0 = 0

;バグり防止のため毎日計算する
CALL 滞在可能キャラ数再計算

;変数初期化
CALL TRAIN_VAR_RESET
CALL TURN_RESET

BEGIN SHOP



@一日終了時_非滞在キャラ処理(キャラ番号)
#DIM キャラ番号

;発情期による性欲最低値保証・最大値上昇処理
IF CFLAG:キャラ番号:発情期フラグ < 0
	MAXBASE:キャラ番号:性欲 = MIN(MAXBASE:キャラ番号:体力 * 3 / 2, 9999)
	IF TALENT:キャラ番号:種族 == 2 || TALENT:キャラ番号:発情期あり
		BASE:キャラ番号:性欲 = MAX(BASE:キャラ番号:性欲, MAXBASE:キャラ番号:性欲 * 8 / 10)
	ELSEIF TALENT:キャラ番号:種族 == 3 || キャラ番号 == 11
		BASE:キャラ番号:性欲 = MAX(BASE:キャラ番号:性欲, MAXBASE:キャラ番号:性欲 * 6 / 10)
	ENDIF
ENDIF

;一度でもリゾートに訪れたことがあるキャラを対象
IF 初体験日:キャラ番号:リゾート初来訪 > 0 && TALENT:キャラ番号:性欲 > -2
	;性欲の微上昇
	BASE:キャラ番号:性欲 = MIN(BASE:キャラ番号:性欲 + 50, MAXBASE:キャラ番号:性欲)
	;もし性欲が一定値以上だったら自慰行為を行う（性知識必要）
	IF BASE:キャラ番号:性欲 > 1000 && (知識素質:キャラ番号:性知識 > 0 || EXP:キャラ番号:自慰経験 > 0)
		BASE:キャラ番号:性欲 -= BASE:キャラ番号:性欲 / 1000 * 500
		EXP:キャラ番号:自慰経験 += BASE:キャラ番号:性欲 / 1000
		EXP:キャラ番号:絶頂経験 += BASE:キャラ番号:性欲 / 1000
	ENDIF
ENDIF
