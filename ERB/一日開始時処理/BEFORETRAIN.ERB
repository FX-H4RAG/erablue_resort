@EVENTTRAIN0
VARSET TARGET

CALL 招待キャラ来訪処理

;初期化・滞在期間処理
FOR LOCAL,0,CHARANUM
	;危険日などを設定する
	CALL SET_PREGNANCY_PALAM(LOCAL)
	;一日期間を減らす
	IF CFLAG:LOCAL:滞在期間 > 0 && CFLAG:LOCAL:滞在期間 < 999  && !CFLAG:LOCAL:長期雇用
		CFLAG:LOCAL:滞在期間 -= 1
		IF CFLAG:LOCAL:滞在期間 == 0
			;0になったら帰宅し滞在期間をマイナスに
			PRINTFORML %CALLNAME:LOCAL%は滞在を終え、帰っていった
			CALL 滞在終了時初期化(LOCAL)
			CFLAG:LOCAL:滞在期間 = -1
			滞在キャラ数 -= 1
		ENDIF
	ENDIF
	;キャラ個別処理があれば呼び出す
	TRYCALLFORM DAYFIRST_CHARAEVENT_{LOCAL}
	;滞在してないキャラはスルー
	IF CFLAG:LOCAL:滞在期間 == -1
		RCVAR:LOCAL:表示カット = 1
		CONTINUE
	ENDIF
	;AUTOCOM変数のリセット
	CALL AUTOCOM用変数リセット()
	;衣装設定
	CALL CLOTHES_TODAY(LOCAL)
	CALL CLOTHES_RESET(LOCAL)
	;射精、勃起、ムード、理性、怒り
	BASE:LOCAL:射精 = 0
	BASE:LOCAL:勃起 = 0
	BASE:LOCAL:ムード = 0
	BASE:LOCAL:理性 = MAXBASE:LOCAL:11
	BASE:LOCAL:怒り = 0
	CFLAG:LOCAL:勃起度 = 0
	CFLAG:LOCAL:同行 = 0
	CFLAG:LOCAL:酔いすぎ = 0
	;経験表示用
	FOR LOCAL:1, 0, 200
		TCVAR:LOCAL:(400 + LOCAL:1) = EXP:LOCAL:(LOCAL:1)
	NEXT
	;射精処理用文字列変数
	CSTR:LOCAL:10 = /
	CSTR:LOCAL:11 = /
NEXT
SIF SAVESTR:10 == ""
	SAVESTR:10 = /
;一日の行動ルーチンの設定・モブ観光客来場
CALL 起床時処理


;15分毎に館内のキャラを移動させる
LOCAL = 1
WHILE LOCAL
	IF TIME + 15 < CFLAG:MASTER:起床予定時刻
		TFLAG:行動前時刻 = TIME + 1440 * DAY
		TIME += 15
		CALL CHARA_MOVEMENT
	ELSE
		FOR LOCAL:1,0,CHARANUM
			;現在位置の保存
			CFLAG:(LOCAL:1):前ターン位置 = CFLAG:(LOCAL:1):現在位置
			CFLAG:(LOCAL:1):前マップ種別 = CFLAG:(LOCAL:1):現在マップ種別
		NEXT
		TIME =  CFLAG:MASTER:起床予定時刻
		;経過時間表示用
		TIME:2 = TIME
		TFLAG:行動前時刻 = TIME + 1440 * DAY
		CALL CHARA_MOVEMENT
		BREAK
	ENDIF
WEND
PRINTW 
CALL GOODMORNING

;起床時刻の記録
CFLAG:MASTER:起床時刻 = 1440 * DAY + TIME

;調教者は誰か
PLAYER = MASTER

@GOODMORNING
#DIM 秘書一覧, 100
#DIM 秘書人数
#DIM 起こしに来る秘書

VARSET LOCAL
VARSET 秘書一覧
TARGET = 0
FOR LOCAL,1,CHARANUM
	SIF CFLAG:MASTER:現在位置 == CFLAG:LOCAL:現在位置 && CFLAG:MASTER:現在マップ種別 == CFLAG:LOCAL:現在マップ種別
		TARGET = LOCAL
NEXT
秘書人数 = 0
FOR LOCAL, 1, CHARANUM
	SIF TALENT:LOCAL:従業員 == 0
		CONTINUE
	IF 現在仕事:LOCAL:0 == 3
		;秘書に従事してるキャラを一覧化
		秘書一覧:(秘書人数) = LOCAL
		秘書人数 += 1
	ENDIF
NEXT
IF TARGET
	IF CFLAG:TARGET:睡眠
		PRINTFORMW %CALLNAME:MASTER%が目覚めると隣ではまだ%CALLNAME:TARGET%がすやすやと眠っていた
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:TARGET%を起こさないように\@ BEDROOM(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置) && !BATHROOM(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置) ? ベッド # その横 \@を抜け出し一日の準備を始めた
	ELSE
		IF 陥落チェック(TARGET)
			PRINTFORMW %CALLNAME:MASTER%が目覚めると%CALLNAME:TARGET%と目があった
			PRINTFORMW しばらく%CALLNAME:TARGET%と視線をかわしてから\@ BEDROOM(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置) && !BATHROOM(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置) ? ベッド # その横 \@を抜け出し一日の準備を始めた
		ELSE
			PRINTFORMW %CALLNAME:MASTER%が目覚めると%CALLNAME:TARGET%と目があった
			PRINTFORMW なぜ%CALLNAME:TARGET%が\@ BEDROOM(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置) && !BATHROOM(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置) ? 部屋にいるのだろうと不審に思いながらもベッド # 隣にいるのだろうと不審に思いながらもその横 \@を抜け出し一日の準備を始めた
		ENDIF
		TRYCALLFORM KOJO_EVENT_逆おはよう_{NO:TARGET}
	ENDIF
ELSEIF 秘書人数 > 0
	起こしに来る秘書 = 秘書一覧:(RAND:秘書人数)
	;秘書がいるなら起こしに来る、秘書業務なら先に起きてるはずなのでキャラの起床処理は入れない
	TRYCCALLFORM KOJO_EVENT_秘書業務_朝起こし_{NO:起こしに来る秘書}
	CATCH
		;朝起こしの口上が無い場合に汎用文章を出す
		PRINTFORMW %CALLNAME:MASTER%が眠っていると、名前を呼ぶ声がする。
		PRINTFORMW %CALLNAME:起こしに来る秘書%が時間通りに起こしに来てくれたようだ。
		PRINTFORMW %CALLNAME:MASTER%が体を起こして礼を言うと、%CALLNAME:起こしに来る秘書%は仕事へと戻っていった。
	ENDCATCH
ENDIF

@滞在終了時初期化(ARG)
#DIM ループ用

CFLAG:ARG:オナニー覚えたて = 0
CFLAG:ARG:現在マップ種別 = 0
CFLAG:ARG:現在位置 = 999

FOR ループ用, 0, 4
	IF BATTLE_STATE:ループ用:0 == ARG
		BATTLE_STATE:ループ用:0 = BATTLE_STATE:(ループ用 + 1):0
		BATTLE_STATE:(ループ用 + 1):0 = BATTLE_STATE:(ループ用 + 2):0
		BATTLE_STATE:(ループ用 + 2):0 = BATTLE_STATE:(ループ用 + 3):0
		BATTLE_STATE:(ループ用 + 3):0 = BATTLE_STATE:(ループ用 + 4):0
	ENDIF
NEXT
CALL 滞在者部屋割り配列削除処理(ARG)
VARSET TCVAR:ARG:0


@発情期判定(ARG)
#FUNCTION
;種族がエルーンかドラフ、素質：発情期あり、もしくはガレヲンだったなら1を返す
IF TALENT:ARG:種族 == 2 || TALENT:LOCAL:発情期あり || TALENT:ARG:種族 == 3 || ARG == 11
	RETURNF 1
ELSE
	RETURNF 0
ENDIF




