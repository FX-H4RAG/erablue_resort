@EVENTTRAIN0
#DIM 定住フラグ

VARSET TARGET
DRAWLINE
CALL 招待キャラ来訪処理
CALL 常連キャラ来訪処理
WAIT
DRAWLINE
;初期化・滞在期間処理
FOR LOCAL,0,CHARANUM
	;危険日などを設定する
	CALL SET_PREGNANCY_PALAM(LOCAL)
	;一日期間を減らす
	IF CFLAG:LOCAL:滞在期間 > 0 && CFLAG:LOCAL:滞在期間 < 999  && !CFLAG:LOCAL:長期雇用
		CFLAG:LOCAL:滞在期間 -= 1
		IF CFLAG:LOCAL:滞在期間 == 0
			定住フラグ = 0
			IF MATCH(開拓エリア開発番号, 大規模番号_居住区エリア) > 0 && TALENT:LOCAL:常連 && TALENT:LOCAL:定住者 == 0 && 定住エリア満杯() == 0
				;常連かつ定住者じゃない時、一定確率で定住を申し出る
				;素質とかで確率を変動させたいけど、とりあえずは25％くらいで
				IF RAND:4 == 0
					PRINTFORML %CALLNAME:LOCAL%はこのリゾート島に定住を希望しているようだ。
					PRINTL 定住を許可しますか？
					PRINTBUTTON "[0] はい", 0
					PRINTL
					PRINTBUTTON "[1] いいえ", 1
					PRINTL
					BINPUT
					IF RESULT == 0
						PRINTFORML %CALLNAME:LOCAL%の定住を許可しました！
						PRINTFORMW %CALLNAME:LOCAL%は居住区エリアの住宅街で暮らし始めました
						TALENT:LOCAL:定住者 = 1
						CFLAG:LOCAL:滞在期間 = 999
						FOR LOCAL:1, 0, 50
							IF 定住者部屋割り配列:(LOCAL:1) == ""
								定住者部屋割り配列:(LOCAL:1) = {LOCAL}_
								BREAK
							ENDIF
						NEXT
						CALL 滞在者部屋割り配列削除処理(LOCAL)
						定住フラグ = 1
					ELSE
						PRINTFORMW %CALLNAME:LOCAL%の定住を断りました
					ENDIF
				ENDIF
			ENDIF
			IF 定住フラグ == 0
				;0になったら帰宅し滞在期間をマイナスに
				PRINTFORML %CALLNAME:LOCAL%は滞在を終え、帰っていった
				IF CFLAG:LOCAL:リゾート来訪回数 >= 3 && TALENT:LOCAL:常連 == 0
					PRINTFORMW %CALLNAME:LOCAL%はリゾートへの来訪を重ね、［常連］になった！
					TALENT:LOCAL:常連 = 1
				ENDIF
				CALL 滞在終了時初期化(LOCAL)
				CFLAG:LOCAL:滞在期間 = -1
			ENDIF
		ENDIF
	ENDIF
	;キャラ個別処理があれば呼び出す
	TRYCALLFORM DAYFIRST_CHARAEVENT_{LOCAL}
	;滞在してないキャラはスルー
	IF CFLAG:LOCAL:滞在期間 == -1
		RCVAR:LOCAL:表示カット = 1
		CONTINUE
	ENDIF
	;AUTOCOM変数のリセット
	CALL AUTOCOM用変数リセット()
	;衣装設定
	CALL CLOTHES_TODAY(LOCAL)
	CALL CLOTHES_RESET(LOCAL)
	CALL CLOTHES_RESET_TRAIN(LOCAL)
	;射精、勃起、ムード、理性、怒り
	BASE:LOCAL:射精 = 0
	BASE:LOCAL:勃起 = 0
	BASE:LOCAL:ムード = 0
	BASE:LOCAL:理性 = MAXBASE:LOCAL:11
	BASE:LOCAL:怒り = 0
	CFLAG:LOCAL:勃起度 = 0
	CFLAG:LOCAL:同行 = 0
	CFLAG:LOCAL:デート = 0
	CFLAG:LOCAL:酔いすぎ = 0
	;経験表示用
	CALL EXP変動記録処理(LOCAL)
	;お祭り参加履歴
	IF 開催予定日取り == DAY
		IF LOCAL
			TRYCALLFORM MOVE_ROUTINE_%開催予定祭り名%(LOCAL, "参加判定")
			IF RESULT
				SPLIT 開催予定祭り名, "_", LOCALS
				CALL 履歴データベース登録(CFLAG:LOCAL:人物番号, "ーーーーー" + LOCALS:1 + "に参加したーーーーー","日常リゾート")
			ENDIF
		ELSE  ;あなたは主催者
			SPLIT 開催予定祭り名, "_", LOCALS
			CALL 履歴データベース登録(CFLAG:LOCAL:人物番号, "ーーーーー" + LOCALS:1 + "を開催したーーーーー","日常リゾート")
		ENDIF
	ENDIF
NEXT
SIF SAVESTR:10 == ""
	SAVESTR:10 = /
;部屋の並び順のために好感度レベルで降順に並び替え
CALL SORT_CFLAG(21)
;一日の行動ルーチンの設定・モブ観光客来場
CALL 起床時処理


;15分毎に館内のキャラを移動させる
LOCAL = 1
WHILE LOCAL
	IF TIME + 15 < CFLAG:MASTER:起床予定時刻
		TFLAG:行動前時刻 = TIME + 1440 * DAY
		TIME += 15
		CALL CHARA_MOVEMENT
	ELSE
		FOR LOCAL:1,0,CHARANUM
			;現在位置の保存
			CFLAG:(LOCAL:1):前ターン位置 = CFLAG:(LOCAL:1):現在位置
			CFLAG:(LOCAL:1):前マップ種別 = CFLAG:(LOCAL:1):現在マップ種別
		NEXT
		TIME = CFLAG:MASTER:起床予定時刻
		;経過時間表示用
		TIME:2 = TIME
		TFLAG:行動前時刻 = TIME + 1440 * DAY
		CALL CHARA_MOVEMENT
		BREAK
	ENDIF
WEND
PRINTW 
CALL GOODMORNING

;起床時刻の記録
CFLAG:MASTER:起床時刻 = 1440 * DAY + TIME

;調教者は誰か
PLAYER = MASTER

@GOODMORNING
#DIM 秘書一覧, 100
#DIM 秘書人数
#DIM 起こしに来る秘書

VARSET LOCAL
VARSET 秘書一覧
TARGET = 0
FOR LOCAL,1,CHARANUM
	SIF CFLAG:MASTER:現在位置 == CFLAG:LOCAL:現在位置 && CFLAG:MASTER:現在マップ種別 == CFLAG:LOCAL:現在マップ種別
		TARGET = LOCAL
NEXT
秘書人数 = 0
FOR LOCAL, 1, CHARANUM
	SIF TALENT:LOCAL:従業員 == 0
		CONTINUE
	IF 現在仕事:LOCAL:0 == 3
		;秘書に従事してるキャラを一覧化
		秘書一覧:(秘書人数) = LOCAL
		秘書人数 += 1
	ENDIF
NEXT
IF TARGET
	IF CFLAG:TARGET:睡眠
		PRINTFORMW %CALLNAME:MASTER%が目覚めると隣ではまだ%CALLNAME:TARGET%がすやすやと眠っていた
		PRINTFORMW %CALLNAME:MASTER%は%CALLNAME:TARGET%を起こさないように\@ BEDROOM(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置) && !BATHROOM(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置) ? ベッド # その横 \@を抜け出し一日の準備を始めた
	ELSE
		IF 陥落チェック(TARGET)
			PRINTFORMW %CALLNAME:MASTER%が目覚めると%CALLNAME:TARGET%と目があった
			PRINTFORMW しばらく%CALLNAME:TARGET%と視線をかわしてから\@ BEDROOM(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置) && !BATHROOM(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置) ? ベッド # その横 \@を抜け出し一日の準備を始めた
		ELSE
			PRINTFORMW %CALLNAME:MASTER%が目覚めると%CALLNAME:TARGET%と目があった
			PRINTFORMW なぜ%CALLNAME:TARGET%が\@ BEDROOM(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置) && !BATHROOM(CFLAG:MASTER:現在マップ種別, CFLAG:MASTER:現在位置) ? 部屋にいるのだろうと不審に思いながらもベッド # 隣にいるのだろうと不審に思いながらもその横 \@を抜け出し一日の準備を始めた
		ENDIF
		TRYCALLFORM KOJO_EVENT_逆おはよう_{NO:TARGET}
	ENDIF
ELSEIF 秘書人数 > 0
	起こしに来る秘書 = 秘書一覧:(RAND:秘書人数)
	;秘書がいるなら起こしに来る、秘書業務なら先に起きてるはずなのでキャラの起床処理は入れない
	TRYCCALLFORM KOJO_EVENT_秘書業務_朝起こし_{NO:起こしに来る秘書}(起こしに来る秘書)
	CATCH
		;朝起こしの口上が無い場合に汎用文章を出す
		IF 陥落チェック(起こしに来る秘書) && EXP:起こしに来る秘書:精飲経験 > 10 && GETBIT(TALENT:MASTER:性別, 1) && RAND:3 == 0
			PRINTFORMW %CALLNAME:MASTER%が眠っていると、なんだか股間が温かいような気がした。
			PRINTFORM 布団をめくってみると、%CALLNAME:起こしに来る秘書%が%CALLNAME:MASTER%の
			SELECTCASE TALENT:MASTER:男性器サイズ
				CASE IS <= -1
					PRINTL 小さなペニスをすっかり咥えこんで舌で転がしている……
				CASE 0
					PRINTL ペニスをしゃぶり、舌で亀頭を舐めている……
				CASEELSE
					PRINTL 大きなペニスをなんとか口に収め、吸いたてている……
			ENDSELECT
			PRINTFORMW これも秘書の務めと、朝フェラで起こしにきてくれたようだ。
			PRINTFORMW %CALLNAME:MASTER%がそのまま射精すると、%CALLNAME:起こしに来る秘書%はこぼさず飲み込んで仕事に戻っていった。
			PRINTFORML Ｃ経験 + 10（%CALLNAME:MASTER%）
			PRINTFORML 絶頂経験 + 1（%CALLNAME:MASTER%）
			PRINTFORML Ｃ絶頂経験 + 1（%CALLNAME:MASTER%）
			PRINTFORML 射精経験 + 1（%CALLNAME:MASTER%）
			PRINTFORML 口淫経験 + 3（%CALLNAME:起こしに来る秘書%）
			PRINTFORML 精飲経験 + 1（%CALLNAME:起こしに来る秘書%）
			WAIT
			EXP:MASTER:Ｃ経験 += 10
			EXP:MASTER:絶頂経験 += 1
			EXP:MASTER:Ｃ絶頂経験 += 1
			EXP:起こしに来る秘書:口淫経験 += 3
			EXP:起こしに来る秘書:精飲経験 += 1
			CALL EXP変動記録処理(MASTER)
			CALL EXP変動記録処理(起こしに来る秘書)
		ELSEIF GETBIT(陥落チェック(起こしに来る秘書), 1) && RAND:3 == 0
			PRINTFORMW %CALLNAME:MASTER%が眠っていると、体全体が快楽に包まれているような感触がする……。
			PRINTFORMW 目を開けてみると、%CALLNAME:起こしに来る秘書%が%CALLNAME:MASTER%の体を愛撫していた。
			PRINTFORMW どうやら朝ペッティングで起こしにきてくれたようだ。
			PRINTFORMW %CALLNAME:MASTER%が起きたことに気づくと、%CALLNAME:起こしに来る秘書%は愛撫の手を速めて%CALLNAME:MASTER%を絶頂させる。
			PRINTFORMW これも秘書の務めと微笑んで、%CALLNAME:起こしに来る秘書%は仕事に戻っていった。
			LOCAL = 1
			PRINTFORML Ｃ経験 + 10（%CALLNAME:MASTER%）
			PRINTFORML Ｃ絶頂経験 + 1（%CALLNAME:MASTER%）
			EXP:MASTER:Ｃ経験 += 10
			EXP:MASTER:Ｃ絶頂経験 += 1
			IF GETBIT(TALENT:MASTER:性別, 1)
				PRINTFORML 射精経験 + 1（%CALLNAME:MASTER%）
				EXP:MASTER:射精経験 += 1
			ENDIF
			IF GETBIT(TALENT:MASTER:性別, 0)
				PRINTFORML Ｂ経験 + 10（%CALLNAME:MASTER%）
				PRINTFORML Ｂ絶頂経験 + 1（%CALLNAME:MASTER%）
				EXP:MASTER:Ｂ経験 += 10
				EXP:MASTER:Ｂ絶頂経験 += 1
				LOCAL += 1
				IF EXP:MASTER:Ｖ経験 > 50
					PRINTFORML Ｖ経験 + 10（%CALLNAME:MASTER%）
					PRINTFORML Ｖ絶頂経験 + 1（%CALLNAME:MASTER%）
					EXP:MASTER:Ｖ経験 += 10
					EXP:MASTER:Ｖ絶頂経験 += 1
					LOCAL += 1
				ENDIF
			ENDIF
			IF EXP:MASTER:Ａ経験 > 50
				PRINTFORML Ａ経験 + 10（%CALLNAME:MASTER%）
				PRINTFORML Ａ絶頂経験 + 1（%CALLNAME:MASTER%）
				EXP:MASTER:Ａ経験 += 10
				EXP:MASTER:Ａ絶頂経験 += 1
				LOCAL += 1
			ENDIF
			PRINTFORML 絶頂経験 + {LOCAL}（%CALLNAME:MASTER%）
				EXP:MASTER:絶頂経験 += LOCAL
			WAIT
			CALL EXP変動記録処理(MASTER)
		ELSEIF 陥落チェック(起こしに来る秘書) && CFLAG:MASTER:ファーストキスフラグ && CFLAG:起こしに来る秘書:ファーストキスフラグ && RAND:3 == 0
			PRINTFORMW %CALLNAME:MASTER%が眠っていると、唇に柔らかい感触を感じた。
			PRINTFORMW 目を開けてみると、%CALLNAME:起こしに来る秘書%が%CALLNAME:MASTER%の唇にキスをしている。
			PRINTFORMW これも秘書の務めと、朝キスで起こしにきてくれたようだ。
			PRINTFORMW %CALLNAME:MASTER%が起きたことに気づくと、%CALLNAME:起こしに来る秘書%は微笑んで仕事に戻っていった。
			PRINTFORML キス経験 + 2（%CALLNAME:MASTER%）
			PRINTFORML キス経験 + 2（%CALLNAME:起こしに来る秘書%）
			WAIT
			EXP:MASTER:キス経験 += 2
			EXP:起こしに来る秘書:キス経験 += 2
			CALL EXP変動記録処理(MASTER)
			CALL EXP変動記録処理(起こしに来る秘書)
		ELSE
			PRINTFORMW %CALLNAME:MASTER%が眠っていると、名前を呼ぶ声がする。
			PRINTFORMW %CALLNAME:起こしに来る秘書%が時間通りに起こしに来てくれたようだ。
			PRINTFORMW %CALLNAME:MASTER%が体を起こして礼を言うと、%CALLNAME:起こしに来る秘書%は仕事へと戻っていった。
		ENDIF
	ENDCATCH
ENDIF

@滞在終了時初期化(ARG)
#DIM ループ用

CFLAG:ARG:オナニー覚えたて = 0
CFLAG:ARG:現在マップ種別 = 0
CFLAG:ARG:現在位置 = 999
CFLAG:ARG:元気になる薬フラグ = 0

FOR ループ用, 0, 4
	IF BATTLE_STATE:ループ用:0 == ARG
		BATTLE_STATE:ループ用:0 = BATTLE_STATE:(ループ用 + 1):0
		BATTLE_STATE:(ループ用 + 1):0 = BATTLE_STATE:(ループ用 + 2):0
		BATTLE_STATE:(ループ用 + 2):0 = BATTLE_STATE:(ループ用 + 3):0
		BATTLE_STATE:(ループ用 + 3):0 = BATTLE_STATE:(ループ用 + 4):0
	ENDIF
NEXT
CALL 滞在者部屋割り配列削除処理(ARG)
VARSET TCVAR:ARG:0

FOR LOCAL:1,0,200
	TCVAR:ARG:(400 + LOCAL:1) = EXP:ARG:(LOCAL:1)
NEXT

@発情期判定(ARG)
#FUNCTION
SIF TALENT:ARG:発情期あり < 0
	RETURNF 0

;各種設定
SIF TALENT:ARG:種族 == 2 && TALENT:ARG:性別 != 2 && GETBIT(発情期切り替えOPTION記憶, 0) == 0
	RETURNF 1
SIF TALENT:ARG:種族 == 2 && TALENT:ARG:性別 == 2 && GETBIT(発情期切り替えOPTION記憶, 1) == 0
	RETURNF 1
SIF (TALENT:ARG:種族 == 3|| キャラ一致チェック(ARG, "[六竜の『金』]ガレヲン")) && TALENT:ARG:性別 != 2 && GETBIT(発情期切り替えOPTION記憶, 2) == 0
	RETURNF 1
SIF (TALENT:ARG:種族 == 3|| キャラ一致チェック(ARG, "[六竜の『金』]ガレヲン")) && TALENT:ARG:性別 == 2 && GETBIT(発情期切り替えOPTION記憶, 3) == 0
	RETURNF 1
SIF TALENT:ARG:発情期あり > 0 && TALENT:ARG:性別 != 2 && GETBIT(発情期切り替えOPTION記憶, 4) == 0
	RETURNF 1
SIF TALENT:ARG:発情期あり > 0 && TALENT:ARG:性別 == 2 && GETBIT(発情期切り替えOPTION記憶, 5) == 0
	RETURNF 1


RETURNF 0





