@人物データベースセット
DT_CREATE "人物データベース"

;NAMEと同様のものが入る想定。名前を付けなかった場合は空
DT_COLUMN_ADD "人物データベース", "名前"

;CALLNAMEと同様のものが入る想定。名前を付けなかった場合は空
DT_COLUMN_ADD "人物データベース", "呼び名"

;NOが入る。"キャラクタ数上限 = 3000"に沿って型を決定
;ランダム・ユニーク問わずキャラではない場合は空。GET_PERSON_ナンバーでは-1が返される
DT_COLUMN_ADD "人物データベース", "ナンバー", 2

;TALENT:性別と同様のものが入る想定。取りうる値(0～3)に沿って型を決定
DT_COLUMN_ADD "人物データベース", "性別", 1

;TALENT:種族と同様のものが入る想定。取りうる値(-3～6)に沿って型を決定
DT_COLUMN_ADD "人物データベース", "種族", 1

;CSTR:プレゼント好みと同様のものが入る想定
DT_COLUMN_ADD "人物データベース", "プレゼント好み"

;周回回数累計:0が入る
;空の場合、GET_PERSON_出会った周回では-1が返される
DT_COLUMN_ADD "人物データベース", "出会った周回", 4

;生まれがゲーム外のキャラの生まれた順序を入れる。姉/兄のほうが大きい値になる
;ゲーム内で生まれた子供等は空。GET_PERSON_出生順では最小値より1小さい値(-129)が返される
DT_COLUMN_ADD "人物データベース", "出生順", 1

;ゲーム中に生まれた場合、DAYが入る
;生まれがゲーム外のユニークキャラ等は空。GET_PERSON_生年日では-1が返される
DT_COLUMN_ADD "人物データベース", "生年日", 4

;人物として父親/母親がゲーム中に存在すればそのidが入る
;母親/父親がゲーム内に存在しない人物ならば空。GET_PERSON_母親/GET_PERSON_父親では空のID(0)が返される
DT_COLUMN_ADD "人物データベース", "母親", 4
DT_COLUMN_ADD "人物データベース", "父親", 4

;「誰々が◯週目の何年目何日に産んだ」のような文字列を入れる想定
DT_COLUMN_ADD "人物データベース", "経歴"


;キャラ同士の親子関係を定義。CSVに記述せず独立した関数にすることに
;1)キャラが全部人物データベースに入ってないとセットに失敗するため
;2)周回で親子の片方だけが引き継がれても、現周回にいる親子が親子関係を持つようにするため
@ユニークキャラ親子関係セット
#DIM ナルメア
#DIM ラルナ
#DIM アリーザ
#DIM アリシア

FOR LOCAL, 0, CHARANUM
	SIF キャラ一致チェック(LOCAL, "[揺らぎの斬姫]ナルメア")
		ナルメア = CFLAG:LOCAL:人物番号
	SIF キャラ一致チェック(LOCAL, "[斬姫の慈母]ラルナ")
		ラルナ = CFLAG:LOCAL:人物番号
	SIF キャラ一致チェック(LOCAL, "[炎舞の紅脚]アリーザ")
		アリーザ = CFLAG:LOCAL:人物番号
	SIF キャラ一致チェック(LOCAL, "[炎姫の母君]アリシア")
		アリシア = CFLAG:LOCAL:人物番号
NEXT

CALL SET_PERSON_母親(ナルメア, ラルナ)
CALL SET_PERSON_母親(アリーザ, アリシア)


;空のキャラを人物データベースに登録する
@ADD_PERSON()
#FUNCTION
#DIM ID

ID = DT_ROW_ADD("人物データベース")
SIF !ID
	THROW 人物データの登録に失敗しました
RETURNF ID


;キャラの追加時に実行。キャラを人物データベースに登録し、IDを振る
@ADD_PERSON_FROM_CHARA(C_ID)
#DIM C_ID

{
	DT_ROW_ADD "人物データベース",
		"名前", NAME:C_ID,
		"呼び名", CALLNAME:C_ID,
		"ナンバー", NO:C_ID,
		"性別", TALENT:C_ID:性別,
		"種族", TALENT:C_ID:種族,
		"プレゼント好み", CSTR:C_ID:プレゼント好み,
		"出会った周回", 周回回数累計:0,
		"出生順", 127
}
;そうそう Utils.TimePoint() が0になることはないはず
SIF !RESULT
	THROW 人物データの登録に失敗しました
CFLAG:C_ID:人物番号 = RESULT
RETURN RESULT


;人物データベースに登録済のキャラのデータを現在のものに更新する
@UPDATE_PERSON_FROM_CHARA(C_ID)
#DIM C_ID

{
	DT_ROW_SET "人物データベース", CFLAG:C_ID:人物番号,
		"名前", NAME:C_ID,
		"呼び名", CALLNAME:C_ID,
		"ナンバー", NO:C_ID,
		"性別", TALENT:C_ID:性別,
		"種族", TALENT:C_ID:種族,
		"プレゼント好み", CSTR:C_ID:プレゼント好み
}
RETURN RESULT


;人物データベースのIDからキャラ登録番号を逆引きする
@FIND_CHARA_FROM_PERSON_ID(ID)
#FUNCTION
#DIM ID
RETURNF FINDCHARA(CFLAG:人物番号, ID)


;人物データベースのIDからキャラとしての母親を探し、キャラ登録番号を返す（見つからなければ-1）
@FIND_MOTHER_CHARA_FROM_PERSON_ID(ID)
#FUNCTION
#DIM ID
#DIM MOTHER_ID
MOTHER_ID = GET_PERSON_母親(ID)
SIF !MOTHER_ID
	RETURNF -1
RETURNF FIND_CHARA_FROM_PERSON_ID(MOTHER_ID)


;人物データベースのIDからキャラとしての父親を探し、キャラ登録番号を返す（見つからなければ-1）
@FIND_FATHER_CHARA_FROM_PERSON_ID(ID)
#FUNCTION
#DIM ID
#DIM FATHER_ID
FATHER_ID = GET_PERSON_父親(ID)
SIF !FATHER_ID
	RETURNF -1
RETURNF FIND_CHARA_FROM_PERSON_ID(FATHER_ID)


;母親・父親・自身を母に持つ子のいずれかが人物データベースに登録されていれば1を返す
;母親/父親はキャラである必要がある。子供はキャラである必要はない
@EXIST_FAMILY(ID)
#FUNCTION
#DIM ID
#DIM CHILD_COUNT
#DIM CHILDREN, 200

SIF FIND_MOTHER_CHARA_FROM_PERSON_ID(ID) >= 0
	RETURNF 1
SIF FIND_FATHER_CHARA_FROM_PERSON_ID(ID) >= 0
	RETURNF 1

CHILD_COUNT = DT_SELECT("人物データベース", @"母親 = {ID}", , CHILDREN)
SIF CHILD_COUNT > 0
	RETURNF 1
RETURNF 0


;指定されたIDを親に持つ人物を取得し、RESULT:0に件数、RESULT:1～にIDが入る
;IDを指定しなかった場合、いずれかの親が設定されている人物が対象となる
@FIND_CHILDREN(MOTHER_ID = 0, FATHER_ID = 0)
#DIM MOTHER_ID
#DIM FATHER_ID
#DIMS FILTER

IF MOTHER_ID
	FILTER '= @"母親 = {MOTHER_ID}"
	IF FATHER_ID
		FILTER += @" And 父親 = {FATHER_ID}"
	ENDIF
ELSEIF FATHER_ID
	FILTER '= @"父親 = {FATHER_ID}"
ELSE
	FILTER '= "母親 Is Not Null Or 父親 Is Not Null"
ENDIF

DT_SELECT "人物データベース", FILTER, "出生順 DESC, 出会った周回 ASC, 生年日 ASC"
RETURN RESULT


;指定したIDの人物が存在するか確認する
@EXIST_PERSON(ID)
#FUNCTION
#DIM ID
RETURNF DT_CELL_ISNULL("人物データベース", ID, "id", 1) == 0


;各カラムのGET系
@GET_PERSON_名前(ID)
#FUNCTIONS
#DIM ID
#DIM C_ID
C_ID = FIND_CHARA_FROM_PERSON_ID(ID)
IF C_ID >= 0
	RETURNF NAME:C_ID
ELSE
	RETURNF DT_CELL_GETS("人物データベース", ID, "名前", 1)
ENDIF

@GET_PERSON_呼び名(ID)
#FUNCTIONS
#DIM ID
#DIM C_ID
C_ID = FIND_CHARA_FROM_PERSON_ID(ID)
IF C_ID >= 0
	RETURNF CALLNAME:C_ID
ELSE
	RETURNF DT_CELL_GETS("人物データベース", ID, "呼び名", 1)
ENDIF

@GET_PERSON_ナンバー(ID)
#FUNCTION
#DIM ID
#DIM C_ID
C_ID = FIND_CHARA_FROM_PERSON_ID(ID)
IF C_ID >= 0
	RETURNF NO:C_ID
ELSEIF DT_CELL_ISNULL("人物データベース", ID, "ナンバー", 1) == 1
	RETURNF -1
ELSE
	RETURNF DT_CELL_GET("人物データベース", ID, "ナンバー", 1)
ENDIF

@GET_PERSON_性別(ID)
#FUNCTION
#DIM ID
#DIM C_ID
C_ID = FIND_CHARA_FROM_PERSON_ID(ID)
IF C_ID >= 0
	RETURNF TALENT:C_ID:性別
ELSE
	RETURNF DT_CELL_GET("人物データベース", ID, "性別", 1)
ENDIF

@GET_PERSON_種族(ID)
#FUNCTION
#DIM ID
#DIM C_ID
C_ID = FIND_CHARA_FROM_PERSON_ID(ID)
IF C_ID >= 0
	RETURNF TALENT:C_ID:種族
ELSE
	RETURNF DT_CELL_GET("人物データベース", ID, "種族", 1)
ENDIF

@GET_PERSON_プレゼント好み(ID)
#FUNCTIONS
#DIM ID
#DIM C_ID
C_ID = FIND_CHARA_FROM_PERSON_ID(ID)
IF C_ID >= 0
	RETURNF CSTR:C_ID:プレゼント好み
ELSE
	RETURNF DT_CELL_GETS("人物データベース", ID, "プレゼント好み", 1)
ENDIF

@GET_PERSON_出会った周回(ID)
#FUNCTION
#DIM ID
IF DT_CELL_ISNULL("人物データベース", ID, "出会った周回", 1) == 1
	RETURNF -1
ELSE
	RETURNF DT_CELL_GET("人物データベース", ID, "出会った周回", 1)
ENDIF

@GET_PERSON_出生順(ID)
#FUNCTION
#DIM ID
IF DT_CELL_ISNULL("人物データベース", ID, "出生順", 1) == 1
	RETURNF -129
ELSE
	RETURNF DT_CELL_GET("人物データベース", ID, "出生順", 1)
ENDIF

@GET_PERSON_生年日(ID)
#FUNCTION
#DIM ID
IF DT_CELL_ISNULL("人物データベース", ID, "生年日", 1) == 1
	RETURNF -1
ELSE
	RETURNF DT_CELL_GET("人物データベース", ID, "生年日", 1)
ENDIF

@GET_PERSON_母親(ID)
#FUNCTION
#DIM ID
RETURNF DT_CELL_GET("人物データベース", ID, "母親", 1)

@GET_PERSON_父親(ID)
#FUNCTION
#DIM ID
RETURNF DT_CELL_GET("人物データベース", ID, "父親", 1)

@GET_PERSON_経歴(ID)
#FUNCTIONS
#DIM ID
RETURNF DT_CELL_GETS("人物データベース", ID, "経歴", 1)


;各カラムのSET系
@SET_PERSON_名前(ID, VAL)
#DIM ID
#DIMS VAL
RETURN DT_CELL_SET("人物データベース", ID, "名前", VAL, 1)

@SET_PERSON_呼び名(ID, VAL)
#DIM ID
#DIMS VAL
RETURN DT_CELL_SET("人物データベース", ID, "呼び名", VAL, 1)

@SET_PERSON_ナンバー(ID, VAL)
#DIM ID
#DIM VAL
RETURN DT_CELL_SET("人物データベース", ID, "ナンバー", VAL, 1)

@SET_PERSON_性別(ID, VAL)
#DIM ID
#DIM VAL
RETURN DT_CELL_SET("人物データベース", ID, "性別", VAL, 1)

@SET_PERSON_種族(ID, VAL)
#DIM ID
#DIM VAL
RETURN DT_CELL_SET("人物データベース", ID, "種族", VAL, 1)

@SET_PERSON_プレゼント好み(ID, VAL)
#DIM ID
#DIMS VAL
RETURN DT_CELL_SET("人物データベース", ID, "プレゼント好み", VAL, 1)

@SET_PERSON_出会った周回(ID, VAL)
#DIM ID
#DIM VAL
RETURN DT_CELL_SET("人物データベース", ID, "出会った周回", VAL, 1)

@SET_PERSON_出生順(ID, VAL)
#DIM ID
#DIM VAL
RETURN DT_CELL_SET("人物データベース", ID, "出生順", VAL, 1)

@SET_PERSON_生年日(ID, VAL)
#DIM ID
#DIM VAL
RETURN DT_CELL_SET("人物データベース", ID, "生年日", VAL, 1)

@SET_PERSON_母親(ID, VAL)
#DIM ID
#DIM VAL
RETURN DT_CELL_SET("人物データベース", ID, "母親", VAL, 1)

@SET_PERSON_父親(ID, VAL)
#DIM ID
#DIM VAL
RETURN DT_CELL_SET("人物データベース", ID, "父親", VAL, 1)

@SET_PERSON_経歴(ID, VAL)
#DIM ID
#DIMS VAL
RETURN DT_CELL_SET("人物データベース", ID, "経歴", VAL, 1)


;どこからも参照できなくなった人物データを消去する
@人物データベースGC
DT_CREATE "人物番号一時リスト"
DT_COLUMN_ADD "人物番号一時リスト", "人物番号", 4

LOCAL:1 = DT_ROW_LENGTH("人物データベース")
FOR LOCAL, 0, LOCAL:1
	DT_ROW_ADD "人物番号一時リスト", "人物番号", DT_CELL_GET("人物データベース", LOCAL, "id")
NEXT

FOR LOCAL, 0, CHARANUM
	CALL 人物データベースGC_マーク(CFLAG:LOCAL:人物番号)
NEXT

LOCAL:1 = DT_ROW_LENGTH("人物番号一時リスト")
FOR LOCAL, 0, LOCAL:1
	DT_ROW_REMOVE "人物データベース", DT_CELL_GET("人物番号一時リスト", LOCAL, "人物番号")
NEXT


@人物データベースGC_マーク(ID)
#DIM DYNAMIC ID
#DIM DYNAMIC CHILD_COUNT
#DIM DYNAMIC CHILD_ITER
#DIM DYNAMIC CHILDREN, 200

DT_SELECT "人物番号一時リスト", @"人物番号 = {ID}"
SIF RESULT == 0
	RETURN

DT_ROW_REMOVE "人物番号一時リスト", RESULT:1

CALL 人物データベースGC_マーク(DT_CELL_GET("人物データベース", ID, "母親", 1))
CALL 人物データベースGC_マーク(DT_CELL_GET("人物データベース", ID, "父親", 1))

CHILD_COUNT = DT_SELECT("人物データベース", @"母親 = {ID} Or 父親 = {ID}", "", CHILDREN)
;Emueraの不具合でFORだと動かない
CHILD_ITER = 0
WHILE CHILD_ITER < CHILD_COUNT
	CALL 人物データベースGC_マーク(CHILDREN:CHILD_ITER)
	CHILD_ITER ++
WEND
RETURN
