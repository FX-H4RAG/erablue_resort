
@アイテム個別文章表示_おいかわ牛乳(ARGS)
;おいかわ牛乳
IF ARGS == "値段"
	詳細文文字列受け渡し変数 = ルピ
	RETURN 800
ENDIF

LOCALS = 
LOCALS += @"■おいかわ牛乳　　　所持数:{DT_CELL_GET("所持アイテムデータベース", 0, "おいかわ牛乳")}<br>"
LOCALS += "及川雫の実家であるおいかわ牧場特製の牛乳<br>"
LOCALS += "<br>"
LOCALS += "保存料の類は使っていないしぼりたてで、あまり日持ちしないようだ。<br>"
LOCALS += "どうやって持ち込んできたんだろう……<br>"
LOCALS += "<br>"
LOCALS += "【効果】<br>"
LOCALS += "味方単体のＨＰを[５０％]回復する。<br>"
LOCALS += "戦闘中に及川雫に対して使用した場合、さらに奥義ゲージが[３０％]アップする。<br>"
LOCALS += "このアイテムは探索が終了するとすべて消滅する。"

詳細文文字列受け渡し変数 = %LOCALS%

@アイテム使用先表示_おいかわ牛乳
CALL 汎用使用先表示("味方単体")

@アイテム効果処理_おいかわ牛乳(ARG)
#DIM 回復量
;ARG = 隊列の何番目か

IF !BATTLE_STATE:ARG:0
	RETURN 0
ENDIF

IF DT_CELL_GET("所持アイテムデータベース", 0, "おいかわ牛乳") < 1
	PRINTW アイテムがありません
	RETURN 0
ENDIF

IF BATTLE_STATE:ARG:最大ＨＰ == BATTLE_STATE:ARG:ＨＰ && !(BATTLE_STATE:ARG:キャラ番号 == 1035 && BATTLE_STATE:ARG:奥義ゲージ < BATTLE_STATE:ARG:ゲージ最大値)
	PRINTW ＨＰが最大です
	RETURN 0
ENDIF

IF BATTLE_STATE:ARG:ＨＰ < 1
	PRINTW 戦闘不能は治療出来ません
	RETURN 0
ENDIF

回復量 = MIN(BATTLE_STATE:ARG:最大ＨＰ - BATTLE_STATE:ARG:ＨＰ, BATTLE_STATE:ARG:最大ＨＰ / 2)
PRINTFORMW %CALLNAME:(BATTLE_STATE:ARG:0)%のＨＰを{回復量}回復しました
BATTLE_STATE:ARG:ＨＰ += 回復量

IF BATTLE_STATE:ARG:キャラ番号 == 1035
	回復量 = MIN(BATTLE_STATE:ARG:ゲージ最大値 - BATTLE_STATE:ARG:奥義ゲージ, 30)
	PRINTFORMW %CALLNAME:(BATTLE_STATE:ARG:0)%の奥義ゲージが{回復量}増加しました
	BATTLE_STATE:ARG:奥義ゲージ += 回復量
ENDIF
RETURN 1
