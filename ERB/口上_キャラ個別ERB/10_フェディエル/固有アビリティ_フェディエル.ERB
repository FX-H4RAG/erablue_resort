
@固有アビ_10_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 対象番号

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 55
	アビ名 = カースドヒューネラル＋
	消費ＭＰ = 450
ELSE
	アビ名 = カースドヒューネラル
	消費ＭＰ = 500
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体にバリア効果［耐久2000/永続］/死の安寧効果［2回被ダメージまで永続/消去不可］
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = バリア
		アビ汎用文字列:バフ・デバフ対象能力 = バリア
		アビ汎用変数:効果量 = 2000
		アビ汎用変数:持続ターン = -1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 死の安寧
		アビ汎用文字列:バフ・デバフ対象能力 = 死の安寧
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		FOR 対象番号, 0, 4
			SIF BATTLE_STATE:対象番号:キャラ番号 < 1
				CONTINUE
			SIF BATTLE_STATE:対象番号:ＨＰ < 1
				CONTINUE
			
			IF バフ存在チェック_枠("死の安寧_弱体無効", 対象番号) < 0
				DT_ROW_ADD @"バフデータベース_{対象番号}", "バフ枠", "死の安寧_弱体無効", "バフ対象能力", "弱体無効", "バフ名", "死の安寧", "持続ターン", -1, "消去不可フラグ", 1
				DT_ROW_ADD @"バフデータベース_{対象番号}", "バフ枠", "死の安寧_ディスペルガード", "バフ対象能力", "ディスペルガード", "バフ名", "死の安寧", "持続ターン", -1, "消去不可フラグ", 1
				DT_ROW_ADD @"バフデータベース_{対象番号}", "バフ枠", "死の安寧_高揚", "バフ対象能力", "高揚", "バフ効果量_固定値", 10, "バフ名", "死の安寧", "持続ターン", -1, "消去不可フラグ", 1
			ENDIF
			
			戦闘中カウント:戦闘行動キャラ:(対象番号) = 2
			戦闘中カウント:戦闘行動キャラ:(対象番号 + 4) = 戦闘中カウント:対象番号:被ダメ合計
		NEXT
ENDSELECT



@固有アビ_10_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 75
	アビ名 = ミアズマハンズ＋
	消費ＭＰ = 100
ELSE
	アビ名 = ミアズマハンズ
	消費ＭＰ = 125
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に6回闇属性［攻撃力×150％］ダメージ
		詳細文文字列受け渡し変数 += @"<br>対象に闇属性防御力累積ダウン効果［累積枠/5ターン/10％］"
		詳細文文字列受け渡し変数 += @"<br>対象にDA率ダウン効果［累積枠/5ターン/10％］/TA率ダウン効果［累積枠/5ターン/10％］"
		詳細文文字列受け渡し変数 += @"<br>◆絶望の闇禍展開中、通常攻撃時に自動発動"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 150
		FOR LOCAL, 0, 6
			CALL アビテンプレート_ダメージ処理_闇属性
		NEXT
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 闇耐性_累積枠
		アビ汎用文字列:バフ・デバフ対象能力 = 闇耐性
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:累積上限_割合 = 40
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_不利効果_累積デバフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = DA率_累積枠
		アビ汎用文字列:バフ・デバフ対象能力 = DA率
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:累積上限_割合 = 40
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_不利効果_累積デバフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = TA率_累積枠
		アビ汎用文字列:バフ・デバフ対象能力 = TA率
		アビ汎用変数:効果割合 = 10
		アビ汎用変数:累積上限_割合 = 40
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_不利効果_累積デバフ効果セット
ENDSELECT



@固有アビ_10_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 対象番号

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 45
	アビ名 = 『黒』の侵蝕
	消費ＭＰ = 200
ENDIF

SIF 戦闘中カウント:戦闘行動キャラ:8
	アビテンプレート用_アビ封印用フラグ = 1

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>フィールドを絶望の闇禍で満たす［永続］
		詳細文文字列受け渡し変数 += @"<br>◆再使用不可/このアビリティは行動回数を消費しない"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:フィールド枠 = 絶望の闇禍
		CALL アビテンプレート_フィールド効果セット
		
		FOR 対象番号, 0, 4
			SIF BATTLE_STATE:対象番号:キャラ番号 < 1
				CONTINUE
			SIF BATTLE_STATE:対象番号:ＨＰ < 1
				CONTINUE
			
			DT_ROW_ADD @"デバフデータベース_{対象番号}", "デバフ枠", "絶望の闇禍_防御力", "デバフ対象能力", "防御力", "デバフ効果量_割合", 25, "デバフ名", "絶望の闇禍", "持続ターン", -1, "消去不可フラグ", 1
			DT_ROW_ADD @"デバフデータベース_{対象番号}", "デバフ枠", "絶望の闇禍_自傷", "デバフ対象能力", "自傷", "デバフ効果量_固定値", 100, "デバフ名", "絶望の闇禍", "持続ターン", -1, "消去不可フラグ", 1
			
			DT_ROW_ADD @"バフデータベース_{対象番号}", "バフ枠", "闇を統べる世界の楔_防御力", "バフ対象能力", "防御力", "バフ効果量_割合", 100, "バフ名", "闇を統べる世界の楔", "持続ターン", -1, "消去不可フラグ", 1
			DT_ROW_ADD @"バフデータベース_{対象番号}", "バフ枠", "闇を統べる世界の楔_DA率", "バフ対象能力", "DA率", "バフ効果量_固定値", 100, "バフ名", "闇を統べる世界の楔", "持続ターン", -1, "消去不可フラグ", 1
			DT_ROW_ADD @"バフデータベース_{対象番号}", "バフ枠", "闇を統べる世界の楔_TA率", "バフ対象能力", "TA率", "バフ効果量_固定値", 30, "バフ名", "闇を統べる世界の楔", "持続ターン", -1, "消去不可フラグ", 1
			DT_ROW_ADD @"バフデータベース_{対象番号}", "バフ枠", "闇を統べる世界の楔_奥義与ダメ", "バフ対象能力", "奥義ダメージ補正_割合", "バフ効果量_割合", 25, "バフ名", "闇を統べる世界の楔", "持続ターン", -1, "消去不可フラグ", 1
		NEXT
		
		FOR 対象番号, 10, 20
			SIF 敵BATTLE_STATE_STR:(対象番号 - 10):エネミー名 == ""
				CONTINUE
			SIF 敵BATTLE_STATE:(対象番号 - 10):ＨＰ < 1
				CONTINUE
			
			DT_ROW_ADD @"デバフデータベース_{対象番号}", "デバフ枠", "絶望の闇禍_防御力", "デバフ対象能力", "防御力", "デバフ効果量_割合", 25, "デバフ名", "絶望の闇禍", "持続ターン", -1, "消去不可フラグ", 1
			DT_ROW_ADD @"デバフデータベース_{対象番号}", "デバフ枠", "絶望の闇禍_自傷", "デバフ対象能力", "自傷", "デバフ効果量_固定値", 500, "デバフ名", "絶望の闇禍", "持続ターン", -1, "消去不可フラグ", 1
		NEXT
		
		アビテンプレート用_行動消費無しフラグ = 1
		
		戦闘中カウント:戦闘行動キャラ:8 = 1 
ENDSELECT



@固有通常攻撃後処理_キャラ_10(全滅フラグ, 連撃数)
#DIM 連撃数
#DIM 全滅フラグ

SIF 全滅フラグ
	RETURN 0

IF 展開中フィールド名格納 == "絶望の闇禍"
	WSTR:(K++) = 《orito!》
	WSTR:(K++) = フェディエルのミアズマハンズ！

	CALL アビ汎用変数文字列リセット
	アビ汎用変数:効果割合 = 150
	FOR LOCAL, 0, 6
		CALL アビテンプレート_ダメージ処理_闇属性
	NEXT
	
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 闇耐性_累積枠
	アビ汎用文字列:バフ・デバフ対象能力 = 闇耐性
	アビ汎用変数:効果割合 = 10
	アビ汎用変数:累積上限_割合 = 40
	アビ汎用変数:持続ターン = 5
	CALL アビテンプレート_不利効果_累積デバフ効果セット
	
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = DA率_累積枠
	アビ汎用文字列:バフ・デバフ対象能力 = DA率
	アビ汎用変数:効果割合 = 10
	アビ汎用変数:累積上限_割合 = 40
	アビ汎用変数:持続ターン = 5
	CALL アビテンプレート_不利効果_累積デバフ効果セット
	
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = TA率_累積枠
	アビ汎用文字列:バフ・デバフ対象能力 = TA率
	アビ汎用変数:効果割合 = 10
	アビ汎用変数:累積上限_割合 = 40
	アビ汎用変数:持続ターン = 5
	CALL アビテンプレート_不利効果_累積デバフ効果セット
ENDIF



@固有ターン終了時処理_バフ_死の安寧
;ターン終了時処理関数から呼び出し
#DIM バフ・デバフ行数
#DIM 隊列番号
#DIM 被ダメ判定
#DIM 死の安寧
#DIM 弱体無効
#DIM ディスペルガード
#DIM 高揚

隊列番号 = キャラ隊列検索(FINDCHARA(NO, 10))
死の安寧 = バフ存在チェック_枠("死の安寧", 戦闘行動キャラ)
弱体無効 = バフ存在チェック_枠("死の安寧_弱体無効", 戦闘行動キャラ)
ディスペルガード = バフ存在チェック_枠("死の安寧_ディスペルガード", 戦闘行動キャラ)
高揚 = バフ存在チェック_枠("死の安寧_高揚", 戦闘行動キャラ)

被ダメ判定 = 0
IF 戦闘中カウント:戦闘行動キャラ:被ダメ合計 > 戦闘中カウント:隊列番号:(戦闘行動キャラ + 4)
	被ダメ判定 = 1
ENDIF

SIF 被ダメ判定 == 1
	戦闘中カウント:隊列番号:(戦闘行動キャラ) -= 1

IF 戦闘中カウント:隊列番号:(戦闘行動キャラ) == 0
	CALL バフ削除(戦闘行動キャラ, 死の安寧)
	CALL バフ削除(戦闘行動キャラ, 弱体無効)
	CALL バフ削除(戦闘行動キャラ, ディスペルガード)
	CALL バフ削除(戦闘行動キャラ, 高揚)
ENDIF

戦闘中カウント:隊列番号:(戦闘行動キャラ + 4) = 戦闘中カウント:戦闘行動キャラ:被ダメ合計



@固有戦闘不能時処理_キャラ_10(隊列番号)
#DIM 隊列番号
#DIM 対象番号
#DIM 防御力
#DIM DA率
#DIM TA率
#DIM 奥義与ダメ

FOR 対象番号, 0, 4
	防御力 = バフ存在チェック_枠("闇を統べる世界の楔_防御力", 対象番号)
	DA率 = バフ存在チェック_枠("闇を統べる世界の楔_DA率", 対象番号)
	TA率 = バフ存在チェック_枠("闇を統べる世界の楔_TA率", 対象番号)
	奥義与ダメ = バフ存在チェック_枠("闇を統べる世界の楔_奥義与ダメ", 対象番号)

	CALL バフ削除(対象番号, 防御力)
	CALL バフ削除(対象番号, DA率)
	CALL バフ削除(対象番号, TA率)
	CALL バフ削除(対象番号, 奥義与ダメ)
NEXT



@固有ターン終了時処理_フィールド_絶望の闇禍
#DIM バフ・デバフ行数
#DIM 隊列番号
#DIM 対象番号

FOR 対象番号, 0, 4
	SIF BATTLE_STATE:対象番号:キャラ番号 < 1
		CONTINUE
	SIF BATTLE_STATE:対象番号:ＨＰ < 1
		CONTINUE
	
	IF デバフ存在チェック_枠("絶望の闇禍_防御力", 対象番号) < 0
		DT_ROW_ADD @"デバフデータベース_{対象番号}", "デバフ枠", "絶望の闇禍_防御力", "デバフ対象能力", "防御力", "デバフ効果量_割合", 25, "デバフ名", "絶望の闇禍", "持続ターン", -1, "消去不可フラグ", 1
		DT_ROW_ADD @"デバフデータベース_{対象番号}", "デバフ枠", "絶望の闇禍_自傷", "デバフ対象能力", "自傷", "デバフ効果量_固定値", 100, "デバフ名", "絶望の闇禍", "持続ターン", -1, "消去不可フラグ", 1
	ENDIF
NEXT

FOR 対象番号, 10, 20
	SIF 敵BATTLE_STATE_STR:(対象番号 - 10):エネミー名 == ""
		CONTINUE
	SIF 敵BATTLE_STATE:(対象番号 - 10):ＨＰ < 1
		CONTINUE
	
	IF デバフ存在チェック_枠("絶望の闇禍_防御力", 対象番号) < 0
		DT_ROW_ADD @"デバフデータベース_{対象番号}", "デバフ枠", "絶望の闇禍_防御力", "デバフ対象能力", "防御力", "デバフ効果量_割合", 25, "デバフ名", "絶望の闇禍", "持続ターン", -1, "消去不可フラグ", 1
		DT_ROW_ADD @"デバフデータベース_{対象番号}", "デバフ枠", "絶望の闇禍_自傷", "デバフ対象能力", "自傷", "デバフ効果量_固定値", 100, "デバフ名", "絶望の闇禍", "持続ターン", -1, "消去不可フラグ", 1
	ENDIF
NEXT



@バフ・デバフ特殊表示_死の安寧(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 死の安寧
PRINTFORML 弱体効果を無効化/強化効果が消去されない/毎ターン奥義ゲージが上昇する状態［消去不可］
PRINTFORML カウント：{戦闘中カウント:戦闘行動キャラ:(隊列番号)}



@フィールド説明文_絶望の闇禍

LOCALS = 絶望の闇禍<br>
LOCALS += "敵味方の防御力が減少/毎ターンダメージを受ける状態<br>"
LOCALS += "効果ターン：永続<br>"

詳細文文字列受け渡し変数 = %LOCALS%

