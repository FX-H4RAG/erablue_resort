;1100.キョウカ 固有奥義
;--------------------------------------------------
;奥義「コスモブルーフラッシュ」「コスモブループラネット」
;--------------------------------------------------
@固有奥義_1100(ARGS, キャラ番号)
#DIM  キャラ番号
#DIM  奥義威力
#DIMS 奥義名
#DIM  クリダメ率加算
#DIM  対象
#DIM  キャラ隊列

キャラ隊列 = キャラ隊列検索(キャラ番号)

; 陥落済 ＋ Lv40以上で強化
IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	; 奥義威力:400％ → 550％
	; 追加：クリティカルダメージ率アップ(+25％,1.5倍→1.75倍)
	奥義名         = コスモブループラネット
	奥義威力       = 550
	クリダメ率加算 = 25
; 通常版
ELSE
	; 奥義威力:400％
	奥義名         = コスモブルーフラッシュ
	奥義威力       = 400
	クリダメ率加算 = 0
ENDIF

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 '= @"奥義威力：{奥義威力}％"
		SIF クリダメ率加算
			詳細文文字列受け渡し変数 += @"<br>◆この奥義のクリティカルダメージ率は＋{クリダメ率加算}％される"
		RETURN 奥義威力
	CASE "奥義前効果"
		; クリティカルダメージ率加算値が0の場合は×
		SIF !クリダメ率加算
			RETURN
		; キャラ隊列が不明な場合は×
		SIF キャラ隊列 == -1
			RETURN
		; 自分のクリティカルダメージ率を増加
		BATTLE_STATE:キャラ隊列:クリティカルダメージ率 += クリダメ率加算
	CASE "奥義追加効果"
		; クリティカルダメージ率加算値が0の場合は×
		SIF !クリダメ率加算
			RETURN
		; キャラ隊列が不明な場合は×
		SIF キャラ隊列 == -1
			RETURN
		; 自分のクリティカルダメージ率を戻す
		BATTLE_STATE:キャラ隊列:クリティカルダメージ率 -= クリダメ率加算
ENDSELECT
