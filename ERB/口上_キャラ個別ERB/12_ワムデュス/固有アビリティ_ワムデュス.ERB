
@固有アビ_12_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM テンタクル・フォース
#DIM バフ行数
#DIM 対象番号

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 55
	アビ名 = ヴォーテクスアトラクター＋
	消費ＭＰ = 100
ELSE
	アビ名 = ヴォーテクスアトラクター
	消費ＭＰ = 150
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>◆ワム、おなか空いた……『つよくなるやつ』、いっぱい吸い込む
		詳細文文字列受け渡し変数 += @"<br>自身を除く敵味方全体の強化効果を全て消去"
		詳細文文字列受け渡し変数 += @"<br>自身にテンタクル・フォース効果［2ターン/消去不可］"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		FOR 対象番号, 10, 20
			SIF 敵BATTLE_STATE_STR:(対象番号 - 10):エネミー名 == ""
				CONTINUE
			SIF 敵BATTLE_STATE:(対象番号 - 10):ＨＰ < 1
				CONTINUE
			
			CALL アビ対象選択テンプレート_指定(対象番号)
			
			SIF DT_ROW_LENGTH(@"デバフデータベース_{対象番号}") == 0
				CONTINUE
			
			FOR バフ行数, 0, DT_ROW_LENGTH(@"バフデータベース_{対象番号}")
				CALL アビ汎用変数文字列リセット
				CALL アビテンプレート_不利効果_バフ消去()
			NEXT
		NEXT
		
		FOR 対象番号, 0, 4
			SIF 対象番号 == キャラ隊列検索(FINDCHARA(NO, 12))
				CONTINUE
			SIF BATTLE_STATE:対象番号:キャラ番号 < 1
				CONTINUE
			SIF BATTLE_STATE:対象番号:ＨＰ < 1
				CONTINUE
			
			CALL アビ対象選択テンプレート_指定(対象番号)
			
			SIF DT_ROW_LENGTH(@"デバフデータベース_{対象番号}") == 0
				CONTINUE
			
			FOR バフ行数, 0, DT_ROW_LENGTH(@"バフデータベース_{対象番号}")
				CALL アビ汎用変数文字列リセット
				CALL アビテンプレート_不利効果_バフ消去()
			NEXT
		NEXT
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = テンタクル・フォース
		アビ汎用文字列:バフ・デバフ対象能力 = テンタクル・フォース
		アビ汎用変数:効果量 = 0
		アビ汎用変数:持続ターン = 2
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		テンタクル・フォース = バフ番号取得_枠("テンタクル・フォース", 戦闘行動キャラ)
		IF テンタクル・フォース > -1
			バフ行数 = バフ番号取得_枠("T・F_通常攻撃ダメージ補正_割合", 戦闘行動キャラ)
			DT_CELL_SET @"バフデータベース_{戦闘行動キャラ}", バフ行数, "持続ターン", 2
			バフ行数 = バフ番号取得_枠("T・F_絶対TA", 戦闘行動キャラ)
			DT_CELL_SET @"バフデータベース_{戦闘行動キャラ}", バフ行数, "持続ターン", 2
			バフ行数 = バフ番号取得_枠("T・F_追撃", 戦闘行動キャラ)
			DT_CELL_SET @"バフデータベース_{戦闘行動キャラ}", バフ行数, "持続ターン", 2
		ELSE
			DT_ROW_ADD @"バフデータベース_{戦闘行動キャラ}", "バフ枠", "T・F_通常攻撃ダメージ補正_割合", "バフ対象能力", "通常攻撃ダメージ補正_割合", "バフ効果量_割合", 50, "バフ名", "テンタクル・フォース", "持続ターン", 2, "消去不可フラグ", 1
			DT_ROW_ADD @"バフデータベース_{戦闘行動キャラ}", "バフ枠", "T・F_絶対TA", "バフ対象能力", "TA率", "バフ効果量_固定値", 1000, "バフ名", "テンタクル・フォース", "持続ターン", 2, "消去不可フラグ", 1
			DT_ROW_ADD @"バフデータベース_{戦闘行動キャラ}", "バフ枠", "T・F_追撃", "バフ対象能力", "追撃", "バフ効果量_割合", 100, "バフ名", "テンタクル・フォース", "持続ターン", 2, "消去不可フラグ", 1
		ENDIF
ENDSELECT



@固有アビ_12_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 75
	アビ名 = イノセントトキシン＋
	消費ＭＰ = 100
ELSE
	アビ名 = イノセントトキシン
	消費ＭＰ = 125
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に6回水属性の［攻撃力✕150％］ダメージ
		詳細文文字列受け渡し変数 += @"<br>対象に麻痺効果［個人枠/2ターン/基礎成功確率90％］"
		詳細文文字列受け渡し変数 += @"<br>対象に劇毒効果［5ターン/回復不可］"
		詳細文文字列受け渡し変数 += @"<br>◆ダメージを受けた時、1ターンに1回自動発動"
	RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 150
		FOR LOCAL, 0, 6
			CALL アビテンプレート_ダメージ処理_水属性
		NEXT
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 麻痺_個人枠
		アビ汎用文字列:バフ・デバフ対象能力 = 麻痺
		アビ汎用変数:効果割合 = 100
		アビ汎用変数:持続ターン = 2
		アビ汎用変数:基礎成功確率 = 90
		CALL アビテンプレート_不利効果_デバフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 劇毒
		アビ汎用文字列:バフ・デバフ対象能力 = 毒
		アビ汎用変数:効果量 = 50
		アビ汎用変数:累積上限_割合 = 500
		アビ汎用変数:持続ターン = 5
		アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_不利効果_累積デバフ効果セット
ENDSELECT



@固有戦闘開始時処理_キャラ_12
;戦闘開始時処理関数から呼び出し
#DIM イノセントトキシン
#DIM 隊列番号

隊列番号 = キャラ隊列検索(FINDCHARA(NO, 12))

イノセントトキシン = バフ番号取得_枠("イノセントトキシン", 隊列番号)
IF イノセントトキシン < 0
	DT_ROW_ADD @"バフデータベース_{隊列番号}", "バフ枠", "イノセントトキシン", "バフ対象能力", "特殊反応_被ダメ時", "バフ名", "イノセントトキシン", "持続回数", 1, "消去不可フラグ", 1, "特殊表示オプション", 1
ENDIF



@固有ターン開始時処理_キャラ_12
;ターン開始時処理関数から呼び出し
#DIM イノセントトキシン
#DIM 隊列番号

隊列番号 = キャラ隊列検索(FINDCHARA(NO, 12))

イノセントトキシン = バフ番号取得_枠("イノセントトキシン", 隊列番号)
IF イノセントトキシン < 0
	DT_ROW_ADD @"バフデータベース_{隊列番号}", "バフ枠", "イノセントトキシン", "バフ対象能力", "特殊反応_被ダメ時", "バフ名", "イノセントトキシン", "持続回数", 1, "消去不可フラグ", 1, "特殊表示オプション", 1
ENDIF



@特殊反応_被ダメ時_イノセントトキシン(攻撃者隊列, 被ダメ回数)
#DIM 被ダメ回数
#DIM 攻撃者隊列

CALL アビ対象選択テンプレート_指定(攻撃者隊列)

WSTR:(K++) = 《emuziiis!》
アビテンプレート用_アビ名 = イノセントトキシン

CALL アビ汎用変数文字列リセット
アビ汎用変数:効果割合 = 150
FOR LOCAL, 0, 6
	CALL アビテンプレート_ダメージ処理_水属性
NEXT

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 麻痺_個人枠
アビ汎用文字列:バフ・デバフ対象能力 = 麻痺
アビ汎用変数:効果割合 = 100
アビ汎用変数:持続ターン = 2
アビ汎用変数:基礎成功確率 = 90
CALL アビテンプレート_不利効果_デバフ効果セット

CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 劇毒
アビ汎用文字列:バフ・デバフ対象能力 = 毒
アビ汎用変数:効果量 = 50
アビ汎用変数:累積上限_割合 = 500
アビ汎用変数:持続ターン = 5
アビ汎用変数:消去不可オプション = 1
CALL アビテンプレート_不利効果_累積デバフ効果セット



@固有アビ_12_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 隊列番号
隊列番号 = キャラ隊列検索(キャラ番号)

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
	アビ名 = 『碧』の月暈
	消費ＭＰ = 300
ENDIF

SIF 隊列番号 > -1 && 戦闘中カウント:隊列番号:0
	アビテンプレート用_アビ封印用フラグ = 1

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に『碧』の月暈効果［永続/消去不可］
		詳細文文字列受け渡し変数 += @"<br>◆再使用不可/このアビリティは行動回数を消費しない"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 『碧』の月暈
		アビ汎用文字列:バフ・デバフ対象能力 = 『碧』の月暈
		アビ汎用変数:効果量 = 0
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		DT_ROW_ADD @"バフデータベース_{戦闘行動キャラ}", "バフ枠", "『碧』の月暈_防御力", "バフ対象能力", "防御力", "バフ効果量_割合", 300, "バフ名", "『碧』の月暈", "持続ターン", -1, "消去不可フラグ", 1
		DT_ROW_ADD @"バフデータベース_{戦闘行動キャラ}", "バフ枠", "『碧』の月暈_弱体無効", "バフ対象能力", "弱体無効", "バフ効果量_固定値", 0, "バフ名", "『碧』の月暈", "持続ターン", -1, "消去不可フラグ", 1
		DT_ROW_ADD @"バフデータベース_{戦闘行動キャラ}", "バフ枠", "『碧』の月暈_被ダメゲージ", "バフ対象能力", "ゲージ上昇効率_被ダメ時", "バフ効果量_割合", 200, "バフ名", "『碧』の月暈", "持続ターン", -1, "消去不可フラグ", 1
		DT_ROW_ADD @"バフデータベース_{戦闘行動キャラ}", "バフ枠", "『碧』の月暈_再生", "バフ対象能力", "再生", "バフ効果量_割合", 5, "バフ名", "『碧』の月暈", "持続ターン", -1, "消去不可フラグ", 1
		
		戦闘中カウント:戦闘行動キャラ:0 = 1
		
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT



@バフ・デバフ特殊表示_テンタクル・フォース(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML テンタクル・フォース
PRINTFORML 通常攻撃与ダメージが上昇/必ずトリプルアタック/通常攻撃時に追撃を行う状態［2ターン/消去不可］



@バフ・デバフ特殊表示_イノセントトキシン(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML イノセントトキシン
PRINTFORML ダメージを受けた際、1ターンに1回イノセントトキシンが発動する状態［永続/消去不可］



@バフ・デバフ特殊表示_『碧』の月暈(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 『碧』の月暈
PRINTFORML 防御性能・被ダメージの奥義ゲージ上昇量が大幅に上昇/毎ターンHPを回復する状態［永続/消去不可］

;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_12(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『ろーどーのよろこび』 □-------------------------------------------------------
KSTR:(K++) = 　最大HPが高いが、奥義ゲージが溜まりにくい
KSTR:(K++) = 　奥義ゲージ最大値200％

CALL 口上変数表示(対象キャラ, 1)


