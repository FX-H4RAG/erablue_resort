
@固有奥義_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIMS 奥義名
#DIM 表示用奥義威力

IF イベント完了フラグ確認("未神宮への参拝") > 0
	奥義名 = 羊瀾怒濤
	奥義威力 = 0
	表示用奥義威力 = 600
ELSE
	奥義名 = 金牙神然
	奥義威力 = 500
	表示用奥義威力 = 500
ENDIF

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		IF 奥義名 == "羊瀾怒濤"
			詳細文文字列受け渡し変数 += "自身に3ターンの追撃［20％/C枠］効果を与える。<br>"
			詳細文文字列受け渡し変数 += "敵全体に火・水・風・土・光・闇属性でそれぞれ［攻撃力✕100％］のダメージを与える。"
		ELSE
			詳細文文字列受け渡し変数 = 奥義威力：{表示用奥義威力}％<br>
			詳細文文字列受け渡し変数 += "自身に3ターンの追撃［20％/C枠］効果を与える。<br>"
		ENDIF
		RETURN 奥義威力
	CASE "奥義説明文_詳細"
		IF 奥義名 == "羊瀾怒濤"
			詳細文文字列受け渡し変数 += "自身に3ターンの追撃［20％/C枠］効果を与える。<br>"
			詳細文文字列受け渡し変数 += "敵全体に火属性［攻撃力✕100％］のダメージを与える。<br>"
			詳細文文字列受け渡し変数 += "敵全体に水属性［攻撃力✕100％］のダメージを与える。<br>"
			詳細文文字列受け渡し変数 += "敵全体に風属性［攻撃力✕100％］のダメージを与える。<br>"
			詳細文文字列受け渡し変数 += "敵全体に土属性［攻撃力✕100％］のダメージを与える。<br>"
			詳細文文字列受け渡し変数 += "敵全体に光属性［攻撃力✕100％］のダメージを与える。<br>"
			詳細文文字列受け渡し変数 += "敵全体に闇属性［攻撃力✕100％］のダメージを与える。<br>"
		ENDIF
	CASE "奥義追加効果"
		;ここに奥義効果を記述する。基本の奥義ダメージ部分は記載する必要はない。
		;直接記述しても良いが、基本的にアビテンプレートの関数を使用すると便利で良い
		;テンプレート使用の例は下部分を参照

		;追撃付与部分
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果割合 = 20
		アビ汎用文字列:バフ・デバフ枠 = 追撃C
		アビ汎用文字列:バフ・デバフ対象能力 = 追撃
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		;奥技が進化後なら、通常の奥義攻撃の代わりに６属性の全体攻撃を行う
		IF 奥義名 == "羊瀾怒濤"
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:ダメージ種類 = 奥義ダメージ
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用変数:効果割合 = 100
			CALL アビテンプレート_ダメージ処理_火属性
			CALL アビテンプレート_ダメージ処理_水属性
			CALL アビテンプレート_ダメージ処理_風属性
			CALL アビテンプレート_ダメージ処理_土属性
			CALL アビテンプレート_ダメージ処理_光属性
			CALL アビテンプレート_ダメージ処理_闇属性
		ENDIF
ENDSELECT
