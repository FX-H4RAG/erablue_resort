
;----------------------------------------------------
;キャラ固有コマンド処理
;----------------------------------------------------
;コマンド名
;----------------------------------------------------
@CHARACOMNAME370_25
#FUNCTION
LOCAL = FINDCHARA(NO, 25)
TSTR:コマンド名受渡 = 肉体再錬成
;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_370_25
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_370_25
LOCALS = <br>■肉体再錬成<br>
LOCALS += "カリオストロに頼んで、ZPを使うことなく肉体の外見を変化させて錬成しなおしてもらう"
LOCALS += "<br>経過時間：60分"
TSTR:ツールチップ受渡 = %LOCALS%

;-------------------------------------------------
;実行処理
;-------------------------------------------------
@CHARACOM370_25
#DIM 変更フラグ, 4
#DIM CONST 変更箇所 = 2, 200, 205, 162
#DIM L_COUNT
LOCAL = FINDCHARA(NO, 25)
VARSET 変更フラグ, -5
L_COUNT = LINECOUNT

$LOOP_START
CLEARLINE LINECOUNT - L_COUNT
PRINT 「
FONTITALIC
PRINT で、どんな設計にするんだ？
FONTSTYLE 0
PRINTL 」
PRINTL どの素質を変化させますか？
DRAWLINE

PRINT 性別【　
FOR LOCAL , 1, 4
	SIF 素質切り替えOPTION記憶:ふたなり_女性 && LOCAL == 3
		CONTINUE
	IF TALENT:性別 == LOCAL && 変更フラグ:0 == LOCAL
		SETCOLOR 0x666666
		PRINTPLAINFORM [{LOCAL, 2}]%GET_TALENTNAME(2, LOCAL)%（現在）　
		RESETCOLOR
	ELSEIF 変更フラグ:0 == LOCAL
		SETCOLOR 0x666666
		PRINTPLAINFORM [{LOCAL, 2}]%GET_TALENTNAME(2, LOCAL)%（予定）
		RESETCOLOR
	ELSEIF TALENT:性別 == LOCAL
		PRINTBUTTON @"[{LOCAL, 2}]%GET_TALENTNAME(2, LOCAL)%（現在）", LOCAL
	ELSE
		PRINTBUTTON @"[{LOCAL, 2}]%GET_TALENTNAME(2, LOCAL)%　　　　", LOCAL
	ENDIF
NEXT
PRINTL 】

PRINT 種族【　
FOR LOCAL , 1, 7
	IF TALENT:種族 == LOCAL && 変更フラグ:1 == LOCAL
		SETCOLOR 0x666666
		PRINTPLAINFORM [{LOCAL+10}]%GET_TALENTNAME(200, LOCAL)%（現在）
		RESETCOLOR
	ELSEIF 変更フラグ:1 == LOCAL
		SETCOLOR 0x666666
		PRINTPLAINFORM [{LOCAL+10}]%GET_TALENTNAME(200, LOCAL)%（予定）
		RESETCOLOR
	ELSEIF TALENT:種族 == LOCAL
		PRINTBUTTON @"[{LOCAL+10}]%GET_TALENTNAME(200, LOCAL)%（現在）", LOCAL+10
	ELSE
		PRINTBUTTON @"[{LOCAL+10}]%GET_TALENTNAME(200, LOCAL)%　　　　", LOCAL+10
	ENDIF
NEXT
PRINTL 】
IF GETBIT(TALENT:性別, 0) || (変更フラグ:0 > -1 && GETBIT(変更フラグ:0, 0))
	PRINT 乳の大きさ【　
	FOR LOCAL, -2, 4
		IF TALENT:バストサイズ == LOCAL && 変更フラグ:2 == LOCAL
			SETCOLOR 0x666666
			PRINTPLAINFORM [{LOCAL+23}]%GET_TALENTNAME(205, LOCAL)%（現在）
			RESETCOLOR
		ELSEIF 変更フラグ:2 == LOCAL
			SETCOLOR 0x666666
			PRINTPLAINFORM [{LOCAL+23}]%GET_TALENTNAME(205, LOCAL)%（予定）
			RESETCOLOR
		ELSEIF TALENT:バストサイズ == LOCAL
			PRINTBUTTON @"[{LOCAL+23}]%GET_TALENTNAME(205, LOCAL)%（現在）", LOCAL+23
		ELSE
			PRINTBUTTON @"[{LOCAL+23}]%GET_TALENTNAME(205, LOCAL)%　　　　", LOCAL+23
		ENDIF
	NEXT
	PRINTL 】
ENDIF

IF GETBIT(TALENT:性別, 1) || (変更フラグ:0 > -1 && GETBIT(変更フラグ:0, 1))
	PRINT 男性器の大きさ【　
	FOR LOCAL , -2, 3
		IF TALENT:男性器サイズ == LOCAL && 変更フラグ:3 == LOCAL
			SETCOLOR 0x666666
			PRINTPLAINFORM [{LOCAL+33}]%GET_TALENTNAME(162, LOCAL)%（現在）
			RESETCOLOR
		ELSEIF 変更フラグ:3 == LOCAL
			SETCOLOR 0x666666
			PRINTPLAINFORM [{LOCAL+33}]%GET_TALENTNAME(162, LOCAL)%（予定）
			RESETCOLOR
		ELSEIF TALENT:男性器サイズ == LOCAL
			PRINTBUTTON @"[{LOCAL+33}]%GET_TALENTNAME(162, LOCAL)%（現在）", LOCAL+33
		ELSE
			PRINTBUTTON @"[{LOCAL+33}]%GET_TALENTNAME(162, LOCAL)%　　　　", LOCAL+33
		ENDIF
	NEXT
	PRINTL 】
ENDIF

PRINT [999]終了する", 999
PRINTL

BINPUT
SELECTCASE RESULT
	CASE 1 TO 9
		変更フラグ:0 = RESULT
		GOTO LOOP_START
	CASE 10 TO 19
		変更フラグ:1 = RESULT - 10
		GOTO LOOP_START
	CASE 20 TO 29
		変更フラグ:2 = RESULT - 23
		GOTO LOOP_START
	CASE 30 TO 39
		変更フラグ:3 = RESULT - 33
		GOTO LOOP_START
	CASE 999
		SIF MAXARRAY(変更フラグ) < -4
			RETURN -1
	CASEELSE
		GOTO LOOP_START
ENDSELECT

PRINTL
PRINT 「
FONTITALIC
PRINT これでいいか？
FONTSTYLE 0
PRINTL 」

PRINTFORM 性別【%GET_TALENTNAME(2, (変更フラグ:0 > -5 ? 変更フラグ:0# TALENT:性別))%】　
PRINTFORM 種族【%GET_TALENTNAME(200, (変更フラグ:1 > -5 ? 変更フラグ:1# TALENT:種族))%】　
SIF GETBIT(TALENT:性別, 0) || (変更フラグ:0 > -1 && GETBIT(変更フラグ:0, 0))
	PRINTFORM 乳の大きさ【%GET_TALENTNAME(205, (変更フラグ:2 > -5 ? 変更フラグ:2# TALENT:バストサイズ))%】
SIF GETBIT(TALENT:性別, 1) || (変更フラグ:0 > -1 && GETBIT(変更フラグ:0, 1))
	PRINTFORM 男性器の大きさ【%GET_TALENTNAME(162, (変更フラグ:3 > -5 ? 変更フラグ:3# TALENT:男性器サイズ))%】

PRINTBUTTONC "[0] はい　", 0
PRINTBUTTONC "[1] いいえ", 1
PRINTL

BINPUT
SIF RESULT != 0
	GOTO LOOP_START

FOR LOCAL, 0, 4
	RFLAG:(GETNUM(RFLAG, "コマンド結果受渡し変数")+LOCAL) = 変更フラグ:LOCAL
	IF 変更フラグ:LOCAL != -5 && TALENT:(変更箇所:LOCAL) != 変更フラグ:LOCAL
		SWAP TALENT:(変更箇所:LOCAL), 変更フラグ:LOCAL
		IF LOCAL == 1 && TALENT:(変更箇所:LOCAL) == 6
			$INPUT_EXRACE
			LOCAL:1 = LINECOUNT
			CALL CHOICE("「サンプル数の少ない種族ね。どれにしよっか？」", "クリスタリア", "ヴァンパイア", "ワーウルフ", "その他")
			SELECTCASE RESULT
				CASE 0
					;クリスタリア
					TALENT:ハーヴィン耳 = 1
					TALENT:吸血鬼 = 0
					TALENT:発情期あり = 0
					TALENT:尻尾あり = 0
					TALENT:エルーン耳 = 0
				CASE 1
					;ヴァンパイア
					TALENT:ハーヴィン耳 = 0
					TALENT:吸血鬼 = 1
					TALENT:発情期あり = 0
					TALENT:尻尾あり = 0
					TALENT:エルーン耳 = 0
				CASE 2
					;ワーウルフ
					TALENT:ハーヴィン耳 = 0
					TALENT:吸血鬼 = 0
					TALENT:発情期あり = 1
					TALENT:尻尾あり = 1
					TALENT:エルーン耳 = 1
				CASE 3
					;自由選択（名前のみ）
					PRINT 「
					FONTITALIC
					PRINT 言っとくが、さすがのオレ様もあんまりレアな種族は殆ど再現出来ないぞ？
					FONTSTYLE 0
					PRINTL 」
					PRINTL 追加種族名を任意で入力してください
					INPUTS
					IF RESULTS != ""
						CSTR:特殊種族 = %RESULTS%
					ELSE
						CLEARLINE LINECOUNT - LOCAL:1
						GOTO INPUT_EXRACE
					ENDIF
			ENDSELECT
			CALL CHOICE(@"いわゆる異人の中でも【%CSTR:特殊種族%】に併せる…これでいい？", "ＯＫ！", "選び直す")
			IF RESULT == 1
				CLEARLINE LINECOUNT - LOCAL:1
				GOTO INPUT_EXRACE
			ENDIF
		ELSEIF LOCAL == 1
			TALENT:ハーヴィン耳 = 0
			TALENT:吸血鬼 = 0
			TALENT:発情期あり = 0
			TALENT:尻尾あり = 0
			TALENT:エルーン耳 = 0
		ENDIF
	ENDIF
NEXT
;一旦TALENTを変更してから、変更後の影響を再計算する
FOR LOCAL, 0, 4
	SIF 変更フラグ:LOCAL == -5
		CONTINUE
	SELECTCASE LOCAL
		CASE 0
			CALL ZP_SHOP_性別特徴変換(TARGET, 変更フラグ:LOCAL, TALENT:(変更箇所:LOCAL))
			CALL SET_BODYSIZE(TARGET, 3)
			IF TALENT:性別 == 2
				TALENT:性転換 = 0
			ELSE
				TALENT:性転換 = 2
			ENDIF
		CASE 1
			CALL ZP_SHOP_種族特徴変換(TARGET, 変更フラグ:LOCAL, TALENT:(変更箇所:LOCAL))
			SIF RESULT == 0
				CALL SET_BODYSIZE(TARGET,0)
		CASE 2
			CALL SET_BODYSIZE(TARGET,2)
	ENDSELECT
NEXT

PRINT カリオストロは現在の肉体を
PRINTFORM 性別【%GET_TALENTNAME(2, TALENT:性別)%】　
PRINTFORM 種族【%GET_TALENTNAME(200, TALENT:種族)%\@ TALENT:種族 == 6? (%CSTR:特殊種族%)# \@】　
SIF GETBIT(TALENT:性別, 0)
	PRINTFORM 乳の大きさ【%GET_TALENTNAME(205, TALENT:バストサイズ)%】
SIF GETBIT(TALENT:性別, 1)
	PRINTFORM 男性器の大きさ【%GET_TALENTNAME(162, TALENT:男性器サイズ)%】
PRINTW へと再錬成しました


TIME += 60

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE370_25
;一括管理
SIF GLOBAL_COMABLE(370)
	RETURN RESULT
LOCAL = FINDCHARA(NO, 25)
SIF TARGET != LOCAL
	RETURN 0
;陥落後にZPショップから外見変更を伴う素質変化を1度は行うまで使用不可
SIF 特定状況口上用フラグ:LOCAL:0 >= 0
	RETURN 0
RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM370_25
LOCAL = FINDCHARA(NO, 25)



