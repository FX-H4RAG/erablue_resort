
@固有奥義_25(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIMS 奥義名

;レベルなどの条件で奥義を強化する場合、奥義名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 35
 	奥義名 = オプス・マグナ
 	奥義威力 = 700
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 18
	奥義名 = アルス・マグナ＋
	奥義威力 = 600
ELSE
	奥義名 = アルス・マグナ
	奥義威力 = 500
ENDIF

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 = 奥義威力：{奥義威力}％<br>敵単体に自属性の［攻撃力✕{奥義威力}％］のダメージを与える。
		SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 18
			詳細文文字列受け渡し変数 += @"<br>3ターンの間、自身の回復力を[20％/独自枠]上昇させる。"
		SIF 基礎BATTLE_STATE:キャラ番号:Lv >= 35
			詳細文文字列受け渡し変数 += @"<br>自身のＭＰを50回復させる。"
		RETURN 奥義威力
	CASE "奥義追加効果"
		;ここに奥義効果を記述する。基本の奥義ダメージ部分は記載する必要はない。
		;直接記述しても良いが、基本的にアビテンプレートの関数を使用すると便利で良い
		;テンプレート使用の例は下部分を参照
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 18
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:効果割合 = 20
			アビ汎用文字列:バフ・デバフ枠 = アルス・マグナ
			アビ汎用文字列:バフ・デバフ対象能力 = 回復力
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 35
			BATTLE_STATE:ARG:ＭＰ = MIN(BATTLE_STATE:ARG:ＭＰ + 50, BATTLE_STATE:ARG:最大ＭＰ)
		ENDIF
ENDSELECT
