
@固有アビ_30_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM バフ・デバフ行数
#DIM 隊列番号
#DIM クールダウン
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	アビ名 = ブルームーン++
	消費ＭＰ = 55
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	アビ名 = ブルームーン＋
	消費ＭＰ = 45
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 1
	アビ名 = ブルームーン
	消費ＭＰ = 40
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に水属性［攻撃力×350～450％］ダメージ
			詳細文文字列受け渡し変数 += @"<br>対象に防御ダウン効果［20％/5ターン/B枠］"
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に水属性［攻撃力×250～350％］ダメージ
			詳細文文字列受け渡し変数 += @"<br>対象に防御ダウン効果［20％/5ターン/B枠］"
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 1
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に水属性［攻撃力×200～300％］ダメージ
			詳細文文字列受け渡し変数 += @"<br>対象に防御ダウン効果［20％/5ターン/B枠］"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
			CALL アビ対象選択テンプレート_敵単体
			アビ汎用変数:効果割合 = RAND(350, 450)
			CALL アビテンプレート_ダメージ処理_水属性
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			CALL アビ対象選択テンプレート_敵単体
			アビ汎用変数:効果割合 = RAND(250, 350)
			CALL アビテンプレート_ダメージ処理_水属性
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 1
			CALL アビ対象選択テンプレート_敵単体
			アビ汎用変数:効果割合 = RAND(200, 300)
			CALL アビテンプレート_ダメージ処理_水属性
		ENDIF
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 20
		アビ汎用文字列:バフ・デバフ枠 = 防御力B
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT


@固有アビ_30_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM バフ・デバフ行数
#DIM 隊列番号
#DIMS アビ名

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
	アビ名 = ケーニヒシルト＋
	消費ＭＰ = 80
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 5
	アビ名 = ケーニヒシルト
	消費ＭＰ = 75
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体に被ダメージカット効果［50％/1ターン］
			詳細文文字列受け渡し変数 += @"<br>味方全体に防御力アップ効果［10％/3ターン/A枠］"
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 5
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体に被ダメージカット効果［50％/1ターン］
		ENDIF
	RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 50
		アビ汎用文字列:バフ・デバフ枠 = ダメージカット
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ増減
		アビ汎用変数:持続ターン = 2
		CALL アビテンプレート_有利効果_バフ効果セット
		
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 30
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 10
			アビ汎用文字列:バフ・デバフ枠 = 防御力A
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT


@固有アビ_30_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
	アビ名 = ソード・オブ・リュミエール+
	消費ＭＰ = 90
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 10
	アビ名 = ソード・オブ・リュミエール
	消費ＭＰ = 65
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に水属性［攻撃力×550％］ダメージ
			詳細文文字列受け渡し変数 += @"<br>対象にスロウ効果［1回/基礎成功率100％］"
			詳細文文字列受け渡し変数 += @"<br>自身に被ダメージ無効効果［1ターン］"
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 10
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に水属性［攻撃力×400～500％］ダメージ
			詳細文文字列受け渡し変数 += @"<br>対象のCTがMAXの時、スロウ効果［1回/基礎成功率100％］"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
			CALL アビ対象選択テンプレート_敵単体
			アビ汎用変数:効果割合 = 550
			CALL アビテンプレート_ダメージ処理_水属性
			
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 1
			アビ汎用変数:基礎成功確率 = 100
			CALL アビテンプレート_不利効果_スロウ
			
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:効果割合 = 100
			アビ汎用文字列:バフ・デバフ枠 = 無敵
			アビ汎用文字列:バフ・デバフ対象能力 = ダメージ増減
			アビ汎用変数:持続ターン = 1
			CALL アビテンプレート_有利効果_バフ効果セット
		ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 10
			CALL アビ対象選択テンプレート_敵単体
			アビ汎用変数:効果割合 = 550
			CALL アビテンプレート_ダメージ処理_水属性
			
			IF 敵BATTLE_STATE:(戦闘行動対象 - 10):チャージターン == 敵BATTLE_STATE:(戦闘行動対象 - 10):最大チャージターン
				CALL アビ汎用変数文字列リセット
				アビ汎用変数:効果量 = 1
				アビ汎用変数:基礎成功確率 = 100
				CALL アビテンプレート_不利効果_スロウ
			ENDIF
		ENDIF
		
ENDSELECT


@固有戦闘開始時処理_キャラ_30
;戦闘開始時処理関数から呼び出し
IF 基礎BATTLE_STATE:30:Lv >= 45
	WSTR:(K++) = 「シャルロッテ、推して参るであります！」
	アビテンプレート用_アビ名 = 正義の使徒
	
	CALL アビ対象選択テンプレート_味方全体_生者のみ
	アビ汎用変数:効果量 = 5
	CALL アビテンプレート_有利効果_奥義ゲージUP
ENDIF


@固有ターン終了時処理_キャラ_30
;ターン終了時処理関数から呼び出し

WSTR:(K++) = 「清く正しく高潔に！」
アビテンプレート用_アビ名 = リュミエール聖騎士団団長
		
CALL アビ対象選択テンプレート_自己のみ
アビ汎用変数:効果量 = 0
アビ汎用変数:消去不可オプション = 1
アビ汎用文字列:バフ・デバフ枠 = リュミエール聖騎士団団長
アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_被ダメ時
アビ汎用変数:持続回数 = 1
CALL アビテンプレート_有利効果_バフ効果セット

CALL アビ汎用変数文字列リセット
アビ汎用変数:効果割合 = 6
アビ汎用変数:累積上限_割合 = 30
アビ汎用文字列:バフ・デバフ枠 = リュミエール聖騎士団団長_攻撃力
アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
アビ汎用変数:持続ターン = -1
CALL アビテンプレート_有利効果_累積バフ効果セット

CALL アビ汎用変数文字列リセット
アビ汎用変数:効果量 = 7
アビ汎用変数:累積上限_固定値 = 35
アビ汎用文字列:バフ・デバフ枠 = リュミエール聖騎士団団長_DA率
アビ汎用文字列:バフ・デバフ対象能力 = DA率
アビ汎用変数:持続ターン = -1
CALL アビテンプレート_有利効果_累積バフ効果セット

CALL アビ汎用変数文字列リセット
アビ汎用変数:効果量 = 6
アビ汎用変数:累積上限_固定値 = 35
アビ汎用文字列:バフ・デバフ枠 = リュミエール聖騎士団団長_TA率
アビ汎用文字列:バフ・デバフ対象能力 = TA率
アビ汎用変数:持続ターン = -1
CALL アビテンプレート_有利効果_累積バフ効果セット


@特殊反応_被ダメ時_リュミエール聖騎士団団長(バフ番号, 隊列番号, 被ダメ回数)
#DIM 被ダメ回数
#DIM 隊列番号
#DIM バフ番号

隊列番号 = キャラ隊列検索(FINDCHARA(NO, 30))

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_自己のみ
アビ汎用変数:効果量 = 1
CALL アビテンプレート_不利効果_バフ消去("リュミエール聖騎士団団長_攻撃力")
CALL アビテンプレート_不利効果_バフ消去("リュミエール聖騎士団団長_DA率")
CALL アビテンプレート_不利効果_バフ消去("リュミエール聖騎士団団長_TA率")


;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_30(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『正義の使徒』 □----------------------------------------------------------------
KSTR:(K++) = 　戦闘開始時、味方全体の奥義ゲージ［5％］アップ
KSTR:(K++) = 　
KSTR:(K++) = □ 特性：『リュミエール聖騎士団団長』 □-------------------------------------------------
KSTR:(K++) = 　ターン終了時、自身の攻撃力・連撃率累積アップ
KSTR:(K++) = 　ダメージを受けると解除される

CALL 口上変数表示(対象キャラ, 1)

