@固有アビ_31_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果数値
#DIM 効果数値２
#DIM 効果数値３
#DIMS アビ名


IF 基礎BATTLE_STATE:キャラ番号:Lv >= 5
	IF キャラ隊列検索(キャラ番号) < 0 || 戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 0
		アビ名 = 胡蝶刃・神楽
	ELSE
		アビ名 = 胡蝶刃・源氏
	ENDIF
	消費ＭＰ = 20
	効果数値 = 10
	効果数値２ = 70
	効果数値３ = 20
ELSE
	アビ名 = 
ENDIF

;戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 0 源氏
;戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 1 神楽

IF キャラ隊列検索(キャラ番号) < 0 || 戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 0
	アビ汎用文字列:実行時メッセージ１ = 「舞えよ胡蝶。刃は踊り、神楽の如く…」
	アビ汎用文字列:実行時メッセージ２ = 「刃は吟じ、神楽の如く…」
ELSE
	アビ汎用文字列:実行時メッセージ１ = 「舞えよ胡蝶。刃は翻り、源氏の如く…」
	アビ汎用文字列:実行時メッセージ２ = 「刃は語り、源氏の如く…」
ENDIF

IF キャラ隊列検索(キャラ番号) >= 0
	SIF 現在ターン数 == 戦闘中カウント:キャラ隊列検索(キャラ番号):1 && アビテンプレート用_アビ封印用フラグ != 1
		アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>構えを変更する。
		詳細文文字列受け渡し変数 += @"自身の攻撃力(独自枠)を[{効果数値}％/累積枠/上限50％/4T]上昇させる。"
		詳細文文字列受け渡し変数 += @"<br>自身に追撃(独自枠)[攻撃力×{効果数値２}％/4T]を付与する。"
		詳細文文字列受け渡し変数 += @"<br>の間、自身の防御力(独自枠)を[{効果数値３}％/3T]減少させる。"
		詳細文文字列受け渡し変数 += @"<br>[胡蝶双舞]状態なら、自身の奥義ゲージを10％上昇する。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF 戦闘中カウント:戦闘行動キャラ:0 == 0
			戦闘中カウント:戦闘行動キャラ:0 = 1
		ELSE
			戦闘中カウント:戦闘行動キャラ:0 = 0
		ENDIF
		CALL アビ対象選択テンプレート_自己のみ
		IF バフ番号取得_枠("胡蝶双舞" , 戦闘行動キャラ) > -1
			アビ汎用変数:効果量 = 10
			CALL アビテンプレート_有利効果_奥義ゲージUP
			CALL アビ汎用変数文字列リセット
		ENDIF
		;追撃{効果数値}4T
		アビ汎用文字列:バフ・デバフ枠 = 胡蝶刃_追撃
		アビ汎用文字列:バフ・デバフ対象能力 = 追撃
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 4
		アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_有利効果_累積バフ効果セット
		;攻撃累積{効果数値２}上限50%4T
		アビ汎用文字列:バフ・デバフ枠 = 胡蝶刃_攻撃力
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 効果数値２
		アビ汎用変数:累積上限_割合 = 50
		CALL アビテンプレート_有利効果_累積バフ効果セット
		;自分の防御ダウン{効果数値３}3T
		アビ汎用文字列:バフ・デバフ枠 = 胡蝶刃_防御力
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 効果数値３
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_不利効果_デバフ効果セット
		CALL アビ汎用変数文字列リセット
		戦闘中カウント:戦闘行動キャラ:1 = 現在ターン数
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT

@固有アビ_31_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果数値
#DIMS アビ名
#DIM 攻撃対象一時退避

;初期状態で習得しない場合は以下のようにする
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	アビ名 = 泡沫夢幻＋
	効果数値 = 400
	消費ＭＰ = 55
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 10
	アビ名 = 泡沫夢幻
	効果数値 = 300
	消費ＭＰ = 45
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>対象に[攻撃力×{効果数値}％]の自属性ダメージを与える。
		詳細文文字列受け渡し変数 += @"<br>対象に[胡蝶相剋/4T]を付与する。"
		IF キャラ隊列検索(キャラ番号) < 0
			詳細文文字列受け渡し変数 += @"<br>構えに応じた追加効果を発動する。"
		ELSE
			IF 戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 0 || バフ番号取得_枠("胡蝶双舞" , キャラ隊列検索(キャラ番号)) > -1
				詳細文文字列受け渡し変数 += @"<br>自身のTA率(独自枠)を[100％/3T]上昇させる。"
			ENDIF
			IF 戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 1 || バフ番号取得_枠("胡蝶双舞" , キャラ隊列検索(キャラ番号)) > -1
				詳細文文字列受け渡し変数 += @"<br>対象の防御力(B枠)を[15％/3T]減少させる。"
			ENDIF
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		;自属性ダメージ
		CALL アビ対象選択テンプレート_敵単体
		攻撃対象一時退避 = アビテンプレート用_対象保存:0
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_自属性
		;胡蝶相剋付与
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 胡蝶相剋
		アビ汎用文字列:バフ・デバフ対象能力 = 胡蝶相剋
		アビ汎用変数:持続ターン = 4
		アビ汎用変数:基礎成功確率 = 100
		アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット
		;胡蝶双舞があれば両方、なければ構えに応じた効果
		IF バフ番号取得_枠("胡蝶双舞" , 戦闘行動キャラ) > -1
			CALL ナルメア2アビ源氏処理
			CALL ナルメア2アビ神楽処理(攻撃対象一時退避)
			CALL バフ削除(戦闘行動キャラ, バフ番号取得_枠("胡蝶双舞", 戦闘行動キャラ))
		ELSEIF 戦闘中カウント:戦闘行動キャラ:0 == 0
			;源氏ならTA率上昇
			CALL ナルメア2アビ源氏処理
		ELSE
			;神楽なら防御ダウン
			CALL ナルメア2アビ神楽処理(攻撃対象一時退避)
		ENDIF
ENDSELECT

@ナルメア2アビ源氏処理
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:消去不可オプション = 1
			アビ汎用文字列:バフ・デバフ枠 = 泡沫夢幻_TA率
			アビ汎用文字列:バフ・デバフ対象能力 = TA率
			アビ汎用変数:効果割合 = 100
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット

@ナルメア2アビ神楽処理(デバフ対象)
#DIM デバフ対象
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_指定(デバフ対象)
			アビ汎用文字列:バフ・デバフ枠 = 防御力B
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:効果割合 = 15
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 80
			CALL アビテンプレート_不利効果_デバフ効果セット

@固有アビ_31_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 12
	アビ名 = 鏡花水月
	消費ＭＰ = 35
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		IF キャラ隊列検索(キャラ番号) < 0
			詳細文文字列受け渡し変数 += @"<br>構えに応じた効果を発動する。"
		ELSE
			IF	戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 0 || バフ番号取得_枠("胡蝶双舞" , キャラ隊列検索(キャラ番号)) > -1
				詳細文文字列受け渡し変数 += @"<br>対象の強化効果を1つ消去し、スロウ[1]効果を与える。基礎成功率[80％]。"
			ENDIF
			IF 戦闘中カウント:キャラ隊列検索(キャラ番号):0 == 1 || バフ番号取得_枠("胡蝶双舞" , キャラ隊列検索(キャラ番号)) > -1
				詳細文文字列受け渡し変数 += @"<br>自身の攻撃力(A枠)を[20％/1T]上昇させる。"
				詳細文文字列受け渡し変数 += @"<br>[胡蝶相剋]状態の敵がいれば攻撃力(独自枠)を[280％/1回]上昇させる。"
				詳細文文字列受け渡し変数 += @"<br>クリティカル確率(A枠)を[50％/3T]上昇させる。"
			ENDIF
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF バフ番号取得_枠("胡蝶双舞" , 戦闘行動キャラ) > -1
			CALL ナルメア3アビ源氏処理
			CALL ナルメア3アビ神楽処理
			CALL バフ削除(戦闘行動キャラ, バフ番号取得_枠("胡蝶双舞", 戦闘行動キャラ))
		ELSEIF 戦闘中カウント:戦闘行動キャラ:0 == 0
			CALL ナルメア3アビ源氏処理
		ELSE
			CALL ナルメア3アビ神楽処理
		ENDIF
ENDSELECT

@ナルメア3アビ源氏処理
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_敵単体
	CALL アビテンプレート_不利効果_バフ消去()
	アビ汎用変数:効果量 = 1
	アビ汎用変数:基礎成功確率 = 80
	CALL アビテンプレート_不利効果_スロウ

@ナルメア3アビ神楽処理
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
	アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
	アビ汎用変数:効果割合 = 20
	アビ汎用変数:持続回数 = 1
	CALL アビテンプレート_有利効果_バフ効果セット
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 鏡花水月_攻撃力
	アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
	アビ汎用変数:効果割合 = 280
	アビ汎用変数:持続回数 = 1
	CALL アビテンプレート_有利効果_バフ効果セット
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = クリティカル率A
	アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
	アビ汎用変数:効果割合 = 50
	アビ汎用変数:持続ターン = 3
	CALL アビテンプレート_有利効果_バフ効果セット

@固有アビ_31_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果数値
#DIM アビリティ使用フラグ
#DIMS アビ名

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 18
	アビ名 = 胡蝶双舞
	効果数値 = 60
	消費ＭＰ = 30
ELSE
	アビ名 = 
ENDIF

アビ汎用文字列:実行時メッセージ１ = 「大知は閑閑たり、小知は間間たり」

SIF 現在ターン数 == アビリティ使用フラグ && アビテンプレート用_アビ封印用フラグ != 1
	アビテンプレート用_アビ封印用フラグ = 1

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に[胡蝶双舞]を付与する
		詳細文文字列受け渡し変数 += @"<br>自身の奥義ダメージを[{効果数値}％/1回]を上昇させる。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 胡蝶双舞
		アビ汎用文字列:バフ・デバフ対象能力 = 胡蝶双舞
		アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		アビ汎用文字列:バフ・デバフ枠 = 胡蝶双舞_奥義
		アビ汎用文字列:バフ・デバフ対象能力 = 奥義ダメージ補正_割合
		アビ汎用変数:効果割合 = 60
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		アビリティ使用フラグ = 現在ターン数
ENDSELECT
