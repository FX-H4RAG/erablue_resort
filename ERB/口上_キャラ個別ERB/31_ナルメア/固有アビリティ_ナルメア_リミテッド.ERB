@スタイル変更_31(対象キャラ, 動作モード)
#DIM 対象キャラ
#DIMS 動作モード
;この関数はいくつ別スタイル作ってもキャラ毎に一個

SELECTCASE 動作モード
	CASE "スタイル名取得"
		VARSET RESULTS
		;RESULTS:Xに肩書を、RESULTS:X+10にリミテッドや水着など通称(？)を入れる
		RESULTS:0 = %NAME:対象キャラ%
		RESULTS:10 = ノーマル

		RESULTS:1 = [揺らぎの想姫]ナルメア
		RESULTS:11 = リミテッド

		;RESULTS:2 = [爽涼の斬姫]ナルメア
		;RESULTS:12 = 水着
	CASE @"スタイル解説_%NAME:対象キャラ%"
		TSTR:スタイル解説 = 
		TSTR:スタイル解説 += @"■%NAME:対象キャラ%<br>"
		TSTR:スタイル解説 += "<br>"
		TSTR:スタイル解説 += "通常のスタイル。ステータス変動なし。闇属性。<br>"
		TSTR:スタイル解説 += "<br>"
		CALL スタイル奥義アビ表示_汎用文章(対象キャラ, NAME:対象キャラ)
	CASE "スタイル解説_[揺らぎの想姫]ナルメア"
		TSTR:スタイル解説 = 
		TSTR:スタイル解説 += "■[揺らぎの想姫]ナルメア<br>"
		TSTR:スタイル解説 += "<br>"
		TSTR:スタイル解説 += "本家のリミテッドVerをモチーフにしたスタイル。風属性。<br>"
		TSTR:スタイル解説 += "必ずトリプルアタック＋通常攻撃で奥義ゲージが上昇しない。<br>"
		TSTR:スタイル解説 += "<br>"
		CALL スタイル奥義アビ表示_汎用文章(対象キャラ, "[揺らぎの想姫]ナルメア")
	CASE "変更後処理"
		;CSTR:対象キャラ:スタイル管理文字列をキーにして処理を実行
		;一人称や二人称、素質の変化などを想定、今回は何もなし
		SELECTCASE CSTR:対象キャラ:スタイル管理文字列
			CASE ""
			CASE "[揺らぎの想姫]ナルメア"
			;CASE RESULTS:2
			;CASE RESULTS:3
		ENDSELECT
ENDSELECT

@スタイルステータス補正_揺らぎの想姫_ナルメア(対象キャラ)
#DIM 対象キャラ
;関数名に[]が使えないため、しょうがなく[]排除時の文字列をキーに

装備ステータス補正保存:対象キャラ:基礎属性変更 = 属性文字列数値変換("風") + 10
装備ステータス補正保存:対象キャラ:TA率 += 1000


@固有アビ_31_10(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 判定値
#DIMS アビ名

IF スタイル判定関数(キャラ番号, "[揺らぎの想姫]ナルメア") == 0
	アビ名 = 
	RETURN 0
ENDIF

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	;原作での＋前後の違いが不明だったのでとりあえず変化なしで
	アビ名 = 槿花泡影+
	消費ＭＰ = 80
ELSE
	アビ名 = 槿花泡影
	消費ＭＰ = 100
ENDIF
SIF アビ名 == ""
	RETURN

IF キャラ隊列検索(キャラ番号) >= 0
	判定値 = 戦闘中カウント:キャラ隊列検索(GETCHARA(31)):0 - 現在ターン数
	SIF 判定値 > 0
		アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		詳細文文字列受け渡し変数 += @"<br>自身に追撃(独自枠)[攻撃力×100％/4T]を付与する。"
		詳細文文字列受け渡し変数 += @"<br>自身に慧眼[4T]を付与する。"
		詳細文文字列受け渡し変数 += @"<br>使用間隔:6ターン、寂寞無為とリンク"
		SIF 判定値 > 0
			詳細文文字列受け渡し変数 += @"（残り{判定値, 2}ターン）"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		;追撃{効果数値}4T
		アビ汎用文字列:バフ・デバフ枠 = 槿花泡影_追撃
		アビ汎用文字列:バフ・デバフ対象能力 = 追撃
		アビ汎用変数:効果割合 = 100
		アビ汎用変数:持続ターン = 4
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 慧眼
		アビ汎用文字列:バフ・デバフ対象能力 = 慧眼
		アビ汎用変数:持続ターン = 4
		CALL アビテンプレート_有利効果_バフ効果セット
		
		戦闘中カウント:キャラ隊列検索(GETCHARA(31)):0 = 現在ターン数 + 6
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT

@固有アビ_31_11(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 判定値
#DIMS アビ名

IF スタイル判定関数(キャラ番号, "[揺らぎの想姫]ナルメア") == 0
	アビ名 = 
	RETURN 0
ENDIF

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
	アビ名 = 寂寞無為＋
	消費ＭＰ = 100
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 4
	アビ名 = 寂寞無為
	消費ＭＰ = 120
ELSE
	アビ名 = 
ENDIF
SIF アビ名 == ""
	RETURN

IF キャラ隊列検索(キャラ番号) >= 0
	判定値 = 戦闘中カウント:キャラ隊列検索(GETCHARA(31)):0 - 現在ターン数
	SIF 判定値 > 0
		アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身にかばう効果［全体/1ターン］
		詳細文文字列受け渡し変数 += @"<br>自身に5回のカウンター［攻撃力×250％］効果［5ターン］"
		詳細文文字列受け渡し変数 += @"<br>自身に5回の反射効果［5ターン］"
		;詳細文文字列受け渡し変数 += @"<br>自身に5回の反射効果［50％/5ターン］"
		詳細文文字列受け渡し変数 += @"<br>使用間隔:6ターン、槿花泡影とリンク"
		SIF 判定値 > 0
			詳細文文字列受け渡し変数 += @"（残り{判定値, 2}ターン）"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 全体かばう
		アビ汎用文字列:バフ・デバフ対象能力 = 全体かばう
		アビ汎用変数:持続ターン = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = カウンター
		アビ汎用文字列:バフ・デバフ対象能力 = カウンター
		アビ汎用変数:効果割合 = 250
		アビ汎用変数:持続回数 = 5
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 反射
		アビ汎用文字列:バフ・デバフ対象能力 = 反射
		アビ汎用変数:効果割合 = 50
		アビ汎用変数:持続回数 = 5
		アビ汎用変数:持続ターン = 5
		CALL アビテンプレート_有利効果_バフ効果セット
		
		戦闘中カウント:キャラ隊列検索(GETCHARA(31)):0 = 現在ターン数 + 6
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT


@固有アビ_31_12(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 判定値
#DIMS アビ名

IF スタイル判定関数(キャラ番号, "[揺らぎの想姫]ナルメア") == 0
	アビ名 = 
	RETURN 0
ENDIF

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 12
	アビ名 = 花風・薄紅舞
	消費ＭＰ = 150
ELSE
	アビ名 = 
ENDIF
SIF アビ名 == ""
	RETURN

IF キャラ隊列検索(キャラ番号) >= 0
	判定値 = 戦闘中カウント:キャラ隊列検索(GETCHARA(31)):1 - 現在ターン数
	SIF 判定値 > 0
		アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}
		;原作は50%だけど別枠乗算なので100に補正
		詳細文文字列受け渡し変数 += @"<br>自身の攻撃力(独自枠)を[100％/1T]上昇させる。"
		詳細文文字列受け渡し変数 += @"<br>自身のクリティカル確率(A枠)を[100％/3T]上昇させる。"
		詳細文文字列受け渡し変数 += @"<br>自身のクリティカルダメージ率(独自枠)を［10％/3T］上昇させる。"
		詳細文文字列受け渡し変数 += @"<br>使用間隔:6ターン"
		SIF 判定値 > 0
			詳細文文字列受け渡し変数 += @"（残り{判定値, 2}ターン）"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 花風・薄紅舞_攻撃力
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 100
		アビ汎用変数:持続ターン = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = クリティカル率A
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
		アビ汎用変数:効果量 = 100
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 花風・薄紅舞_CT倍率
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカルダメージ率
		アビ汎用変数:効果量 = 10
		アビ汎用変数:持続ターン = 3

		戦闘中カウント:キャラ隊列検索(GETCHARA(31)):1 = 現在ターン数 + 6
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT


@揺らぎの想姫固有戦闘開始時処理_キャラ
#DIM バフ・デバフ行数
#DIM 隊列番号
#DIM 対象番号

;バフ付け文章は出さないので1を入れておく
アビテンプレート用_アビ名表示済フラグ = 1

CALL アビ対象選択テンプレート_自己のみ
アビ汎用文字列:適用範囲 = 味方全体
アビ汎用文字列:バフ・デバフ枠 = 胡蝶泡沫装
アビ汎用文字列:バフ・デバフ対象能力 = 追撃
アビ汎用変数:効果割合 = 10
アビ汎用変数:持続ターン = -1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_有利効果_パッシブバフ効果セット

CALL アビ対象選択テンプレート_自己のみ
CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 天司
アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_通常攻撃ダメージ
アビ汎用変数:効果割合 = 20
アビ汎用変数:持続ターン = -1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_有利効果_パッシブバフ効果セット

CALL アビ対象選択テンプレート_自己のみ
CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 通常攻撃_ゲージ上昇率
アビ汎用文字列:バフ・デバフ対象能力 = 通常攻撃_ゲージ上昇率
アビ汎用変数:効果割合 = 100
アビ汎用変数:持続ターン = -1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_不利効果_パッシブデバフ効果セット

CALL アビ対象選択テンプレート_自己のみ
CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = ディスペルガード
アビ汎用文字列:バフ・デバフ対象能力 = ディスペルガード
アビ汎用変数:持続ターン = -1
アビ汎用変数:特殊表示オプション = -1
CALL アビテンプレート_有利効果_パッシブバフ効果セット

@揺らぎの想姫固有戦闘不能時処理_キャラ
#DIM 対象番号
#DIM 胡蝶泡沫装

;FOR 対象番号, 0, 4
;	胡蝶泡沫装 = バフ番号取得_枠("胡蝶泡沫装", 対象番号)
;	CALL 戦闘効果削除(胡蝶泡沫装)
;NEXT

;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@揺らぎの想姫口上_ステータス画面_固有特性解説(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『万里鵬翼』 □---------------------------------------------------------------
KSTR:(K++) = 　通常攻撃でゲージが上がらないが通常攻撃の与ダメージUP/必ずトリプルアタック/
KSTR:(K++) = 　強化効果が無効化されない
KSTR:(K++) = 　
KSTR:(K++) = □ 特性：『胡蝶泡沫装』 □-----------------------------------------------------------------
KSTR:(K++) = 　味方全体に追撃効果を付与する

CALL 口上変数表示(対象キャラ, 1)


@揺らぎの想姫固有奥義(ARGS, キャラ番号)
;@固有奥義31_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 奥義威力
#DIMS 奥義名

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 25
	奥義名 = 胡蝶刃・屠龍＋
	奥義威力 = 800
ELSE
	奥義名 = 胡蝶刃・屠龍
	奥義威力 = 700
ENDIF

SELECTCASE ARGS
	CASE "奥義名"
		TSTR:コマンド名受渡 = %奥義名%
	CASE "奥義説明文"
		詳細文文字列受け渡し変数 = 奥義威力：{奥義威力}％
		詳細文文字列受け渡し変数 += @"<br>自分のアビリティ再使用間隔を2ターン短縮する。"
		詳細文文字列受け渡し変数 += @"<br>次のターン進行時に攻撃行動を2回行う。"
		RETURN 奥義威力
	CASE "奥義前効果"
	
	CASE "奥義追加効果"
		SIF バフ番号取得_枠("再行動" , 戦闘行動キャラ) < 0
			BATTLE_STATE:戦闘行動キャラ:行動済みフラグ = 1
		戦闘中カウント:キャラ隊列検索(キャラ番号):0 -= 2
		戦闘中カウント:キャラ隊列検索(キャラ番号):1 -= 2
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 再行動
		アビ汎用文字列:バフ・デバフ対象能力 = 再行動
		アビ汎用変数:効果量 = 1
		アビ汎用変数:持続ターン = 2
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT
