@固有アビ_33_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 効果数値
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 45
	アビ名 = 紅蓮＋＋
	効果数値 = 350
	消費ＭＰ = 150
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	アビ名 = 紅蓮＋
	効果数値 = 300
	消費ＭＰ = 90
ELSE
	アビ名 = 紅蓮
	効果数値 = 250
	消費ＭＰ = 75
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵全体に[攻撃力×{効果数値}％]の自属性ダメージを与える。<br>
		詳細文文字列受け渡し変数 += @"3Tの間、対象に[狐火]を付与する。"
		IF アビ名 == "紅蓮＋＋"
			詳細文文字列受け渡し変数 += @"味方全体のDA率（A枠）を[30％／3T]上昇させる。"
;			詳細文文字列受け渡し変数 += @"味方全体のTA率（A枠）を[15％／3T]上昇させる。"
			詳細文文字列受け渡し変数 += @"味方全体に追撃効果（A枠）[20％／3T]を付与する。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
;全体ダメージ処理
	CALL アビ対象選択テンプレート_敵全体_生者のみ
	アビ汎用変数:効果割合 = 効果数値
	CALL アビテンプレート_ダメージ処理_自属性
;狐火
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 狐火
	アビ汎用文字列:バフ・デバフ対象能力 = 火傷
	アビ汎用変数:効果割合 = 1
	アビ汎用変数:持続ターン = 3
	アビ汎用変数:基礎成功確率 = 100
	アビ汎用変数:消去不可オプション = 1
	CALL アビテンプレート_不利効果_デバフ効果セット
;味方全体にDA30 TA15 追撃20 3T
	IF アビ名 == "紅蓮＋＋"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = DA率A
		アビ汎用文字列:バフ・デバフ対象能力 = DA率
		アビ汎用変数:効果割合 = 30
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
;		アビ汎用文字列:バフ・デバフ枠 = TA率A
;		アビ汎用文字列:バフ・デバフ対象能力 = TA率
;		アビ汎用変数:効果割合 = 15
;		CALL アビテンプレート_有利効果_バフ効果セット
		アビ汎用文字列:バフ・デバフ枠 = 追撃A
		アビ汎用文字列:バフ・デバフ対象能力 = 追撃
		アビ汎用変数:効果割合 = 20
		CALL アビテンプレート_有利効果_バフ効果セット
	ENDIF
ENDSELECT

@固有アビ_33_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 効果数値
#DIM ダメージ回数
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 40
	アビ名 = 燕返し＋＋
	効果数値 = 80
	ダメージ回数 = 4
	消費ＭＰ = 80
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 24
	アビ名 = 燕返し＋
	効果数値 = 80
	ダメージ回数 = 3
	消費ＭＰ = 60
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 8
	アビ名 = 燕返し
	効果数値 = 80
	ダメージ回数 = 2
	消費ＭＰ = 40
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>対象に自属性の[攻撃力×{効果数値}％]ダメージを{ダメージ回数}回与える。<br>
		詳細文文字列受け渡し変数 += @"自身の攻撃力とDA率を[20％/累積枠/上限60％]上昇させる。<br>"
;		詳細文文字列受け渡し変数 += @"自身のTA率を[10％/累積枠/上限30％]上昇させる。
		詳細文文字列受け渡し変数 += @"対象が[狐火]状態の場合は、2回発動する。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
	CALL アビ対象選択テンプレート_敵単体
	アビテンプレート用_対象保存_一時退避用:0 = アビテンプレート用_対象保存:0
	アビテンプレート用_対象保存_一時退避用:1 = 効果数値
	アビテンプレート用_対象保存_一時退避用:2 = ダメージ回数
	IF デバフ存在チェック_枠("狐火",アビテンプレート用_対象保存:0 ) != -1
		CALL ユエル2アビダメージ処理
		CALL ユエル2アビバフ処理
	ENDIF
	CALL ユエル2アビダメージ処理
	CALL ユエル2アビバフ処理
ENDSELECT

@ユエル2アビダメージ処理
	アビテンプレート用_対象保存:0 = アビテンプレート用_対象保存_一時退避用:0
	アビ汎用変数:効果割合 = アビテンプレート用_対象保存_一時退避用:1
	FOR LOCAL:1, 0, アビテンプレート用_対象保存_一時退避用:2
		CALL アビテンプレート_ダメージ処理_自属性
	NEXT
	CALL アビ汎用変数文字列リセット

@ユエル2アビバフ処理
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ枠 = 攻撃力_累積
	アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
	アビ汎用変数:効果割合 = 20
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:累積上限_割合 = 60
	CALL アビテンプレート_有利効果_累積バフ効果セット

	アビ汎用文字列:バフ・デバフ枠 = DA率_累積
	アビ汎用文字列:バフ・デバフ対象能力 = DA率
	アビ汎用変数:効果割合 = 20
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:累積上限_割合 = 60
	CALL アビテンプレート_有利効果_累積バフ効果セット
	
;	アビ汎用文字列:バフ・デバフ枠 = TA率_累積
;	アビ汎用文字列:バフ・デバフ対象能力 = TA率
;	アビ汎用変数:効果割合 = 10
;	アビ汎用変数:持続ターン = -1
;	アビ汎用変数:累積上限_割合 = 30
;	CALL アビテンプレート_有利効果_累積バフ効果セット
	CALL アビ汎用変数文字列リセット
	
@固有アビ_33_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;初期状態で習得しない場合は以下のようにする
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 18
	アビ名 = 迎暁の儀
	消費ＭＰ = 60
ELSE
	アビ名 = 
ENDIF


SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体を[回復力×120％]で回復し、弱体効果を1つ回復する。
		RETURN 消費ＭＰ
	CASE "アビ効果"
	CALL アビ対象選択テンプレート_味方全体_生者のみ
	アビ汎用変数:効果割合 = 120
	CALL アビテンプレート_回復処理_MAXでも回復
	CALL アビテンプレート_有利効果_デバフ消去()
;120ヒール
;クリア

ENDSELECT

@固有アビ_33_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;初期状態で習得しない場合は以下のようにする
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 50
	アビ名 = 参之舞･燼花
	消費ＭＰ = 100
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>その攻撃の間のみTA率を100％上昇させ、通常攻撃を2回行う。
		RETURN 消費ＭＰ
	CASE "アビ効果"
	CALL アビ対象選択テンプレート_敵単体
	アビ汎用変数:効果割合 = 100
	アビ汎用変数:TA率補正値 = 100
	CALL アビテンプレート_通常攻撃処理
	CALL アビテンプレート_通常攻撃処理
ENDSELECT
