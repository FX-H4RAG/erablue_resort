;固有アビリティ_マギサ
@固有アビ_38_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果数値
#DIM 効果成功率
#DIMS アビ名

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 28
	アビ名 = イラプション＋＋
	効果数値 = 250
	効果成功率 = 90
	消費ＭＰ = 60
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 16
	アビ名 = イラプション＋
	効果数値 = 170
	効果成功率 = 90
	消費ＭＰ = 45
ELSE
	アビ名 = イラプション
	効果数値 = 100
	効果成功率 = 90
	消費ＭＰ = 25
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵全体に自属性の［攻撃力✕{効果数値}％］ダメージを与える。
		詳細文文字列受け渡し変数 += @"<br>対象に３ターン火傷［最大HPの2％］効果を与える。基礎成功率{効果成功率}％"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ

		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_自属性
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 2
		アビ汎用文字列:バフ・デバフ枠 = 火傷
		アビ汎用文字列:バフ・デバフ対象能力 = 火傷
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT
RETURN RESULT

@固有アビ_38_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果ターン

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 36
	アビ名 = ウォール＋＋
	消費ＭＰ = 60
	効果数値 = 30
	効果ターン = 3
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
	アビ名 = ウォール＋
	消費ＭＰ = 50
	効果数値 = 15
	効果ターン = 3
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 4
	アビ名 = ウォール
	消費ＭＰ = 40
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体の状態異常および弱体化を一度だけ防ぐ魔法防壁を展開する。
		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			詳細文文字列受け渡し変数 += @"<br>パーティ全体に{効果ターン}ターン{効果数値}％攻撃力および防御力を上昇させる。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 弱体無効
		アビ汎用文字列:バフ・デバフ対象能力 = 弱体無効
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット

		IF 基礎BATTLE_STATE:キャラ番号:Lv >= 20
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 30
			アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
			アビ汎用文字列:バフ・デバフ枠 = 防御力A
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT
RETURN RESULT

@固有アビ_38_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIM 効果数値
#DIM 効果成功率
#DIMS アビ名

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 44
	アビ名 = ヴァルプルギス＋＋
	効果数値 = 400
	効果成功率 = 90
	消費ＭＰ = 120
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 32
	アビ名 = ヴァルプルギス＋
	効果数値 = 300
	効果成功率 = 85
	消費ＭＰ = 100
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 12
	アビ名 = ヴァルプルギス
	効果数値 = 200
	効果成功率 = 80
	消費ＭＰ = 80
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵全体に闇属性の［攻撃力✕{効果数値}％］ダメージを与える。
		詳細文文字列受け渡し変数 += @"<br>対象に暗闇効果を与える。基礎成功率{効果成功率}％。状態異常暗闇：50％で攻撃をミスをする状態。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ

		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_闇属性

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 暗闇
		アビ汎用文字列:バフ・デバフ対象能力 = 暗闇
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果成功率
		アビ汎用変数:効果割合 = 50
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT
RETURN RESULT

;----------------------------------------------------------------------------------
;アビリティテンプレート例
;----------------------------------------------------------------------------------
;敵単体に、攻撃力の200%威力で火属性のダメージを与える効果
;----------------------------------------------------------------------------------	
	; CALL アビ対象選択テンプレート_敵単体
	; アビ汎用変数:効果割合 = 200
	; CALL アビテンプレート_ダメージ処理_火属性

;----------------------------------------------------------------------------------
;戦闘不能ではない敵全体に、防御力の300%威力で自属性のダメージを与える効果
;----------------------------------------------------------------------------------	
	; CALL アビ対象選択テンプレート_敵全体_生者のみ
	; アビ汎用変数:効果割合 = 300
	; アビ汎用文字列:攻撃側使用能力値 = 防御力
	; CALL アビテンプレート_ダメージ処理_自属性

;----------------------------------------------------------------------------------
;戦闘不能ではない味方全体に、3ターン持続する30%の攻撃力UPを与える効果
;----------------------------------------------------------------------------------
	; CALL アビ対象選択テンプレート_味方全体_生者のみ
	; アビ汎用変数:効果割合 = 30
	; アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
	; アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
	; アビ汎用変数:持続ターン = 3
	; CALL アビテンプレート_有利効果_バフ効果セット

;----------------------------------------------------------------------------------
;敵縦列に、3ターン持続する暗闇（攻撃失敗率40%）を与える効果　基礎成功率70％
;----------------------------------------------------------------------------------
	; CALL アビ対象選択テンプレート_敵縦列
	; アビ汎用変数:効果割合 = 40
	; アビ汎用文字列:バフ・デバフ枠 = 暗闇
	; アビ汎用文字列:バフ・デバフ対象能力 = 暗闇
	; アビ汎用変数:持続ターン = 3
	; アビ汎用変数:基礎成功確率 = 70
	; CALL アビテンプレート_不利効果_デバフ効果セット

;----------------------------------------------------------------------------------
;戦闘不能状態の味方単体を蘇生し、回復力の100%威力でHPを回復する効果
;----------------------------------------------------------------------------------	
	; CALL アビ対象選択テンプレート_味方単体_死者のみ
	; アビ汎用変数:効果割合 = 100
	; アビ汎用変数:蘇生可能オプション = 1
	; CALL アビテンプレート_回復処理
