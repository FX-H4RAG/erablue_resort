
@固有アビ_64_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 55
	アビ名 = 死ト愛ノ世界+
	消費ＭＰ = 100
ELSE
	アビ名 = 死ト愛ノ世界
	消費ＭＰ = 150
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>フィールドを死ト愛ノ世界に染め上げる［3ターン］
		詳細文文字列受け渡し変数 += @"<br>◆このアビリティは行動を消費しない"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:フィールド枠 = 死ト愛ノ世界
		IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70
			展開中フィールド残りターン数 = 6
		ELSE 
			展開中フィールド残りターン数 = 3
		ENDIF
		CALL アビテンプレート_フィールド効果セット
		
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT



@固有アビ_64_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 対象

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 65
	アビ名 = 愛シイ人+
	消費ＭＰ = 175
ELSE
	アビ名 = 愛シイ人
	消費ＭＰ = 200
ENDIF

IF 戦闘中カウント:戦闘行動キャラ:0 < 2
	IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70
		アビテンプレート用_アビ封印用フラグ = 0
	ELSE
		アビテンプレート用_アビ封印用フラグ = 1
	ENDIF
ENDIF

IF 戦闘中カウント:戦闘行動キャラ:0 < 2
	IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70 && 展開中フィールド名格納 == "死ト愛ノ世界"
		アビテンプレート用_アビ封印用フラグ = 0
	ELSE
		アビテンプレート用_アビ封印用フラグ = 1
	ENDIF
ENDIF

SIF バフ存在チェック_枠("愛シイ人", 戦闘行動キャラ) > -1
	アビテンプレート用_アビ封印用フラグ = 1

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方単体と自身に愛シイ人効果［4ターン/消去不可］
		詳細文文字列受け渡し変数 += @"<br>◆愛ノ救イが2減少/味方一人にのみ付与可能/このアビリティは行動を消費しない"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方単体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 愛シイ人
		アビ汎用文字列:バフ・デバフ対象能力 = 愛シイ人
		アビ汎用変数:持続ターン = 4
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		対象 = アビテンプレート用_対象保存:0
		DT_ROW_ADD @"バフデータベース_{対象}", "バフ枠", "愛シイ人_絶対TA", "バフ対象能力", "TA率", "バフ効果量_固定値", 1000, "バフ名", "愛シイ人", "持続ターン", 4, "消去不可フラグ", 1
		DT_ROW_ADD @"バフデータベース_{対象}", "バフ枠", "愛シイ人_被ダメ軽減", "バフ対象能力", "ダメージ増減", "バフ効果量_割合", 50, "バフ名", "愛シイ人", "持続ターン", 4, "消去不可フラグ", 1
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 愛シイ人
		アビ汎用文字列:バフ・デバフ対象能力 = 愛シイ人
		アビ汎用変数:持続ターン = 4
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		DT_ROW_ADD @"バフデータベース_{戦闘行動キャラ}", "バフ枠", "愛シイ人_絶対TA", "バフ対象能力", "TA率", "バフ効果量_固定値", 1000, "バフ名", "愛シイ人", "持続ターン", 4, "消去不可フラグ", 1
		
		IF !(陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70 && 展開中フィールド名格納 == "死ト愛ノ世界")
			戦闘中カウント:戦闘行動キャラ:0 -= 2
		ENDIF
		
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT



@固有アビ_64_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
	アビ名 = 終焉ノ愛
	消費ＭＰ = 100

IF 戦闘中カウント:戦闘行動キャラ:0 < 1
	IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70
		アビテンプレート用_アビ封印用フラグ = 0
	ELSE
		アビテンプレート用_アビ封印用フラグ = 1
	ENDIF
ENDIF

IF 戦闘中カウント:戦闘行動キャラ:0 < 1
	IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70 && 展開中フィールド名格納 == "死ト愛ノ世界"
		アビテンプレート用_アビ封印用フラグ = 0
	ELSE
		アビテンプレート用_アビ封印用フラグ = 1
	ENDIF
ENDIF

SIF 戦闘中カウント:戦闘行動キャラ:1 == 1
	アビテンプレート用_アビ封印用フラグ = 1

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方単体に奥義再発動効果［1回］
		詳細文文字列受け渡し変数 += @"<br>◆愛ノ救イが1減少/1ターンに1回のみ発動可能/このアビリティは行動を消費しない"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方単体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 奥義再発動
		アビ汎用文字列:バフ・デバフ対象能力 = 奥義再発動
		アビ汎用変数:持続回数 = 1
		アビ汎用変数:消去不可オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		IF !(陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70 && 展開中フィールド名格納 == "死ト愛ノ世界")
			戦闘中カウント:戦闘行動キャラ:0 -= 1
		ENDIF
		
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT



@固有アビ_64_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 80
	アビ名 = 永久ノ誓イ
	消費ＭＰ = 300
ENDIF

IF 戦闘中カウント:戦闘行動キャラ:0 < 4
	IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70 && 展開中フィールド名格納 == "死ト愛ノ世界"
		アビテンプレート用_アビ封印用フラグ = 0
	ELSE
		アビテンプレート用_アビ封印用フラグ = 1
	ENDIF
ENDIF

SIF 戦闘中カウント:戦闘行動キャラ:2 == 1
	アビテンプレート用_アビ封印用フラグ = 1

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>自身に永久ノ誓イ効果［永続/消去不可］
		詳細文文字列受け渡し変数 += @"<br>◆再使用不可/愛ノ救イが4減少/このアビリティは行動回数を消費しない"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 永久ノ誓イ
		アビ汎用文字列:バフ・デバフ対象能力 = 永久ノ誓イ
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		DT_ROW_ADD @"バフデータベース_{戦闘行動キャラ}", "バフ枠", "永久ノ誓イ_攻撃力", "バフ対象能力", "攻撃力", "バフ効果量_割合", 50, "持続ターン", -1, "消去不可フラグ", 1
		DT_ROW_ADD @"バフデータベース_{戦闘行動キャラ}", "バフ枠", "永久ノ誓イ_防御力", "バフ対象能力", "防御力", "バフ効果量_割合", 50, "持続ターン", -1, "消去不可フラグ", 1
		DT_ROW_ADD @"バフデータベース_{戦闘行動キャラ}", "バフ枠", "永久ノ誓イ_弱体無効", "バフ対象能力", "弱体無効", "持続ターン", -1, "消去不可フラグ", 1
		
		IF !(陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 70 && 展開中フィールド名格納 == "死ト愛ノ世界")
			戦闘中カウント:戦闘行動キャラ:0 -= 4
		ENDIF
		
		戦闘中カウント:戦闘行動キャラ:2 = 1
		
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT



@固有戦闘開始時処理_キャラ_64
#DIM バフ・デバフ行数
#DIM ニーア番号
#DIM 隊列番号

ニーア番号 = FINDCHARA(NO, 64)
隊列番号 = キャラ隊列検索(ニーア番号)

IF 陥落チェック(ニーア番号)
	KSTR:(K++) = 「どこまでも……あなたと共に！」
ELSE
	KSTR:(K++) = 「あなたも私を否定するの……？」
ENDIF

DT_ROW_ADD @"バフデータベース_{隊列番号}", "バフ枠", "愛ノ救イ", "バフ対象能力", "愛ノ救イ", "バフ名", "愛ヲノゾム意志", "持続ターン", -1, "消去不可フラグ", 1, "特殊表示オプション", 1
DT_ROW_ADD @"バフデータベース_{隊列番号}", "バフ枠", "愛ノ救イ_被ダメ軽減", "バフ対象能力", "ダメージ増減", "バフ効果量_割合", 90, "バフ名", "愛ヲノゾム意志", "持続ターン", -1, "消去不可フラグ", 1

戦闘中カウント:戦闘行動キャラ:0 = 13

アビテンプレート用_アビ名 = 恋人の正位置

CALL アビ対象選択テンプレート_味方全体_生者のみ
アビ汎用文字列:バフ・デバフ枠 = 渇望
アビ汎用文字列:バフ・デバフ対象能力 = 自動復活
アビ汎用変数:効果割合 = 50
アビ汎用変数:持続回数 = 1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = 1
CALL アビテンプレート_有利効果_バフ効果セット

IF 陥落チェック(ニーア番号) && 基礎BATTLE_STATE:ニーア番号:Lv >= 75
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 恋人の正位置
	アビ汎用文字列:バフ・デバフ対象能力 = 弱体無効
	アビ汎用変数:持続回数 = 1
	アビ汎用変数:消去不可オプション = 1
	CALL アビテンプレート_有利効果_バフ効果セット
ENDIF



@固有通常攻撃後処理_バフ_永久ノ誓イ(全滅フラグ, 連撃数)
#DIM 連撃数
#DIM 全滅フラグ

SIF 全滅フラグ
	RETURN 0

アビテンプレート用_アビ名 = 永久ノ誓イ

CALL アビ汎用変数文字列リセット
アビ汎用変数:効果割合 = 200
FOR LOCAL, 0, 6
	CALL アビテンプレート_ダメージ処理_闇属性
NEXT



@固有ターン終了時処理_キャラ_64
;ターン終了時処理関数から呼び出し
#DIM バフ・デバフ行数
#DIM 隊列番号

隊列番号 = キャラ隊列検索(FINDCHARA(NO, 64))

IF 戦闘中カウント:戦闘行動キャラ:0 > 2
	戦闘中カウント:戦闘行動キャラ:0 -= 2
ELSEIF 戦闘中カウント:戦闘行動キャラ:0 < 3
	戦闘中カウント:戦闘行動キャラ:0 = 0
	BATTLE_STATE:隊列番号:ＨＰ = 0
	WSTR:(K++) = %CALLNAME:(BATTLE_STATE:隊列番号:キャラ番号)%は倒れた！
	CALL 戦闘不能時処理(隊列番号, -1)
ENDIF

戦闘中カウント:戦闘行動キャラ:1 = 0



@固有戦闘不能時処理_キャラ_64(隊列番号)
#DIM 隊列番号
#DIM 対象番号
#DIM 永久ノ誓イ
#DIM 渇望
#DIM 自動復活

永久ノ誓イ = バフ存在チェック_枠("永久ノ誓イ", 隊列番号)
自動復活 = バフ存在チェック("自動復活", 隊列番号)

IF 永久ノ誓イ > -1
	CALL バフ削除(隊列番号, 永久ノ誓イ)
ENDIF

IF 戦闘中カウント:隊列番号:0 > 0
	CALL アビ対象選択テンプレート_味方全体_生者のみ
	アビ汎用文字列:バフ・デバフ枠 = 残執
	アビ汎用文字列:バフ・デバフ対象能力 = 残執
	アビ汎用変数:持続ターン = 4
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = 1
	CALL アビテンプレート_不利効果_デバフ効果セット
	
	FOR 対象番号, 0, 4
		SIF BATTLE_STATE:対象番号:キャラ番号 < 1
			CONTINUE
		SIF BATTLE_STATE:対象番号:ＨＰ < 1
			CONTINUE
		
		DT_ROW_ADD @"デバフデータベース_{対象番号}", "デバフ枠", "残執_防御力", "デバフ対象能力", "防御力", "デバフ効果量_割合", 50, "持続ターン", 4, "消去不可フラグ", 1
		DT_ROW_ADD @"デバフデータベース_{対象番号}", "デバフ枠", "残執_連撃率", "デバフ対象能力", "連続攻撃率", "デバフ効果量_固定値", 100, "持続ターン", 4, "消去不可フラグ", 1
		DT_ROW_ADD @"デバフデータベース_{対象番号}", "デバフ枠", "残執_強圧", "デバフ対象能力", "強圧", "持続ターン", 4, "消去不可フラグ", 1
	NEXT
	
	CALL アビ対象選択テンプレート_敵全体_生者のみ
	アビ汎用文字列:バフ・デバフ枠 = 残執
	アビ汎用文字列:バフ・デバフ対象能力 = 残執
	アビ汎用変数:持続ターン = 4
	アビ汎用変数:消去不可オプション = 1
	アビ汎用変数:特殊表示オプション = 1
	CALL アビテンプレート_不利効果_デバフ効果セット
	
	FOR 対象番号, 10, 20
		SIF 敵BATTLE_STATE_STR:(対象番号 - 10):エネミー名 == ""
			CONTINUE
		SIF 敵BATTLE_STATE:(対象番号 - 10):ＨＰ < 1
			CONTINUE
		
		DT_ROW_ADD @"デバフデータベース_{対象番号}", "デバフ枠", "残執_防御力", "デバフ対象能力", "防御力", "デバフ効果量_割合", 50, "持続ターン", 4, "消去不可フラグ", 1
		DT_ROW_ADD @"デバフデータベース_{対象番号}", "デバフ枠", "残執_連撃率", "デバフ対象能力", "連続攻撃率", "デバフ効果量_固定値", 100, "持続ターン", 4, "消去不可フラグ", 1
		DT_ROW_ADD @"デバフデータベース_{対象番号}", "デバフ枠", "残執_強圧", "デバフ対象能力", "強圧", "持続ターン", 4, "消去不可フラグ", 1
	NEXT
ENDIF

IF 自動復活 > -1
	戦闘中カウント:隊列番号:0 = 13
ENDIF



@固有行動開始時処理_フィールド_死ト愛ノ世界
#DIM バフ行数

バフ行数 = バフ存在チェック_枠("死ト愛ノ世界_攻撃力", 戦闘行動キャラ)

IF バフ行数 > -1
	DT_CELL_SET @"バフデータベース_{戦闘行動キャラ}", バフ行数, "持続ターン", 展開中フィールド残りターン数

	IF 戦闘行動キャラ < 5
		バフ行数 = バフ存在チェック_枠("死ト愛ノ世界_追撃", 戦闘行動キャラ)
		DT_CELL_SET @"バフデータベース_{戦闘行動キャラ}", バフ行数, "持続ターン", 展開中フィールド残りターン数
	ENDIF
ELSE
	DT_ROW_ADD @"バフデータベース_{戦闘行動キャラ}", "バフ枠", "死ト愛ノ世界_攻撃力", "バフ対象能力", "攻撃力", "バフ効果量_割合", 20, "バフ名", "死ト愛ノ世界", "持続ターン", 展開中フィールド残りターン数, "消去不可フラグ", 1

	IF 戦闘行動キャラ < 5
		DT_ROW_ADD @"バフデータベース_{戦闘行動キャラ}", "バフ枠", "死ト愛ノ世界_追撃", "バフ対象能力", "追撃", "バフ効果量_割合", 30, "バフ名", "死ト愛ノ世界", "持続ターン", 展開中フィールド残りターン数, "消去不可フラグ", 1
	ENDIF
ENDIF



@探索欄表示_キャラ_64(レイヤー番号, 隊列番号)
#DIM 隊列番号
#DIM レイヤー番号

GDRAWTEXT レイヤー番号, "愛ノ救イ:", 120, 90
GSETBRUSH レイヤー番号, 属性数値文字色変換_透明度込(BATTLE_STATE:隊列番号:属性)
GDRAWTEXT レイヤー番号, @"♥{戦闘中カウント:隊列番号:0}", 200, 90



@バフ・デバフ特殊表示_愛シイ人(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 愛シイ人
PRINTFORML 必ずトリプルアタックを行い、被ダメージを軽減する状態［消去不可］
PRINTFORML 持続ターン：{DT_CELL_GET(@"バフデータベース_{隊列番号}", バフ・デバフ行数, "持続ターン")}



@バフ・デバフ特殊表示_永久ノ誓イ(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 永久ノ誓イ
PRINTFORML 攻撃力・防御力が上昇/弱体効果を無効化/通常攻撃時に6回闇属性追加ダメージを与える状態［消去不可］
PRINTFORML ◆ニーアが戦闘不能時、乗機に永久ノ誓イ効果
PRINTFORML 持続ターン：永続



@バフ・デバフ特殊表示_愛ノ救イ(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 愛ノ救イ
PRINTFORML 毎ターン2減少し、0になったターン終了時に戦闘不能になるが、被ダメージを軽減する状態［消去不可］
PRINTFORML カウント：{戦闘中カウント:戦闘行動キャラ:0}



@バフ・デバフ特殊表示_渇望(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 渇望
PRINTFORML 戦闘不能になった時、1度だけ復活する状態［消去不可］
PRINTFORML 持続回数：1



@バフ・デバフ特殊表示_残執(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 残執
PRINTFORML 防御力・連続攻撃率が大幅に減少/HPが回復できない状態［回復不可］
PRINTFORML 持続ターン：{DT_CELL_GET(@"デバフデータベース_{隊列番号}", バフ・デバフ行数, "持続ターン")}



@フィールド説明文_死ト愛ノ世界

LOCALS = 死ト愛ノ世界<br>
LOCALS += "敵味方の攻撃力が上昇/味方全体が通常攻撃時に追撃を行う状態<br>"
LOCALS += @"効果ターン：{展開中フィールド残りターン数}ターン<br>"

詳細文文字列受け渡し変数 = %LOCALS%

