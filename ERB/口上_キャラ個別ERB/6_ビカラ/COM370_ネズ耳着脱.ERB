
;----------------------------------------------------
;キャラ固有コマンド処理
;----------------------------------------------------
;コマンド名
;----------------------------------------------------
@CHARACOMNAME370_6
#FUNCTION
LOCAL = FINDCHARA(NO, 6)
IF ビカラ付け耳フラグ
	TSTR:コマンド名受渡 = ネズ耳を付ける
ELSE
	TSTR:コマンド名受渡 = ネズ耳をはずす
ENDIF
;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_370_6
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("日常")

@COM_TOOLTIP_370_6
LOCALS = <br>■ネズ耳をはずす<br>
LOCALS += "ネズ耳を外して陰ビカラに変化させるコマンド。<br>ネズ耳が外れている時、[ネズ耳を付ける]に変化する。<br>複数の素質と名前、グラフィックが変化する。"
LOCALS += "<br>COMタイプ：日常<br>取得ソース：なし"
LOCALS += "<br>固有の取得経験：なし<br>"
TSTR:ツールチップ受渡 = %LOCALS%

;-------------------------------------------------
;実行処理
;-------------------------------------------------
@CHARACOM370_6
LOCAL = FINDCHARA(NO, 6)
IF ビカラ付け耳フラグ
	TALENT:LOCAL:陽気／陰気 = 1
	TALENT:LOCAL:目立ちたがり = 1
	TALENT:LOCAL:度胸 = 0
	TALENT:LOCAL:戦闘好き = 0
	TALENT:LOCAL:会話資質 = 1
	TALENT:LOCAL:羞恥心 = -1
	TALENT:LOCAL:自慰しやすい = 0
	TALENT:LOCAL:楽観的 = 1
	CALLNAME:LOCAL = ビカラ
	NAME:LOCAL = [北の守護神]ビカラ
	ビカラ付け耳フラグ = 0
	SWAP ビカラ画像フォルダ保存, CSTR:LOCAL:画像フォルダ
ELSE
	TALENT:LOCAL:陽気／陰気 = -1
	TALENT:LOCAL:目立ちたがり = 0
	TALENT:LOCAL:度胸 = -1
	TALENT:LOCAL:戦闘好き = -1
	TALENT:LOCAL:羞恥心 = 1
	TALENT:LOCAL:自慰しやすい = 1
	TALENT:LOCAL:会話資質 = -1
	TALENT:LOCAL:楽観的 = -1
	CALLNAME:LOCAL = ビカラ（陰）
	NAME:LOCAL = [北の守護神]ビカラ（陰）
	ビカラ付け耳フラグ = 1
	SWAP ビカラ画像フォルダ保存, CSTR:LOCAL:画像フォルダ
ENDIF

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE370_6
;一括管理
SIF GLOBAL_COMABLE(370)
	RETURN RESULT
LOCAL = FINDCHARA(NO, 6)
SIF TARGET != LOCAL
	RETURN 0
RETURN 1


;-------------------------------------------------
;実行時メッセージ
;-------------------------------------------------
@MESSAGE_COM370_6
LOCAL = FINDCHARA(NO, 6)
IF ビカラ付け耳フラグ
	PRINTFORML %CALLNAME:PLAYER%はビカラのネズ耳を外した。
ELSE
	PRINTFORML %CALLNAME:PLAYER%はビカラにネズ耳を返した。
ENDIF



