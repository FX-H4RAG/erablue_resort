
@固有アビ_6_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 55
	アビ名 = グリーティング・ドーマウス+
	消費ＭＰ = 100
ELSE
	アビ名 = グリーティング・ドーマウス
	消費ＭＰ = 150
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に5回闇属性［攻撃力×100％］ダメージ/ドーちゃんをけしかける［1回/消去不可］
		詳細文文字列受け渡し変数 += @"<br>◆敵が大技発動時、トラップが起動"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 100
		FOR LOCAL, 0, 5
			CALL アビテンプレート_ダメージ処理_闇属性
		NEXT
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = ドーちゃん
		アビ汎用文字列:バフ・デバフ対象能力 = ドーちゃん
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT



@固有アビ_6_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 対象番号
#DIM バフ行数
#DIM テンション判定
#DIM テンション割合

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 75
	IF 展開中フィールド名格納 == "ゆ～とぴあ"
		アビ名 = エキセントリックパレード
	ELSE
		アビ名 = エンチャンテッド・ドリーム++
	ENDIF
	消費ＭＰ = 175
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= 65
	アビ名 = エンチャンテッド・ドリーム+
	消費ＭＰ = 175
ELSE
	アビ名 = エンチャンテッド・ドリーム
	消費ＭＰ = 200
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF 展開中フィールド名格納 == "ゆ～とぴあ" && 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 75
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体に［20％］追撃効果［1回］
			詳細文文字列受け渡し変数 += @"<br>対象に被ダメージ［220］減少効果［1ターン］"
			詳細文文字列受け渡し変数 += @"<br>対象のHPを［220］回復"
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>さぁ、夢とキラキラのゆ～とぴあへ！
			SIF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 75
				詳細文文字列受け渡し変数 += @"<br>◆このアビリティは行動回数を消費しない/ゆ～とぴあ展開中、『エキセントリックパレード』に変化"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF 展開中フィールド名格納 == "ゆ～とぴあ" && 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 75
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用文字列:バフ・デバフ枠 = 追撃_アビ特殊C枠
			アビ汎用文字列:バフ・デバフ対象能力 = 追撃
			アビ汎用変数:効果割合 = 20
			アビ汎用変数:持続回数 = 1
			CALL アビテンプレート_有利効果_バフ効果セット
			
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 被ダメージ減少
			アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
			アビ汎用変数:効果量 = 220
			アビ汎用変数:持続ターン = 1
			CALL アビテンプレート_有利効果_バフ効果セット
			
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 220
			CALL アビテンプレート_回復処理_MAXでも回復
		ELSE
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:フィールド枠 = ゆ～とぴあ
			展開中フィールド残りターン数 = 5
			CALL アビテンプレート_フィールド効果セット
			
			DT_ROW_ADD @"バフデータベース_{戦闘行動キャラ}", "バフ枠", "子神宮の主_絶対TA", "バフ対象能力", "TA率", "バフ効果量_固定値", 1000, "バフ名", "子神宮の主", "持続ターン", 5, "消去不可フラグ", 1
			DT_ROW_ADD @"バフデータベース_{戦闘行動キャラ}", "バフ枠", "子神宮の主_敵対心", "バフ対象能力", "敵対心", "バフ効果量_固定値", 300, "バフ名", "子神宮の主", "持続ターン", 5, "消去不可フラグ", 1
			DT_ROW_ADD @"バフデータベース_{戦闘行動キャラ}", "バフ枠", "子神宮の主_回避率", "バフ対象能力", "回避率", "バフ効果量_固定値", 50, "バフ名", "子神宮の主", "持続ターン", 5, "消去不可フラグ", 1
			
			FOR 対象番号, 0, 4
				SIF BATTLE_STATE:対象番号:キャラ登録番号 < 1
					CONTINUE
				SIF BATTLE_STATE:対象番号:ＨＰ < 1
					CONTINUE
				
				バフ行数 = バフ番号取得_枠("テンション", 対象番号)
				
				IF バフ行数 < 0
					テンション判定 = RAND:100 + 1
					IF テンション判定 < 6
						テンション割合 = 100
					ELSEIF テンション判定 < 21
						テンション割合 = 60
					ELSEIF テンション判定 < 51
						テンション割合 = 30
					ELSE
						テンション割合 = 10
					ENDIF
					
					CALL アビ汎用変数文字列リセット
					CALL アビ対象選択テンプレート_指定(対象番号)
					アビ汎用文字列:バフ・デバフ枠 = テンション
					アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
					アビ汎用変数:効果割合 = テンション割合
					アビ汎用変数:持続ターン = 1
					アビ汎用変数:特殊表示オプション = 1
					CALL アビテンプレート_有利効果_バフ効果セット
				ELSE
					テンション割合 = DT_CELL_GET(@"バフデータベース_{対象番号}", バフ行数, "バフ効果量_割合")
					
					IF テンション割合 == 60
						テンション割合 = 100
					ELSEIF テンション割合 == 30
						テンション割合 = 60
					ELSEIF テンション割合 == 10
						テンション割合 = 30
					ENDIF
					
					DT_CELL_SET @"バフデータベース_{対象番号}", バフ行数, "バフ効果量_割合", テンション割合
				ENDIF
			NEXT
			
			IF !(陥落チェック(GETCHARA(6)) && 基礎BATTLE_STATE:キャラ番号:Lv >= 75)
				FOR 対象番号, 10, 20
					SIF 敵BATTLE_STATE_STR:(対象番号 - 10):エネミー名 == ""
						CONTINUE
					SIF 敵BATTLE_STATE:(対象番号 - 10):ＨＰ < 1
						CONTINUE
					
					IF バフ番号取得_枠("ゆ～とぴあ_攻撃力", 対象番号) < 0
						DT_ROW_ADD @"バフデータベース_{対象番号}", "バフ枠", "ゆ～とぴあ_攻撃力", "バフ対象能力", "攻撃力", "バフ効果量_割合", 20, "バフ名", "ゆ～とぴあ", "持続ターン", 展開中フィールド残りターン数, "消去不可フラグ", 1
					ENDIF
				NEXT
			ELSE
				アビテンプレート用_行動消費無しフラグ = 1
			ENDIF
		ENDIF
ENDSELECT



@固有アビ_6_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 対象番号

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= 45
	アビ名 = 一子之夢
	消費ＭＰ = 100
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体に一子之夢効果［2回被ダメージまで永続/消去不可］
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 一子之夢
		アビ汎用文字列:バフ・デバフ対象能力 = 一子之夢
		アビ汎用変数:持続被ダメ回数 = 2
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		FOR 対象番号, 0, 4
			SIF BATTLE_STATE:対象番号:キャラ登録番号 < 1
				CONTINUE
			SIF BATTLE_STATE:対象番号:ＨＰ < 1
				CONTINUE
			
			IF バフ番号取得_枠("一子之夢_ゲージ上昇効率", 対象番号) < 0
				DT_ROW_ADD @"バフデータベース_{対象番号}", "バフ枠", "一子之夢_ゲージ上昇効率", "バフ対象能力", "ゲージ上昇効率", "バフ効果量_割合", 50, "バフ名", "一子之夢", "持続被ダメ回数", 2, "消去不可フラグ", 1
				DT_ROW_ADD @"バフデータベース_{対象番号}", "バフ枠", "一子之夢_奥義与ダメ", "バフ対象能力", "ダメージ補正_奥義ダメージ", "バフ効果量_割合", 10, "バフ名", "一子之夢", "持続被ダメ回数", 2, "消去不可フラグ", 1
				DT_ROW_ADD @"バフデータベース_{対象番号}", "バフ枠", "一子之夢_回避率", "バフ対象能力", "回避率", "バフ効果量_固定値", 30, "バフ名", "一子之夢", "持続被ダメ回数", 2, "消去不可フラグ", 1
			ENDIF
		NEXT
ENDSELECT



@固有アビ_6_4(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 生存キャラ数
#DIM 対象番号
#DIM バフ行数
#DIM テンション判定

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= 80
	アビ名 = 陽鼠虹城
	消費ＭＰ = 300
ENDIF

生存キャラ数 = 0
テンション判定 = 0

FOR 対象番号, 0, 4
	SIF BATTLE_STATE:対象番号:キャラ登録番号 < 1
		CONTINUE
	SIF BATTLE_STATE:対象番号:ＨＰ < 1
		CONTINUE
	
	生存キャラ数 += 1
	バフ行数 = バフ番号取得_枠("テンション", 対象番号)
	
	IF DT_CELL_GET(@"バフデータベース_{対象番号}", バフ行数, "バフ効果量_割合") == 100
		テンション判定 += 1
	ENDIF
NEXT

IF テンション判定 == 生存キャラ数
	アビテンプレート用_アビ封印用フラグ = 0
ELSE
	アビテンプレート用_アビ封印用フラグ = 1
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体に絶対回避効果［1回］/再行動効果［1回］
		詳細文文字列受け渡し変数 += @"<br>◆味方全体のテンションLvが4の時にのみ発動可能"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 完全回避
		アビ汎用文字列:バフ・デバフ対象能力 = 回避率
		アビ汎用変数:効果量 = 1000
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 再行動
		アビ汎用文字列:バフ・デバフ対象能力 = 再行動
		アビ汎用変数:効果量 = 1
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT



@固有ターン終了時処理_キャラ_6
#DIM 対象番号
#DIM トラップ起動
#DIM ドーちゃん

FOR LOCAL, 10, 20
	IF 行動種別フラグ:LOCAL:大技発動 && デバフ番号取得_枠("ドーちゃん", LOCAL) > -1
		トラップ起動 = 1
		対象番号 = LOCAL
		ドーちゃん = デバフ番号取得_枠("ドーちゃん", LOCAL)
		BREAK
	ENDIF
NEXT

IF トラップ起動
	アビテンプレート用_アビ名 = ドーちゃん
	
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_敵全体_生者のみ
	アビ汎用変数:効果量 = 999
	CALL アビテンプレート_ダメージ処理_無属性
	
	CALL アビ汎用変数文字列リセット
	アビ汎用文字列:バフ・デバフ枠 = 捕獲
	アビ汎用文字列:バフ・デバフ対象能力 = 麻痺
	アビ汎用変数:効果割合 = 100
	アビ汎用変数:基礎成功確率 = 120
	アビ汎用変数:持続ターン = 3
	アビ汎用変数:特殊表示オプション = 1
	CALL アビテンプレート_不利効果_デバフ効果セット
	
	CALL デバフ削除(対象番号, ドーちゃん)
ENDIF



@固有回避時処理_キャラ_6(全滅フラグ)
#DIM 全滅フラグ
#DIM バフ行数
#DIM ビカラ番号
#DIM ビカラ隊列
#DIM 対象番号
#DIM 効果ターン

SIF 全滅フラグ
	RETURN 0

ビカラ番号 = GETCHARA(6)
ビカラ隊列 = キャラ隊列検索(ビカラ番号)

KSTR:(K++) = 「いぇいっ！」
WSTR:(K++) = ビカラのビート・ザ・マウス！

CALL アビ汎用変数文字列リセット
CALL アビ対象選択テンプレート_敵全体_生者のみ
アビ汎用変数:効果割合 = 500
CALL アビテンプレート_ダメージ処理_闇属性
SIF DT_CELL_GET(@"デバフデータベース_{ビカラ隊列}", バフ番号取得_枠("テンション", ビカラ隊列), "バフ効果量_割合")
	CALL アビテンプレート_ダメージ処理_闇属性

CALL アビ汎用変数文字列リセット
CALL アビテンプレート_不利効果_バフ消去
SIF DT_CELL_GET(@"デバフデータベース_{ビカラ隊列}", バフ番号取得_枠("テンション", ビカラ隊列), "バフ効果量_割合")
	CALL アビテンプレート_不利効果_バフ消去

IF 陥落チェック(ビカラ番号) && 基礎BATTLE_STATE:ビカラ番号:Lv >= 70
	FOR 対象番号, 0, 4
		SIF BATTLE_STATE:対象番号:キャラ登録番号 < 1
			CONTINUE
		SIF BATTLE_STATE:対象番号:ＨＰ < 1
			CONTINUE
		
		バフ行数 = バフ番号取得_枠("テンション", 対象番号)
		
		IF バフ行数 > -1
			効果ターン = DT_CELL_GET(@"バフデータベース_{対象番号}", バフ行数, "持続ターン")
			DT_CELL_SET @"バフデータベース_{対象番号}", バフ行数, "持続ターン", 効果ターン + 1
			SIF DT_CELL_GET(@"デバフデータベース_{ビカラ隊列}", バフ番号取得_枠("テンション", ビカラ隊列), "バフ効果量_割合")
				DT_CELL_SET @"バフデータベース_{対象番号}", バフ行数, "持続ターン", 効果ターン + 1
		ENDIF
	NEXT
ENDIF



@固有ターン開始時処理_フィールド_ゆ～とぴあ
#DIM バフ行数
#DIM 隊列番号
#DIM ビカラ番号
#DIM ビカラ隊列
#DIM 対象番号
#DIM テンション判定
#DIM テンション割合

ビカラ番号 = GETCHARA(6)

FOR 対象番号, 0, 4
	SIF BATTLE_STATE:対象番号:キャラ登録番号 < 1
		CONTINUE
	SIF BATTLE_STATE:対象番号:ＨＰ < 1
		CONTINUE
	
	バフ行数 = バフ番号取得_枠("テンション", 対象番号)
	
	IF バフ行数 < 0
		テンション判定 = RAND:100 + 1
		IF テンション判定 < 6
			テンション割合 = 100
		ELSEIF テンション判定 < 21
			テンション割合 = 60
		ELSEIF テンション判定 < 51
			テンション割合 = 30
		ELSE
			テンション割合 = 10
		ENDIF
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_指定(対象番号)
		アビ汎用文字列:バフ・デバフ枠 = テンション
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = テンション割合
		アビ汎用変数:持続ターン = 1
		アビ汎用変数:特殊表示オプション = 1
		CALL アビテンプレート_有利効果_バフ効果セット
	ELSE
		テンション割合 = DT_CELL_GET(@"バフデータベース_{対象番号}", バフ行数, "バフ効果量_割合")
		
		IF テンション割合 == 60
			テンション割合 = 100
		ELSEIF テンション割合 == 30
			テンション割合 = 60
		ELSEIF テンション割合 == 10
			テンション割合 = 30
		ENDIF
		
		DT_CELL_SET @"バフデータベース_{対象番号}", バフ行数, "バフ効果量_割合", テンション割合
	ENDIF
NEXT

IF !(陥落チェック(ビカラ番号) && 基礎BATTLE_STATE:ビカラ番号:Lv >= 75)
	FOR 対象番号, 10, 20
		SIF 敵BATTLE_STATE_STR:(対象番号 - 10):エネミー名 == ""
			CONTINUE
		SIF 敵BATTLE_STATE:(対象番号 - 10):ＨＰ < 1
			CONTINUE
		
		バフ行数 = バフ番号取得_枠("ゆ～とぴあ_攻撃力", 対象番号)
		
		IF バフ行数 < 0
			DT_ROW_ADD @"バフデータベース_{対象番号}", "バフ枠", "ゆ～とぴあ_攻撃力", "バフ対象能力", "攻撃力", "バフ効果量_割合", 20, "持続ターン", 展開中フィールド残りターン数, "消去不可フラグ", 1
		ELSE
			DT_CELL_SET @"バフデータベース_{対象番号}", バフ行数, "持続ターン", 展開中フィールド残りターン数
		ENDIF
	NEXT
ENDIF



@バフ・デバフ特殊表示_テンション(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
#DIM テンションLv
VARSET LOCALS

IF DT_CELL_GET(@"バフデータベース_{隊列番号}", バフ・デバフ行数, "バフ効果量_割合") == 100
	テンションLv = 4
ELSEIF DT_CELL_GET(@"バフデータベース_{隊列番号}", バフ・デバフ行数, "バフ効果量_割合") == 60
	テンションLv = 3
ELSEIF DT_CELL_GET(@"バフデータベース_{隊列番号}", バフ・デバフ行数, "バフ効果量_割合") == 30
	テンションLv = 2
ELSEIF DT_CELL_GET(@"バフデータベース_{隊列番号}", バフ・デバフ行数, "バフ効果量_割合") == 10
	テンションLv = 1
ENDIF

PRINTFORML テンション
PRINTFORML Lvに応じて攻撃力が上昇する状態
PRINTFORML Lv：{テンションLv}　　　　　持続ターン：{DT_CELL_GET(@"バフデータベース_{隊列番号}", バフ・デバフ行数, "持続ターン")}



@バフ・デバフ特殊表示_ドーちゃん(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML ドーちゃん
PRINTFORML 大技発動時、無属性ダメージ/捕獲効果を受ける状態［消去不可］
PRINTFORML 持続回数：1



@バフ・デバフ特殊表示_捕獲(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 捕獲
PRINTFORML 攻撃行動を封じられた状態
PRINTFORML 持続ターン：{DT_CELL_GET(@"デバフデータベース_{隊列番号}", バフ・デバフ行数, "持続ターン")}



@バフ・デバフ特殊表示_一子之夢(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 一子之夢
PRINTFORML 奥義ゲージ上昇効率・奥義与ダメージ・回避率が上昇した状態［消去不可］
PRINTFORML 持続回数：{DT_CELL_GET(@"バフデータベース_{隊列番号}", バフ・デバフ行数, "持続被ダメ回数")}



@フィールド説明文_ゆ～とぴあ
#DIM ビカラ番号

ビカラ番号 = GETCHARA(6)

LOCALS = ゆ～とぴあ<br>
IF 陥落チェック(ビカラ番号) && 基礎BATTLE_STATE:ビカラ番号:Lv >= 75
	LOCALS += "ターン開始時、味方全体にテンション効果<br>"
ELSE
	LOCALS += "ターン開始時、味方全体にテンション効果/敵全体の攻撃力が上昇<br>"
ENDIF
LOCALS += @"効果ターン：{展開中フィールド残りターン数}ターン<br>"

詳細文文字列受け渡し変数 = %LOCALS%

;--------------------------------------------------
;戦闘能力画面に表示されるキャラごとの固有処理説明
;--------------------------------------------------
;・使い方
;「XXXX」をキャラクター番号に置換し、それぞれの関数に処理を入れる
;　口上と同じ感覚で使う為に書式を統一しているが、ステータス画面で表示されるので
;　基本的にWAIT系列は使わないことを推奨
;--------------------------------------------------
@口上_ステータス画面_固有特性解説_6(対象キャラ)
#DIM 対象キャラ
CALL 口上変数初期化

KSTR:(K++) = □ 特性：『子神宮の主』 □---------------------------------------------------------------
IF 陥落チェック(対象キャラ) && 基礎BATTLE_STATE:対象キャラ:Lv >= 70
	KSTR:(K++) = 　ゆ～とぴあ展開中、自身が必ずTA/敵対心・回避率が上昇/奥義発動時、味方全体にテンション効果・ディスペルガード効果
ELSE
	KSTR:(K++) = 　ゆ～とぴあ展開中、自身が必ずTA/敵対心・回避率が上昇/奥義発動時、味方全体にテンション効果
ENDIF
KSTR:(K++) = 　
KSTR:(K++) = □ 特性：『ビート・ザ・マウス』 □------------------------------------------------------
IF 陥落チェック(対象キャラ) && 基礎BATTLE_STATE:対象キャラ:Lv >= 70
	KSTR:(K++) = 　攻撃回避時、敵全体に闇属性ダメージ/強化効果を消去/味方全体のテンション効果を延長
	KSTR:(K++) = 　◆テンションLv4の時、2回発動
ELSE
	KSTR:(K++) = 　攻撃回避時、敵全体に闇属性ダメージ/強化効果を消去
ENDIF

CALL 口上変数表示(対象キャラ, 1)



