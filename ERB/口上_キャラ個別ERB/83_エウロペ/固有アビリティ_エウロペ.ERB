
@固有アビ_83_1(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM Lv閾値 =  0, 15, 30, 55

;レベルなどの条件でアビを強化する場合、アビ名の文字列を条件分岐で変化させる
IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:2
	アビ名 = マナ・ブラスト＋＋
	消費ＭＰ = 75
	効果数値 = 80
		IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3
			アビ名 = 星華のマナ・ブラスト
			消費ＭＰ = 60
			効果数値 = 100
		ENDIF
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:1
	アビ名 = マナ・ブラスト＋
	消費ＭＰ = 75
	効果数値 = 60
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:0
	アビ名 = マナ・ブラスト
	消費ＭＰ = 75
	効果数値 = 40
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>ターゲットに関わらず3回水属性［攻撃力×80％］ダメージ
		詳細文文字列受け渡し変数 += @"<br>敵全体に氷結効果［3ターン］"
		詳細文文字列受け渡し変数 += @"<br>味方全体のＨＰを［回復力✕{効果数値}％］回復させる"
		詳細文文字列受け渡し変数 += @"<br>味方全体のデバフを一つ削除する"
		詳細文文字列受け渡し変数 += @"<br>◆敵が氷結状態の時、攻撃回数2倍"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
			FOR LOCAL, 0, 3
				アビ汎用変数:敵対心無視オプション = 1
				CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
				アビ汎用変数:効果割合 = 80
				CALL アビテンプレート_ダメージ処理_水属性
			NEXT
		IF デバフ番号取得_枠("氷結") > -1
			FOR LOCAL, 0, 3
				アビ汎用変数:敵対心無視オプション = 1
				CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
				アビ汎用変数:効果割合 = 80
				CALL アビテンプレート_ダメージ処理_水属性
			NEXT	
		ENDIF
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 氷結
		アビ汎用文字列:バフ・デバフ対象能力 = 氷結
		CALL バフ込み数値算出("攻撃力")
		アビ汎用変数:効果量 = RESULT * 効果数値:0 / 100
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 90
		CALL アビテンプレート_不利効果_デバフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_回復処理_MAXでも回復
		
		CALL アビテンプレート_有利効果_デバフ消去

ENDSELECT

@固有アビ_83_2(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM Lv閾値 =  0, 20, 40, 75

IF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:2
	アビ名 = テュロス・アジリス＋＋
	消費ＭＰ = 180
	効果数値 = 3000
		IF 陥落チェック(キャラ番号) && 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:3
			アビ名 = 星域のテュロス・アジリス
			消費ＭＰ = 180
			効果数値 = 5000
		ENDIF
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:1
	アビ名 = テュロス・アジリス＋
	消費ＭＰ = 200
	効果数値 = 1000
ELSEIF 基礎BATTLE_STATE:キャラ番号:Lv >= Lv閾値:0
	アビ名 = テュロス・アジリス
	消費ＭＰ = 200
	効果数値 = 500
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>味方全体の攻撃力を15％上昇させる[2ターン]
		詳細文文字列受け渡し変数 += @"<br>味方全体の被ダメージを50％軽減[2ターン]"
		詳細文文字列受け渡し変数 += @"<br>味方全体にディスペルガードを付与[2ターン]"
		詳細文文字列受け渡し変数 += @"<br>味方全体にバリアを[{効果数値}]展開"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 15
		アビ汎用変数:持続ターン = 2
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 被ダメージ
		アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
		アビ汎用変数:効果割合 = 50
		アビ汎用変数:持続ターン = 2
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = ディスペルガード
		アビ汎用文字列:バフ・デバフ対象能力 = ディスペルガード
		アビ汎用変数:持続ターン = 2
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = バリア
		アビ汎用文字列:バフ・デバフ対象能力 = バリア
		アビ汎用変数:効果量 = 効果数値
		CALL アビテンプレート_有利効果_バフ効果セット

ENDSELECT

@固有アビ_83_3(ARGS, キャラ番号)
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名

IF 基礎BATTLE_STATE:キャラ番号:Lv >= 15
	アビ名 = プレアデス
	消費ＭＰ = 80
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：{消費ＭＰ}<br>敵単体に水属性［攻撃力×700％］ダメージ
		詳細文文字列受け渡し変数 += @"<br>自身の連続攻撃率を[100％]アップ[3ターン]"
		詳細文文字列受け渡し変数 += @"<br>自身に水属性追撃効果[3ターン]"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 700
		CALL アビテンプレート_ダメージ処理_水属性

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = DA/TA率１
		アビ汎用文字列:バフ・デバフ対象能力 = 連続攻撃率
		アビ汎用変数:効果量 = 100
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 追撃
		アビ汎用文字列:バフ・デバフ対象能力 = 追撃
		アビ汎用変数:効果割合 = 70
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

ENDSELECT