@口上変数初期化
VARSET KSTR
VARSET WSTR
VARSET NSTR
VARSET WNSTR
K = 0
A_M_NUM = 0
VARSET K_CALL
VARSET KCOLOR,-1

VARSET 切り替え顔グラリソース
VARSET 切り替え名前
TSTR:メッセージ欄文字列受け渡し_下側 = %メッセージ欄スクロール表示(, , 0)%</div>
メッセージ欄追加数カウント = 0

;KSTR:(K++) = 
;WSTR:(K++) = 
;NSTR:(K++) = 

;K_CALL:K = 
;KCOLOR:K = 


@口上変数表示,ARG = -1,ARG:1 = 0
SIF !口上有無確認(0,1)
	RETURN 0
SIF ARG == -1
	ARG = TARGET
FOR LOCAL,0,1000
	IF !(K_CALL:LOCAL == "")
		CALLFORM %K_CALL:LOCAL%
		SIF LOCAL > 0
			CALL WSTR書き込み,LOCAL - 1
	ENDIF
	SIF !口上有無確認(LOCAL,1)
		BREAK
	SIF KCOLOR:LOCAL > -1
		SETCOLOR KCOLOR:LOCAL
	IF !(WSTR:LOCAL == "")
		CALL PRINT_STRW(WSTR:LOCAL,ARG)
	ELSEIF !(KSTR:LOCAL == "")
		CALL PRINT_STRL(KSTR:LOCAL,ARG)
	ELSEIF !(NSTR:LOCAL == "")
		CALL PRINT_STR(NSTR:LOCAL,ARG)
	ELSEIF !(WNSTR:LOCAL == "")
		CALL PRINT_STRWN(WNSTR:LOCAL,ARG)
	ENDIF
NEXT
RESETCOLOR
SIF WSTR:(LOCAL - 2) == "" && ARG:1 == 0
	WAIT
RETURN 1

@WSTR書き込み,ARG
IF !(WSTR:ARG == "")
ELSEIF !(KSTR:ARG == "")
	WSTR:ARG = %KSTR:ARG%
ELSEIF !(NSTR:ARG == "")
	WSTR:ARG = %NSTR:ARG%
ENDIF

@口上有無確認,ARG,ARG:1 = 0
#FUNCTION
;ARG:1　0だとKを口上のない番号に移動
IF ARG:1 == 0
	FOR LOCAL,0,1000
		SIF !(WSTR:LOCAL == "")
			CONTINUE
		SIF !(KSTR:LOCAL == "")
			CONTINUE
		SIF !(NSTR:LOCAL == "")
			CONTINUE
		SIF !(WNSTR:LOCAL == "")
			CONTINUE
		K = LOCAL
		BREAK
	NEXT
ENDIF
SIF !(WSTR:ARG == "")
	RETURNF 1
SIF !(KSTR:ARG == "")
	RETURNF 1
SIF !(NSTR:ARG == "")
	RETURNF 1
SIF !(WNSTR:ARG == "")
	RETURNF 1
RETURNF 0


@コメント文頭処理(対象キャラ = -1, 処理モード = 0)
#DIM 対象キャラ
#DIM 処理モード

SIF 対象キャラ == -1
	対象キャラ = TARGET
LOCAL:1 = 1
LOCALS '= "│"
SIF 処理モード == 1
	LOCALS '= " "
IF 処理モード == 2 && GETBIT(画像サイズ切り替えOPTION記憶, 0) || (GETBIT(画像サイズ切り替えOPTION記憶, 4) && 二人目画像表示用())
	LOCALS '= "　　　　　　　"
ELSEIF 処理モード == 2
	RETURN 0
ENDIF
FOR LOCAL, 0, 1000
	IF !(NSTR:LOCAL == "")
		SIF LOCAL:1
			NSTR:LOCAL '= "　　" + LOCALS + " 　　" + NSTR:LOCAL
		LOCAL:1 = 0
	ELSEIF !(WSTR:LOCAL == "")
		SIF LOCAL:1
			WSTR:LOCAL '= "　　" + LOCALS + " 　　" + WSTR:LOCAL
		LOCAL:1 = 1
	ELSEIF !(KSTR:LOCAL == "")
		SIF LOCAL:1
			KSTR:LOCAL '= "　　" + LOCALS + " 　　" + KSTR:LOCAL
		LOCAL:1 = 1
	ELSE
		BREAK
	ENDIF
NEXT

