;-------------------------------------------------
;@特定状況口上条件分岐
;処女喪失や初対面など、特定の状況に発動する口上の条件分岐をまとめるファイル
;-------------------------------------------------

;-------------------------------------------------
;遭遇時（TARGETを切り替えた時）に条件判定するもの
;-------------------------------------------------
@KOJO_遭遇時EVENT
	;-------------------------------------------------
	;以下、うふふ状態だと発動しない（＝連れ込まれ処理時）
	;-------------------------------------------------
	SIF CFLAG:TARGET:うふふ
		RETURN 0

	;-------------------------------------------------
	;初対面
	;-------------------------------------------------
	IF 初体験日:TARGET:初対面 == 0 && !CFLAG:PLAYER:隠密
		初体験日:TARGET:初対面 = DAY
		CALL 履歴データベース登録(CFLAG:MASTER:人物番号, @"初めて%CALLNAME:TARGET%と出会った","日常")
		CALL 履歴データベース登録(CFLAG:TARGET:人物番号, @"初めて%CALLNAME:MASTER%と出会った","日常")
		CALL メッセージ呼び出し用構文("初対面")
		IF フレーバー素質:TARGET:素質表示設定 == 2
			PRINTL
			PRINTW 初対面時のため、フレーバー素質設定画面を開きます。
			CALL フレーバー素質個別設定画面(TARGET)
		ENDIF
	ENDIF

	;-------------------------------------------------
	;常駐業務場所訪問時、一日一回
	;-------------------------------------------------
	IF 現在仕事:TARGET:0 == 2 && !CFLAG:PLAYER:隠密 && CFLAG:TARGET:現在マップ種別 == 現在仕事:TARGET:1 / 10000 && CFLAG:TARGET:現在位置 == 現在仕事:TARGET:1 % 10000 && TCVAR:TARGET:常駐業務イベフラグ == 0
		TCVAR:TARGET:常駐業務イベフラグ = 1
		CALL メッセージ呼び出し用構文(@"業務訪問_%MAP_GET("常駐業務名", GETPLACENAME(現在仕事:TARGET:1 / 10000, 現在仕事:TARGET:1 % 10000))%")
	ENDIF

	;-------------------------------------------------
	;恋慕陥落条件達成時
	;-------------------------------------------------
	IF 告白成功判定(TARGET) && 初体験日:TARGET:恋慕自覚 == 0 && !CFLAG:PLAYER:隠密
		初体験日:TARGET:恋慕自覚 = DAY
		CALL メッセージ呼び出し用構文("恋慕自覚")
	ENDIF


;-------------------------------------------------
;コマンド実行後に条件判定するもの
;タイミングは射精・絶頂・EXP取得の後、刻印取得やPALAM上昇の前
;-------------------------------------------------
@KOJO_MESSAGE_PALAMCNG_AFTER
	;条件分岐を満たせばメッセージ呼び出し用構文が飛ぶ
	;上から順に呼び出し、条件を満たしたものは全て呼ぶ
	;初回のみの呼び出しなどは「特定状況口上用フラグ」や「初体験日」などの変数を使って記録すること、CFLAGなどは非推奨

	;-------------------------------------------------
	;処女
	;-------------------------------------------------
	IF RCVAR:TARGET:破瓜
		CALL メッセージ呼び出し用構文("処女喪失")
	ENDIF

	;-------------------------------------------------
	;連れ込み満足
	;-------------------------------------------------
	IF RCVAR:TARGET:連れ込み満足フラグ
		CALL メッセージ呼び出し用構文("連れ込み満足")
	ENDIF







@メッセージ呼び出し用構文(ARGS)
IF FLAG:汎用文章表示切り替えオプション > 0
	DRAWLINE
	TRYCALLFORM PALAM_MESSAGE_%ARGS%
	PRINTL
ENDIF
IF FLAG:口上表示切り替えオプション <= 0
	DRAWLINE
	RETURN 0
ENDIF

TRYCALLFORM KOJO_EVENT_%ARGS%_{NO:TARGET}(TARGET)
DRAWLINE

;-------------------------------------------------
;ここから汎用メッセージ表示関数
;-------------------------------------------------
@PALAM_MESSAGE_処女喪失
PRINTFORMW %CALLNAME:TARGET%の秘唇から、処女の証である赤い血が垂れ落ちていった……

@PALAM_MESSAGE_連れ込み満足
PRINTFORML %CALLNAME:TARGET%は満足する絶頂が出来たようだ
PRINTW そろそろ離してもらえるだろう…

@PALAM_MESSAGE_初対面
PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:TARGET%と初めて出会った
FORCEWAIT

@PALAM_MESSAGE_業務訪問_バーテンダー
PRINTFORMW %CALLNAME:TARGET%がバーカウンターの中で接客をしている。

@PALAM_MESSAGE_業務訪問_司書
PRINTFORMW %CALLNAME:TARGET%は本の整理をしているようだ。

@PALAM_MESSAGE_業務訪問_プール監視員
PRINTFORMW %CALLNAME:TARGET%は溺れている人がいないかプールを見張っている。

@PALAM_MESSAGE_業務訪問_道具屋店員
PRINTDATA
	DATAFORM %CALLNAME:TARGET%はレジで接客をしている。
	DATAFORM %CALLNAME:TARGET%はレジで観光客の相手をしているようだ。
	DATAFORM %CALLNAME:TARGET%は商品の点検と確認をしている。
	DATAFORM %CALLNAME:TARGET%は空いた時間で店舗の掃除をしているようだ。
ENDDATA
PRINTW 

@PALAM_MESSAGE_業務訪問_土産物屋店員
PRINTDATA
	DATAFORM %CALLNAME:TARGET%はレジで接客をしている。
	DATAFORM %CALLNAME:TARGET%はレジで観光客の相手をしているようだ。
	DATAFORM %CALLNAME:TARGET%は商品の点検と確認をしている。
	DATAFORM %CALLNAME:TARGET%は空いた時間で店舗の掃除をしているようだ。
ENDDATA
PRINTW 


@PALAM_MESSAGE_業務訪問_食事処店員
IF 知識素質:TARGET:料理人 > 0
	PRINTFORMW %CALLNAME:TARGET%は厨房で料理の腕を振るっている。
ELSE
	PRINTFORMW %CALLNAME:TARGET%は出来上がった料理を客のテーブルへ運んでいる。
ENDIF

@PALAM_MESSAGE_業務訪問_装飾品店店員
PRINTDATA
	DATAFORM %CALLNAME:TARGET%はレジで接客をしている。
	DATAFORM %CALLNAME:TARGET%はレジで観光客の相手をしているようだ。
	DATAFORM %CALLNAME:TARGET%は商品の点検と確認をしている。
	DATAFORM %CALLNAME:TARGET%は空いた時間で店舗の掃除をしているようだ。
ENDDATA
PRINTW 
