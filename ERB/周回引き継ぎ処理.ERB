@周回引き継ぎ処理
#DIM 引き継ぎ枠

DRAWLINE
PRINTL 一部のデータを引き継ぎ、新しいゲームを開始します。
PRINTL
PRINTL ・引き継ぐデータ
PRINTL 　　あなた特殊能力・ランダムキャラの存在（従業員ではなくなります）
PRINTL 　　各キャラへの肉体熟知及び弱点開示状況・所持ルピ・指輪・耳飾り・素材アイテム
PRINTL 　　探索アイテム・探索衣装・従業員制服・選択したキャラクターの能力全般
PRINTL 　　その他一部の特殊なフラグ
DRAWLINE
引き継ぎ枠 = 引き継ぎ枠判定()
PRINTFORML ■引き継ぎ予定キャラ（最大{引き継ぎ枠}名）
FOR LOCAL, 0, 引き継ぎ枠
	IF 引き継ぎキャラ基礎配列:LOCAL:0
		PRINTBUTTON @"{LOCAL, 2}：%NAME:(引き継ぎキャラ基礎配列:LOCAL:0)%", LOCAL
		PRINTL
	ELSE
		PRINTBUTTON @"{LOCAL, 2}：--------------------", LOCAL
		PRINTL
	ENDIF
NEXT
PRINTL
PRINTBUTTON "[998] これで新しいゲームを開始する", 998
PRINTL
PRINTBUTTON "[999] 戻る", 999

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 999
		RETURN 1
	CASE 998
		CALL 周回実処理()
	CASEELSE
		CALL 陥落キャラ選択()
		引き継ぎキャラ基礎配列:LOCAL:0 = RESULT
		RESTART
ENDSELECT



@引き継ぎ枠判定
#FUNCTION
#DIM 引き継ぎ枠
引き継ぎ枠 = 0

;ハーレム能力に応じて増加
SELECTCASE あなた特殊能力:ハーレム
	CASE 1
		引き継ぎ枠 += 1
	CASE 2
		引き継ぎ枠 += 1
	CASE 3
		引き継ぎ枠 += 2
	CASE 4
		引き継ぎ枠 += 3
ENDSELECT

;セフレ数に応じて増加
LOCAL:1 = 0
FOR LOCAL, 1, CHARANUM
	SIF TALENT:LOCAL:セフレ
		LOCAL:1 += 1
NEXT
IF LOCAL:1 >= 9
	引き継ぎ枠 += 3
ELSEIF LOCAL:1 >= 4
	引き継ぎ枠 += 2
ELSEIF LOCAL:1 >= 1
	引き継ぎ枠 += 1
ENDIF

;最低１人
引き継ぎ枠 = MAX(引き継ぎ枠, 1)
RETURNF 引き継ぎ枠

@陥落キャラ選択
#DIM 現在ページ
#DIM 表示候補, 500
#DIM ソート基準値, 500
#DIM 候補人数
現在ページ = 0

$最初から
;表示候補選定
LOCAL:1 = 0
VARSET 表示候補
VARSET LOCALS
;陥落者のみ
FOR LOCAL, 1, CHARANUM
	IF TALENT:LOCAL:恋慕 || TALENT:LOCAL:セフレ
		表示候補:(LOCAL:1) = LOCAL
		LOCAL:1 += 1
	ENDIF
NEXT
候補人数 = LOCAL:1

$表示処理_LOOP
DRAWLINE
VARSET LOCAL
VARSET LOCALS
;表示処理
FOR LOCAL, 現在ページ * 30, 候補人数
	LOCALS += @"<button value = '{表示候補:LOCAL}'>[{表示候補:LOCAL,3}]%NAME:(表示候補:LOCAL),40,LEFT%</button>"

	IF TALENT:(表示候補:LOCAL):恋慕
		LOCALS += @"　　　<font color='#FF3399'>恋慕陥落</font>　"
	ELSE
		LOCALS += @"　　セフレ陥落　"
	ENDIF
	LOCAL:1 += 1
	IF LOCAL:1 % 2 == 0
		HTML_PRINT LOCALS
		LOCALS = 
	ENDIF
	SIF LOCAL:1 > 29
		BREAK
NEXT
IF LOCALS != ""
	HTML_PRINT LOCALS
	LOCALS = 
ENDIF
PRINTL 
IF 現在ページ > 0
	PRINTLC  [900]前のページへ
ELSE
	PRINTLC  
ENDIF
IF 現在ページ < 候補人数 / 30
	PRINTLC  [901]次のページへ
ENDIF
PRINTL
DRAWLINE
PRINTL
PRINTBUTTONLC "[1000]戻る", 1000
PRINTL

BINPUT
IF RESULT == 1000
	RETURN -1
ELSEIF RESULT == 900
	現在ページ = MAX(現在ページ - 1, 0)
	GOTO 表示処理_LOOP
ELSEIF RESULT == 901
	現在ページ = MIN(現在ページ + 1, 候補人数 / 30)
	GOTO 表示処理_LOOP
ELSE
	RETURN RESULT
ENDIF


@周回実処理

RESETDATA
BEGIN FIRST