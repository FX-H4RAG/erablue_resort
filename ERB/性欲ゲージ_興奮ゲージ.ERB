

;-------------------------------------------------------------------------------------------
;性欲ゲージ関連処理
;上昇量を素質で補正すること
;-------------------------------------------------------------------------------------------

@性欲ゲージ増加(キャラ番号)
#DIM キャラ番号
;性欲無しだと何もしない
SIF TALENT:キャラ番号:性欲 == -2
	RETURN 0

;基礎値=時間経過分/4の性欲（一日360）ABL欲望１につき10%の補正
LOCAL = (10 + ABL:キャラ番号:欲望) * TIME_PROGRESS(TFLAG:行動前時刻) / 40

;性欲素質 1=+20% -1=-20%
LOCAL = LOCAL * (10 + TALENT:キャラ番号:性欲 * 2) / 10

;乱数で-20%～+20%まで上下
LOCAL = LOCAL * (100 + RAND:40 - 19) / 100

BASE:キャラ番号:性欲 = MIN(BASE:キャラ番号:性欲 + LOCAL, MAXBASE:キャラ番号:性欲)

;発情期は常時最大値の8割保証。エルーン系以外ならば6割保証
IF CFLAG:キャラ番号:発情期フラグ < 0
	IF TALENT:キャラ番号:種族 == 2 || GETBIT(TALENT:キャラ番号:発情期あり, 1) || キャラ一致チェック(キャラ番号, "[六竜の『金』]ガレヲン") 
		BASE:キャラ番号:性欲 = MAX(BASE:キャラ番号:性欲, MAXBASE:キャラ番号:性欲 * 8 / 10)
	ELSEIF TALENT:キャラ番号:種族 == 3 || TALENT:キャラ番号:発情期あり
		BASE:キャラ番号:性欲 = MAX(BASE:キャラ番号:性欲, MAXBASE:キャラ番号:性欲 * 6 / 10)
	ENDIF
ENDIF


@性欲ゲージ減少処理(キャラ番号)
#DIM キャラ番号

;うふふ後、満足ゲージに応じて性欲ゲージ減少
;うふふ終了時以外に呼んでも当然なにもならない

BASE:キャラ番号:性欲 = MAX(0, BASE:キャラ番号:性欲 - BASE:キャラ番号:満足)

;ついでに絶頂累積消去
TCVAR:キャラ番号:うふふ中Ｃ絶頂累積 = 0
TCVAR:キャラ番号:うふふ中Ｂ絶頂累積 = 0
TCVAR:キャラ番号:うふふ中Ｖ絶頂累積 = 0
TCVAR:キャラ番号:うふふ中Ａ絶頂累積 = 0
TCVAR:キャラ番号:うふふ中Ｓ絶頂累積 = 0
TCVAR:キャラ番号:うふふ中消費体力累積 = 0
BASE:キャラ番号:満足 = 0


@満足ゲージ上昇処理(キャラ番号)
#DIM キャラ番号
#DIM 満足係数
#DIM 満足上昇量

;絶頂時、素質に応じて満足ゲージを上げる
;あなたは関係ない
IF キャラ番号 == MASTER
	RETURN 0
ENDIF
;連れ込み中は条件達成しないと上がらない
IF 連れ込みパターン != ""
	RETURN 0
ENDIF
;うふふじゃないなら上げない
IF !CFLAG:キャラ番号:うふふ
	RETURN 0
ENDIF

満足係数 = 1
;各絶頂の強度が高いほど満足
満足係数 += NOWEX:キャラ番号:Ｃ絶頂
満足係数 += NOWEX:キャラ番号:Ｖ絶頂
満足係数 += NOWEX:キャラ番号:Ａ絶頂
満足係数 += NOWEX:キャラ番号:Ｂ絶頂
満足係数 += NOWEX:キャラ番号:Ｓ絶頂

;係数１０倍
満足係数 = 満足係数 * 10
;素質影響
SIF TALENT:キャラ番号:精神的余裕 > 0
	満足係数 += 2
SIF TALENT:キャラ番号:精神的余裕 < 0
	満足係数 -= 2
SIF TALENT:キャラ番号:自制心 < 0
	満足係数 -= 1
SIF TALENT:キャラ番号:性欲 > 0
	満足係数 -= 2
SIF TALENT:キャラ番号:性欲 < 0
	満足係数 += 5
SIF TALENT:キャラ番号:羞恥心 > 0
	満足係数 += 3
SIF TALENT:キャラ番号:快感応答 > 0
	満足係数 += 1
SIF TALENT:キャラ番号:快感応答 < 0
	満足係数 -= 1


;体力消費量に掛ける
満足上昇量 = DOWNBASE:キャラ番号:体力 * 満足係数 / 10

BASE:キャラ番号:満足 = MIN(MAXBASE:キャラ番号:満足, BASE:キャラ番号:満足 + 満足上昇量)


@精力ゲージ_初期化(キャラ番号)
#DIM キャラ番号

MAXBASE:キャラ番号:精力 = 0
BASE:キャラ番号:精力 = 0
CALL 精力ゲージ_最大値変動処理(キャラ番号)


@精力ゲージ_最大値変動処理(キャラ番号)
#DIM キャラ番号
#DIM 変動前数値

変動前数値 = MAXBASE:キャラ番号:精力
IF キャラ番号 == MASTER
	MAXBASE:キャラ番号:精力 = 9999
ELSEIF TALENT:キャラ番号:絶倫
	;絶倫は3倍
	MAXBASE:キャラ番号:精力 = 900
ELSE
	MAXBASE:キャラ番号:精力 = 300
ENDIF

IF 変動前数値 >= MAXBASE:キャラ番号:精力
	BASE:キャラ番号:精力 = LIMIT(BASE:キャラ番号:精力, 0, MAXBASE:キャラ番号:精力)
ELSE
	BASE:キャラ番号:精力 = LIMIT(BASE:キャラ番号:精力 + MAXBASE:キャラ番号:精力 - 変動前数値, 0, MAXBASE:キャラ番号:精力)
ENDIF


@精力ゲージ_一日終了時回復処理(キャラ番号)
#DIM キャラ番号
#DIM 精力回復量

CALL 精力ゲージ_最大値変動処理(キャラ番号)
IF キャラ番号 == MASTER || CFLAG:キャラ番号:発情期フラグ < 0
	;あなたと発情期は全回復
	BASE:キャラ番号:精力 = MAXBASE:キャラ番号:精力
ELSE
	;それ以外は1/3回復（3日で全回復）
	精力回復量 = MAXBASE:キャラ番号:精力 * (10 + TALENT:キャラ番号:回復速度) / 30
	BASE:キャラ番号:精力 = MIN(BASE:キャラ番号:精力 + 精力回復量, MAXBASE:キャラ番号:精力)
ENDIF


@精力ゲージ_日中回復処理()
#DIM 経過時間
#DIM 精力回復量

SIF BASE:MASTER:精力 == MAXBASE:MASTER:精力
	RETURN

;経過時間(分)
経過時間 = TIME_PROGRESS(TFLAG:行動前時刻)

;1日(1440分)で全回復する、精力が低いときほど大きく回復する
精力回復量 = (10000 - BASE:MASTER:精力) * 経過時間 * 11513 / 1800000

BASE:MASTER:精力 = MIN(BASE:MASTER:精力 + 精力回復量, MAXBASE:MASTER:精力)


@最小射精量(キャラ番号, 前立腺フラグ = 0)
#FUNCTION
#DIM キャラ番号
#DIM 前立腺フラグ

SIF !前立腺フラグ
	RETURNF 0
RETURNF (5 + EXP:キャラ番号:前立腺開発 / 100) * (5 - TALENT:キャラ番号:男性器サイズ) / 5


@最大射精量(キャラ番号)
#FUNCTION
#DIM キャラ番号

RETURNF 30 * 5 / (5 - TALENT:キャラ番号:男性器サイズ)


@射精処理_コマンド(射精キャラ, 絶頂強度, 前立腺フラグ = 0)
#DIM 射精キャラ
#DIM 絶頂強度
#DIM 前立腺フラグ
#DIM 消費量
#DIM 総消費量

NOWEX:射精キャラ:射精 = 0
RCVAR:射精キャラ:射精量 = 0
RCVAR:射精キャラ:ドライオーガズムフラグ = 0

SIF 絶頂強度 <= 0
	RETURN
SIF GETBIT(TALENT:射精キャラ:性別, 1) == 0 || TALENT:射精キャラ:Ｃ感度 == -2
	RETURN

総消費量 = 0
DO
	SIF BASE:射精キャラ:精力 <= 0
		BREAK

	消費量 = MAX(BASE:射精キャラ:精力 * 3 / 4, 最小射精量(射精キャラ, 前立腺フラグ))
	消費量 = MIN(消費量, 最大射精量(射精キャラ), BASE:射精キャラ:精力)

	NOWEX:射精キャラ:射精 += 1
	BASE:射精キャラ:精力 -= 消費量
	総消費量 += 消費量
LOOP NOWEX:射精キャラ:射精 < 絶頂強度

IF 総消費量 > 0
	RCVAR:射精キャラ:射精量 = 総消費量
ELSEIF 前立腺フラグ
	RCVAR:射精キャラ:ドライオーガズムフラグ = 絶頂強度
ELSE
RETURN


@コマンド外精力消費処理(射精キャラ, 絶頂強度, 前立腺フラグ = 0)
#DIM 射精キャラ
#DIM 絶頂強度
#DIM 前立腺フラグ
#DIM 消費量
#DIM 射精回数
#DIM 総消費量

RESULT:1 = 0

SIF 絶頂強度 <= 0
	RETURN 0
SIF GETBIT(TALENT:射精キャラ:性別, 1) == 0 || TALENT:射精キャラ:Ｃ感度 == -2
	RETURN 0

総消費量 = 0
射精回数 = 0
DO
	SIF BASE:射精キャラ:精力 <= 0
		BREAK

	消費量 = MAX(BASE:射精キャラ:精力 * 3 / 4, 最小射精量(射精キャラ, 前立腺フラグ))
	消費量 = MIN(消費量, 最大射精量(射精キャラ), BASE:射精キャラ:精力)

	射精回数 += 1
	BASE:射精キャラ:精力 -= 消費量
	総消費量 += 消費量
LOOP 射精回数 < 絶頂強度

RESULT:1 = 総消費量
RETURN 射精回数


@精力ゲージ_勃起上昇補正(キャラ番号, 補正前数値)
#FUNCTION
#DIM キャラ番号
#DIM 補正前数値

IF BASE:キャラ番号:精力 >= MAXBASE:キャラ番号:精力 / 6
	RETURNF 補正前数値
ELSE
	RETURNF 補正前数値 * 6 * BASE:キャラ番号:精力 / MAXBASE:キャラ番号:精力
ENDIF


@勃起度上昇(キャラ番号, 上昇量)
#DIM キャラ番号
#DIM 上昇量

BASE:キャラ番号:勃起 = MIN(MAXBASE:キャラ番号:勃起, BASE:キャラ番号:勃起 + 精力ゲージ_勃起上昇補正(キャラ番号, 上昇量))
