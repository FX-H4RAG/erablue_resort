;-------------------------------------------------
;探索に出かける
;-------------------------------------------------
@COMNAME601
#FUNCTIONS
TSTR:コマンド名受渡 = 探索に出かける

;===================================================
;コマンドタイプ
;===================================================
@COMTYPE_601
#FUNCTION
TFLAG:コマンドタイプ受渡 = COMTYPE("単独可能_探索")

@COM601
#DIM ループ用
;-------------------------------------------------
;ソース追加処理
;-------------------------------------------------
;現状はお金がちょっと上がって好感度UP用の仮アイテムが手に入る

IF 探索フラグ
	PRINTL 探索は一日一回までです。
	RETURN 0
ENDIF

DRAWLINE
PRINTL 探索形式を選んでください。
DRAWLINE
PRINTBUTTONLC "[0] 簡易探索", 0
PRINTL 
PRINTL 　　　：周囲の安全な地帯を探索し、ルピや素材などを手に入れる（※開発中のため報酬が大幅に増加しています）
PRINTL 
IF BATTLE_STATE:0:0
	PRINTBUTTONLC "[1] ダンジョン探索", 1
ELSE
	SETCOLOR 0x666666
	PRINTPLAIN [1] ダンジョン探索（パーティメンバーが一人以上必要です）
ENDIF
PRINTL 
PRINTL 　　　：解放されたダンジョンを攻略する
RESETCOLOR
PRINTL 
PRINTL
PRINTBUTTONLC "[999] 戻る", 999
$INPUT_LOOP
INPUT

SELECTCASE RESULT
	CASE 0
		PRINTL 外を探索し、魔物を討伐しました。
		PRINTL お金を50000ルピ手に入れました
		MONEY += 50000

		PRINTL 好感度上昇用のアイテムを５個手に入れました
		PRINTL 恋慕上昇用のアイテムを５個手に入れました
		ITEM:好感度上昇用仮アイテム += 5
		ITEM:恋慕上昇用仮アイテム += 5

		EXP:MASTER:探索経験 ++
		FOR ループ用, 0, 4
			IF BATTLE_STATE:ループ用:0
				EXP:(BATTLE_STATE:ループ用:0):探索経験 ++
				CALL SOURCE_CALC_好感度要素_信頼度UP(BATTLE_STATE:ループ用:0, PLAYER, 100)
			ENDIF
		NEXT
		PRINTW あなたとパーティメンバーの探索経験が増加しました
	CASE 1
		IF BATTLE_STATE:0:0 < 1
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		CALL ダンジョンリスト描画
		SIF RESULT == -1
			RETURN -1
	CASE 999
		RETURN -1
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT

TIME += 120
探索フラグ ++
RETURN 1

;-------------------------------------------------
;実行判定
;-------------------------------------------------
@COM_ABLE601
;移動実行判定
SIF !TFLAG:ゲームフェイズ管理
	RETURN 0
;一括管理
SIF GLOBAL_COMABLE(601)
	RETURN RESULT
;玄関
CALLFORM エリア情報登録_{CFLAG:PLAYER:現在マップ種別}("エリア移動可能場所")
SIF CFLAG:PLAYER:現在位置 != RESULT
	RETURN 0
RETURN 1


@ダンジョンリスト描画
#DIMS 表示用リスト_未クリア, 50
#DIMS 表示用リスト_クリア済み, 100
#DIM レベルソート_未クリア, 50
#DIM レベルソート_クリア済み, 100
#DIMS ダンジョン名保存
#DIMS 未クリアリスト表示文
#DIMS クリア済みリスト表示文
#DIM 表示候補数

VARSET LOCAL
VARSET LOCALS
VARSET 表示用リスト_未クリア
VARSET 表示用リスト_クリア済み
VARSET レベルソート_未クリア
VARSET レベルソート_クリア済み

表示候補数 = ENUMFUNCBEGINSWITH("ダンジョン初期処理_")

DRAWLINE
PRINTL 侵入するダンジョンを選んでください。
DRAWLINE


VARSET LOCAL
FOR LOCAL, 0, 表示候補数
	ダンジョン名保存 = %SUBSTRING(RESULTS:LOCAL, 19)%
	IF GETVAR(@"ダンジョン出現フラグ_%ダンジョン名保存%")
		IF GETVAR(@"ダンジョンクリアフラグ_%ダンジョン名保存%") == 1
			表示用リスト_クリア済み:(LOCAL:2) = %ダンジョン名保存%
			CALLFORM ダンジョン解説文_%ダンジョン名保存%
			;降順ソートするためマイナス数値
			レベルソート_クリア済み:(LOCAL:2) = RESULT * -1
			LOCAL:2 += 1
		ELSE
			表示用リスト_未クリア:(LOCAL:1) = %ダンジョン名保存%
			CALLFORM ダンジョン解説文_%ダンジョン名保存%
			;降順ソートするためマイナス数値
			レベルソート_未クリア:(LOCAL:1) = RESULT * -1
			LOCAL:1 += 1
		ENDIF
	ENDIF
NEXT

;推奨レベルでソート
ARRAYMSORT レベルソート_クリア済み, 表示用リスト_クリア済み
ARRAYMSORT レベルソート_未クリア, 表示用リスト_未クリア

VARSET LOCAL
VARSET LOCALS
未クリアリスト表示文 = 
クリア済みリスト表示文 = 
FOR LOCAL, 0, 表示候補数
	IF 表示用リスト_未クリア:LOCAL == ""
		BREAK
	ENDIF
	CALLFORM ダンジョン解説文_%表示用リスト_未クリア:LOCAL%
	未クリアリスト表示文 += @"<button value='{LOCAL}'>[{LOCAL}]%表示用リスト_未クリア:LOCAL, 34, LEFT%</button>推奨Lv：{RESULT, 3}<br>"
NEXT

FOR LOCAL, 0, 表示候補数
	IF 表示用リスト_クリア済み:LOCAL == ""
		BREAK
	ENDIF
	CALLFORM ダンジョン解説文_%表示用リスト_クリア済み:LOCAL%
	LOCALS:2 = <button value='{LOCAL + 50}'>[{LOCAL + 50}]%表示用リスト_クリア済み:LOCAL, 34, LEFT%</button>推奨Lv：{RESULT, 3}<br>
	クリア済みリスト表示文 += @"%LOCALS:2%"
NEXT

LOCALS = <div rect='81,31,2875,2812' border='31' bcolor='#C0C0C0'></div>
LOCALS += "<div rect='3000,31,2875,2812' border='31' bcolor='#C0C0C0'></div>"
LOCALS += "<div width='950' height='125' xpos='337' ypos='0' color='#101010'>未クリアダンジョン</div>"
LOCALS += "<div width='950' height='125' xpos='3256' ypos='0' color='#101010'>クリア済ダンジョン</div>"
LOCALS += @"<div rect='156,156,2875,2812'>%未クリアリスト表示文%</div>"
LOCALS += @"<div rect='3075,156,2875,2812'>%クリア済みリスト表示文%</div>"
HTML_PRINT LOCALS

FOR LOCAL, 0, 25
	PRINTL
NEXT
PRINTBUTTON "[999]戻る", 999
PRINTL
$INPUT_LOOP
INPUT

LOCAL = RESULT
SELECTCASE LOCAL
	CASE IS < 0
		REUSELASTLINE
		GOTO INPUT_LOOP
	CASE IS < 50
		IF 表示用リスト_未クリア:LOCAL == ""
			REUSELASTLINE
			GOTO INPUT_LOOP
		ENDIF
		CALLFORM ダンジョン解説文_%表示用リスト_未クリア:LOCAL%
		PRINTFORML ■%表示用リスト_未クリア:LOCAL%　　推奨Lv：{RESULT,3}
		PRINTL
		HTML_PRINT 詳細文文字列受け渡し変数
		PRINTL
		PRINTL このダンジョンに入りますか？
		PRINTBUTTONLC "[0] はい", 0
		PRINTBUTTONLC "[1] いいえ", 1
		PRINTL
		BINPUT
		IF RESULT == 0
			CALL DUNGEON_MAIN_LOOP(表示用リスト_未クリア:LOCAL)
		ELSE
			RESTART
		ENDIF
	CASE IS < 150
		IF 表示用リスト_クリア済み:(LOCAL - 50) == ""
			REUSELASTLINE
			GOTO INPUT_LOOP
		ENDIF
		CALLFORM ダンジョン解説文_%表示用リスト_クリア済み:(LOCAL - 50)%
		PRINTFORML ■%表示用リスト_クリア済み:(LOCAL - 50)%　　推奨Lv：{RESULT,3}
		PRINTL
		HTML_PRINT 詳細文文字列受け渡し変数
		PRINTL
		PRINTL このダンジョンに入りますか？
		PRINTBUTTONLC "[0] はい", 0
		PRINTBUTTONLC "[1] いいえ", 1
		PRINTL
		BINPUT
		IF RESULT == 0
			CALL DUNGEON_MAIN_LOOP(表示用リスト_クリア済み:(LOCAL - 50))
		ELSE
			RESTART
		ENDIF
	CASE 999
		RETURN -1
	CASEELSE
		REUSELASTLINE
		GOTO INPUT_LOOP
ENDSELECT


