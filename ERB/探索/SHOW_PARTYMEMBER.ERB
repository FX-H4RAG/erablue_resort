
@探索系集約表示
DRAWLINE
LOCALS = <div rect='200,0,5662,1250' border='31' bcolor='#C0C0C0'></div>
LOCALS += "<div rect='262, 62, 5537, 1125'>"
LOCALS += "探索系の情報を選択して見ることが出来ます。<br>"
LOCALS += "装備変更は「■探索メンバー表示」から各キャラの画像or名前をクリックし、詳細画面に移動してください。<br>"
LOCALS += "--------------------------------------------------------------------------------------------------------------<br>"
IF 表示切り替え文字列 == "探索集約_探索メンバー"
	LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>■探索メンバー表示</font>　　"
ELSE
	LOCALS += "<button value='探索集約_探索メンバー'>■探索メンバー表示</button>　　"
ENDIF
IF 表示切り替え文字列 == "探索集約_メンバー編成"
	LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>■バトルメンバー編成画面</font>　　"
ELSE
	LOCALS += "<button value='探索集約_メンバー編成'>■バトルメンバー編成画面</button>　　"
ENDIF
IF 装備乗機ID > 0
	IF 表示切り替え文字列 == "探索集約_サブメンバー編成"
		LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>■サブメンバー編成画面</font>　　"
	ELSE
		LOCALS += "<button value='探索集約_サブメンバー編成'>■サブメンバー編成画面</button>　　"
	ENDIF
ENDIF
LOCALS += "<br>"
IF 表示切り替え文字列 == "探索集約_指輪"
	LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>■指輪一覧表示</font>　　"
ELSE
	LOCALS += "<button value='探索集約_指輪'>■指輪一覧表示</button>　　"
ENDIF
IF 表示切り替え文字列 == "探索集約_耳飾り"
	LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>■耳飾り一覧表示</font>　　"
ELSE
	LOCALS += "<button value='探索集約_耳飾り'>■耳飾り一覧表示</button>　　"
ENDIF
IF 表示切り替え文字列 == "探索集約_衣装"
	LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>■探索衣装一覧表示</font>　　"
ELSE
	LOCALS += "<button value='探索集約_衣装'>■探索衣装一覧表示</button>　　"
ENDIF
LOCALS += "<br>"
IF 表示切り替え文字列 == "探索集約_アイテム"
	LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>■探索アイテム一覧表示</font>　　"
ELSE
	LOCALS += "<button value='探索集約_アイテム'>■探索アイテム一覧表示</button>　　"
ENDIF
IF 表示切り替え文字列 == "探索集約_素材"
	LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>■素材アイテム一覧表示</font>　　"
ELSE
	LOCALS += "<button value='探索集約_素材'>■素材アイテム一覧表示</button>　　"
ENDIF
LOCALS += "<br>"
IF 鍛冶場作成許可フラグ
	IF 表示切り替え文字列 == "探索集約_乗機"
		LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>■乗機一覧表示</font>　　"
	ELSE
		LOCALS += "<button value='探索集約_乗機'>■乗機一覧表示</button>　　"
	ENDIF
	IF 表示切り替え文字列 == "探索集約_搭載兵器"
		LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>■搭載兵器一覧表示</font>　　"
	ELSE
		LOCALS += "<button value='探索集約_搭載兵器'>■搭載兵器一覧表示</button>　　"
	ENDIF
ENDIF
LOCALS += "</div>"

HTML_PRINT LOCALS, 1
SELECTCASE 表示切り替え文字列
	CASE "探索集約_指輪"
		CALL 所持指輪リスト表示_USERCOM
	CASE "探索集約_耳飾り"
		CALL 所持耳飾りリスト表示_USERCOM
	CASE "探索集約_衣装"
		CALL 所持衣装リスト表示_USERCOM
	CASE "探索集約_アイテム"
		CALL ダンジョンアイテム画面表示_USERCOM(1300)
	CASE "探索集約_素材"
		CALL 素材アイテム一覧表示_USERCOM(1300)
	CASE "探索集約_メンバー編成"
		CALL メンバー編成画面(0, 1300)
	CASE "探索集約_サブメンバー編成"
		CALL メンバー編成画面(1, 1300)
	CASE "探索集約_乗機"
		CALL 所持乗機リスト表示_USERCOM(1300)
	CASE "探索集約_搭載兵器"
		CALL 所持兵器リスト表示_USERCOM(1300)
	CASEELSE
		CALL SHOW_PARTYMEMBER(1300)
ENDSELECT
IF RESULT == -1
	表示切り替え文字列 = 
	RETURN 0
ENDIF
RESTART

@SHOW_PARTYMEMBER(y軸位置 = 0)
#DIM y軸位置
#DIM ループ用
#DIM ループ用２
#DIM 縦幅
#DIM 横幅
#DIM 属性一時保存
#DIM 横始点
#DIM 表示位置調整用
CALL 画像描画終了

縦幅 = 2250
横幅 = 1125

LOCALS = 
FOR ループ用, 0, 4
	IF BATTLE_STATE:ループ用:0 > 0
		IF 装備ステータス補正保存:(BATTLE_STATE:ループ用:0):属性
			;0を火属性にしちゃったので数値そのままだと属性変更してるかどうか判別がつかないため、装備による属性変更は属性番号+10で保存している
			属性一時保存 = 装備ステータス補正保存:(BATTLE_STATE:ループ用:0):属性 - 10
		ELSE
			属性一時保存 = 基礎BATTLE_STATE:(BATTLE_STATE:ループ用:0):属性
		ENDIF
		LOCALS += @"<div rect='{550 + ループ用 * 1250}, {y軸位置}, {横幅 + 100}, {縦幅 + 100}' border='31' bcolor='#%属性数値文字色変換_HTML(属性一時保存)%'></div>"
	ELSE
		LOCALS += @"<div rect='{550 + ループ用 * 1250}, {y軸位置}, {横幅 + 100}, {縦幅 + 100}' border='31' bcolor='#C0C0C0'></div>"
	ENDIF
	LOCALS:1 = {600 + ループ用 * 1250}, {y軸位置 + 50}, {横幅}, {縦幅}
	IF BATTLE_STATE:ループ用:0 > 0
		LOCALS:2 '= 短冊グラ表示用文字列関数((BATTLE_STATE:ループ用:0), ループ用)
		LOCALS += @"<div rect='%LOCALS:1%'><button value='{ループ用}' title='%RESULTS:(2 + ループ用)%'>%LOCALS:2%</button></div>"
		LOCALS += @"<div rect='{550 + ループ用 * 1250}, {縦幅 + y軸位置 + 120}, {横幅 + 100}, 200' border='31' bcolor='#%属性数値文字色変換_HTML(属性一時保存)%'></div>"
		RESULTS:(2 + ループ用) '= ""
	ELSE
		LOCALS += @"<div rect='%LOCALS:1%'><button value='{ループ用}'><shape type='rect' param='%LOCALS:1%' color='#%TOSTR(GETBGCOLOR(), "X")%'></button></div>"
		LOCALS += @"<div rect='{550 + ループ用 * 1250}, {縦幅 + y軸位置 + 120}, {横幅 + 100}, 200' border='31' bcolor='#C0C0C0'></div>"
	ENDIF
	LOCALS:1 = {600 + ループ用 * 1250}, {縦幅 + y軸位置 + 170}, {横幅}, 200
	IF BATTLE_STATE:ループ用:0
		LOCALS += @"<div rect='%LOCALS:1%'><button value='{ループ用}'>■%CALLNAME:(BATTLE_STATE:ループ用:0)%</button></div>"
	ELSE
		LOCALS += @"<div rect='%LOCALS:1%'><button value='{ループ用}'>ENPTY</button></div>"
	ENDIF
NEXT

;乗機枠サブメンバー
LOCALS += @"<div rect='194, {縦幅 + y軸位置 + 370}, 5662, 900' border='31' bcolor='#C0C0C0'></div>"
LOCALS += @"<div rect='280, {縦幅 + y軸位置 + 450}, 5550, 900'>"
IF 鍛冶場作成許可フラグ == 0
	LOCALS += "■乗機：システム未開放"
ELSEIF 装備乗機ID == 0
	LOCALS += "<button value='探索集約_乗機'>■乗機：未設定</button>"
ELSE
	LOCALS += @"<button value='探索集約_乗機'>■乗機：%DT_CELL_GETS("所持乗機データベース", 装備乗機ID, "乗機名", 1)%</button>　　"
	LOCALS += @"搭載可能人数：{DT_CELL_GET("所持乗機データベース", 装備乗機ID, "サブ搭乗数_強化後", 1), 2}人<br>"
	IF DT_CELL_GET("所持乗機データベース", 装備乗機ID, "サブ搭乗数_強化後", 1) > 0
		FOR LOCAL, 0, DT_CELL_GET("所持乗機データベース", 装備乗機ID, "サブ搭乗数_強化後", 1)
			LOCALS += @"サブパイロット{LOCAL + 1}："
			IF BATTLE_STATE:(LOCAL + 10):0 < 1
				LOCALS += "未設定<br>"
			ELSE
				LOCALS += @"%NAME:(BATTLE_STATE:(LOCAL + 10):0)%<br>"
			ENDIF
		NEXT
	ENDIF
ENDIF
LOCALS += "</div>"


LOCALS += @"<div rect='262,{y軸位置 + 3575},5537,3625'>"
LOCALS += "　　　　　　　　　　"
LOCALS += "　　　　　　　　　　"
LOCALS += "　　　　　　　　　　　　　　　　　　　　　　　"
LOCALS += "　　<button value='999'>[999]戻る</button></div>"
HTML_PRINT LOCALS, 1

FOR ループ用, 0, 32 + (y軸位置 / 100)
	PRINTL
NEXT

BINPUTS
IF ISNUMERIC(RESULTS) == 0
	VARSET 表示用ステータス格納配列:0:0
	表示切り替え文字列 = %RESULTS%
	RETURN 0
ENDIF
LOCAL = TOINT(RESULTS)
SELECTCASE LOCAL
	CASE 0 TO 3
		IF BATTLE_STATE:LOCAL:0 > 0
			CALL 探索個別ステータス表示(LOCAL)
		ELSE
			表示切り替え文字列 = メンバー編成
		ENDIF
		RETURN 0
	CASE 999
		RETURN -1
ENDSELECT


@探索個別ステータス表示(ARG)
;ARG=探索メンバー番号(0~3)

CALL 戦闘能力表示画面(BATTLE_STATE:ARG:0)
DRAWLINE
IF ARG > 0
	PRINTBUTTONLC "[100]前のキャラへ", 100
ELSE
	PRINTLC  
ENDIF
IF BATTLE_STATE:(ARG + 1):0 > 0
	PRINTBUTTONLC "[101]次のキャラへ", 101
ELSE
	PRINTLC  
ENDIF
PRINTL
PRINTL
PRINTBUTTONLC "[110]指輪装備変更", 110
PRINTBUTTONLC "[111]耳飾り装備変更", 111
PRINTBUTTONLC "[112]探索衣装装備変更", 112
PRINTL
PRINTBUTTONLC "[113]経験値増加アイテムを使う", 113
PRINTL
PRINTBUTTONLC "[114]カットイン個別設定変更", 114
PRINTL
PRINTL
PRINTBUTTONLC "[998]PTから外す", 998
PRINTBUTTONLC "[999]戻る", 999
$INPUT_LOOP
INPUT

SELECTCASE RESULT
	CASE 100
		IF ARG == 0
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		ARG -= 1
		RESTART
	CASE 101
		IF BATTLE_STATE:(ARG + 1):0 < 1
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		ARG += 1
		RESTART
	CASE 110
		CALL 指輪付け替え処理画面(BATTLE_STATE:ARG:0)
		CALL 戦闘初期処理_味方(0)
		RESTART
	CASE 111
		CALL 耳飾り付け替え処理画面(BATTLE_STATE:ARG:0)
		CALL 戦闘初期処理_味方(0)
		RESTART
	CASE 112
		CALL 衣装付け替え処理画面(BATTLE_STATE:ARG:0)
		CALL 戦闘初期処理_味方(0)
		RESTART
	CASE 113
		CALL 経験値増加アイテム処理画面(BATTLE_STATE:ARG:0)
		CALL 戦闘初期処理_味方(0)
		RESTART
	CASE 114
		CALL 奥義カットイン個別設定画面(BATTLE_STATE:ARG:0)
		RESTART
	CASE 998
		PRINTFORML %CALLNAME:(BATTLE_STATE:ARG:0)%を探索メンバーから外しますか？
		PRINTBUTTONLC "[0]はい", 0
		PRINTBUTTONLC "[1]いいえ", 1

		$INPUT_LOOP2
		INPUT

		SELECTCASE RESULT
			CASE 0
				LOCAL = BATTLE_STATE:ARG:0
				BATTLE_STATE:ARG:0 = BATTLE_STATE:(ARG + 1):0
				BATTLE_STATE:(ARG + 1):0 = BATTLE_STATE:(ARG + 2):0
				BATTLE_STATE:(ARG + 2):0 = BATTLE_STATE:(ARG + 3):0
				BATTLE_STATE:(ARG + 3):0 = BATTLE_STATE:(ARG + 4):0
				PRINTFORMW %CALLNAME:LOCAL%を探索メンバーから外しました
				RETURN 0
			CASE 1
				RETURN 0
			CASEELSE
				REUSELASTLINE
				GOTO INPUT_LOOP2
		ENDSELECT
	CASE 999
		RETURN -1
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT


@最大レベル算出保存
#DIM 配列番号
;全キャラの中で最大レベルがいくつかを算出して保存する

最大レベル保存 = 0
FOR 配列番号, 1, CHARANUM
	IF 基礎BATTLE_STATE:配列番号:Lv > 最大レベル保存
		最大レベル保存 = 基礎BATTLE_STATE:配列番号:Lv
	ENDIF
NEXT

@メンバー編成画面(メインサブ = 0, y軸位置 = 0)
#DIM ページ番号
#DIMS ソート法則
#DIM y軸位置
#DIM メインサブ
#DIM 番号管理
#DIM 入れ替え候補_メンバー = -1
#DIM 入れ替え候補_リスト = -1
#DIM ソート基準配列, 1000
VARSET ソート基準配列
VARSET 表示用ステータス格納配列:0:0

;現在パーティー表示
LOCALS = 
CALL 戦闘初期処理_味方(1)
CALL 探索用顔グラフィック生成
CALL DUNGEON_キャラ描画
LOCALS += @"<div rect='3375,{y軸位置},2500,5000'>"
FOR LOCAL, メインサブ * 10, メインサブ * 10 + 4
	IF メインサブ && LOCAL - 9 > DT_CELL_GET("所持乗機データベース", 装備乗機ID, "サブ搭乗数_強化後", 1)
		BREAK
	ENDIF
	IF 入れ替え候補_メンバー == LOCAL
		LOCALS += @"<button value='{LOCAL}' ><img src='キャラ_{LOCAL}_ボタン' height='750' width='2462' ypos='{(LOCAL - メインサブ * 10) * 681}'></button>"
	ELSE
		LOCALS += @"<button value='{LOCAL}' ><img src='キャラ_{LOCAL}' srcb='キャラ_{LOCAL}_ボタン' height='750' width='2462' ypos='{(LOCAL - メインサブ * 10) * 681}'></button>"
	ENDIF
NEXT
LOCALS += "</div>"

HTML_PRINT LOCALS, 1

;入れ替え候補者表示
LOCALS += @"<div rect='200,{y軸位置 - 20},3150,3750' border='31' bcolor='#C0C0C0'></div>"
LOCALS += @"<div rect='250,{y軸位置 + 30},3050,3650'>"
LOCALS += @"名前　　　　　　　　　　　　　　　　　　　　<button value='50'>属性</button>　<button value='51'>Ｌｖ</button>　<button value='52'>固有</button><br>"
LOCALS += @"-------------------------------------------------------------<br>"
;候補者のリスト化
LOCAL:1 = 0
FOR LOCAL, 1, CHARANUM
	SIF TALENT:LOCAL:非戦闘キャラ
		CONTINUE
	SIF CFLAG:LOCAL:探索参加許可フラグ == 0 && CFLAG:LOCAL:長期雇用 == 0 && TALENT:LOCAL:従業員 == 0
		CONTINUE
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	IF メインサブ == 0
		SIF GROUPMATCH(LOCAL, BATTLE_STATE:0:0, BATTLE_STATE:1:0, BATTLE_STATE:2:0, BATTLE_STATE:3:0)
			CONTINUE
	ELSE
		SIF GROUPMATCH(LOCAL, BATTLE_STATE:10:0, BATTLE_STATE:11:0, BATTLE_STATE:12:0, BATTLE_STATE:13:0)
			CONTINUE
	ENDIF
	表示用ステータス格納配列:(LOCAL:1):0 = LOCAL
	表示用ステータス格納配列:(LOCAL:1):1 = 基礎BATTLE_STATE:LOCAL:Lv
	IF ENUMFUNCBEGINSWITH(@"固有アビ_{NO:LOCAL}_") || EXISTFUNCTION(@"固有奥義_{NO:LOCAL}")
		表示用ステータス格納配列:(LOCAL:1):2 = 1
	ELSE
		表示用ステータス格納配列:(LOCAL:1):2 = 0
	ENDIF
	IF 装備ステータス補正保存:LOCAL:属性
		;0を火属性にしちゃったので数値そのままだと属性変更してるかどうか判別がつかないため、装備による属性変更は属性番号+10で保存している
		表示用ステータス格納配列:(LOCAL:1):9 = 装備ステータス補正保存:LOCAL:属性 - 10
	ELSE
		表示用ステータス格納配列:(LOCAL:1):9 = 基礎BATTLE_STATE:LOCAL:属性
	ENDIF
	;ソート用情報保存
	SELECTCASE ソート法則
		CASE "固有"
			ソート基準配列:(LOCAL:1) = (表示用ステータス格納配列:(LOCAL:1):2 + 1) * -1
		CASE "属性"
			ソート基準配列:(LOCAL:1) = 表示用ステータス格納配列:(LOCAL:1):9 + 1
		CASEELSE
			ソート基準配列:(LOCAL:1) = 表示用ステータス格納配列:(LOCAL:1):1 * -1
	ENDSELECT
	LOCAL:1 += 1
NEXT
ARRAYMSORT ソート基準配列, 表示用ステータス格納配列

FOR LOCAL, ページ番号 * 28, ページ番号 * 28 + 28
	SIF 表示用ステータス格納配列:LOCAL:0 < 1
		BREAK
	IF メインサブ == 0
		IF GROUPMATCH(表示用ステータス格納配列:LOCAL:0, BATTLE_STATE:10:0, BATTLE_STATE:11:0, BATTLE_STATE:12:0, BATTLE_STATE:13:0)
			LOCALS:1 = %NAME:(表示用ステータス格納配列:LOCAL:0)%（サブ編成）
		ELSE
			LOCALS:1 = %NAME:(表示用ステータス格納配列:LOCAL:0)%
		ENDIF
	ELSE
		IF GROUPMATCH(表示用ステータス格納配列:LOCAL:0, BATTLE_STATE:0:0, BATTLE_STATE:1:0, BATTLE_STATE:2:0, BATTLE_STATE:3:0)
			LOCALS:1 = %NAME:(表示用ステータス格納配列:LOCAL:0)%（メイン編成）
		ELSE
			LOCALS:1 = %NAME:(表示用ステータス格納配列:LOCAL:0)%
		ENDIF
	ENDIF
	LOCALS += @"<button value='{LOCAL + 100}'>"
	IF 入れ替え候補_リスト == 表示用ステータス格納配列:LOCAL:0
		SIF STRLENS(LOCALS:1) > 42
			LOCALS:1 = %SUBSTRING(LOCALS:1, 0, 42)%
		LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>　%LOCALS:1, 42, LEFT%</font>"
	ELSE
		SIF STRLENS(LOCALS:1) > 44
			LOCALS:1 = %SUBSTRING(LOCALS:1, 0, 44)%
		LOCALS += @"%LOCALS:1, 44, LEFT%"
	ENDIF
	LOCALS += @"<font color='#%属性数値文字色変換_HTML(表示用ステータス格納配列:LOCAL:9)%'>"
	LOCALS += @"%属性数値文字列変換(表示用ステータス格納配列:LOCAL:9), 4%</font>"
	LOCALS += @"{表示用ステータス格納配列:LOCAL:1, 6}"
	IF 表示用ステータス格納配列:LOCAL:2
		LOCALS += "　あり</button><br>"
	ELSE
		LOCALS += @"　<font color='#%カラーパレット_HTML("グレーアウト")%'>なし</font></button><br>"
	ENDIF
NEXT

LOCALS += "</div>"

IF 入れ替え候補_メンバー > -1 && BATTLE_STATE:入れ替え候補_メンバー:0 > 0
	LOCALS += @"<div rect='4700,{y軸位置 + 3262},2000,300'>"
	LOCALS += "<button value='9010'>[9010] パーティから外す</button>"
	LOCALS += "</div>"
ENDIF

LOCALS += @"<div rect='262,{y軸位置 + 3562},5537,300'>"
IF ページ番号 > 0
	LOCALS += "<button value='9000'>[9000]前のページ</button>　"
ELSE
	LOCALS += "　　　　　　　　　"
ENDIF
LOCALS += @"- PAGE {ページ番号 + 1, 2, LEFT} -　　"
IF 表示用ステータス格納配列:(29 + (ページ番号 * 28)):0 > 0
	LOCALS += "<button value='9001'>[9001]次のページ</button>　"
ELSE
	LOCALS += "　　　　　　　　　"
ENDIF
LOCALS += "<button value='9999'>[9999]戻る</button></div>"
HTML_PRINT LOCALS, 1

FOR LOCAL, 0, 32 + (y軸位置 / 100)
	PRINTL
NEXT

BINPUTS
IF ISNUMERIC(RESULTS) == 0
	VARSET 表示用ステータス格納配列:0:0
	表示切り替え文字列 = %RESULTS%
	入れ替え候補_メンバー = -1
	入れ替え候補_リスト = -1
	ページ番号 = 0
	RETURN 0
ENDIF
LOCAL = TOINT(RESULTS)
SELECTCASE LOCAL
	CASE 9000
		ページ番号 -= 1
	CASE 9001
		ページ番号 += 1
	CASE 9010
		;隊列を詰める
		FOR 番号管理, 入れ替え候補_メンバー, メインサブ * 10 + 4
			BATTLE_STATE:番号管理:0 = BATTLE_STATE:(番号管理 + 1):0
		NEXT
		入れ替え候補_メンバー = -1
		入れ替え候補_リスト = -1
	CASE 0 TO 13
		IF 入れ替え候補_リスト > 0
			BATTLE_STATE:LOCAL:0 = 入れ替え候補_リスト
			;メイン/サブの方をチェック
			FOR 番号管理, !メインサブ * 10, !メインサブ * 10 + 4
				IF BATTLE_STATE:番号管理:0 == 入れ替え候補_リスト
					FOR LOCAL, 番号管理, !メインサブ * 10 + 4
						BATTLE_STATE:LOCAL:0 = BATTLE_STATE:(LOCAL + 1):0
					NEXT
					BREAK
				ENDIF
			NEXT
			入れ替え候補_メンバー = -1
			入れ替え候補_リスト = -1
			ページ番号 = 0
		ELSE
			IF 入れ替え候補_メンバー == LOCAL
				入れ替え候補_メンバー = -1
			ELSE
				入れ替え候補_メンバー = LOCAL
			ENDIF
		ENDIF
	CASE 50
		ソート法則 = 属性
		入れ替え候補_リスト = -1
	CASE 51
		ソート法則 = 
		入れ替え候補_リスト = -1
	CASE 52
		ソート法則 = 固有
		入れ替え候補_リスト = -1
	CASE 9999
		入れ替え候補_メンバー = -1
		入れ替え候補_リスト = -1
		ページ番号 = 0
		RETURN -1
	CASE IS >= 100
		IF 入れ替え候補_メンバー > -1
			BATTLE_STATE:入れ替え候補_メンバー:0 = 表示用ステータス格納配列:(LOCAL - 100):0
			;メイン/サブの方をチェック
			FOR 番号管理, !メインサブ * 10, !メインサブ * 10 + 4
				IF BATTLE_STATE:番号管理:0 == BATTLE_STATE:入れ替え候補_メンバー:0
					FOR LOCAL, 番号管理, !メインサブ * 10 + 4
						BATTLE_STATE:LOCAL:0 = BATTLE_STATE:(LOCAL + 1):0
					NEXT
					BREAK
				ENDIF
			NEXT
			入れ替え候補_メンバー = -1
			入れ替え候補_リスト = -1
			ページ番号 = 0
		ELSE
			IF 入れ替え候補_リスト == 表示用ステータス格納配列:(LOCAL - 100):0
				入れ替え候補_リスト = -1
			ELSE
				入れ替え候補_リスト = 表示用ステータス格納配列:(LOCAL - 100):0
			ENDIF
		ENDIF
ENDSELECT

