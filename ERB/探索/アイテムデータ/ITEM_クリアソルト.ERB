
@アイテム個別文章表示_クリアソルト(ARGS)
VARSET LOCALS
CALL 口上変数初期化

IF ARGS == "値段"
	詳細文文字列受け渡し変数 = ルピ
	RETURN 2500
ENDIF


LOCALS += @"■クリアソルト　　　所持数:{DT_CELL_GET("所持アイテムデータベース", 0, "クリアソルト")}<br>"
;LOCALS +="－－－－－－－－－－－－－－－－－－－－－－－－－－－－<br>" 改行目安
LOCALS += "浄化の魔力を含んだ塩の結晶。<br>"
LOCALS += "起動すると付近の魔力に応じた浄化の光を発生させる。<br>"
LOCALS += "効果範囲は狭いが、使い手次第で威力を高められるため有用な<br>"
LOCALS += "攻撃道具である。<br>"
LOCALS += "<br>"
LOCALS:1 += "【効果】<br>"
LOCALS:1 += "敵単体へ[ＰＴの最大レベル✕５０]の光属性ダメージ<br>"

詳細文文字列受け渡し変数 = %LOCALS%%LOCALS:1%
KSTR:(K++) = %LOCALS:1%

@アイテム使用先表示_クリアソルト
CALL 汎用使用先表示("敵単体")

@アイテム効果処理_クリアソルト(ARG)
#DIM レベル記録
;ARG = エネミーが何番目か、10からカウント
CALL 口上変数初期化

レベル記録 = 0
FOR LOCAL, 0, 14
	SIF LOCAL > 3 && LOCAL < 10
		CONTINUE
	SIF BATTLE_STATE:LOCAL:0 < 1
		CONTINUE
	SIF BATTLE_STATE:LOCAL:Lv > レベル記録
		レベル記録 = BATTLE_STATE:LOCAL:Lv
NEXT

アビ汎用変数:効果量 = レベル記録 * 50
CALL アビテンプレート_ダメージ処理_光属性

SIF アビテンプレート用_キャンセルフラグ
	RETURN -1

IF バフ番号取得_枠("闇のオーラ", アビテンプレート用_対象保存:0) > -1
	CALL アビ汎用変数文字列リセット
	アビ汎用変数:消去不可バフデバフ削除オプション = 1
	CALL アビテンプレート_不利効果_バフ消去("闇のオーラ")
ENDIF

RETURN 1
