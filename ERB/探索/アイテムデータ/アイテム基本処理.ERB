@DUNGEON_ITEM描画
#DIM レイヤー番号
レイヤー番号 = 1

GDISPOSE レイヤー番号
SPRITEDISPOSE ("ダンジョン_1")
GCREATE レイヤー番号, 960, 500
GSETFONT レイヤー番号, GETFONT(), 18
GSETPEN レイヤー番号, 0x00FFFFFF, -1

;周囲の枠
GSETBRUSH レイヤー番号, 0xFFC0C0C0
GFILLRECTANGLE レイヤー番号, 32 , 30, 480, 5
GFILLRECTANGLE レイヤー番号, 32 , 30, 5, 480
GFILLRECTANGLE レイヤー番号, 32 , 495, 480, 5
GFILLRECTANGLE レイヤー番号, 507 , 30, 5, 480

;基礎情報
LOCALS = %月算出()%
GDRAWTEXT レイヤー番号, @"{RESULT}年目 %LOCALS% {DAY % 30 + 1}日(%GET_DAY()%)：■%ダンジョン名%", 32, 7
CALL リソース登録(@"ダンジョン_1", レイヤー番号)


@ダンジョンアイテム画面表示
IF 選択用変数 > 0
	詳細文文字列受け渡し変数 = 
	TRYCALLFORM アイテム個別文章表示_%表示アイテム名:選択用変数%
	TRYCALLFORM アイテム使用先表示_%表示アイテム名:選択用変数%
	IF ダンジョン表示モード == "戦闘画面"
		CALL メッセージ欄表示用関数(,,,0)
		RETURN -1
	ENDIF
	LOCALS = %詳細文文字列受け渡し変数%
ELSE
	VARSET 表示アイテム名
	LOCALS = 
	DT_COLUMN_NAMES "所持アイテムデータベース", 表示アイテム名
	FOR LOCAL, 1, 25
		IF 表示アイテム名:LOCAL != ""
			LOCALS += @"<button value = '{LOCAL}'>[{LOCAL, 2}]%表示アイテム名:LOCAL, 40, LEFT%×%TOFULL(TOSTR(DT_CELL_GET("所持アイテムデータベース", 0, 表示アイテム名:LOCAL))), 4%</button><br>"
		ELSE
			LOCALS += "<br>"
		ENDIF
	NEXT
ENDIF

HTML_PRINT @"<div rect = '250, 206, 2937, 2937'>%LOCALS%</div>", 1
HTML_PRINT "<div rect = '250, 2800, 2937, 187'>　　　　　　　　　　　　　　　　　　　　　　　<button value = '999'>[999]戻る</button></div>", 1


@DUNGEON_操作処理_アイテム画面
$INPUT_LOOP
INPUT

LOCAL = RESULT
IF 選択用変数 > -1
	IF LOCAL == 999
		キャラ画像ボタン化フラグ = 0
		CLEARLINE 1
		選択用変数 = -1
		RETURN -1
	ENDIF
	TRYCCALLFORM アイテム効果処理_%表示アイテム名:選択用変数%(LOCAL)
		IF RESULT == 0
			RETURN -1
		ENDIF
	CATCH
		REUSELASTLINE 
		GOTO INPUT_LOOP
	ENDCATCH
ELSE
	SELECTCASE LOCAL
		CASE 1 TO 24
			IF DT_CELL_GET("所持アイテムデータベース", 0, 表示アイテム名:LOCAL)
				選択用変数 = LOCAL
			ELSE
				REUSELASTLINE 
				GOTO INPUT_LOOP
			ENDIF
		CASE 999
			ダンジョン表示モード = マップ画面
			キャラ画像ボタン化フラグ = 1
			選択用変数 = -1
		CASEELSE
			REUSELASTLINE 
			GOTO INPUT_LOOP
	ENDSELECT
ENDIF
RETURN -1



@アイテム増減汎用処理(アイテム名, 個数)
#DIMS アイテム名
#DIM 個数
;何らかの理由で失敗時はRETURN 0
;ちゃんと増減した場合はRETURN 1

;まず増減するアイテム名が誤字ってないかチェック
ENUMFUNCBEGINSWITH @"アイテム個別文章表示_%アイテム名%"
IF RESULT < 1
	;個別文章がない場合はアイテムがないと判断
	PRINTL 指定したアイテムがみつかりません
	RETURN 0
ENDIF

;同じアイテムを持ってるかどうかチェック
IF DT_COLUMN_EXIST("所持アイテムデータベース", アイテム名)
	;持ってる
	LOCAL:1 = DT_CELL_GET("所持アイテムデータベース", 0, アイテム名) + 個数
	IF LOCAL:1 > 0
		DT_CELL_SET "所持アイテムデータベース", 0, アイテム名, LOCAL:1
	ELSEIF LOCAL:1 == 0
		DT_COLUMN_REMOVE "所持アイテムデータベース", アイテム名
	ELSE
		PRINTFORML %アイテム名%が足りません。
		RETURN 0
	ENDIF
ELSE
	;未所持
	IF 個数 > 0
		DT_COLUMN_ADD "所持アイテムデータベース", アイテム名, 1
		DT_CELL_SET "所持アイテムデータベース", 0, アイテム名, 個数
	ELSE
		PRINTFORML %アイテム名%を所持していません。
		RETURN 0
	ENDIF
ENDIF

RETURN 1


@素材アイテム一覧表示(表示条件 = "所持素材数 > 0", ボタン化フラグ = 0, 選択解除フラグ = 0)
#DIMS 表示条件
#DIM ページ番号
#DIM ボタン化フラグ
#DIM 選択解除フラグ
#DIM ソート後素材順, 1000
#DIM 現在表示ランク
VARSET ソート後素材順, -1

;データベースをランク順にソート
DT_SELECT "所持素材データベース", 表示条件, "素材ランク ASC, 素材属性 ASC", ソート後素材順

DO
VARSET LOCAL
VARSET LOCALS
LOCALS = <div rect='200,0,5537,3625' border='31' bcolor='#C0C0C0'></div>
LOCALS += "<div rect='262,75,5537,3625'><nobr>"

現在表示ランク = -1
FOR COUNT, ページ番号 * 25, ページ番号 * 25 + 41
	IF ソート後素材順:COUNT < 0
		BREAK
	ENDIF
	IF 現在表示ランク < DT_CELL_GET("所持素材データベース", ソート後素材順:COUNT, "素材ランク", 1)
		SIF LOCAL
			LOCALS += "<br>"
		IF LOCAL % 2 == 1
			LOCALS += "<br>"
			LOCAL += 1
		ENDIF
		現在表示ランク = DT_CELL_GET("所持素材データベース", ソート後素材順:COUNT, "素材ランク", 1)
		LOCALS += @"---- ランク%TOFULL(TOSTR(現在表示ランク))% ------------------------------------------------------------<br>"
	ENDIF
	LOCALS:1 = %カラーパレット_HTML(@"%属性数値文字列変換(DT_CELL_GET("所持素材データベース", ソート後素材順:COUNT, "素材属性", 1))%属性")%
	IF STRFIND(表示条件, "プレゼントカテゴリ") > -1
		LOCALS:2 = %DT_CELL_GETS("所持素材データベース", ソート後素材順:COUNT, "プレゼントカテゴリ", 1)%
	ELSE
		LOCALS:2 = %DT_CELL_GETS("所持素材データベース", ソート後素材順:COUNT, "素材カテゴリ", 1)%
	ENDIF
	IF ボタン化フラグ
		LOCALS += @"　<font color='#%LOCALS:1%'><button value='{COUNT}'>%LOCALS:2, 14, LEFT%：%DT_CELL_GETS("所持素材データベース", ソート後素材順:COUNT, "素材アイテム名", 1), 26, LEFT%×%TOFULL(TOSTR(DT_CELL_GET("所持素材データベース", ソート後素材順:COUNT, "所持素材数", 1))), 6%</button></font>　　"
	ELSE
		LOCALS += @"　<font color='#%LOCALS:1%'>%LOCALS:2, 14, LEFT%：%DT_CELL_GETS("所持素材データベース", ソート後素材順:COUNT, "素材アイテム名", 1), 26, LEFT%×%TOFULL(TOSTR(DT_CELL_GET("所持素材データベース", ソート後素材順:COUNT, "所持素材数", 1))), 6%</font>　　"
	ENDIF
	LOCAL += 1
	IF LOCAL % 2 == 0
		LOCALS += "<br>"
	ENDIF
NEXT

LOCALS += "</div>"

LOCALS += "<div rect='262,3437,5537,3625'>"
IF ページ番号 > 0
	LOCALS += "<button value='1000'>[1000]前のページ</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF (ページ番号 * 25) + 25 < 200 && 表示アイテム名:((ページ番号 * 25) + 25) != ""
	LOCALS += "<button value='1001'>[1001]次のページ</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF 選択解除フラグ
	LOCALS += "　　　　　　　　　　　　　　　　　　　<button value='998'>[998] 選択を外す</button>"
ELSE
	LOCALS += "　　　　　　　　　　　　　　　　　　　　　　　　　"
ENDIF
LOCALS += "　　<button value='999'>[999]戻る</button></div>"

DRAWLINE
HTML_PRINT LOCALS, 1
FOR COUNT, 0, 33
	PRINTL
NEXT

BINPUT
SELECTCASE RESULT
	CASE 999
		ページ番号 = 0
		選択用変数 = 0
		RETURN -1
	CASE 998
		ページ番号 = 0
		選択用変数 = 0
		RETURN -2
	CASE 1000
		ページ番号 -= 1
	CASE 1001
		ページ番号 += 1
	CASEELSE
		ページ番号 = 0
		選択用変数 = 0
		RETURN ソート後素材順:RESULT
ENDSELECT
LOOP 1


@ダンジョンアイテム画面表示_USERCOM
#DIM ページ番号
DO
LOCALS = 
LOCALS += "<div rect='200,0,5662,3750' border='31' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='262,100,5662,3750'>"
IF 選択用変数 > 0
	詳細文文字列受け渡し変数 = 
	TRYCALLFORM アイテム個別文章表示_%表示アイテム名:選択用変数%
	LOCALS += @"%詳細文文字列受け渡し変数%"
ELSE
	VARSET 表示アイテム名
	DT_COLUMN_NAMES "所持アイテムデータベース", 表示アイテム名
	FOR LOCAL, (ページ番号 * 25) + 1, (ページ番号 * 25) + 25
		IF 表示アイテム名:LOCAL != ""
			LOCALS += @"<button value = '{LOCAL}'>[{LOCAL, 2}]%表示アイテム名:LOCAL, 40, LEFT%×%TOFULL(TOSTR(DT_CELL_GET("所持アイテムデータベース", 0, 表示アイテム名:LOCAL))), 4%</button><br>"
		ELSE
			LOCALS += "<br>"
		ENDIF
	NEXT
ENDIF
LOCALS += "</div>"

LOCALS += "<div rect='262,3562,5537,3625'>"
IF ページ番号 > 0
	LOCALS += "<button value='1000'>[1000]前のページ</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF (ページ番号 * 25) + 25 < 200 && 表示アイテム名:((ページ番号 * 25) + 25) != ""
	LOCALS += "<button value='1001'>[1001]次のページ</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
LOCALS += "　　　　　　　　　　　　　　　　　　　　　　　　　　"
LOCALS += "　　<button value='999'>[999]戻る</button></div>"
HTML_PRINT LOCALS, 1
FOR LOCAL, 0, 34
	PRINTL
NEXT

BINPUT
SELECTCASE RESULT
	CASE 999
		ページ番号 = 0
		選択用変数 = 0
		RETURN 0
	CASE 1000
		ページ番号 -= 1
	CASE 1001
		ページ番号 += 1
	CASEELSE
		選択用変数 = RESULT
ENDSELECT
LOOP 1
