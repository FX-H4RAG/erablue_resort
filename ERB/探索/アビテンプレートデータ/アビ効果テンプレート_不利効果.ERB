;不利効果とは基本的に行動キャラが敵陣営のキャラに掛けるもの
;そのためマウントなどの効果を受ける
;スキルのデメリットによって自分が受ける効果などは弱体耐性貫通オプションをつけること

@アビテンプレート_不利効果_デバフ効果セット
;------------------------------------------------------------------------
;・使用変数
;アビ汎用文字列:バフ・デバフ枠
;アビ汎用文字列:バフ・デバフ対象能力
;アビ汎用変数:効果割合
;アビ汎用変数:効果量
;アビ汎用変数:持続ターン
;アビ汎用変数:持続回数
;アビ汎用変数:基礎成功確率
;------------------------------------------------------------------------
;◯強化、◯耐性、連続攻撃率については初期値が０のため効果割合を入れても効果がないことに注意
;------------------------------------------------------------------------
;返り値-1:対象が不正 0:同じ枠で効果高いバフがすでにある 1:成功
;------------------------------------------------------------------------
#DIM 対象番号
#DIM デバフ番号
#DIM スキップフラグ
#DIM 弱体無効フラグ
#DIMS 名前一時格納
#DIMS 効果量一時格納
#DIM データベース行数

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

FOR 対象番号, 0, 10
	スキップフラグ = 0
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号
	
	IF 戦闘行動対象 < 10
		名前一時格納 = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ番号)%
		;基本的にデバフは死者にはかからない
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
	ELSE
		名前一時格納 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
		;基本的にデバフは死者にはかからない
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	弱体無効フラグ = バフ存在チェック("弱体無効", 戦闘行動対象)
	IF 弱体無効フラグ > -1
		;弱体無効バフがあると弾く
		スキップフラグ = 3
		;回数持続の効果の回数を減少
		CALL バフ回数経過処理(戦闘行動対象, "弱体無効")
		GOTO メッセージ表示部
	ELSEIF アビ汎用変数:基礎成功確率
		;基礎成功率が設定されてる場合は成功率判定
		IF RAND:100 > アビ汎用変数:基礎成功確率
			スキップフラグ = 2
			GOTO メッセージ表示部
		ENDIF
	ENDIF

	;既存のデバフと枠が被ってないかチェック
	データベース行数 = DT_ROW_LENGTH(@"デバフデータベース_{戦闘行動対象}")
	FOR デバフ番号, 0, データベース行数
		IF DT_CELL_GETS(@"デバフデータベース_{戦闘行動対象}", デバフ番号, "デバフ枠") == アビ汎用文字列:バフ・デバフ枠
			;すでに同じ枠のデバフがある場合
			IF DT_CELL_GET(@"デバフデータベース_{戦闘行動対象}", デバフ番号, "デバフ効果量_割合") == 0 && アビ汎用変数:効果割合 == 0
				;大本デバフも追加デバフも割合が０の時、固定値を比較
				IF DT_CELL_GET(@"デバフデータベース_{戦闘行動対象}", デバフ番号, "デバフ効果量_固定値") <= アビ汎用変数:効果量
					;追加する方の割合が高いか同じ場合は消して新しく追加
					DT_ROW_REMOVE @"デバフデータベース_{戦闘行動対象}", DT_CELL_GET(@"デバフデータベース_{戦闘行動対象}", デバフ番号, "id")
				ELSE
					;そうでない場合は終了
					スキップフラグ = 1
				ENDIF
			ELSEIF DT_CELL_GET(@"デバフデータベース_{戦闘行動対象}", デバフ番号, "デバフ効果量_割合") <= アビ汎用変数:効果割合
				;追加する方の割合が高いか同じ場合は消して新しく追加
				DT_ROW_REMOVE @"デバフデータベース_{戦闘行動対象}", DT_CELL_GET(@"デバフデータベース_{戦闘行動対象}", デバフ番号, "id")
			ELSE
				;そうでない場合は終了
				スキップフラグ = 1
			ENDIF
			BREAK
		ENDIF
	NEXT

	$メッセージ表示部
	IF スキップフラグ == 1
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%には効果が無いようだ…
	ELSEIF スキップフラグ == 2
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は%アビ汎用文字列:バフ・デバフ対象能力%効果を受けなかった…
	ELSEIF スキップフラグ == 3
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は弱体無効効果により%アビ汎用文字列:バフ・デバフ対象能力%効果を弾いた！
	ELSE
		IF アビ汎用変数:効果割合
			効果量一時格納 = {アビ汎用変数:効果割合}\%
			IF アビ汎用変数:効果量
				効果量一時格納 += @" + {アビ汎用変数:効果割合}"
			ENDIF
		ELSE
			効果量一時格納 = {アビ汎用変数:効果量}
		ENDIF
		IF GETNUM(BATTLE_STATE, アビ汎用文字列:バフ・デバフ対象能力, 2) > 0
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%アビ汎用文字列:バフ・デバフ対象能力%が%効果量一時格納%下がった！
		ELSEIF アビ汎用文字列:バフ・デバフ対象能力 == "連続攻撃率"
			効果量一時格納 = {アビ汎用変数:効果量}
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%アビ汎用文字列:バフ・デバフ対象能力%が%効果量一時格納%\%下がった！
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は%アビ汎用文字列:バフ・デバフ対象能力%効果を受けた！
		ENDIF
		DT_ROW_ADD @"デバフデータベース_{戦闘行動対象}", "デバフ枠", アビ汎用文字列:バフ・デバフ枠, "デバフ対象能力", アビ汎用文字列:バフ・デバフ対象能力, "デバフ効果量_割合", アビ汎用変数:効果割合, "デバフ効果量_固定値", アビ汎用変数:効果量, "デバフ名", アビテンプレート用_アビ名, "持続ターン", アビ汎用変数:持続ターン, "持続回数", アビ汎用変数:持続回数, "持続行動回数", アビ汎用変数:持続行動回数
	ENDIF
NEXT

@アビテンプレート_不利効果_バフ消去(消去枠 = "")
#DIMS 消去枠
#DIM データベース行数
#DIMS 名前一時格納
#DIM 対象番号
#DIM 消滅ID保存

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

FOR 対象番号, 0, 10
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF 戦闘行動対象 < 10
		名前一時格納 = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ番号)%
		;基本的にデバフは死者にはかからないのでスキップ
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
	ELSE
		名前一時格納 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
		;基本的にデバフは死者にはかからないのでスキップ
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	IF 消去枠 == ""
		;枠指定無しの場合、最新１個を消す
		データベース行数 = DT_ROW_LENGTH(@"バフデータベース_{戦闘行動対象}")
		IF データベース行数
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%DT_CELL_GETS(@"バフデータベース_{戦闘行動対象}", データベース行数 - 1, "バフ対象能力")%バフが消え去った！
			CALL バフ削除(戦闘行動対象, データベース行数 - 1)
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%には効果がなかった…
		ENDIF
	ELSE
		;枠指定がある場合、その枠を消す。ないなら何もしない
		;枠指定は完全一致なので一個しかないはず
		データベース行数 = バフ存在チェック_枠(消去枠)
		IF データベース行数 > -1
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%DT_CELL_GETS(@"バフデータベース_{戦闘行動対象}", データベース行数, "バフ対象能力")%バフが消え去った！
			CALL バフ削除(戦闘行動対象, データベース行数)
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%には効果がなかった…
		ENDIF
	ENDIF
NEXT


@アビテンプレート_不利効果_スロウ
#DIM 対象番号
#DIM スキップフラグ
#DIM 減少チャージ

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

FOR 対象番号, 0, 10
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF 戦闘行動対象 < 10
		;味方はスロウ効果なし
	ELSE
		;基本的にデバフは死者にはかからないのでスキップ
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
		IF アビ汎用変数:基礎成功確率
			;基礎成功率が設定されてる場合は成功率判定
			IF RAND:100 > アビ汎用変数:基礎成功確率
				スキップフラグ = 2
			ENDIF
		ENDIF

		IF 敵BATTLE_STATE:(戦闘行動対象 - 10):チャージターン < 1
			;そもそもチャージ溜まってない
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%には効果が無いようだ…
		ELSEIF スキップフラグ == 2
			;スロウ外した
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%はスロウ効果を受けなかった…
		ELSE
			減少チャージ = MIN(敵BATTLE_STATE:(戦闘行動対象 - 10):チャージターン, アビ汎用変数:効果量)
			敵BATTLE_STATE:(戦闘行動対象 - 10):チャージターン = MAX(敵BATTLE_STATE:(戦闘行動対象 - 10):チャージターン - アビ汎用変数:効果量, 0)
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %戦闘キャラ名前取得(戦闘行動対象)%のチャージが{減少チャージ}下がった！
		ENDIF
	ENDIF
NEXT
