;刻印とはキャラに付与されるバフの一種、５個まで重ねられる
;しかし単体では効果がなく、アビや奥義の使用条件などに関わる
;ここでは名前自由な刻印を簡単にセット・除去出来るようにしている


@アビテンプレート_刻印追加(刻印数 = 1)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用文字列:バフ・デバフ対象能力（火の刻印、のように入力を推奨）
;------------------------------------------------------------------------
;返り値-1:対象が不正 1:成功
;------------------------------------------------------------------------
#DIM 対象番号
#DIM バフ番号
#DIM スキップフラグ
#DIMS 名前一時格納
#DIM データベース行数
#DIM 刻印数
#DIM 現在値

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

FOR 対象番号, 0, アビ対象最大数
	スキップフラグ = 0
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF 戦闘行動対象 < 10
		名前一時格納 = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ番号)%
		;基本的にバフは死者にはかからない
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
	ELSE
		名前一時格納 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
		;基本的にバフは死者にはかからない
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	;枠を対象能力と同じにする
	アビ汎用文字列:バフ・デバフ枠 = %アビ汎用文字列:バフ・デバフ対象能力%

	;既存のバフと枠が被ってないかチェック
	データベース行数 = DT_ROW_LENGTH(@"バフデータベース_{戦闘行動対象}")
	FOR バフ番号, 0, データベース行数
		IF DT_CELL_GETS(@"バフデータベース_{戦闘行動対象}", バフ番号, "バフ枠") == アビ汎用文字列:バフ・デバフ枠
			;すでに同じ枠のバフがある場合、数の分だけ固定値に追加
			;最大値は５なのでそれ未満の時のみ
			現在値 = DT_CELL_GET(@"バフデータベース_{戦闘行動対象}", バフ番号, "バフ効果量_固定値")
			IF 現在値 < 5
				DT_CELL_SET @"バフデータベース_{戦闘行動対象}", バフ番号, "バフ効果量_固定値", MIN(5, 刻印数 + 現在値)
				スキップフラグ = MIN(5 - 現在値, 刻印数)
			ENDIF
			BREAK
		ENDIF
	NEXT

	IF スキップフラグ
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は%アビ汎用文字列:バフ・デバフ対象能力%を{スキップフラグ, 2}得た！
	ELSE
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は%アビ汎用文字列:バフ・デバフ対象能力%を{MIN(5, 刻印数), 2}得た！
		;累積時でないなら新しくデータベースにセット
		{
		DT_ROW_ADD @"バフデータベース_{戦闘行動対象}", "バフ枠", アビ汎用文字列:バフ・デバフ対象能力, "バフ対象能力", アビ汎用文字列:バフ・デバフ対象能力,
			"バフ効果量_固定値", MIN(5, 刻印数), "バフ名", アビ汎用文字列:バフ・デバフ対象能力,
			"持続ターン", -1, "消去不可フラグ", 1
		}
	ENDIF
NEXT


@刻印現在数確認(刻印名, 対象隊列 = -1)
;これはアビテンプレートではなく、現在の刻印数を返す関数
;使用条件のチェックなどに使う
#FUNCTION
#DIMS 刻印名
#DIM 対象隊列
#DIM データベース行数
#DIM バフ番号

;戦闘中以外は常に０を返す（能力表示画面で呼ばれた時の対策）
SIF ダンジョン表示モード != "戦闘画面"
	RETURNF 0

SIF 対象隊列 == -1
	対象隊列 = 戦闘行動キャラ

データベース行数 = DT_ROW_LENGTH(@"バフデータベース_{対象隊列}")
FOR バフ番号, 0, データベース行数
	IF DT_CELL_GETS(@"バフデータベース_{対象隊列}", バフ番号, "バフ枠") == 刻印名
		;バフ名一致で返す
		RETURNF DT_CELL_GET(@"バフデータベース_{対象隊列}", バフ番号, "バフ効果量_固定値")
	ENDIF
NEXT

;ないなら０
RETURNF 0



@アビテンプレート_刻印減少(削除数 = 1)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用文字列:バフ・デバフ対象能力（火の刻印、のように入力を推奨）
;------------------------------------------------------------------------
;返り値-1:対象が不正 1:成功
;------------------------------------------------------------------------
#DIM 対象番号
#DIM バフ番号
#DIMS 名前一時格納
#DIM データベース行数
#DIM 削除数
#DIM 現在値
#DIM 実削除フラグ

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

FOR 対象番号, 0, アビ対象最大数
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF 戦闘行動対象 < 10
		名前一時格納 = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ番号)%
		;基本的に刻印は死者に存在しない
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
	ELSE
		名前一時格納 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
		;基本的に刻印は死者に存在しない
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	;枠を対象能力と同じにする
	アビ汎用文字列:バフ・デバフ枠 = %アビ汎用文字列:バフ・デバフ対象能力%

	;刻印が存在するかチェック
	データベース行数 = DT_ROW_LENGTH(@"バフデータベース_{戦闘行動対象}")
	実削除フラグ = 0
	FOR バフ番号, 0, データベース行数
		IF DT_CELL_GETS(@"バフデータベース_{戦闘行動対象}", バフ番号, "バフ枠") == アビ汎用文字列:バフ・デバフ枠
			;すでに同じ枠のバフがある場合、数の分だけ固定値を減少
			現在値 = DT_CELL_GET(@"バフデータベース_{戦闘行動対象}", バフ番号, "バフ効果量_固定値")
			IF 現在値 <= 削除数
				;全部削除
				CALL バフ削除(戦闘行動対象, バフ番号)
				実削除フラグ = 現在値
			ELSE
				DT_CELL_SET @"バフデータベース_{戦闘行動対象}", バフ番号, "バフ効果量_固定値", 現在値 - 削除数
				実削除フラグ = 削除数
			ENDIF
			BREAK
		ENDIF
	NEXT

	SIF 実削除フラグ
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%アビ汎用文字列:バフ・デバフ対象能力%が{実削除フラグ, 2}減少した。
NEXT
