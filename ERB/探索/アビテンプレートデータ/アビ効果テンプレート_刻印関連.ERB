;刻印とはキャラに付与されるバフの一種、５個まで重ねられる
;しかし単体では効果がなく、アビや奥義の使用条件などに関わる
;ここでは名前自由な刻印を簡単にセット・除去出来るようにしている


@アビテンプレート_刻印追加(刻印数 = 1)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用文字列:バフ・デバフ対象能力（火の刻印、のように入力を推奨）
;アビ汎用変数:累積上限_固定値（スタック上限）
;アビ汎用変数:持続ターン
;アビ汎用変数:特殊表示オプション
;------------------------------------------------------------------------
;返り値-1:対象が不正 1:成功
;------------------------------------------------------------------------
#DIM 対象番号
#DIM バフ番号
#DIM スキップフラグ
#DIMS 名前一時格納
#DIM 刻印数
#DIM 現在値
#DIM スタック上限
#DIM 文章スキップ

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

IF アビ汎用変数:累積上限_固定値 > 0
	スタック上限 = アビ汎用変数:累積上限_固定値
ELSE
	スタック上限 = 5
ENDIF

SIF アビ汎用変数:持続ターン == 0
	アビ汎用変数:持続ターン = -1

FOR 対象番号, 0, アビ対象最大数
	スキップフラグ = 0
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF 戦闘行動対象 < 10
		名前一時格納 = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%
		;基本的にバフは死者にはかからない
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
	ELSE
		名前一時格納 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
		;基本的にバフは死者にはかからない
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	;枠を対象能力と同じにする
	アビ汎用文字列:バフ・デバフ枠 = %アビ汎用文字列:バフ・デバフ対象能力%

	;既存のバフと枠が被ってないかチェック
	FOR バフ番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
		IF DT_CELL_GETS("戦闘効果データベース", バフ番号, "効果枠") == アビ汎用文字列:バフ・デバフ枠 && DT_CELL_GETS("戦闘効果データベース", バフ番号, "適用範囲") == @"{戦闘行動対象}"
			;すでに同じ枠のバフがある場合、数の分だけ固定値に追加
			;最大値未満の時のみ
			現在値 = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値")
			IF 現在値 < スタック上限
				DT_CELL_SET "戦闘効果データベース", バフ番号, "効果量_固定値", MIN(スタック上限, 刻印数 + 現在値)
				SIF DT_CELL_GET("戦闘効果データベース", バフ番号, "持続ターン") > 0 && DT_CELL_GET("戦闘効果データベース", バフ番号, "持続ターン") < アビ汎用変数:持続ターン
					DT_CELL_SET "戦闘効果データベース", バフ番号, "持続ターン", アビ汎用変数:持続ターン
				スキップフラグ = MIN(スタック上限 - 現在値, 刻印数) + 10
			ENDIF
			BREAK
		ENDIF
	NEXT

	文章スキップ = 0
	IF アビ汎用文字列:テンプレ代替メッセージ１ != ""
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) '= アビ汎用文字列:テンプレ代替メッセージ１
		SIF アビ汎用文字列:テンプレ代替メッセージ２ != ""
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) '= アビ汎用文字列:テンプレ代替メッセージ２
		SIF アビ汎用文字列:テンプレ代替メッセージ３ != ""
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) '= アビ汎用文字列:テンプレ代替メッセージ３
		SIF アビ汎用文字列:テンプレ代替メッセージ４ != ""
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) '= アビ汎用文字列:テンプレ代替メッセージ４
		文章スキップ = 1
	ENDIF

	IF スキップフラグ == 10
		;スタック上限なので取得出来なかったパターン
		;文章は出さない
	ELSEIF スキップフラグ
		SIF 文章スキップ == 0
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は%アビ汎用文字列:バフ・デバフ対象能力%を{スキップフラグ, 2}得た！
	ELSE
		SIF 文章スキップ == 0
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%は%アビ汎用文字列:バフ・デバフ対象能力%を{MIN(スタック上限, 刻印数), 2}得た！
		;累積時でないなら新しくデータベースにセット
		{
		DT_ROW_ADD "戦闘効果データベース", "効果名", アビ汎用文字列:バフ・デバフ対象能力, "バフ・デバフフラグ", "バフ", "適用範囲", @"{戦闘行動対象}",
			"効果枠", アビ汎用文字列:バフ・デバフ対象能力, "対象能力", アビ汎用文字列:バフ・デバフ対象能力,
			"効果量_固定値", MIN(スタック上限, 刻印数), 
			"持続ターン", アビ汎用変数:持続ターン, "消去不可フラグ", 1, "特殊表示オプション", アビ汎用変数:特殊表示オプション
		}
	ENDIF
NEXT


@刻印現在数確認(刻印名, 対象隊列 = -1)
;これはアビテンプレートではなく、現在の刻印数を返す関数
;使用条件のチェックなどに使う
#FUNCTION
#DIMS 刻印名
#DIM 対象隊列
#DIM バフ番号

;戦闘中以外は常に０を返す（能力表示画面で呼ばれた時の対策）
SIF ダンジョン表示モード != "戦闘画面"
	RETURNF 0

SIF 対象隊列 == -1
	対象隊列 = 戦闘行動キャラ

;刻印は全体系はない
FOR バフ番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
	IF DT_CELL_GETS("戦闘効果データベース", バフ番号, "効果枠") == 刻印名 && DT_CELL_GETS("戦闘効果データベース", バフ番号, "適用範囲") == @"{対象隊列}"
		;バフ名一致で返す
		RETURNF DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値")
	ENDIF
NEXT

;ないなら０
RETURNF 0



@アビテンプレート_刻印減少(削除数 = 1)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用文字列:バフ・デバフ対象能力（火の刻印、のように入力を推奨）
;------------------------------------------------------------------------
;返り値-1:対象が不正 1:成功
;------------------------------------------------------------------------
#DIM 対象番号
#DIM バフ番号
#DIMS 名前一時格納
#DIM 削除数
#DIM 現在値
#DIM 実削除フラグ

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
	RETURN 0

FOR 対象番号, 0, アビ対象最大数
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	戦闘行動対象 = アビテンプレート用_対象保存:対象番号

	IF 戦闘行動対象 < 10
		名前一時格納 = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ登録番号)%
		;基本的に刻印は死者に存在しない
		SIF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
	ELSE
		名前一時格納 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
		;基本的に刻印は死者に存在しない
		SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
	ENDIF

	;枠を対象能力と同じにする
	アビ汎用文字列:バフ・デバフ枠 = %アビ汎用文字列:バフ・デバフ対象能力%

	;刻印が存在するかチェック
	実削除フラグ = 0
	FOR バフ番号, 0, DT_ROW_LENGTH("戦闘効果データベース")
		IF DT_CELL_GETS("戦闘効果データベース", バフ番号, "効果枠") == アビ汎用文字列:バフ・デバフ枠 && DT_CELL_GETS("戦闘効果データベース", バフ番号, "適用範囲") == @"{戦闘行動対象}"
			;すでに同じ枠のバフがある場合、数の分だけ固定値を減少
			現在値 = DT_CELL_GET("戦闘効果データベース", バフ番号, "効果量_固定値")
			IF 現在値 <= 削除数
				;全部削除
				CALL 戦闘効果削除(バフ番号)
				実削除フラグ = 現在値
			ELSE
				DT_CELL_SET "戦闘効果データベース", バフ番号, "効果量_固定値", 現在値 - 削除数
				実削除フラグ = 削除数
			ENDIF
			BREAK
		ENDIF
	NEXT

	SIF 実削除フラグ
		アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %名前一時格納%の%アビ汎用文字列:バフ・デバフ対象能力%が{実削除フラグ, 2}減少した。
NEXT
