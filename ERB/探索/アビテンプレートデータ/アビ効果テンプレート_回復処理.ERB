@アビテンプレート_回復処理(回復実行フラグ = 0)
#DIM 対象番号
#DIM 回復量計算値
#DIM 回復実行フラグ

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

IF 戦闘行動キャラ >= 10
	;敵が使用する回復は全員HPMAXでも戻らないのでフラグ立てておく
	回復実行フラグ = 1
ENDIF

FOR 対象番号, 0, アビ対象最大数
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	IF アビテンプレート用_対象保存:対象番号 < 10
		;味方対象
		戦闘行動対象 = アビテンプレート用_対象保存:対象番号
		IF アビ汎用変数:蘇生可能オプション == 0 && BATTLE_STATE:戦闘行動対象:ＨＰ < 1
			CONTINUE
		ENDIF
		CALL 回復量計算処理(アビ汎用変数:効果割合, アビ汎用文字列:回復側使用能力値, アビ汎用変数:数値乱数幅)
		回復量計算値 = RESULT + アビ汎用変数:効果量
		回復量計算値 = MIN(回復量計算値, BATTLE_STATE:戦闘行動対象:最大ＨＰ - BATTLE_STATE:戦闘行動対象:ＨＰ)
		IF 回復量計算値 > 0
			IF デバフ存在チェック("強圧", 戦闘行動対象) >= 0
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ番号)%のＨＰは回復しなかった…
				CALL バフ回数経過処理(戦闘行動対象, "強圧")
			ELSE
				IF BATTLE_STATE:戦闘行動対象:ＨＰ < 1
					アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ番号)%が復活した！
					IF 戦闘行動キャラ < 10
						;味方＞味方の回復の場合、口上用変数を記録
						CALL 反応口上記録処理("被蘇生時")
					ENDIF
				ENDIF
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ番号)%のＨＰが{回復量計算値}回復した！
				BATTLE_STATE:戦闘行動対象:ＨＰ = BATTLE_STATE:戦闘行動対象:ＨＰ + 回復量計算値
			ENDIF
			回復実行フラグ = 1
			IF 戦闘行動キャラ < 10
				;味方＞味方の回復の場合、口上用変数を記録
				CALL 反応口上記録処理("被回復時")
			ENDIF
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ番号)%のＨＰは既に最大だ
		ENDIF
	ELSE
		;敵対象
		戦闘行動対象 = アビテンプレート用_対象保存:対象番号
		SIF アビ汎用変数:蘇生可能オプション == 0 && 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
			CONTINUE
		CALL 回復量計算処理(アビ汎用変数:効果割合, アビ汎用文字列:回復側使用能力値, アビ汎用変数:数値乱数幅)
		回復量計算値 = RESULT + アビ汎用変数:効果量
		回復量計算値 = MIN(回復量計算値, 敵BATTLE_STATE:(戦闘行動対象 - 10):最大ＨＰ - 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ)
		IF 回復量計算値 > 0
			IF デバフ存在チェック("強圧", 戦闘行動対象) >= 0
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%のＨＰは回復しなかった…
				CALL バフ回数経過処理(戦闘行動対象, "強圧")
			ELSE
				SIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ < 1
					アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%が復活した！
				アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%のＨＰが{回復量計算値}回復した！
				敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ = 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ + 回復量計算値
			ENDIF
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%のＨＰは既に最大だ
		ENDIF
	ENDIF
	CALL ダメージ表示アニメ(回復量計算値, 戦闘行動対象, 0, 1, 0)
NEXT

IF 回復実行フラグ == 0
	;誰も回復者がいないならキャンセルして戻る
	CALL 口上変数初期化
	WSTR:(K++) = 対象のＨＰは既に最大です
	CALL メッセージ欄表示用関数(,,,0)
	アビテンプレート用_キャンセルフラグ = 1
	RETURN -1
ENDIF


@アビテンプレート_回復処理_MAXでも回復
JUMP アビテンプレート_回復処理(1)

@アビテンプレート_ＭＰ回復処理(回復実行フラグ = 0)
#DIM 対象番号
#DIM 回復量計算値
#DIM 回復実行フラグ

CALL アビテンプレート_効果共通処理
SIF RESULT == -1
	RETURN -1

IF 戦闘行動キャラ >= 10
	;敵が使用する回復は全員MPMAXでも戻らないのでフラグ立てておく
	回復実行フラグ = 1
ENDIF

FOR 対象番号, 0, アビ対象最大数
	SIF アビテンプレート用_対象保存:対象番号 < 0
		CONTINUE
	IF アビテンプレート用_対象保存:対象番号 < 10
		;味方対象
		戦闘行動対象 = アビテンプレート用_対象保存:対象番号
		CALL 回復量計算処理(アビ汎用変数:効果割合, アビ汎用文字列:回復側使用能力値, アビ汎用変数:数値乱数幅)
		回復量計算値 = RESULT + アビ汎用変数:効果量
		回復量計算値 = MIN(回復量計算値, BATTLE_STATE:戦闘行動対象:最大ＭＰ - BATTLE_STATE:戦闘行動対象:ＭＰ)
		IF 回復量計算値 > 0
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ番号)%のＭＰが{回復量計算値}回復した！
			BATTLE_STATE:戦闘行動対象:ＭＰ = BATTLE_STATE:戦闘行動対象:ＭＰ + 回復量計算値
			回復実行フラグ = 1
			IF 戦闘行動キャラ < 10
				;味方＞味方の回復の場合、口上用変数を記録
				CALL 反応口上記録処理("被ＭＰ回復時")
			ENDIF
		ELSE
			アビテンプレート用_表示メッセージ格納:対象番号:(A_M_NUM:対象番号++) = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ番号)%のＭＰは既に最大だ
		ENDIF
	ELSE
		;敵対象はＭＰ無いので何もしない
	ENDIF
NEXT

IF 回復実行フラグ == 0
	;誰も回復者がいないならキャンセルして戻る
	CALL 口上変数初期化
	WSTR:(K++) = 対象のＭＰは既に最大です
	CALL メッセージ欄表示用関数(,,,0)
	アビテンプレート用_キャンセルフラグ = 1
	RETURN -1
ENDIF

@アビテンプレート_ＭＰ回復処理_MAXでも回復
JUMP アビテンプレート_ＭＰ回復処理(1)
