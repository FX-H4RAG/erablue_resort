@アビテンプレ変数リセット
VARSET アビテンプレート用_対象保存, -1
VARSET アビテンプレート用_表示メッセージ格納:0:0
VARSET A_M_NUM
アビテンプレート用_キャンセルフラグ = 0
アビテンプレート用_全体フラグ = 0
アビテンプレート用_アビ名表示済フラグ = 0
アビテンプレート用_アビ封印用フラグ = 0
アビテンプレート用_行動消費無しフラグ = 0
アビテンプレート用_回避フラグ = 0
CALL アビ汎用変数文字列リセット
CALL 口上変数初期化

@アビ汎用変数文字列リセット
VARSET アビ汎用変数:0
VARSET アビ汎用文字列:0
アビ汎用変数:数値乱数幅 = -1
アビ汎用変数:クリティカル率計算値保存 = -1
アビ汎用変数:クリティカルダメージ計算値保存 = -1

@アビテンプレート用_表示メッセージ変換処理
#DIM 対象番号
#DIM メッセージ番号

FOR 対象番号, 0, 10
	FOR メッセージ番号, 0, 50
		SIF アビテンプレート用_表示メッセージ格納:対象番号:メッセージ番号 == ""
			BREAK
		WSTR:(K++) = %アビテンプレート用_表示メッセージ格納:対象番号:メッセージ番号%
	NEXT
NEXT

@アビテンプレート_効果共通処理
SIF アビテンプレート用_キャンセルフラグ
	RETURN -1

IF アビテンプレート用_対象保存:0 < 0
	PRINTW 対象が不正です
	アビテンプレート用_キャンセルフラグ = 1
	RETURN -1
ENDIF

IF アビテンプレート用_アビ名表示済フラグ == 0
	IF 戦闘行動キャラ < 10
		WSTR:(K++) = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ番号)%の%アビテンプレート用_アビ名%！
	ELSE
		WSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%の%アビテンプレート用_アビ名%！
	ENDIF
	アビテンプレート用_アビ名表示済フラグ = 1
ENDIF

@アビ対象選択テンプレート_自己のみ
SIF アビテンプレート用_キャンセルフラグ
	RETURN -1

VARSET アビテンプレート用_対象保存, -1
アビテンプレート用_対象保存:0 = 戦闘行動キャラ


@アビ対象選択テンプレート_敵単体
SIF アビテンプレート用_キャンセルフラグ
	RETURN -1

VARSET アビテンプレート用_対象保存, -1
SIF アビ汎用文字列:選択時メッセージ１ == ""
	アビ汎用文字列:選択時メッセージ１ = 誰に使用しますか？
SIF アビ汎用文字列:選択時メッセージ２ == ""
	アビ汎用文字列:選択時メッセージ２ = 　
SIF アビ汎用文字列:選択時メッセージ３ == ""
	アビ汎用文字列:選択時メッセージ３ = 　
SIF アビ汎用文字列:選択時メッセージ４ == ""
	アビ汎用文字列:選択時メッセージ４ = 　
	
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ１%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ２%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ３%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ４%
KSTR:(K++) = <button value='999'>[999]戻る</button>
エネミー画像ボタン化フラグ = 1
CALL メッセージ欄表示用関数(@"探索用_{戦闘行動キャラ}_顔", @"%CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ番号)%",,0)
CALL 口上変数初期化
$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 10 TO 19
		IF 敵BATTLE_STATE_STR:(RESULT - 10):エネミー名 == ""
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ELSEIF 敵BATTLE_STATE:(RESULT - 10):ＨＰ < 1
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		アビテンプレート用_対象保存:0 = RESULT
	CASE 999
		アビテンプレート用_キャンセルフラグ = 1
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT

@アビ対象選択テンプレート_敵横列
SIF アビテンプレート用_キャンセルフラグ
	RETURN -1

VARSET アビテンプレート用_対象保存, -1
SIF アビ汎用文字列:選択時メッセージ１ == ""
	アビ汎用文字列:選択時メッセージ１ = どの横列に使用しますか？
SIF アビ汎用文字列:選択時メッセージ２ == ""
	アビ汎用文字列:選択時メッセージ２ = 　
SIF アビ汎用文字列:選択時メッセージ３ == ""
	アビ汎用文字列:選択時メッセージ３ = 　
SIF アビ汎用文字列:選択時メッセージ４ == ""
	アビ汎用文字列:選択時メッセージ４ = 　
	
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ１%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ２%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ３%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ４%
KSTR:(K++) = <button value='999'>[999]戻る</button>
エネミー画像ボタン化フラグ = 1
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 10 TO 19
		IF 敵BATTLE_STATE_STR:(RESULT - 10):エネミー名 == ""
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ELSEIF 敵BATTLE_STATE:(RESULT - 10):ＨＰ < 1
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		アビテンプレート用_対象保存:0 = RESULT
		IF アビテンプレート用_対象保存:0 % 2
			アビテンプレート用_対象保存:1 = アビテンプレート用_対象保存:0 - 1
		ELSE
			アビテンプレート用_対象保存:1 = アビテンプレート用_対象保存:0 + 1
		ENDIF
	CASE 999
		アビテンプレート用_キャンセルフラグ = 1
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT

@アビ対象選択テンプレート_敵縦列
#DIM 敵番号

SIF アビテンプレート用_キャンセルフラグ
	RETURN -1

VARSET アビテンプレート用_対象保存, -1
SIF アビ汎用文字列:選択時メッセージ１ == ""
	アビ汎用文字列:選択時メッセージ１ = どの縦列に使用しますか？
SIF アビ汎用文字列:選択時メッセージ２ == ""
	アビ汎用文字列:選択時メッセージ２ = 　
SIF アビ汎用文字列:選択時メッセージ３ == ""
	アビ汎用文字列:選択時メッセージ３ = 　
SIF アビ汎用文字列:選択時メッセージ４ == ""
	アビ汎用文字列:選択時メッセージ４ = 　
	
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ１%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ２%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ３%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ４%
KSTR:(K++) = <button value='999'>[999]戻る</button>
エネミー画像ボタン化フラグ = 1
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 10 TO 19
		IF 敵BATTLE_STATE_STR:(RESULT - 10):エネミー名 == ""
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ELSEIF 敵BATTLE_STATE:(RESULT - 10):ＨＰ < 1
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		LOCAL = 0
		IF RESULT % 2
			FOR 敵番号, 11, 21, 2
				SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
					CONTINUE
				SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
					CONTINUE
				アビテンプレート用_対象保存:LOCAL = 敵番号
				LOCAL += 1
			NEXT
		ELSE
			FOR 敵番号, 10, 20, 2
				SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
					CONTINUE
				SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
					CONTINUE
				アビテンプレート用_対象保存:LOCAL = 敵番号
				LOCAL += 1
			NEXT
		ENDIF
	CASE 999
		アビテンプレート用_キャンセルフラグ = 1
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT

@アビ対象選択テンプレート_敵全体(生者死者 = 0)
#DIM 生者死者
#DIM 敵番号
#DIM 対象数

SIF アビテンプレート用_キャンセルフラグ
	RETURN -1

VARSET アビテンプレート用_対象保存, -1
対象数 = 0
FOR 敵番号, 10, 20
	SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
		CONTINUE
	IF 生者死者 == 1
		SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
			CONTINUE
	ELSEIF 生者死者 == 2
		SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ > 0
			CONTINUE
	ENDIF
	アビテンプレート用_対象保存:対象数 = 敵番号
	対象数 += 1
NEXT
アビテンプレート用_全体フラグ = 1

@アビ対象選択テンプレート_敵全体_生者のみ
CALL アビ対象選択テンプレート_敵全体(1)

@アビ対象選択テンプレート_敵全体_死者のみ
CALL アビ対象選択テンプレート_敵全体(2)

@アビ対象選択テンプレート_敵ランダムＸ体(ランダム回数)
#DIM ランダム回数
#DIM 対象数
#DIM 敵番号
#DIM 敵対心合計
#DIM 敵対心, 10
#DIM ランダム候補, 100

SIF アビテンプレート用_キャンセルフラグ
	RETURN -1

VARSET ランダム候補, -1
VARSET アビテンプレート用_対象保存, -1
対象数 = 0

IF 戦闘行動キャラ >= 10 || アビ汎用変数:敵対心無視オプション
	;敵が敵を選ぶ場合は敵対心処理は挟まない
	FOR 敵番号, 10, 20
		SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
			CONTINUE
		SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
			CONTINUE
		ランダム候補:対象数 = 敵番号
		対象数 += 1
	NEXT
ELSE
	VARSET 敵対心, -1
	敵対心合計 = 0
	FOR 敵番号, 10, 20
		SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
			CONTINUE
		SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
			CONTINUE
		CALL バフ・デバフ測定(敵番号, "敵対心")
		敵対心:敵番号 = MAX(RESULT, 0)
		敵対心合計 += 敵対心:敵番号
	NEXT
	VARSET LOCAL
	FOR 敵番号, 10, 20
		SIF 敵対心:敵番号 < 0
			CONTINUE
		LOCAL += 敵対心:敵番号 * 100 / 敵対心合計
		LOCAL = MIN(LOCAL, 100)
		VARSET ランダム候補, 敵番号, LOCAL:1, LOCAL - 1
		LOCAL:1 = LOCAL
	NEXT
	対象数 = 100
ENDIF

FOR 敵番号, 0, ランダム回数
	アビテンプレート用_対象保存:敵番号 = ランダム候補:(RAND:対象数)
NEXT

@アビ対象選択テンプレート_味方単体(生者死者 = 0)
#DIM 生者死者

SIF アビテンプレート用_キャンセルフラグ
	RETURN -1

VARSET アビテンプレート用_対象保存, -1
SIF アビ汎用文字列:選択時メッセージ１ == ""
	アビ汎用文字列:選択時メッセージ１ = 誰に使用しますか？
SIF アビ汎用文字列:選択時メッセージ２ == ""
	アビ汎用文字列:選択時メッセージ２ = 　
SIF アビ汎用文字列:選択時メッセージ３ == ""
	アビ汎用文字列:選択時メッセージ３ = 　
SIF アビ汎用文字列:選択時メッセージ４ == ""
	アビ汎用文字列:選択時メッセージ４ = 　
	
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ１%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ２%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ３%
KSTR:(K++) = %アビ汎用文字列:選択時メッセージ４%
KSTR:(K++) = <button value='999'>[999]戻る</button>
キャラ画像ボタン化フラグ = 1
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 0 TO 3
		IF BATTLE_STATE:RESULT:キャラ番号 < 1
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		IF 生者死者 == 1
			;生者のみ
			IF BATTLE_STATE:RESULT:ＨＰ < 1
				REUSELASTLINE 
				GOTO INPUT_LOOP
			ENDIF
		ELSEIF 生者死者 == 2
			;死者のみ
			IF BATTLE_STATE:RESULT:ＨＰ > 0
				REUSELASTLINE 
				GOTO INPUT_LOOP
			ENDIF
		ENDIF
		アビテンプレート用_対象保存:0 = RESULT
	CASE 999
		アビテンプレート用_キャンセルフラグ = 1
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT

@アビ対象選択テンプレート_味方単体_生者のみ
CALL アビ対象選択テンプレート_味方単体(1)

@アビ対象選択テンプレート_味方単体_死者のみ
CALL アビ対象選択テンプレート_味方単体(2)

@アビ対象選択テンプレート_味方全体(生者死者 = 0)
#DIM 生者死者
#DIM 味方番号
#DIM 対象数

SIF アビテンプレート用_キャンセルフラグ
	RETURN -1

VARSET アビテンプレート用_対象保存, -1
対象数 = 0
FOR 味方番号, 0, 4
	SIF BATTLE_STATE:味方番号:キャラ番号 < 1
		CONTINUE
	IF 生者死者 == 1
		;生者のみ
		SIF BATTLE_STATE:味方番号:ＨＰ < 1
			CONTINUE
	ELSEIF 生者死者 == 2
		;死者のみ
		SIF BATTLE_STATE:味方番号:ＨＰ > 0
			CONTINUE
	ENDIF
	アビテンプレート用_対象保存:対象数 = 味方番号
	対象数 += 1
NEXT
アビテンプレート用_全体フラグ = 1

@アビ対象選択テンプレート_味方全体_生者のみ
CALL アビ対象選択テンプレート_味方全体(1)

@アビ対象選択テンプレート_味方全体_死者のみ
CALL アビ対象選択テンプレート_味方全体(2)

@アビ対象選択テンプレート_味方ランダムＸ体(ランダム回数)
#DIM ランダム回数
#DIM 対象数
#DIM 味方番号
#DIM 敵対心合計
#DIM 敵対心, 10
#DIM ランダム候補, 100

SIF アビテンプレート用_キャンセルフラグ
	RETURN -1

VARSET ランダム候補, -1
VARSET アビテンプレート用_対象保存, -1
対象数 = 0

IF 戦闘行動キャラ < 10 || アビ汎用変数:敵対心無視オプション
	;味方が味方を選ぶ場合は敵対心処理は挟まない
	FOR 味方番号, 0, 4
		SIF BATTLE_STATE:味方番号:キャラ番号 < 1
			CONTINUE
		SIF BATTLE_STATE:味方番号:ＨＰ < 1
			CONTINUE
		ランダム候補:対象数 = 味方番号
		対象数 += 1
	NEXT
ELSE
	VARSET 敵対心, -1
	敵対心合計 = 0
	FOR 味方番号, 0, 4
		SIF BATTLE_STATE:味方番号:キャラ番号 < 1
			CONTINUE
		SIF BATTLE_STATE:味方番号:ＨＰ < 1
			CONTINUE
		CALL バフ・デバフ測定(味方番号, "敵対心")
		敵対心:味方番号 = MAX(RESULT, 0)
		敵対心合計 += 敵対心:味方番号
	NEXT
	VARSET LOCAL
	FOR 味方番号, 0, 4
		SIF 敵対心:味方番号 < 0
			CONTINUE
		LOCAL += 敵対心:味方番号 * 100 / 敵対心合計
		LOCAL = MIN(LOCAL, 100)
		VARSET ランダム候補, 味方番号, LOCAL:1, LOCAL
		LOCAL:1 = MIN(LOCAL, 99)
	NEXT
	
	対象数 = LOCAL
ENDIF

FOR 味方番号, 0, ランダム回数
	アビテンプレート用_対象保存:味方番号 = ランダム候補:(RAND:対象数)
NEXT

