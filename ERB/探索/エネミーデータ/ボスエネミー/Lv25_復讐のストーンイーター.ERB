@敵BATTLE_STATE_Lv25_復讐のストーンイーター(対象エネミー)
#DIM 対象エネミー
敵BATTLE_STATE:対象エネミー:Lv = 6
敵BATTLE_STATE:対象エネミー:最大ＨＰ = 22000
敵BATTLE_STATE:対象エネミー:ＨＰ = 22000
敵BATTLE_STATE:対象エネミー:攻撃力 = 1300
敵BATTLE_STATE:対象エネミー:防御力 = 760
敵BATTLE_STATE:対象エネミー:回復力 = 0
敵BATTLE_STATE:対象エネミー:行動速度 = 560
敵BATTLE_STATE:対象エネミー:DA率 = 0
敵BATTLE_STATE:対象エネミー:TA率 = 50
敵BATTLE_STATE:対象エネミー:命中率 = 105
敵BATTLE_STATE:対象エネミー:回避率 = 5
敵BATTLE_STATE:対象エネミー:属性 = 3
敵BATTLE_STATE:対象エネミー:所持EXP = 2455
敵BATTLE_STATE:対象エネミー:所持ルピ = 3115
敵BATTLE_STATE:対象エネミー:最大チャージターン = 3

敵BATTLE_STATE:対象エネミー:ボスフラグ = 1
敵BATTLE_STATE:対象エネミー:ランダム不許可フラグ = 1

敵BATTLE_STATE_STR:対象エネミー:種族 = 動物_爬虫


@敵討伐後処理_Lv25_復讐のストーンイーター(隊列番号)
#DIM 隊列番号
#DIM ドロップ確率
#DIMS ドロップアイテム
#DIM データベース行数
#DIM トレハンLv

トレハンLv = 0
データベース行数 = デバフ番号取得("ドロップ率UP")
SIF データベース行数 > -1
	トレハンLv = DT_CELL_GET(@"デバフデータベース_{隊列番号}", データベース行数, "デバフ効果量_固定値")

ドロップ確率 = 30 + トレハンLv * 2
ドロップアイテム = 渦琥珀
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)


@敵トリガー行動_Lv25_復讐のストーンイーター
IF GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0) == 0
	;初手ストーンバレット
	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%はリベンジに燃えている！
	アビ汎用文字列:実行時メッセージ２ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%のストーンバレット！

	CALL アビ対象選択テンプレート_味方全体_生者のみ
	アビ汎用変数:効果割合 = 100
	CALL アビテンプレート_ダメージ処理_自属性
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0

	;HPトリガー使い終わったのでフラグを立てる
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0
	RETURN 1
ELSEIF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ / 2 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1) == 0
	;HPが半分以下になったら呪いを撒いてくる
	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%は大きく傷ついたことで怒りに燃えている！
	アビ汎用文字列:実行時メッセージ２ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%の特殊行動が変化した！
	アビ汎用文字列:実行時メッセージ３ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%のカースドジュエル！

	CALL アビ対象選択テンプレート_味方全体_生者のみ
	アビ汎用変数:効果割合 = 100
	CALL アビテンプレート_ダメージ処理_自属性

	CALL アビ汎用変数文字列リセット
	アビ汎用変数:効果割合 = 50
	アビ汎用変数:基礎成功確率 = 100
	アビ汎用文字列:バフ・デバフ枠 = 呪い
	アビ汎用文字列:バフ・デバフ対象能力 = 呪い
	アビ汎用変数:持続ターン = -1
	CALL アビテンプレート_不利効果_デバフ効果セット

	;HPトリガー使い終わったのでフラグを立てる
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1
	敵BATTLE_STATE:(戦闘行動キャラ - 10):最大チャージターン = 1
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = MAX(敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン - 2, 0)
	RETURN 1
ENDIF


@敵チャージ行動_Lv25_復讐のストーンイーター
#DIM ループ用
IF 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大チャージターン == 2
	アビテンプレート用_アビ名 = ストーンバレット

	CALL アビ対象選択テンプレート_味方全体_生者のみ
	アビ汎用変数:効果割合 = 100
	CALL アビテンプレート_ダメージ処理_自属性
ELSE
	CALL アビ対象選択テンプレート_味方ランダム１体_特定デバフ("呪い")
	IF RESULT == -1
		;呪いが一人もいない場合
		アビテンプレート用_アビ名 = カースドジュエル

		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果割合 = 50
		アビ汎用変数:基礎成功確率 = 100
		アビ汎用文字列:バフ・デバフ枠 = 呪い
		アビ汎用文字列:バフ・デバフ対象能力 = 呪い
		アビ汎用変数:持続ターン = -1
		CALL アビテンプレート_不利効果_デバフ効果セット
	ELSE
		アビテンプレート用_アビ名 = ストーンバラージ

		FOR ループ用, 0, 7
			IF ループ用 > 0
				CALL アビ汎用変数文字列リセット
				CALL アビ対象選択テンプレート_味方ランダム１体_特定デバフ("呪い")
				SIF RESULT == -1
					BREAK
			ENDIF
			アビ汎用変数:効果割合 = 50
			CALL アビテンプレート_ダメージ処理_自属性
		NEXT
	ENDIF
ENDIF
