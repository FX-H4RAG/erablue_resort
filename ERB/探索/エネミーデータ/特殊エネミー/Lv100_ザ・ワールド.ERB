
@敵BATTLE_STATE_Lv100_ザ・ワールド(ARG)
敵BATTLE_STATE:ARG:Lv = 100
敵BATTLE_STATE:ARG:最大ＨＰ = 360000
敵BATTLE_STATE:ARG:ＨＰ = 360000
敵BATTLE_STATE:ARG:攻撃力 = 1000
敵BATTLE_STATE:ARG:防御力 = 1000
敵BATTLE_STATE:ARG:回復力 = 0
敵BATTLE_STATE:ARG:行動速度 = 1000
敵BATTLE_STATE:ARG:DA率 = 50
敵BATTLE_STATE:ARG:TA率 = 25
敵BATTLE_STATE:ARG:命中率 = 120
敵BATTLE_STATE:ARG:回避率 = 20
敵BATTLE_STATE:ARG:属性 = 6
敵BATTLE_STATE:ARG:所持EXP = 45000
敵BATTLE_STATE:ARG:所持ルピ = 90000
敵BATTLE_STATE:ARG:最大チャージターン = 2

敵BATTLE_STATE:ARG:ボスフラグ = 1
敵BATTLE_STATE:ARG:ランダム不許可フラグ = 1

敵BATTLE_STATE_STR:ARG:種族 = 星晶獣

@敵討伐後処理_Lv100_ザ・ワールド(隊列番号)
#DIM 隊列番号
#DIM ドロップ確率
#DIMS ドロップアイテム
#DIM データベース行数
#DIM トレハンLv

トレハンLv = 0
データベース行数 = デバフ番号取得("ドロップ率UP")
SIF データベース行数 > -1
	トレハンLv = DT_CELL_GET(@"デバフデータベース_{隊列番号}", データベース行数, "デバフ効果量_固定値")

ドロップ確率 = 100
ドロップアイテム = 世界のイデア
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 50 + トレハンLv * 2
ドロップアイテム = 世界のイデア
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)



@敵トリガー行動_Lv100_ザ・ワールド
#DIM 味方番号
#DIM ダメージ量

IF バフ番号取得_枠("ヴァース・クライシス", 戦闘行動キャラ) > -1
	IF 戦闘中カウント:10:被ダメ合計 > 20000
		WSTR:(K++) = ヴァース・クライシスが解除された！
		
		CALL バフ削除_枠(戦闘行動キャラ, "ヴァース・クライシス")
		RETURN 1
	ELSE
		;『ヴァース・クライシス』：味方全体に現在HP70％分のダメージ/強化効果を全て消去・奥義ゲージDOWN効果
		WSTR:(K++) = 「創造は…この手で成る！」
		WSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%のヴァース・クライシス！
		
		FOR 味方番号, 0, 4
			SIF BATTLE_STATE:味方番号:キャラ登録番号 < 1
				CONTINUE
			SIF BATTLE_STATE:味方番号:ＨＰ < 1
				CONTINUE
			
			ダメージ量 = (BATTLE_STATE:味方番号:ＨＰ / 10) * 7
			
			BATTLE_STATE:味方番号:ＨＰ = MAX(BATTLE_STATE:味方番号:ＨＰ - ダメージ量, 0)
			WSTR:(K++) = %CALLNAME:(BATTLE_STATE:味方番号:キャラ登録番号)%は{ダメージ量}のダメージを受けた！
			CALL メッセージ欄表示用関数(,,,0)
		NEXT
		
		CALL 口上変数初期化
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		FOR LOCAL, 0, 9
			CALL アビテンプレート_不利効果_バフ消去()
		NEXT
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 100
		CALL アビテンプレート_不利効果_奥義ゲージDOWN
		
		敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0

		行動種別フラグ:戦闘行動キャラ:大技発動 = 1
		CALL バフ削除_枠(戦闘行動キャラ, "ヴァース・クライシス")
		RETURN 1
	ENDIF
ELSEIF バフ番号取得_枠("スターリー・ノヴァ", 戦闘行動キャラ) > -1
	IF 戦闘中カウント:10:被ダメ合計 > 10000
		WSTR:(K++) = スターリー・ノヴァが解除された！
		
		CALL バフ削除_枠(戦闘行動キャラ, "スターリー・ノヴァ")
		RETURN 1
	ELSE
		;『スターリー・ノヴァ』：味方全体にランダム属性4倍ダメージ/ザ・ワールドに与ダメ増加効果・TA率UP効果
		SELECTCASE RAND:2
			CASE 0
				WSTR:(K++) = 「新たな理だ…！」
			CASE 1
				WSTR:(K++) = 「愚かな者に……幸福な死を！」
		ENDSELECT
		
		アビテンプレート用_アビ名 = スターリー・ノヴァ
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 400
		SELECTCASE RAND:6
			CASE 0
				CALL アビテンプレート_ダメージ処理_火属性
			CASE 1
				CALL アビテンプレート_ダメージ処理_水属性
			CASE 2
				CALL アビテンプレート_ダメージ処理_土属性
			CASE 3
				CALL アビテンプレート_ダメージ処理_風属性
			CASE 4
				CALL アビテンプレート_ダメージ処理_光属性
			CASE 5
				CALL アビテンプレート_ダメージ処理_闇属性
		ENDSELECT
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 100
		アビ汎用文字列:バフ・デバフ枠 = 与ダメ増加
		アビ汎用文字列:バフ・デバフ対象能力 = 与ダメージ
		アビ汎用変数:持続ターン = 4
		CALL アビテンプレート_有利効果_バフ効果セット
		
		CALL アビ汎用変数文字列リセット
		アビ汎用変数:効果量 = 50
		アビ汎用文字列:バフ・デバフ枠 = TA率
		アビ汎用文字列:バフ・デバフ対象能力 = TA率
		アビ汎用変数:持続ターン = 4
		CALL アビテンプレート_有利効果_バフ効果セット
		
		敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0
		
		行動種別フラグ:戦闘行動キャラ:大技発動 = 1
		CALL バフ削除_枠(戦闘行動キャラ, "スターリー・ノヴァ")
		RETURN 1
	ENDIF
ELSEIF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < (敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ / 4) * 3 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0) == 0
	;75%で真の力を解放/弱体効果を回復
	WSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%が真の力を解放した！
	
	アビテンプレート用_アビ名 = 真の力解放
	
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ
	FOR LOCAL, 0, DT_ROW_LENGTH(@"バフデータベース_{10}")
		CALL アビテンプレート_有利効果_デバフ消去()
	NEXT

	;HPトリガー使い終わったのでフラグを立てる
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0
	RETURN 1
ELSEIF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ / 2 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1) == 0
	;50%で『ヴァース・クライシス』を構え始める
	WSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%の掌に創造の光が収束する…！
	
	アビテンプレート用_アビ名 = 大技待機

	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:特殊表示オプション = 1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = ヴァース・クライシス
	アビ汎用文字列:バフ・デバフ対象能力 = ヴァース・クライシス
	アビ汎用変数:持続ターン = -1
	CALL アビテンプレート_有利効果_バフ効果セット
	
	戦闘中カウント:10:被ダメ合計 = 0
	
	;HPトリガー使い終わったのでフラグを立てる
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1
	RETURN 1
ELSEIF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ / 5 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 2) == 0
	;25%で『スターリー・ノヴァ』を構え始める
	WSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%が星の力を溜め始めた…
	
	アビテンプレート用_アビ名 = 大技待機
	
	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用変数:特殊表示オプション = 1
	アビ汎用変数:消去不可オプション = 1
	アビ汎用文字列:バフ・デバフ枠 = スターリー・ノヴァ
	アビ汎用文字列:バフ・デバフ対象能力 = スターリー・ノヴァ
	アビ汎用変数:持続ターン = -1
	CALL アビテンプレート_有利効果_バフ効果セット
	
	戦闘中カウント:10:被ダメ合計 = 0
	
	;HPトリガー使い終わったのでフラグを立てる
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 2
	RETURN 1
ENDIF



@敵チャージ行動_Lv100_ザ・ワールド
IF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < (敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ / 4) * 3
	SELECTCASE RAND:2
		CASE 0
			SELECTCASE RAND:2
				CASE 0
					WSTR:(K++) = 「新たな理だ…！」
				CASE 1
					WSTR:(K++) = 「愚かな者に……幸福な死を！」
			ENDSELECT
			
			アビテンプレート用_アビ名 = スターリー・ノヴァ
			
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用変数:効果割合 = 400
			SELECTCASE RAND:6
				CASE 0
					CALL アビテンプレート_ダメージ処理_火属性
				CASE 1
					CALL アビテンプレート_ダメージ処理_水属性
				CASE 2
					CALL アビテンプレート_ダメージ処理_土属性
				CASE 3
					CALL アビテンプレート_ダメージ処理_風属性
				CASE 4
					CALL アビテンプレート_ダメージ処理_光属性
				CASE 5
					CALL アビテンプレート_ダメージ処理_闇属性
			ENDSELECT
			
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:効果量 = 100
			アビ汎用文字列:バフ・デバフ枠 = 与ダメ増加
			アビ汎用文字列:バフ・デバフ対象能力 = 与ダメージ
			アビ汎用変数:持続ターン = 4
			CALL アビテンプレート_有利効果_バフ効果セット
			
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 50
			アビ汎用文字列:バフ・デバフ枠 = TA率
			アビ汎用文字列:バフ・デバフ対象能力 = TA率
			アビ汎用変数:持続ターン = 4
			CALL アビテンプレート_有利効果_バフ効果セット
		CASE 1
			SELECTCASE RAND:2
				CASE 0
					WSTR:(K++) = 「オレがァ！ 神だあああァァ！」
				CASE 1
					WSTR:(K++) = 「終ゥ焉の時だァ…！」
			ENDSELECT
			
			アビテンプレート用_アビ名 = メビ・コメット
			
			CALL アビ汎用変数文字列リセット
			
			FOR LOCAL, 0, 19
				CALL アビ対象選択テンプレート_味方ランダムＸ体(1)
				アビ汎用変数:効果割合 = 70
				SELECTCASE RAND:6
					CASE 0
						CALL アビテンプレート_ダメージ処理_火属性
					CASE 1
						CALL アビテンプレート_ダメージ処理_水属性
					CASE 2
						CALL アビテンプレート_ダメージ処理_土属性
					CASE 3
						CALL アビテンプレート_ダメージ処理_風属性
					CASE 4
						CALL アビテンプレート_ダメージ処理_光属性
					CASE 5
						CALL アビテンプレート_ダメージ処理_闇属性
				ENDSELECT
			NEXT
			
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用変数:効果量 = 0
			アビ汎用文字列:バフ・デバフ枠 = アビ封印
			アビ汎用文字列:バフ・デバフ対象能力 = アビ封印
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 100
			CALL アビテンプレート_不利効果_デバフ効果セット
			
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 0
			アビ汎用文字列:バフ・デバフ枠 = 奥義封印
			アビ汎用文字列:バフ・デバフ対象能力 = 奥義封印
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 100
			CALL アビテンプレート_不利効果_デバフ効果セット
	ENDSELECT
ELSE
	SELECTCASE RAND:2
		CASE 0
			WSTR:(K++) = 「新たな神の誕生だ……思い知れェ！」

			アビテンプレート用_アビ名 = セレスティアル・スフィア
			
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用変数:効果割合 = 300
			SELECTCASE RAND:6
				CASE 0
					CALL アビテンプレート_ダメージ処理_火属性
				CASE 1
					CALL アビテンプレート_ダメージ処理_水属性
				CASE 2
					CALL アビテンプレート_ダメージ処理_土属性
				CASE 3
					CALL アビテンプレート_ダメージ処理_風属性
				CASE 4
					CALL アビテンプレート_ダメージ処理_光属性
				CASE 5
					CALL アビテンプレート_ダメージ処理_闇属性
			ENDSELECT
			
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 0
			アビ汎用文字列:バフ・デバフ枠 = 強圧
			アビ汎用文字列:バフ・デバフ対象能力 = 強圧
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 100
			CALL アビテンプレート_不利効果_デバフ効果セット
			
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果割合 = 50
			アビ汎用文字列:バフ・デバフ枠 = 暗闇
			アビ汎用文字列:バフ・デバフ対象能力 = 暗闇
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 100
			CALL アビテンプレート_不利効果_デバフ効果セット
		CASE 1
			CALL アビ汎用変数文字列リセット
			WSTR:(K++) = 「真理は…オレの手にある！」

			アビテンプレート用_アビ名 = ワールズアンサー
			
			FOR LOCAL, 0, 41
				CALL アビ対象選択テンプレート_味方ランダムＸ体(1)
				アビ汎用変数:効果割合 = 30
				SELECTCASE RAND:6
					CASE 0
						CALL アビテンプレート_ダメージ処理_火属性
					CASE 1
						CALL アビテンプレート_ダメージ処理_水属性
					CASE 2
						CALL アビテンプレート_ダメージ処理_土属性
					CASE 3
						CALL アビテンプレート_ダメージ処理_風属性
					CASE 4
						CALL アビテンプレート_ダメージ処理_光属性
					CASE 5
						CALL アビテンプレート_ダメージ処理_闇属性
				ENDSELECT
			NEXT
			
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用変数:効果量 = 50
			アビ汎用文字列:バフ・デバフ枠 = DA率
			アビ汎用文字列:バフ・デバフ対象能力 = DA率
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 100
			CALL アビテンプレート_不利効果_デバフ効果セット
			
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 50
			アビ汎用文字列:バフ・デバフ枠 = TA率
			アビ汎用文字列:バフ・デバフ対象能力 = TA率
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 100
			CALL アビテンプレート_不利効果_デバフ効果セット
	ENDSELECT
ENDIF



@固有戦闘開始時処理_エネミー_Lv100_ザ・ワールド()
SELECTCASE RAND:2
	CASE 0
		WSTR:(K++) = 「試練の先にこそ……！」
	CASE 1
		WSTR:(K++) = 「新世界の礎となれ……！」
ENDSELECT
	
アビテンプレート用_アビ名 = プロジェクション・ワールド

CALL アビ対象選択テンプレート_自己のみ
アビ汎用変数:特殊表示オプション = 1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:効果量 = 0
アビ汎用文字列:バフ・デバフ枠 = プロジェクション・ワールド
アビ汎用文字列:バフ・デバフ対象能力 = プロジェクション・ワールド
アビ汎用変数:持続ターン = 2
CALL アビテンプレート_有利効果_バフ効果セット



@バフ・デバフ特殊表示_プロジェクション・ワールド(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

SETCOLOR カラーパレット("紫")
PRINTFORML ⚠予兆発生中！⚠
RESETCOLOR
PRINTFORML プロジェクション・ワールドが発動している状態［2ターン/消去不可］
PRINTFORML ◆攻撃行動を行わないが、ターン終了時にザ・ワールドの攻撃力と防御力が累積上昇［永続/消去不可］
SETCOLOR カラーパレット("紫")
PRINTFORML ⚔25000ダメージ or 効果ターン終了で解除
RESETCOLOR



@固有行動開始時処理_バフ_プロジェクション・ワールド(バフ・デバフ行数)
#DIM バフ・デバフ行数

IF 戦闘中カウント:10:被ダメ合計 > 500
	WSTR:(K++) = プロジェクション・ワールドが解除された！
	
	CALL バフ削除_枠(戦闘行動キャラ, "プロジェクション・ワールド")
ELSE
	WSTR:(K++) = ザ・ワールドは蒼空を新世界へと塗り替えている…
	行動不能フラグ = 1
ENDIF



@固有ターン終了時処理_エネミー_Lv100_ザ・ワールド()
#DIM プロジェクション・ワールド

プロジェクション・ワールド = バフ番号取得_枠("プロジェクション・ワールド", 10)
IF プロジェクション・ワールド > -1
	IF 戦闘中カウント:10:被ダメ合計 > 500
		WSTR:(K++) = プロジェクション・ワールドが解除された！
		
		CALL バフ削除_枠(戦闘行動キャラ, "プロジェクション・ワールド")
	ELSE
		アビテンプレート用_アビ名 = プロジェクション・ワールド
		
		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果割合 = 50
		アビ汎用変数:累積上限_割合 = 100
		アビ汎用変数:持続ターン = -1
		アビ汎用変数:消去不可オプション = 1
		
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力_P・W
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		CALL アビテンプレート_有利効果_累積バフ効果セット
		
		アビ汎用文字列:バフ・デバフ枠 = 防御力_P・W
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		CALL アビテンプレート_有利効果_累積バフ効果セット
	ENDIF
ENDIF



@バフ・デバフ特殊表示_ヴァース・クライシス(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

SETCOLOR カラーパレット("黄")
PRINTFORML ⚠予兆発生中！⚠
RESETCOLOR
PRINTFORML ヴァース・クライシスを構えている状態［消去不可］
PRINTFORML ◆味方全体に現在HP70％分のダメージ/強化効果を全て消去・奥義ゲージDOWN効果
SETCOLOR カラーパレット("黄")
PRINTFORML ⚔20000ダメージで解除
RESETCOLOR



@バフ・デバフ特殊表示_スターリー・ノヴァ(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

SETCOLOR カラーパレット("紫")
PRINTFORML ⚠予兆発生中！⚠
RESETCOLOR
PRINTFORML スターリー・ノヴァを唱えている状態［消去不可］
PRINTFORML ◆味方全体にランダム属性4倍ダメージ/ザ・ワールドに与ダメ増加効果・TA率UP効果
SETCOLOR カラーパレット("紫")
PRINTFORML ⚔10000ダメージで解除
RESETCOLOR



@戦闘画面表示_Lv100_ザ・ワールド(レイヤー番号, 隊列番号)
#DIM レイヤー番号
#DIM 隊列番号
#DIM プロジェクション・ワールド
#DIM ヴァース・クライシス
#DIM スターリー・ノヴァ

プロジェクション・ワールド = バフ番号取得_枠("プロジェクション・ワールド", 10)
IF プロジェクション・ワールド > -1
	GSETBRUSH レイヤー番号, 0xFFC070C0
	GDRAWTEXT レイヤー番号, "⚠プロジェクション・ワールド", 10, 70
ENDIF

ヴァース・クライシス = バフ番号取得_枠("ヴァース・クライシス", 10)
IF ヴァース・クライシス > -1
	GSETBRUSH レイヤー番号, 0xFFFFD900
	GDRAWTEXT レイヤー番号, "⚠ヴァース・クライシス", 10, 70
ENDIF

スターリー・ノヴァ = バフ番号取得_枠("スターリー・ノヴァ", 10)
IF スターリー・ノヴァ > -1
	GSETBRUSH レイヤー番号, 0xFFC070C0
	GDRAWTEXT レイヤー番号, "⚠スターリー・ノヴァ", 10, 70
ENDIF

