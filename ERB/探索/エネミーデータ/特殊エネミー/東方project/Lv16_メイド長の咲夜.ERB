
;コンセプト：攻撃デバフとかばうで攻撃を捌いてもらう
;なので攻撃力は高いが単体攻撃しかしないしHPと防御も低め
;霊夢と魔理沙の助力もあるので、デバフさえ入れればどうにかなるはず

@敵BATTLE_STATE_Lv16_メイド長の咲夜(ARG)
敵BATTLE_STATE:ARG:Lv = 16
敵BATTLE_STATE:ARG:最大ＨＰ = 15500
敵BATTLE_STATE:ARG:ＨＰ = 15500
敵BATTLE_STATE:ARG:攻撃力 = 700
敵BATTLE_STATE:ARG:防御力 = 490
敵BATTLE_STATE:ARG:回復力 = 0
敵BATTLE_STATE:ARG:行動速度 = 99999
敵BATTLE_STATE:ARG:DA率 = 50
敵BATTLE_STATE:ARG:TA率 = 50
敵BATTLE_STATE:ARG:命中率 = 100
敵BATTLE_STATE:ARG:回避率 = 5
敵BATTLE_STATE:ARG:属性 = 5
敵BATTLE_STATE:ARG:所持EXP = 1335
敵BATTLE_STATE:ARG:所持ルピ = 0
敵BATTLE_STATE:ARG:最大チャージターン = 3

敵BATTLE_STATE:ARG:ボスフラグ = 1
敵BATTLE_STATE:ARG:ランダム不許可フラグ = 1

敵BATTLE_STATE_STR:ARG:種族 = 人型_メイド_人間

@敵トリガー行動_Lv16_メイド長の咲夜
#DIM 攻撃回数
#DIMS 立ち絵_咲夜

;立ち絵リソース記録に必要な画像のリソースを生成する
CALLF 顔グラ表示用文字列関数(キャラ検索("[完全で瀟洒な従者]十六夜咲夜"),100,,,1,1)
立ち絵_咲夜 = %立ち絵リソース記録%


IF GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 2) == 0

	;顔グラ込みのセリフを出す
	WSTR:(K++) = 「ここは私の世界。時も時計も動かない」
	CALL メッセージ欄表示用関数(立ち絵_咲夜,"咲夜",,0)
	CALL 口上変数初期化

	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%のスペルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = ──幻世 「ザ・ワールド」──

	CALL アビ対象選択テンプレート_自己のみ
	アビ汎用文字列:バフ・デバフ枠 = 幻世 「ザ・ワールド」
	アビ汎用文字列:バフ・デバフ対象能力 = 再行動
	アビ汎用変数:効果量 = 2
	アビ汎用変数:持続ターン = -1
	アビ汎用変数:消去不可オプション = 1
	CALL アビテンプレート_有利効果_バフ効果セット

	;トリガーフラグをセット
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 2
ELSEIF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ * 2 / 3 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0) == 0

	;顔グラ込みのセリフを出す
	WSTR:(K++) = 「タネ無し手品は得意技ですわ」
	CALL メッセージ欄表示用関数(立ち絵_咲夜,"咲夜",,0)
	CALL 口上変数初期化

	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%のスペルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = ──奇術 「ミスディレクション」──


	CALL アビ対象選択テンプレート_味方ランダムＸ体(1)
	アビ汎用変数:効果割合 = 150
	CALL アビテンプレート_ダメージ処理_自属性()
	
	CALL アビ汎用変数文字列リセット
	CALL アビ対象選択テンプレート_自己のみ()
	CALL アビテンプレート_有利効果_デバフ消去()
	CALL アビテンプレート_有利効果_デバフ消去()

	;トリガーフラグをセット
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 0
	;チャージリセット
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0
	
ELSEIF 敵BATTLE_STATE:(戦闘行動キャラ - 10):ＨＰ < 敵BATTLE_STATE:(戦闘行動キャラ - 10):最大ＨＰ * 1 / 3 && GETBIT(敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1) == 0

	;顔グラ込みのセリフを出す
	WSTR:(K++) = 「お嬢様に怒られる前に、せめて１人でも潰させないと」
	CALL メッセージ欄表示用関数(立ち絵_咲夜,"咲夜",,0)
	CALL 口上変数初期化

	アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%のスペルカード宣言！
	アビ汎用文字列:実行時メッセージ２ = ──傷魂「ソウルスカルプチュア」──

	CALL アビ対象選択テンプレート_味方ランダムＸ体(1)
	アビ汎用変数:効果割合 = 100
	FOR 攻撃回数, 0, 20
		CALL アビテンプレート_ダメージ処理_自属性()
	NEXT

	;トリガーフラグをセット
	SETBIT 敵BATTLE_STATE:(戦闘行動キャラ - 10):トリガー行動フラグ, 1
	;チャージリセット
	敵BATTLE_STATE:(戦闘行動キャラ - 10):チャージターン = 0
ELSE
	RETURN 0
ENDIF

RETURN 1

@敵チャージ行動_Lv16_メイド長の咲夜
#DIM 攻撃回数

アビ汎用文字列:実行時メッセージ１ = %敵BATTLE_STATE_STR:(戦闘行動キャラ - 10):エネミー名%のスペルカード宣言！
アビ汎用文字列:実行時メッセージ２ = ──メイド秘技 「操りドール」──

FOR 攻撃回数, 0, 5
	CALL アビ対象選択テンプレート_味方ランダムＸ体(1)
	アビ汎用変数:効果割合 = 90
	CALL アビテンプレート_ダメージ処理_自属性()
NEXT

