
@簡易アニメ作成(アニメフレームID,画像ID,横位置,縦位置,倍率,透明度)
;座標、拡大縮小の入力サポート
#DIM アニメフレームID
#DIM 画像ID
#DIM 横位置
#DIM 縦位置
#DIM 倍率
#DIM 透明度
#DIM レイヤー番号
レイヤー番号 = 50

CALL カットイン用カラーマトリクス取得(透明度)
GDRAWG レイヤー番号 + アニメフレームID, レイヤー番号 + 20 + 画像ID, GWIDTH(レイヤー番号 + アニメフレームID) * 横位置 / 100, GHEIGHT(レイヤー番号 + アニメフレームID) * 縦位置 / 100, GWIDTH(レイヤー番号 + アニメフレームID) * 倍率 / 100, GHEIGHT(レイヤー番号 + アニメフレームID) * 倍率 / 100, 0, 0, GWIDTH(レイヤー番号 + 20 + 画像ID), GHEIGHT(レイヤー番号 + 20 + 画像ID), カットイン用カラーマトリクス:0:0


@グラフィックトリミング(レイヤー番号, 目標横幅, 目標縦幅)
#DIM 目標横幅
#DIM 目標縦幅
#DIM レイヤー番号
;TEMP_gID 作業用レイヤー
;任意のサイズの画像を目的のサイズに加工。目標アスペクト比に合わせて両端から切り取る。
;GRAPHICデータのまま取り扱いたいとき向け

;どちらが余っているかを判断する処理
IF 100 * 目標横幅 / 目標縦幅 > 100 * GWIDTH (レイヤー番号) / GHEIGHT(レイヤー番号)
	;目的アス比が元のアス比より横に長い場合、元の画像は縦を（）切り取る
	GDISPOSE TEMP_gID
	GCREATE TEMP_gID, GWIDTH (レイヤー番号), GHEIGHT(レイヤー番号)
	GDRAWG TEMP_gID, レイヤー番号, 0, GHEIGHT(レイヤー番号) - (GWIDTH (レイヤー番号) * 目標縦幅 / 目標横幅), GWIDTH (レイヤー番号), GHEIGHT(レイヤー番号), 0, (GHEIGHT(レイヤー番号) - (GWIDTH (レイヤー番号) * 目標縦幅 / 目標横幅)) / 2, GWIDTH (レイヤー番号), GHEIGHT(レイヤー番号)

	GDISPOSE レイヤー番号
	GCREATE レイヤー番号, 目標横幅, 目標縦幅
	GDRAWG レイヤー番号, TEMP_gID, 0, 0, 目標横幅, (GHEIGHT(TEMP_gID) * 目標横幅 / GWIDTH (TEMP_gID)), 0, GHEIGHT(TEMP_gID) - (GWIDTH (TEMP_gID) * 目標縦幅 / 目標横幅), GWIDTH (TEMP_gID), GHEIGHT(TEMP_gID)

ELSEIF 100 * 目標横幅 / 目標縦幅 < 100 * GWIDTH (レイヤー番号) / GHEIGHT(レイヤー番号)
	;目的アス比が元のアス比より縦に長い場合、元の画像は横を（）に切り取る
	GDISPOSE TEMP_gID
	GCREATE TEMP_gID, GWIDTH (レイヤー番号), GHEIGHT(レイヤー番号)
	GDRAWG TEMP_gID, レイヤー番号, GWIDTH (レイヤー番号) - (GHEIGHT(レイヤー番号) * 目標横幅 / 目標縦幅), 0, GWIDTH (レイヤー番号), GHEIGHT(レイヤー番号), (GWIDTH (レイヤー番号) - (GHEIGHT(レイヤー番号) * 目標横幅 / 目標縦幅)) / 2, 0, GWIDTH (レイヤー番号), GHEIGHT(レイヤー番号)

	GDISPOSE レイヤー番号
	GCREATE レイヤー番号, 目標横幅, 目標縦幅
	GDRAWG レイヤー番号, TEMP_gID, 0, 0, (GWIDTH (TEMP_gID) * 目標縦幅 / GHEIGHT(TEMP_gID)), 目標縦幅, GWIDTH (TEMP_gID) - (GHEIGHT(TEMP_gID) * 目標横幅 / 目標縦幅), 0,  GWIDTH (TEMP_gID), GHEIGHT(TEMP_gID)
ENDIF
	;両方のアスペクト比が一致したときは何もしない


@カットイン用カラーマトリクス取得(ARG)
;カットイン用カラーマトリクス ,5 ,5
;透明度を指定する
;256なら濃度等倍で透過しない

カットイン用カラーマトリクス:0:0 = 256,  0,  0,  0,  0;R
カットイン用カラーマトリクス:1:0 =   0,256,  0,  0,  0;G
カットイン用カラーマトリクス:2:0 =   0,  0,256,  0,  0;B
カットイン用カラーマトリクス:3:0 =   0,  0,  0, 256 * (100 - ARG) / 100,  0;A
カットイン用カラーマトリクス:4:0 =   0,  0,  0,  0,256;平行


