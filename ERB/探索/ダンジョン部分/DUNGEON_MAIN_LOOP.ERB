
@DUNGEON_MAIN_LOOP(ARGS)

ダンジョン名 = %ARGS%

;ダンジョン入場時処理
ダンジョン表示モード = マップ画面
ダンジョン終了フラグ = 0
;情報初期化
VARSET DUNGEON_MAP:0
VARSET DUNGEON_MAP_MASK:0
VARSET ダンジョンイベント:0:0
CALLFORM ダンジョン初期処理_%ダンジョン名%
選択用変数 = -1
キャラ画像ボタン化フラグ = 1
CALL 探索用顔グラフィック生成


;ステータス代入
CALL 戦闘初期処理_味方(1)


;ダンジョンメインループ
$DUNGEON_LOOP
CALL 口上変数初期化
; CALL DUNGEON_MAP描画
; CALL DUNGEON_キャラ描画
; CALL ダンジョン画像表示部分
VARSET LOCAL
FOR LOCAL, 0, 50
	;アイコン種類が０＝その番号のイベントは存在しない扱い
	SIF ダンジョンイベント:LOCAL:アイコン種類 == 0
		CONTINUE
	SIF ダンジョンイベント:LOCAL:終了フラグ
		CONTINUE
	IF ダンジョンイベント:LOCAL:X軸位置 == ダンジョン現在位置:0 && ダンジョンイベント:LOCAL:Y軸位置 == ダンジョン現在位置:1
		CALLFORM ダンジョンイベント_%ダンジョン名%_{現在階層変数}_{LOCAL}
		LOCAL:1 = 1
	ENDIF
	IF ダンジョンイベント:LOCAL:移動タイプ > 0
		CALL ダンジョンイベント_移動処理(LOCAL)
	ENDIF
	IF LOCAL:1
		;既に何らかのイベント処理をした場合は飛ばす
	ELSEIF ダンジョンイベント:LOCAL:X軸位置 == ダンジョン現在位置:0 && ダンジョンイベント:LOCAL:Y軸位置 == ダンジョン現在位置:1
		;イベントの移動後に位置が重なった時用にもう一度呼ぶ
		CALLFORM ダンジョンイベント_%ダンジョン名%_{現在階層変数}_{LOCAL}
	ENDIF
NEXT
IF ダンジョン終了フラグ == 1
	CALL ダンジョン終了時処理
	RETURN
ENDIF
$描画_LOOP
CALL 画面再描画
CALLFORM DUNGEON_操作処理_%ダンジョン表示モード%
IF RESULT == -1
	GOTO 描画_LOOP
ENDIF
IF ダンジョン終了フラグ == 1
	CALL ダンジョン終了時処理
	RETURN
ENDIF

GOTO DUNGEON_LOOP


@探索用顔グラフィック生成
;画像リソースを全解放
SPRITEDISPOSEALL 0
;ダンジョン中の顔グラフィックを生成
FOR LOCAL, 0, 4
	GDISPOSE 2000 + LOCAL
	SIF BATTLE_STATE:LOCAL:キャラ番号 < 1
		BREAK
	LOCAL:1 = NO:(BATTLE_STATE:LOCAL:キャラ番号)
	IF CSTR:(BATTLE_STATE:LOCAL:キャラ番号):顔グラ表示切り替え用文字列 == ""
		GCREATEFROMFILE 2000 + LOCAL, @"{LOCAL:1}%CALLNAME:(BATTLE_STATE:LOCAL:キャラ番号)%/顔_デフォルト.png"
	ELSE
		GCREATEFROMFILE 2000 + LOCAL, @"{LOCAL:1}%CALLNAME:(BATTLE_STATE:LOCAL:キャラ番号)%/%CSTR:(BATTLE_STATE:LOCAL:キャラ番号):顔グラ表示切り替え用文字列%.png"
	ENDIF
	IF RESULT == 0
		;該当画像が無い場合
		IF CSTR:(BATTLE_STATE:LOCAL:キャラ番号):顔グラ表示切り替え用文字列 == ""
			;そもそもデフォルト画像が無い場合はなんもしない
			RETURN 0
		ELSE
			;変更後画像が無い場合はデフォルト顔を表示させる
			GCREATEFROMFILE 2000 + LOCAL, @"{LOCAL:1}%CALLNAME:(BATTLE_STATE:LOCAL:キャラ番号)%/顔_デフォルト.png"
		ENDIF
	ENDIF
	CALLF スプライト作成(@"探索用_{LOCAL}_顔", 2000 + LOCAL, 0)

	;SPRITECREATE @"探索用_{LOCAL}_顔", 2000 + LOCAL
NEXT


@ダンジョン終了時処理
;画像リソースを全解放
SPRITEDISPOSEALL 0
