
@ダンジョン突入アニメ
#DIM CONST レイヤー番号 = 50
#DIM CONST 画像位置 = 22, 248
#DIM CONST 文字位置 = 22, 350
#DIM CONST 画像位置キャラ0 = 542, 216
#DIM CONST 画像位置キャラ1 = 542, 342
#DIM CONST 画像位置キャラ2 = 542, 469
#DIM CONST 画像位置キャラ3 = 542, 596

;非表示オプション対応する
IF カットイン演出一括表示フラグ
	;非表示
	RETURN
ENDIF


GCREATEFROMFILE レイヤー番号 + 15 , "ダンジョン背景/" + ダンジョン背景画像 + ".png"
;背景を保管
GDISPOSE レイヤー番号 + 16 
GCREATE レイヤー番号 + 16 , 1600, 900
GSETBRUSH レイヤー番号 + 16 , 0xFF101010
GFILLRECTANGLE レイヤー番号 + 16 , 0, 0, 1600, 900
GDRAWG レイヤー番号 + 16 , レイヤー番号 + 15 , 画像位置:0, 画像位置:1, GWIDTH(レイヤー番号 + 15 ), GHEIGHT(レイヤー番号 + 15 ) - 20, 0, 0, GWIDTH(レイヤー番号 + 15 ), GHEIGHT(レイヤー番号 + 15 ) - 20
;背景フェードイン
FOR LOCAL, 0, 10
CBGREMOVERANGE -1, -1
GDISPOSE レイヤー番号 + 1 
GCREATE レイヤー番号 + 1 , 1600, 900
CALL カットイン用カラーマトリクス取得(90 - LOCAL * 10)
GDRAWG レイヤー番号 + 1 , レイヤー番号 + 16 , 0, 0, 1600, 900, 0, 0, 1600, 900, カットイン用カラーマトリクス:0:0
CBGSETG レイヤー番号 + 1, 0, 0, -1
AWAIT 1
NEXT

;顔スライド、GID 2000 + LOCAL のリソースを引用する
FOR LOCAL, 0, 35
    CBGSETG レイヤー番号 + 2 , 0, 0, -2
    AWAIT 1
    CBGREMOVERANGE -2, -2
    GDISPOSE レイヤー番号 + 2 
    GCREATE レイヤー番号 + 2 , 1600, 900
    IF LOCAL > 25
        GDRAWG レイヤー番号 + 2 , 2003, 画像位置キャラ3:0 + 10, 画像位置キャラ3:1 + 10, GWIDTH(2003) * 100 / 180, GHEIGHT(2003) * 100 / 180, 0, 0, GWIDTH(2003), GHEIGHT(2003)
    ELSEIF LOCAL > 15
        CALL カットイン用カラーマトリクス取得(10 * (25 - LOCAL))
        GDRAWG レイヤー番号 + 2 , 2003, 画像位置キャラ3:0 + 2 * (25 - LOCAL) + 10, 画像位置キャラ3:1 + 10, GWIDTH(2003) * 100 / 180, GHEIGHT(2003) * 100 / 180, 0, 0, GWIDTH(2003), GHEIGHT(2003), カットイン用カラーマトリクス:0:0
    ENDIF
    IF LOCAL > 20
        GDRAWG レイヤー番号 + 2 , 2002, 画像位置キャラ2:0 + 10, 画像位置キャラ2:1 + 10, GWIDTH(2002) * 100 / 180, GHEIGHT(2002) * 100 / 180, 0, 0, GWIDTH(2002), GHEIGHT(2002)
    ELSEIF LOCAL > 10
        CALL カットイン用カラーマトリクス取得(10 * (20 - LOCAL))
        GDRAWG レイヤー番号 + 2 , 2002, 画像位置キャラ2:0 + 2 * (20 - LOCAL) + 10, 画像位置キャラ2:1 + 10, GWIDTH(2002) * 100 / 180, GHEIGHT(2002) * 100 / 180, 0, 0, GWIDTH(2002), GHEIGHT(2002), カットイン用カラーマトリクス:0:0
    ENDIF
    IF LOCAL > 15
        GDRAWG レイヤー番号 + 2 , 2001, 画像位置キャラ1:0 + 10, 画像位置キャラ1:1 + 10, GWIDTH(2001) * 100 / 180, GHEIGHT(2001) * 100 / 180, 0, 0, GWIDTH(2001), GHEIGHT(2001)
    ELSEIF LOCAL > 5
        CALL カットイン用カラーマトリクス取得(10 * (15 - LOCAL))
        GDRAWG レイヤー番号 + 2 , 2001, 画像位置キャラ1:0 + 2 * (15 - LOCAL) + 10, 画像位置キャラ1:1 + 10, GWIDTH(2001) * 100 / 180, GHEIGHT(2001) * 100 / 180, 0, 0, GWIDTH(2001), GHEIGHT(2002), カットイン用カラーマトリクス:0:0
    ENDIF
    IF LOCAL > 10
        GDRAWG レイヤー番号 + 2 , 2000, 画像位置キャラ0:0 + 10, 画像位置キャラ0:1 + 10, GWIDTH(2000) * 100 / 180, GHEIGHT(2000) * 100 / 180, 0, 0, GWIDTH(2000), GHEIGHT(2000)
    ELSE
        CALL カットイン用カラーマトリクス取得(10 * (10 - LOCAL))
        GDRAWG レイヤー番号 + 2 , 2000, 画像位置キャラ0:0 + 2 * (10 - LOCAL) + 10, 画像位置キャラ0:1 + 10, GWIDTH(2000) * 100 / 180, GHEIGHT(2000) * 100 / 180, 0, 0, GWIDTH(2000), GHEIGHT(2000), カットイン用カラーマトリクス:0:0
    ENDIF
NEXT
CBGSETG レイヤー番号 + 2 , 0, 0, -2

;テキストレイヤーの作成
GDISPOSE レイヤー番号 + 17 
GCREATE レイヤー番号 + 17 , 1600, 900
GSETBRUSH レイヤー番号 + 17 , 0xFFC0C0C0

{
    ;----------------------------攻略目標の表示したい
    ;FONT +4 で取消し線
    ;メインクエスト
IF GETVAR("ダンジョンクリアフラグ_" + ダンジョン名)
    GSETFONT レイヤー番号 + 17 , GETFONT(), 18, 4
ELSE
    GSETFONT レイヤー番号 + 17 , GETFONT(), 18, 0
ENDIF
GSETPEN レイヤー番号 + 17 , 0xFFF0F0F0, 1
GDRAWTEXT レイヤー番号 + 17 , "メインクエスト：人食い鳥の掃討", 文字位置:0 + 100, 文字位置:1 + 108
    ;二行目座標メモ、フォントは個別に指定する
GDRAWTEXT レイヤー番号 + 17 , "サブクエスト：水のエレメント１０体の討伐", 文字位置:0 + 100, 文字位置:1 + 140
}

;今はタイトルだけなのでY座標を下にずらしておく
GSETFONT レイヤー番号 + 17 , GETFONT(), 90, 11
GSETPEN レイヤー番号 + 17 , 0xFF101010, 1
GDRAWTEXT レイヤー番号 + 17 , ダンジョン名, 文字位置:0, 文字位置:1 + 50
;テキストレイヤーフェードイン
FOR LOCAL, 0, 5
    GDISPOSE レイヤー番号 + 3 
    GCREATE レイヤー番号 + 3 , 1600, 900
    CALL カットイン用カラーマトリクス取得(50 - LOCAL * 10)
    GDRAWG レイヤー番号 + 3 , レイヤー番号 + 17 , 0, 0, 1600, 900, 0, 0, 1600, 900, カットイン用カラーマトリクス:0:0
    CBGSETG レイヤー番号 + 3 , 0, 0, -3
    AWAIT 50
NEXT

;フェードアウト用に全要素を統合したグラフィックを用意する
GDISPOSE レイヤー番号 + 0 
GCREATE レイヤー番号 + 0 , 1600, 900
GDRAWG レイヤー番号 + 0 , レイヤー番号 + 16 , 0, 0, 1600, 900, 0, 0, 1600, 900
GDRAWG レイヤー番号 + 0 , レイヤー番号 + 2 , 0, 0, 1600, 900, 0, 0, 1600, 900
GDRAWG レイヤー番号 + 0 , レイヤー番号 + 17 , 0, 0, 1600, 900, 0, 0, 1600, 900


;裏で画像切り替えをする
CALL 画面再描画

AWAIT 400

;統合前画像を削除
CBGREMOVERANGE -3, -1
;統合画像をフェードアウトさせる
FOR LOCAL, 0, 10
    CBGREMOVERANGE -1, -1
    GDISPOSE レイヤー番号 + 1
    GCREATE レイヤー番号 + 1 , 1600, 900
    CALL カットイン用カラーマトリクス取得(LOCAL * 10)
    GDRAWG レイヤー番号 + 1 , レイヤー番号 + 0 , 0, 0, 1600, 900, 0, 0, 1600, 900, カットイン用カラーマトリクス:0:0
    CBGSETG レイヤー番号 + 1 , 0, 0, -1
    AWAIT 10
NEXT

;リソースの解放
CBGREMOVERANGE -1, -1
GDISPOSE レイヤー番号 + 0 
GDISPOSE レイヤー番号 + 1 
GDISPOSE レイヤー番号 + 2 
GDISPOSE レイヤー番号 + 3 
GDISPOSE レイヤー番号 + 15 
GDISPOSE レイヤー番号 + 16 
GDISPOSE レイヤー番号 + 17 

