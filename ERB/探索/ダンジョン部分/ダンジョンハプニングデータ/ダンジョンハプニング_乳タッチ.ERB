@ダンジョンハプニング_乳タッチ(選定対象変数名 = "", 選定条件数値)
#DIMS 選定対象変数名
#DIM 選定条件数値
#DIM 対象キャラ番号
VARSET LOCAL

;条件なしなのでパーティー全員からランダム
FOR LOCAL, 0, 4
	IF BATTLE_STATE:LOCAL:0 > 0
		LOCAL:1 += 1
	ENDIF
NEXT
対象キャラ番号 = RAND:(LOCAL:1)

CALL 口上変数初期化
WSTR:(K++) = %CALLNAME:MASTER%は何かに躓いてしまった！
KSTR:(K++) = 幸いにも%CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%が受け止めてくれたが、
SELECTCASE TALENT:(BATTLE_STATE:対象キャラ番号:0):バストサイズ
	CASE IS <= 0
		KSTR:(K++) = その拍子に、胸の部分に手が当たってしまった。_PAGE
	CASEELSE
		KSTR:(K++) = その拍子に、豊かな胸を鷲掴みしてしまった。_PAGE
ENDSELECT

CALL セクハラ反応タイプ判定(BATTLE_STATE:対象キャラ番号:0)
TRYCCALLFORM KOJO_ハプニング_乳タッチ_{NO:(BATTLE_STATE:対象キャラ番号:0)}
CATCH
	KSTR:(K++) = %CALLNAME:MASTER%はすぐに離れて謝罪したが、
	SELECTCASE TFLAG:コマンド結果受渡し変数２
		CASE 1
			;無知
			KSTR:(K++) = %CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%は特に何も気にしていないようだ。
		CASE 2
			;無反応
			KSTR:(K++) = %CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%は何もなかったかのように探索に戻った。
		CASE 3
			;嫌悪
			SELECTCASE TFLAG:コマンド結果受渡し変数
				CASE 1
					;拒絶
					KSTR:(K++) = %CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%は嫌そうな顔をして%CALLNAME:MASTER%を睨んでいる……
				CASE 2
					;受入れ
					KSTR:(K++) = %CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%はため息を吐いて探索に戻った。
				CASE 3
					;喜び
					KSTR:(K++) = %CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%は%CALLNAME:MASTER%に怪我はないか心配してくれている。
			ENDSELECT
		CASE 4
			;拒否
			SELECTCASE TFLAG:コマンド結果受渡し変数
				CASE 1
					;拒絶
					KSTR:(K++) = %CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%は厳しい顔で足元に気を付けるようにと注意している。
				CASE 2
					;受入れ
					KSTR:(K++) = %CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%は%CALLNAME:MASTER%に怪我はないか心配してくれている。
				CASE 3
					;喜び
					KSTR:(K++) = %CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%は転ばないよう手を繋がないか、と提案している。
			ENDSELECT
		CASE 5
			;余裕
			SELECTCASE TFLAG:コマンド結果受渡し変数
				CASE 1
					;拒絶
					KSTR:(K++) = %CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%は厳しい顔で足元に気を付けるようにと注意している。
				CASE 2
					;受入れ
					KSTR:(K++) = %CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%は気にしなくてもいいと微笑んだ。
				CASE 3
					;喜び
					KSTR:(K++) = %CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%はもっと触ってもいいけれど、と冗談を言っている。
			ENDSELECT
		CASE 6
			;羞恥
			SELECTCASE TFLAG:コマンド結果受渡し変数
				CASE 1
					;拒絶
					KSTR:(K++) = %CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%は顔を真っ赤にして飛びのいた。
				CASE 2
					;受入れ
					KSTR:(K++) = %CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%は真っ赤な顔で胸を抑えている。
				CASE 3
					;喜び
					KSTR:(K++) = %CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%は顔を赤く染めて、こちらを見つめている……
			ENDSELECT
		CASE 7
			;愉悦
			SELECTCASE TFLAG:コマンド結果受渡し変数
				CASE 1
					;拒絶
					KSTR:(K++) = %CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%は触り方がなってないと冗談を言っている。
				CASE 2
					;受入れ
					KSTR:(K++) = %CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%は大げさに離れすぎだと笑っている。
				CASE 3
					;喜び
					KSTR:(K++) = %CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%は触りたいならいつでもいいのに、と妖艶に笑っている。
			ENDSELECT
		CASE 8
			;汎用
			SELECTCASE TFLAG:コマンド結果受渡し変数
				CASE 1
					;拒絶
					KSTR:(K++) = %CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%は少し不快そうな顔をして探索に戻った。
				CASE 2
					;受入れ
					KSTR:(K++) = %CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%は苦笑しながら着衣を整えている。
				CASE 3
					;喜び
					KSTR:(K++) = %CALLNAME:(BATTLE_STATE:対象キャラ番号:0)%は%CALLNAME:MASTER%に怪我はないか心配してくれている。
			ENDSELECT
	ENDSELECT
	CALL メッセージ欄表示用関数()
ENDCATCH

;ソース増減部分
SELECTCASE TFLAG:コマンド結果受渡し変数
	CASE 1
		;拒絶
		CALL SOURCE_CALC_不快((BATTLE_STATE:対象キャラ番号:0), PLAYER, 50)
		CALL SOURCE_CALC_反感((BATTLE_STATE:対象キャラ番号:0), PLAYER, 50)
		CALL SOURCE_CALC_接触((BATTLE_STATE:対象キャラ番号:0), PLAYER, 100)
		CALL SOURCE_CALC_露出((BATTLE_STATE:対象キャラ番号:0), PLAYER, 100)
	CASE 2
		;受け入れ
		CALL SOURCE_CALC_歓楽((BATTLE_STATE:対象キャラ番号:0), PLAYER, 20)
		CALL SOURCE_CALC_接触((BATTLE_STATE:対象キャラ番号:0), PLAYER, 100)
		CALL SOURCE_CALC_露出((BATTLE_STATE:対象キャラ番号:0), PLAYER, 100)
		CALL SOURCE_CALC_好感度要素_性愛度UP((BATTLE_STATE:対象キャラ番号:0), PLAYER, 10)
		CALL SOURCE_CALC_快Ｂ((BATTLE_STATE:対象キャラ番号:0), PLAYER, 10)
	CASE 3
		;喜び
		CALL SOURCE_CALC_歓楽((BATTLE_STATE:対象キャラ番号:0), PLAYER, 50)
		CALL SOURCE_CALC_接触((BATTLE_STATE:対象キャラ番号:0), PLAYER, 100)
		CALL SOURCE_CALC_露出((BATTLE_STATE:対象キャラ番号:0), PLAYER, 100)
		CALL SOURCE_CALC_好感度要素_性愛度UP((BATTLE_STATE:対象キャラ番号:0), PLAYER, 50)
		CALL SOURCE_CALC_快Ｂ((BATTLE_STATE:対象キャラ番号:0), PLAYER, 10)
ENDSELECT

CALL ダンジョン内_SOURCE_CHECK(BATTLE_STATE:対象キャラ番号:0)

;なにかに使えるかもしれないので関数の終わりに対象キャラ番号を返す
RETURN 対象キャラ番号
