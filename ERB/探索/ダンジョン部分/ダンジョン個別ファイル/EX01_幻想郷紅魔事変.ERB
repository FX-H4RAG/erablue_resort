@固有戦闘開始時処理_特殊_霊夢の助力_紅魔事変()
;ここ２行はエラー回避のためのもの。おまじない程度においておくこと
戦闘行動キャラ = 0
CALL アビ対象選択テンプレート_自己のみ
;バフ付け文章は出さないので1を入れておく
アビテンプレート用_アビ名表示済フラグ = 1
;大元のバフセット（効果なし）
CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 霊夢の助力_紅魔事変
アビ汎用文字列:バフ・デバフ対象能力 = 霊夢の助力_紅魔事変
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = 1
アビ汎用文字列:適用範囲 = 味方全体
CALL アビテンプレート_有利効果_バフ効果セット
;回避UP部分
CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 霊夢の助力_紅魔事変_回避率
アビ汎用文字列:バフ・デバフ対象能力 = 回避率
アビ汎用変数:効果量 = 15
アビ汎用文字列:適用範囲 = 味方全体
CALL アビテンプレート_有利効果_付随バフ効果セット("霊夢の助力_紅魔事変")
;カウンター部分
CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 霊夢の助力_紅魔事変_援護攻撃
アビ汎用文字列:バフ・デバフ対象能力 = 特殊反応_回避時
アビ汎用文字列:適用範囲 = 味方全体
CALL アビテンプレート_有利効果_付随バフ効果セット("霊夢の助力_紅魔事変")

@特殊反応_回避時_霊夢の助力_紅魔事変_援護攻撃(バフ番号, 攻撃者隊列, 回避記録順)
#DIM 回避記録順
#DIM 攻撃者隊列
#DIM バフ番号
#DIMS 立ち絵_霊夢

;立ち絵リソース記録に必要な画像のリソースを生成する
CALLF 顔グラ表示用文字列関数(キャラ検索("[楽園の素敵な巫女]博麗霊夢"),100,,,1,1)
立ち絵_霊夢 = %立ち絵リソース記録%

;顔グラ込みのセリフを出す
WSTR:(K++) = 「%TEXTR("そこっ！/隙だらけね/貰ったわ！")%」
CALL メッセージ欄表示用関数(立ち絵_霊夢,"霊夢",,0)
CALL 口上変数初期化

;攻撃処理
CALL アビテンプレ変数リセット
CALL アビ対象選択テンプレート_指定(攻撃者隊列)
アビテンプレート用_アビ名 = 霊夢の援護攻撃

アビ汎用文字列:実行時メッセージ１ = 霊夢の援護攻撃！
アビ汎用変数:効果量 = 500
CALL アビテンプレート_ダメージ処理_光属性

CALL アビテンプレート用_表示メッセージ変換処理
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化

@バフ・デバフ特殊表示_霊夢の助力_紅魔事変(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 霊夢の助力
PRINTFORML 回避率+15％・回避時に霊夢が援護攻撃を放つ［消去不可］
PRINTFORML 持続ターン：永続

@固有戦闘開始時処理_特殊_魔理沙の助力_紅魔事変()
;ここ２行はエラー回避のためのもの。おまじない程度においておくこと
戦闘行動キャラ = 0
CALL アビ対象選択テンプレート_自己のみ
;バフ付け文章は出さないので1を入れておく
アビテンプレート用_アビ名表示済フラグ = 1
;大元のバフセット（効果なし）
CALL アビ汎用変数文字列リセット
アビ汎用文字列:バフ・デバフ枠 = 魔理沙の助力_紅魔事変
アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
アビ汎用変数:効果割合 = 30
アビ汎用変数:持続ターン = -1
アビ汎用変数:消去不可オプション = 1
アビ汎用変数:特殊表示オプション = 1
アビ汎用文字列:適用範囲 = 味方全体
CALL アビテンプレート_有利効果_バフ効果セット

@バフ・デバフ特殊表示_魔理沙の助力_紅魔事変(隊列番号, バフ・デバフ行数)
#DIM 隊列番号
#DIM バフ・デバフ行数
VARSET LOCALS

PRINTFORML 魔理沙の助力
PRINTFORML 攻撃力+30％［消去不可］
PRINTFORML 持続ターン：永続
