
@イベント_リゾート島開拓作業_ランダム_旅の僧侶
#DIM 回復量

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 特に変わった様子はない。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = %CALLNAME:MASTER%たちが探索していると、島の開拓依頼を受けた騎空士と出会った。
	WSTR:(K++) = 騎空士は僧侶を本業としており、依頼を受けて路銀を稼ぎながら旅をしているらしい。
	KSTR:(K++) = 僧侶は厚意で回復魔法を使い、君たちの傷を癒やしてくれた！
	;HPバーの表示タイミングを調整するためここで一旦メッセージ表示
	CALL メッセージ欄表示用関数()
	CALL 口上変数初期化
	
	回復量 = ダンジョン階層記録 * 50
	WSTR:(K++) = 全員のＨＰが{回復量, 4}回復した！
	CALL 探索中全体回復_ＨＰ(回復量)
	WSTR:(K++) = %CALLNAME:MASTER%たちは僧侶にお礼を述べ、探索に戻った。
	CALL メッセージ欄表示用関数(,,,0)
	
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_修行中の格闘家
#DIMS 素材名

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 特に変わった様子はない。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = %CALLNAME:MASTER%たちが探索していると、島の開拓依頼を受けた騎空士と出会った。
	WSTR:(K++) = 騎空士は格闘家で、肉体鍛錬のために色々な依頼を受けているらしい。
	KSTR:(K++) = 格闘家は動きが鈍るから、と狩り過ぎた魔物の素材を%CALLNAME:MASTER%たちに分けてくれた！_PAGE

	素材名 = %ランダム素材名("エネミー素材", RAND(1, 2))%
	WSTR:(K++) = %素材名%を３個手に入れた！
	WSTR:(K++) = %CALLNAME:MASTER%たちは格闘家にお礼を述べ、探索に戻った。
	CALL 素材入手処理(素材名, 3)
	CALL メッセージ欄表示用関数(,,,0)
	
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_見習い戦士
#DIM 経験値量
#DIM レベルアップフラグ

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 特に変わった様子はない。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = %CALLNAME:MASTER%たちが探索していると、島の開拓依頼を受けた騎空士と出会った。
	WSTR:(K++) = 騎空士はまだ見習いの戦士で、魔物との戦いで強くなるために依頼を受けているらしい。
	WSTR:(K++) = 見習いらしく、ところどころ動きが危なっかしい……
	KSTR:(K++) = %CALLNAME:MASTER%たちは見習い戦士へアドバイスと簡単な稽古をつけてあげた。_PAGE

	経験値量 = ダンジョン階層記録 * 100
	WSTR:(K++) = 見習い戦士は喜んで%CALLNAME:MASTER%たちにお礼を言い、探索へ戻っていく。
	WSTR:(K++) = %CALLNAME:MASTER%たちは他人に戦闘の心得を教えたことで{経験値量, 4}の経験値を得た！
	
	レベルアップフラグ = 0
	FOR LOCAL, 0, 14
		SIF LOCAL > 3 && LOCAL < 10
			CONTINUE
		SIF BATTLE_STATE:LOCAL:キャラ登録番号 < 1
			CONTINUE
		基礎BATTLE_STATE:(BATTLE_STATE:LOCAL:キャラ登録番号):蓄積EXP += 経験値量
		CALL レベルアップ処理(BATTLE_STATE:LOCAL:キャラ登録番号)
		SIF レベルアップフラグ < RESULT
			レベルアップフラグ = RESULT
	NEXT
	IF レベルアップフラグ
		;ステータス代入
		CALL 戦闘初期処理_味方
	ENDIF

	CALL メッセージ欄表示用関数(,,,0)
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_ベテラン吟遊詩人

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 特に変わった様子はない。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = %CALLNAME:MASTER%たちが探索していると、島の開拓依頼を受けた騎空士と出会った。
	WSTR:(K++) = 騎空士はベテランの吟遊詩人で、歌のネタを探すために騎空士業と兼業しているのだとか。
	WSTR:(K++) = ここで会ったのもなにかの縁と、吟遊詩人は一曲弾き語りを披露してくれた。
	WNSTR:(K++) = …………
	KSTR:(K++) = 素晴らしい歌だ！　%CALLNAME:MASTER%たちの体に活力が湧いてくる！

	;HPバーの表示タイミングを調整するためここで一旦メッセージ表示
	CALL メッセージ欄表示用関数()
	CALL 口上変数初期化
	
	WSTR:(K++) = 全員の奥義ゲージが３０％増加した！
	CALL 探索中全体回復_奥義ゲージ(30)
	WSTR:(K++) = %CALLNAME:MASTER%たちは吟遊詩人にお礼を述べ、探索に戻った。
	CALL メッセージ欄表示用関数(,,,0)
	
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_さすらいの料理人

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 特に変わった様子はない。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = %CALLNAME:MASTER%たちが探索していると、島の開拓依頼を受けた騎空士と出会った。
	WSTR:(K++) = 騎空士はさすらいの料理人で、新たな料理のレシピを求めて騎空士をしているらしい。
	KSTR:(K++) = 料理人はせっかくだからと、%CALLNAME:MASTER%たちに料理のレシピを教えてくれた！_PAGE

	CALL レシピランダム取得処理()
	IF RESULT == -1
		;レシピ全部知ってる場合
		WSTR:(K++) = しかし、%CALLNAME:MASTER%たちは全てのレシピを知っているようだ……
	ELSE
		WSTR:(K++) = %RESULTS%のレシピを教えてもらった！
	ENDIF
	WSTR:(K++) = %CALLNAME:MASTER%たちは料理人にお礼を述べ、探索に戻った。
	CALL メッセージ欄表示用関数(,,,0)
	
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_休憩出来る地形
#DIM 回復量
#DIM 対象キャラ

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 特に変わった様子はない。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = %CALLNAME:MASTER%たちが探索していると、広場のような開けた場所に出た。
	KSTR:(K++) = 魔物が近づいてきたらすぐわかる地形だ。せっかくだから少し休憩していこう。
	;HPバーの表示タイミングを調整するためここで一旦メッセージ表示
	CALL メッセージ欄表示用関数()
	CALL 口上変数初期化
	
	回復量 = ダンジョン階層記録 * 25
	WSTR:(K++) = 全員のＨＰが{回復量, 4}回復した！
	CALL 探索中全体回復_ＨＰ(回復量)
	
	対象キャラ = 味方ランダム選出_陥落済み()

	IF 対象キャラ > -1
		WSTR:(K++) = ついでに、%CALLNAME:MASTER%は%CALLNAME:(BATTLE_STATE:対象キャラ:0)%と一緒に少し『休憩』をした。
		WSTR:(K++) = ZPを 5入手した！
		FLAG:ZP所持量 += 5
	ENDIF
	WSTR:(K++) = %CALLNAME:MASTER%たちは休憩を切り上げ、探索に戻った。
	CALL メッセージ欄表示用関数(,,,0)
	
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_晶石の採掘場
#DIM 採掘済みフラグ

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 == "イベント完了"
	KSTR:(K++) = 特に変わった様子はない。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	採掘済みフラグ = 0
	WSTR:(K++) = %CALLNAME:MASTER%たちが探索していると、晶石が露出している岩肌を見つけた。
	WSTR:(K++) = ここで採掘を行えば晶石を手に入れられるだろうが、しかし音を聞きつけた魔物が襲ってくるかもしれない。
	$採掘ループ
	KSTR:(K++) = どうしようか？
	KSTR:(K++) = BUTTON_[採掘を行う]
	KSTR:(K++) = BUTTON_[この場を離れる]
	CALL メッセージ欄表示用関数(,,,0)
	CALL 口上変数初期化
	$LOOP_MESSAGE
	BINPUTS
	SELECTCASE RESULTS
		CASE "[採掘を行う]"
			WSTR:(K++) = 多少の危険は問題ない。採掘していこう。
			WSTR:(K++) = …………
			SELECTCASE RAND:(5 - 採掘済みフラグ)
				CASE 0
					KSTR:(K++) = …………採掘の音を聞きつけた魔物が、君たちに襲いかかってきた！
					CALL メッセージ欄表示用関数()
					CALL 口上変数初期化
					
					CALL ランダムエネミーセット関数(4, ダンジョン階層記録)
					CALL 雑魚敵遭遇汎用処理
					
					IF RESULT == 0
						WSTR:(K++) = 魔物を無事に討伐したが、戦闘の余波で周囲の晶石が砕けてしまった。
						WSTR:(K++) = 岩を掘り進めればまだ見つかるかもしれないが、それほど大掛かりな採掘をする暇はない。
						WSTR:(K++) = 残念だが、探索に戻るとしよう。
					ELSEIF RESULT == -1
						WSTR:(K++) = 魔物から無事に逃げおおせた。
						WSTR:(K++) = しばらくあの場所には魔物がうろついているだろう。
						WSTR:(K++) = 残念だが戻るのは危険だ。探索に戻るとしよう。
					ENDIF
				CASEELSE
					WSTR:(K++) = …………立派な晶石を掘り出すことが出来た！　%CALLNAME:PLAYER%たちは星晶塊を一つ手に入れた。
					WSTR:(K++) = さて、まだ周囲には晶石が残っているが……
					採掘済みフラグ ++
					GOTO 採掘ループ
			ENDSELECT
		CASE "[この場を離れる]"
			IF 採掘済みフラグ
				WSTR:(K++) = これ以上の採掘は危険だろう。
			ELSE
				WSTR:(K++) = 危険を冒してまで採掘をする必要もない。
			ENDIF
			WSTR:(K++) = %CALLNAME:PLAYER%たちはこの場を離れ、探索に戻った。
		CASEELSE
			CLEARLINE 1
			REUSELASTLINE  
			GOTO LOOP_MESSAGE
	ENDSELECT
	CALL メッセージ欄表示用関数(,,,0)
	
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):イベント状態 = イベント完了
	CALL 口上変数初期化
ENDIF
CALL 画面再描画
