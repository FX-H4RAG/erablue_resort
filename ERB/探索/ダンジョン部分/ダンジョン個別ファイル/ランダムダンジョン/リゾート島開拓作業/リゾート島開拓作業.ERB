
@ダンジョン初期処理_リゾート島開拓作業
;ダンジョン構造の読み込み
;0でない時、そのマスは存在する
;
#DIM 深度
#DIM 分岐
#DIMS 接続先配列,2

VARSET ダンジョン構造配列_リゾート島開拓作業
VARSET 分割ブロックリスト
VARSET ランダムダンジョン配列
VARSET ダンジョン内一時変数
ランダムダンジョン中フラグ = 1
ランダムダンジョン階層記録 += 1
CALL 穴掘り方式()
CALL 部屋タイプ割り当て_リゾート島開拓作業

ダンジョンサブタイトル = NEVER ENDING DEVELOPMENT
ダンジョンマス画像URL_伏せ状態 = 壁_森
ダンジョン現在位置:0 = ランダムダンジョン初期位置:0
ダンジョン現在位置:1 = ランダムダンジョン初期位置:1
ダンジョン背景画像 = 背景_森

;スタート地点は必ず存在
;初期からオープン済みなので3を入れる
;ビット0：存在するか　ビット1：移動できるか　ビット2：そこから通路を伸ばすか　ビット3：入れないが表示する場合に使用
;ビット4以降：自由枠フラグ

IF GETVAR(@"ダンジョン構造配列_%ダンジョン名%:{ランダムダンジョン初期位置:0}:{ランダムダンジョン初期位置:1}") == 0
	SETVAR @"ダンジョン構造配列_%ダンジョン名%:{ランダムダンジョン初期位置:0}:{ランダムダンジョン初期位置:1}", 3
ENDIF


FOR 深度, 1, 100
	LOCAL = 0
	FOR 分岐, 0, 9
		IF ダンジョン構造配列_リゾート島開拓作業:深度:分岐
			;既にマス情報が入ってるなら飛ばす
			LOCAL = 1
			CONTINUE
		ELSEIF ENUMFUNCBEGINSWITH(@"ダンジョンマス情報_リゾート島開拓作業_{深度}_{分岐}")
			LOCAL = 1
			ダンジョン構造配列_リゾート島開拓作業:深度:分岐 = 1
		ENDIF
	NEXT
	IF LOCAL
		ダンジョン最大深度 = 深度
	ENDIF
NEXT

@ダンジョン解説文_リゾート島開拓作業

詳細文文字列受け渡し変数 = 
詳細文文字列受け渡し変数 += "リゾート島の未開拓地域を制圧し、開拓するためのダンジョン。<br>"
詳細文文字列受け渡し変数 += "自然の力は強く、一回の探索でボスを倒さなければやり直しになってしまうだろう。<br>"
詳細文文字列受け渡し変数 += "その代わり、ボスを倒せば次は続きから始められることに加え、大型施設を作る土地を取得することが出来る。<br>"
詳細文文字列受け渡し変数 += "入るたびに構造が変化する。<br>"
詳細文文字列受け渡し変数 += "階層を登るごとにエネミーが強化される。（およそ適正レベル＝階層数）<br>"

;推奨レベルを返す
RETURN 1

@敵討伐後処理_ダンジョン固有_リゾート島開拓作業(討伐エネミー数)
;ダンジョン内で戦闘した後に呼ばれる関数、主にダンジョンごとのドロップアイテム取得に使用
;まだ敵データはリセットしていないので敵BATTLE_STATEで色々情報は取得可能
#DIM 討伐エネミー数
#DIM ドロップ確率
#DIMS ドロップアイテム

ドロップ確率 = 5 * 討伐エネミー数
ドロップアイテム = 星晶の破片
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 1 * 討伐エネミー数
ドロップアイテム = 赤銅の古紋
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

;----------
;部屋の内容を割り当てる
;----------
@部屋タイプ割り当て_リゾート島開拓作業
#DIM ランダムイベ数
#DIM キャライベ数
#DIMS ランダムイベ名
#DIM 対象キャラ
;部屋の属性を調べて対応する条件の部屋からランダムに割り当てる
;ランダムダンジョン配列:深度:分岐:1 = イベントの名前
#DIM 深度
#DIM 分岐

;固有イベントをチェック
VARSET RESULTS
VARSET LOCALS
LOCAL:1 = 0
キャライベ数 = ENUMFUNCBEGINSWITH("イベント_リゾート島開拓作業_キャラ依頼_")
FOR LOCAL, 0, キャライベ数
	;依頼のキャラを引っ張ってくる
	対象キャラ = TOINT(SUBSTRING(RESULTS:LOCAL, 33))
	SIF 対象キャラ < 1
		CONTINUE
	;キャラが島にいないとダメ
	SIF CFLAG:対象キャラ:滞在期間 < 0
		CONTINUE
	;キャラがPTメンバーにいるとダメ
	SIF GROUPMATCH(対象キャラ, BATTLE_STATE:0:0, BATTLE_STATE:1:0, BATTLE_STATE:2:0, BATTLE_STATE:3:0, BATTLE_STATE:10:0, BATTLE_STATE:11:0, BATTLE_STATE:12:0, BATTLE_STATE:13:0)
		CONTINUE
	LOCALS:(LOCAL:1) = %RESULTS:LOCAL%
	LOCAL:1 += 1
NEXT
キャライベ数 = LOCAL:1

;最初に次のエリアに行くマスを確保
DO
	深度 = RAND:10
	分岐 = RAND:10
	IF ランダムダンジョン配列:深度:分岐:0 != ""
		SIF 深度 == ランダムダンジョン初期位置:0 && 分岐 == ランダムダンジョン初期位置:1
			CONTINUE
		ランダムダンジョン配列:深度:分岐:1 = 次階層
		ランダムダンジョン配列:深度:分岐:2 = 次階層
		BREAK
	ENDIF
LOOP 1

VARSET RESULTS
ランダムイベ数 = ENUMFUNCBEGINSWITH("イベント_リゾート島開拓作業_ランダム_")
FOR 深度, 0, 10
	FOR 分岐, 0, 10
		SIF ランダムダンジョン配列:深度:分岐:0 == ""
			CONTINUE
		SIF ランダムダンジョン配列:深度:分岐:1 != ""
			CONTINUE
		IF 深度 == ランダムダンジョン初期位置:0 && 分岐 == ランダムダンジョン初期位置:1
			;初期位置
			ランダムダンジョン配列:深度:分岐:1 = 初期マス
		ELSE
			;イベントがあるのは2/3のマス
			IF RAND:3 == 0
				ランダムダンジョン配列:深度:分岐:1 = 何もなし
				CONTINUE
			ENDIF
			;戦闘は残り1/3
			IF RAND:3 == 0
				ランダムダンジョン配列:深度:分岐:1 = 雑魚敵
				ランダムダンジョン配列:深度:分岐:2 = 雑魚敵
				CONTINUE
			ENDIF
			;残りは汎用イベ
			ランダムイベ名 = %RESULTS:(RAND:ランダムイベ数)%
			ランダムダンジョン配列:深度:分岐:1 = %REPLACE(ランダムイベ名, "イベント_リゾート島開拓作業_", "")%
			IF STRFIND(ランダムイベ名, "えっちイベ") > -1
				ランダムダンジョン配列:深度:分岐:2 = えっちイベ
			ELSE
				ランダムダンジョン配列:深度:分岐:2 = イベント
			ENDIF
		ENDIF
	NEXT
NEXT


;----------
;マスタイプ
;----------
@マスタイプ_リゾート島開拓作業(深度,分岐)
#DIM 深度
#DIM 分岐
SELECTCASE ランダムダンジョン配列:深度:分岐:2
	;マスタイプで分岐
	CASE IS == "イベント"
		ダンジョンマス画像URL = 床_草原
		IF ランダムダンジョン配列:深度:分岐:0 != "イベント完了"
			ダンジョンマス画像URL_重ね表示 = 会話アイコン
		ENDIF
	CASE IS == "次階層"
		ダンジョンマス画像URL = 床_草原
		ダンジョンマス画像URL_重ね表示 = 右アイコン
	CASE IS == "雑魚敵"
		ダンジョンマス画像URL = 床_草原
		IF ランダムダンジョン配列:深度:分岐:0 != "雑魚敵討伐完了"
			ダンジョンマス画像URL_重ね表示 = エネミーアイコン
		ENDIF
	CASE IS == "えっちイベ"
		ダンジョンマス画像URL = 床_草原
		IF ランダムダンジョン配列:深度:分岐:0 != "えっちイベ完了"
			ダンジョンマス画像URL_重ね表示 = えっちハート
		ENDIF
	CASE IS == "宝箱"
		ダンジョンマス画像URL = 床_草原
		IF ランダムダンジョン配列:深度:分岐:0 != "宝箱取得済み"
			ダンジョンマス画像URL_重ね表示 = 宝箱アイコン_木箱_開
		ELSE
			ダンジョンマス画像URL_重ね表示 = 宝箱アイコン_木箱
		ENDIF
	CASE IS == ""
		ダンジョンマス画像URL = 床_草原
	CASEELSE
		ダンジョンマス画像URL = 床_草原
ENDSELECT



;----------
;イベント内容
;----------
@イベント_リゾート島開拓作業_初期マス
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = 周囲を回って開拓を進めていこう。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	KSTR:(K++) = 未開拓エリアに到着した。
	KSTR:(K++) = 警戒しながら進もう。
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_次階層
CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
CALL 画面再描画
CALL 口上変数初期化
IF ランダムダンジョン階層記録 < 10
	WSTR:(K++) = ここから次の階層に進めそうだ。
	KSTR:(K++) = 進みますか？（この階層に戻ってくることは出来ません）
	KSTR:(K++) = BUTTON_[はい]
	KSTR:(K++) = BUTTON_[いいえ]
	CALL メッセージ欄表示用関数(,,,0)
	CALL 口上変数初期化
	$LOOP_MESSAGE
	INPUTS
	SELECTCASE RESULTS
		CASE "[はい]"
			KSTR:(K++) = %CALLNAME:PLAYER%たちは次の階層へと進んだ…
			CALL メッセージ欄表示用関数()
			;次階層生成
			CALL ダンジョン初期処理_リゾート島開拓作業
			CALL 踏破マス開放処理
			CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
		CASE "[いいえ]"
			KSTR:(K++) = もう少しこの階層を探索してみよう。
			CALL メッセージ欄表示用関数()
		CASEELSE
			CLEARLINE 1
			GOTO LOOP_MESSAGE
	ENDSELECT
ELSE
	WSTR:(K++) = ここを抜ければ切りの良いところまで開拓が終わるだろう。
	KSTR:(K++) = 進みますか？（ダンジョンから抜けます）
	KSTR:(K++) = BUTTON_[はい]
	KSTR:(K++) = BUTTON_[いいえ]
	CALL メッセージ欄表示用関数(,,,0)
	CALL 口上変数初期化
	$LOOP_MESSAGE2
	INPUTS
	SELECTCASE RESULTS
		CASE "[はい]"
			WSTR:(K++) = %CALLNAME:PLAYER%たちは開拓を終え、リゾートに帰還した。
			IF 開拓エリア解放フラグ:0 == 0
				WSTR:(K++) = 新たなエリアが改造可能になった！
				開拓エリア解放フラグ:0 = 1
			ELSE
				WSTR:(K++) = 開拓済みエリアを再探索し、赤銅の古紋を５枚手に入れた。
				CALL 素材入手処理("赤銅の古紋", 5)
			ENDIF
			CALL メッセージ欄表示用関数(,,,0)
			;帰還処理
			ダンジョンクリアフラグ_リゾート島開拓作業 = 1
			CALL 口上変数初期化
			ダンジョン終了状況 = 踏破達成
		CASE "[いいえ]"
			KSTR:(K++) = もう少しこの階層を探索してみよう。
			CALL メッセージ欄表示用関数()
		CASEELSE
			CLEARLINE 1
			GOTO LOOP_MESSAGE2
	ENDSELECT
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_何もなし
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = 特に変わった様子はない。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	KSTR:(K++) = この辺りは特に変わった様子はない。
	KSTR:(K++) = 先に進もう。
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_雑魚敵
CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 == "雑魚敵討伐完了"
	KSTR:(K++) = 特に変わった様子はない。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	KSTR:(K++) = 先に進む君たちの前に、魔物たちが立ちふさがった！
	CALL メッセージ欄表示用関数()
	
	CALL ランダムエネミーセット関数(ランダムダンジョン階層記録)
	CALL 雑魚敵遭遇汎用処理
	IF RESULT == 0
		CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
		ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 = 雑魚敵討伐完了
	ENDIF
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_えっちイベ
CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 == "えっちイベ完了"
	KSTR:(K++) = 特に変わった様子はない。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	CALL ダンジョンハプニング基本処理()
	IF RESULT
		ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 = えっちイベ完了
	ENDIF
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_調合知識イベ１
CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 == "イベント完了"
	KSTR:(K++) = 特に変わった様子はない。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 妙な形の葉っぱを付けた低木の群生地を見つけた。
	CALL 知識技能所持者検索("調合知識", 1)
	LOCAL = RESULT
	IF LOCAL > -1
		WSTR:(K++) = %CALLNAME:LOCAL%はその葉っぱがポーションに使われる薬草になることに気づいた。
		WSTR:(K++) = この場で調合すれば即席のポーションとして使えるだろう。
		WSTR:(K++) = 日持ちしない薬草のためこの探索中にしか持たないが、十分役立つはずだ。
		LOCAL:1 = RAND:3 + 1
		KSTR:(K++) = %CALLNAME:PLAYER%たちは「即席オールポーション」を{LOCAL:1}個に手に入れた！
		CALL アイテム増減汎用処理("即席オールポーション", LOCAL:1)
		ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 = イベント完了
	ELSE
		WSTR:(K++) = %CALLNAME:PLAYER%たちにはそれ以上のことはわからなかった。
		KSTR:(K++) = 調合知識があればどんな樹木かわかったかもしれないが……。
	ENDIF
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_調合知識イベ２
CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 == "イベント完了"
	KSTR:(K++) = 特に変わった様子はない。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 妙な形の葉っぱを付けた低木の群生地を見つけた。
	CALL 知識技能所持者検索("調合知識", 1)
	LOCAL = RESULT
	IF LOCAL > -1
		WSTR:(K++) = %CALLNAME:LOCAL%はその木が魔力を貯蔵する実を付けていることに気づいた。
		WSTR:(K++) = 収穫してしまうとすぐに魔力は霧散してしまうが、この場で回復するには問題ない。
		LOCAL:1 = RAND:3 + 1
		KSTR:(K++) = %CALLNAME:PLAYER%たちのＭＰが{LOCAL:1 * 50}回復した！
		CALL 探索中全体回復_ＭＰ(LOCAL:1 * 50)
		ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 = イベント完了
	ELSE
		WSTR:(K++) = %CALLNAME:PLAYER%たちにはそれ以上のことはわからなかった。
		KSTR:(K++) = 調合知識があればどんな樹木かわかったかもしれないが……。
	ENDIF
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_調合知識イベ３
CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 == "イベント完了"
	KSTR:(K++) = 特に変わった様子はない。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 妙な形の葉っぱを付けた低木の群生地を見つけた。
	CALL 知識技能所持者検索("調合知識", 1)
	LOCAL = RESULT
	IF LOCAL > -1
		WSTR:(K++) = %CALLNAME:LOCAL%はその葉っぱが一時的な魔力向上薬になることに気づいた。
		WSTR:(K++) = 調合した薬を飲むと、君たちの体に力が漲る！
		KSTR:(K++) = %CALLNAME:PLAYER%たちの奥義が即座に発動可能になった！
		CALL 探索中全体回復_奥義ゲージ(100)
		ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 = イベント完了
	ELSE
		WSTR:(K++) = %CALLNAME:PLAYER%たちにはそれ以上のことはわからなかった。
		KSTR:(K++) = 調合知識があればどんな樹木かわかったかもしれないが……。
	ENDIF
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_魔法知識イベ１
CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 == "イベント完了"
	KSTR:(K++) = 特に変わった様子はない。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 岩の陰に魔力を感じる石を見つけた。
	KSTR:(K++) = 拾ってみますか？
	KSTR:(K++) = BUTTON_[はい]
	KSTR:(K++) = BUTTON_[いいえ]
	CALL メッセージ欄表示用関数(,,,0)
	CALL 口上変数初期化
	$LOOP_MESSAGE
	INPUTS
	SELECTCASE RESULTS
		CASE "[はい]"
			CALL 知識技能所持者検索("魔法知識", 1)
			LOCAL = RESULT
			IF LOCAL > -1
				WSTR:(K++) = %CALLNAME:LOCAL%が鑑定したところ、どうやら騎空艇の動力になる風の魔石のようだ。
				WSTR:(K++) = 持ち帰って売ればそこそこの金になるだろう！
				KSTR:(K++) = %CALLNAME:PLAYER%たちは５万ルピを手に入れた。
				MONEY += 50000
			ELSE
				WSTR:(K++) = 何かはわからないが、綺麗な魔石だ。
				WSTR:(K++) = 持ち帰って売れば換金出来るかもしれない。
				KSTR:(K++) = %CALLNAME:PLAYER%たちは１万ルピを手に入れた。
				MONEY += 10000
			ENDIF
			ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 = イベント完了
		CASE "[いいえ]"
			WSTR:(K++) = 下手に触ると何が起こるかわからず危険だ。放置しよう。
			KSTR:(K++) = 魔法知識があれば利用出来たかもしれないが……。
	ENDSELECT
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_魔法知識イベ２
#DIM レベル記録

CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 == "イベント完了"
	KSTR:(K++) = 特に変わった様子はない。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 岩の陰に魔力を感じる石を見つけた。
	KSTR:(K++) = 拾ってみますか？
	KSTR:(K++) = BUTTON_[はい]
	KSTR:(K++) = BUTTON_[いいえ]
	CALL メッセージ欄表示用関数(,,,0)
	CALL 口上変数初期化
	$LOOP_MESSAGE
	INPUTS
	SELECTCASE RESULTS
		CASE "[はい]"
			CALL 知識技能所持者検索("魔法知識", 1)
			LOCAL = RESULT
			IF LOCAL > -1
				WSTR:(K++) = 近づいてみると、%CALLNAME:LOCAL%はそれが火の魔石であることに気づいた。
				WSTR:(K++) = うっかり触っていたら炎が噴き出して怪我をしてしまっていただろう。
				WSTR:(K++) = %CALLNAME:LOCAL%は魔石から余計な魔力を取り除き、魔石を簡単な道具として加工した。
				LOCAL:1 = RAND:3 + 1
				KSTR:(K++) = %CALLNAME:PLAYER%たちは「火の魔石」を{LOCAL:1}個に手に入れた！
				CALL アイテム増減汎用処理("火の魔石", LOCAL:1)
			ELSE
				WSTR:(K++) = 近づいてみると、突然魔石から炎が噴き出した！！
				レベル記録 = 0
				FOR LOCAL, 0, 14
					SIF LOCAL > 3 && LOCAL < 10
						CONTINUE
					SIF BATTLE_STATE:LOCAL:0 < 1
						CONTINUE
					SIF BATTLE_STATE:LOCAL:Lv > レベル記録
						レベル記録 = BATTLE_STATE:LOCAL:Lv
				NEXT
				KSTR:(K++) = %CALLNAME:PLAYER%たち全員のＨＰに{レベル記録 * 20}のダメージ！
				CALL メッセージ欄表示用関数()
				CALL 口上変数初期化
				FOR LOCAL, 0, 14
					SIF LOCAL > 3 && LOCAL < 10
						CONTINUE
					SIF BATTLE_STATE:LOCAL:0 < 1
						CONTINUE
					BATTLE_STATE:LOCAL:ＨＰ = MAX(1, BATTLE_STATE:LOCAL:ＨＰ - (レベル記録 * 20))
				NEXT
				WSTR:(K++) = どうやら火の魔石に魔力が溜まりすぎていたようだ。
				WSTR:(K++) = 魔力を使い切った魔石はただの石ころになってしまっている……。
				KSTR:(K++) = 魔法知識があれば防げたかもしれないが……気を取り直して君たちは先に進む。
			ENDIF
			ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 = イベント完了
		CASE "[いいえ]"
			WSTR:(K++) = 下手に触ると何が起こるかわからず危険だ。放置しよう。
			KSTR:(K++) = 魔法知識があれば利用出来たかもしれないが……。
	ENDSELECT
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_リゾート島開拓作業_ランダム_魔法知識イベ３
CALL 口上変数初期化
IF ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 == "イベント完了"
	KSTR:(K++) = 特に変わった様子はない。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 岩の陰に魔力を感じる石を見つけた。
	KSTR:(K++) = 拾ってみますか？
	KSTR:(K++) = BUTTON_[はい]
	KSTR:(K++) = BUTTON_[いいえ]
	CALL メッセージ欄表示用関数(,,,0)
	CALL 口上変数初期化
	$LOOP_MESSAGE
	INPUTS
	SELECTCASE RESULTS
		CASE "[はい]"
			CALL 知識技能所持者検索("魔法知識", 1)
			LOCAL = RESULT
			IF LOCAL > -1
				WSTR:(K++) = 近づいてみると、%CALLNAME:LOCAL%はそれが光の魔石であることに気づいた。
				WSTR:(K++) = 生命を司る魔力が治癒の力を大きく増加させる……！
				WSTR:(K++) = %CALLNAME:LOCAL%は魔石の魔力を使い、戦闘不能を含む全員のＨＰを１００回復した！
				CALL 探索中全体回復_ＨＰ_蘇生(100)
			ELSE
				WSTR:(K++) = 近づいてみると、突然魔石から温かな光が湧き出した。
				WSTR:(K++) = 光に包まれた君たちの傷が癒えていく……。
				KSTR:(K++) = %CALLNAME:PLAYER%たちのＨＰが１００回復した！
				CALL メッセージ欄表示用関数()
				CALL 探索中全体回復_ＨＰ(100)
				CALL 口上変数初期化
				WSTR:(K++) = どうやら光の魔石に魔力が溜まりすぎていたようだ。
				WSTR:(K++) = 魔力を使い切った魔石はただの石ころになってしまっている……。
				KSTR:(K++) = 魔法知識があればもっと有効に使えたかもしれないが……気を取り直して君たちは先に進む。
			ENDIF
			ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 = イベント完了
		CASE "[いいえ]"
			WSTR:(K++) = 下手に触ると何が起こるかわからず危険だ。放置しよう。
			KSTR:(K++) = 魔法知識があれば利用出来たかもしれないが……。
	ENDSELECT
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画


