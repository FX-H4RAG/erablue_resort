
@ダンジョン初期処理_困りごと解決
;ダンジョン構造の読み込み
;0でない時、そのマスは存在する
;
#DIM 深度
#DIM 分岐
#DIMS 接続先配列,2

VARSET ダンジョン構造配列_困りごと解決
VARSET 分割ブロックリスト
VARSET ランダムダンジョン配列
ランダムダンジョン中フラグ = 1
CALL ブロック生成(8)
CALL ブロック内部屋生成(20)
CALL 部屋タイプ割り当て_困りごと解決


ダンジョンマス画像URL_伏せ状態 = 壁_街
ダンジョン現在位置:0 = ランダムダンジョン初期位置:0
ダンジョン現在位置:1 = ランダムダンジョン初期位置:1
ダンジョン背景画像 = 背景_街

;スタート地点は必ず存在
;初期からオープン済みなので3を入れる
;ビット0：存在するか　ビット1：移動できるか　ビット2：そこから通路を伸ばすか　ビット3：入れないが表示する場合に使用
;ビット4以降：自由枠フラグ

IF GETVAR(@"ダンジョン構造配列_%ダンジョン名%:{ランダムダンジョン初期位置:0}:{ランダムダンジョン初期位置:1}") == 0
	SETVAR @"ダンジョン構造配列_%ダンジョン名%:{ランダムダンジョン初期位置:0}:{ランダムダンジョン初期位置:1}", 3
ENDIF


FOR 深度, 1, 100
	LOCAL = 0
	FOR 分岐, 0, 9
		IF ダンジョン構造配列_困りごと解決:深度:分岐
			;既にマス情報が入ってるなら飛ばす
			LOCAL = 1
			CONTINUE
		ELSEIF ENUMFUNCBEGINSWITH(@"ダンジョンマス情報_困りごと解決_{深度}_{分岐}")
			LOCAL = 1
			ダンジョン構造配列_困りごと解決:深度:分岐 = 1
		ENDIF
	NEXT
	IF LOCAL
		ダンジョン最大深度 = 深度
	ENDIF
NEXT

@ダンジョン解説文_困りごと解決

詳細文文字列受け渡し変数 = 
詳細文文字列受け渡し変数 += "リゾート島を回り、ちょっとした困りごとを解決する。<br>"
詳細文文字列受け渡し変数 += "業務の一貫なので解決してもルピは貰えないが、<br>"
詳細文文字列受け渡し変数 += "その代わりにお礼として様々なプレゼントを貰えるだろう。<br>"

;推奨レベルを返す
RETURN 5

@敵討伐後処理_ダンジョン固有_困りごと解決(討伐エネミー数)
;ダンジョン内で戦闘した後に呼ばれる関数、主にダンジョンごとのドロップアイテム取得に使用
;まだ敵データはリセットしていないので敵BATTLE_STATEで色々情報は取得可能
#DIM 討伐エネミー数
#DIM ドロップ確率
#DIMS ドロップアイテム

ドロップ確率 = 20 * 討伐エネミー数
ドロップアイテム = %ランダム素材名("贈答アイテム", 3)%
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 15 * 討伐エネミー数
ドロップアイテム = %ランダム素材名("贈答アイテム", 3)%
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 10 * 討伐エネミー数
ドロップアイテム = %ランダム素材名("贈答アイテム", 3)%
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 5 * 討伐エネミー数
ドロップアイテム = %ランダム素材名("贈答アイテム", 3)%
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 20 * 討伐エネミー数
ドロップアイテム = %ランダム素材名("贈答アイテム", 2)%
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 15 * 討伐エネミー数
ドロップアイテム = %ランダム素材名("贈答アイテム", 2)%
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 10 * 討伐エネミー数
ドロップアイテム = %ランダム素材名("贈答アイテム", 2)%
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 5 * 討伐エネミー数
ドロップアイテム = %ランダム素材名("贈答アイテム", 2)%
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

;----------
;部屋の内容を割り当てる
;----------
@部屋タイプ割り当て_困りごと解決
#DIM キャライベ数
#DIM 対象キャラ
;部屋の属性を調べて対応する条件の部屋からランダムに割り当てる
;ランダムダンジョン配列:深度:分岐:1 = イベントの名前
#DIM 深度
#DIM 分岐

;固有イベントをチェック
VARSET RESULTS
VARSET LOCALS
LOCAL:1 = 0
キャライベ数 = ENUMFUNCBEGINSWITH("イベント_困りごと解決_キャラ依頼_")
FOR LOCAL, 0, キャライベ数
	;依頼のキャラを引っ張ってくる
	対象キャラ = TOINT(SUBSTRING(RESULTS:LOCAL, 33))
	SIF 対象キャラ < 1
		CONTINUE
	;キャラが島にいないとダメ
	SIF CFLAG:対象キャラ:滞在期間 < 0
		CONTINUE
	;キャラがPTメンバーにいるとダメ
	SIF GROUPMATCH(対象キャラ, BATTLE_STATE:0:0, BATTLE_STATE:1:0, BATTLE_STATE:2:0, BATTLE_STATE:3:0, BATTLE_STATE:10:0, BATTLE_STATE:11:0, BATTLE_STATE:12:0, BATTLE_STATE:13:0)
		CONTINUE
	LOCALS:(LOCAL:1) = %RESULTS:LOCAL%
	LOCAL:1 += 1
NEXT
キャライベ数 = LOCAL:1

FOR 深度, 0, 10
	FOR 分岐, 0, 10
		SIF ランダムダンジョン配列:深度:分岐:0 == ""
			CONTINUE
		IF 深度 == ランダムダンジョン初期位置:0 && 分岐 == ランダムダンジョン初期位置:1
			;初期位置
			ランダムダンジョン配列:深度:分岐:1 = 初期マス
		ELSE
			;イベントがあるのは半分のマス
			IF RAND:2
				ランダムダンジョン配列:深度:分岐:1 = 何もなし
				CONTINUE
			ENDIF
			;更に1/2でキャライベを格納
			IF キャライベ数 > 0 && RAND:2
				LOCAL = RAND:キャライベ数
				ランダムダンジョン配列:深度:分岐:1 = SUBSTRING(LOCALS:LOCAL, 22)
				ランダムダンジョン配列:深度:分岐:2 = 依頼
				ARRAYSHIFT LOCALS, -1, "", LOCAL
				キャライベ数 -= 1
				CONTINUE
			ENDIF
			;残りは汎用イベ
			SELECTCASE RAND:6
				CASE 5
					ランダムダンジョン配列:深度:分岐:1 = 疲れたおじいさん
				CASE 4
					ランダムダンジョン配列:深度:分岐:1 = 遊戯場修理
				CASE 3
					ランダムダンジョン配列:深度:分岐:1 = ナンパに困る女性
				CASE 2
					ランダムダンジョン配列:深度:分岐:1 = 忘れっぽい青年
				CASE 1
					ランダムダンジョン配列:深度:分岐:1 = 迷子の男の子
				CASE 0
					ランダムダンジョン配列:深度:分岐:1 = ペット探し
			ENDSELECT
			ランダムダンジョン配列:深度:分岐:2 = 依頼
		ENDIF
	NEXT
NEXT


;----------
;マスタイプ
;----------
@マスタイプ_困りごと解決(深度,分岐)
#DIM 深度
#DIM 分岐
SELECTCASE ランダムダンジョン配列:深度:分岐:2
	;マスタイプで分岐
	CASE IS == "依頼"
		ダンジョンマス画像URL = 床_街
		IF ランダムダンジョン配列:深度:分岐:0 != "依頼完了"
			ダンジョンマス画像URL_重ね表示 = 会話アイコン
		ENDIF
	CASE IS == ""
		ダンジョンマス画像URL = 床_街
	CASEELSE
		ダンジョンマス画像URL = 床_街
ENDSELECT



;----------
;イベント内容
;----------
@イベント_困りごと解決_初期マス
ダンジョンクリアフラグ_困りごと解決 = 1
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = 周囲を回って困りごとを解決していこう。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	KSTR:(K++) = 人が暮らしていれば、いろんな困りごとが発生する。
	KSTR:(K++) = 周囲を回って解決していこう。
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_困りごと解決_何もなし
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = この辺りに困っている人はいないようだ。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	KSTR:(K++) = この辺りに困っている人はいないようだ。
	KSTR:(K++) = 別の場所を回ってみよう。
	CALL メッセージ欄表示用関数()
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_困りごと解決_ペット探し
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = この辺りに困っている人はいないようだ。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 少女が半泣きで何かの名前を呼んでいる。
	WSTR:(K++) = どうやら連れてきたペットが迷子になってしまったようだ。
	KSTR:(K++) = 一緒に探してあげよう。_PAGE
	WSTR:(K++) = …
	WSTR:(K++) = ……
	WSTR:(K++) = ………見つけた！
	LOCALS = %ランダム素材名("贈答アイテム", 1)%
	WSTR:(K++) = 首輪を付けた猫を少女に返してあげると、少女はお礼として「%LOCALS%」をくれた。
	KSTR:(K++) = %CALLNAME:PLAYER%たちは嬉しそうに笑う少女と別れ、他の困りごとを探しに行く。
	CALL メッセージ欄表示用関数()
	CALL 素材入手処理(LOCALS, 1)
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 = 依頼完了
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_困りごと解決_迷子の男の子
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = この辺りに困っている人はいないようだ。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 小さな男の子が一人できょろきょろと回りを見回している。
	WSTR:(K++) = どうやら親とはぐれてしまった迷子のようだ。
	KSTR:(K++) = 一緒に家族を探してあげよう。_PAGE
	WSTR:(K++) = …
	WSTR:(K++) = ……
	WSTR:(K++) = ………見つけた！
	LOCALS = %ランダム素材名("贈答アイテム", 1)%
	WSTR:(K++) = 母親の元に迷子の男の子を連れてくると、母親はお礼にと「%LOCALS%」をくれた。
	KSTR:(K++) = %CALLNAME:PLAYER%たちは母親に甘える男の子と別れ、他の困りごとを探しに行く。
	CALL メッセージ欄表示用関数()
	CALL 素材入手処理(LOCALS, 1)
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 = 依頼完了
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_困りごと解決_忘れっぽい青年
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = この辺りに困っている人はいないようだ。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 青年がプールの前で困っている。
	WSTR:(K++) = どうやら泳いで遊ぶつもりだったのに水着を忘れてしまったようだ。
	LOCALS = %ランダム素材名("贈答アイテム", 2)%
	WSTR:(K++) = 水着の貸出窓口まで連れて行くと、青年は喜んでお礼に「%LOCALS%」をくれた。
	KSTR:(K++) = %CALLNAME:PLAYER%たちは遊ぶぞーと気合を入れる青年と別れ、他の困りごとを探しに行く。
	CALL メッセージ欄表示用関数()
	CALL 素材入手処理(LOCALS, 1)
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 = 依頼完了
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_困りごと解決_ナンパに困る女性
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = この辺りに困っている人はいないようだ。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 若い女性が男性２人に話しかけられている。
	WSTR:(K++) = どうやらナンパを上手く断れなくて困っているようだ。
	KSTR:(K++) = %CALLNAME:PLAYER%は間に入り、リゾートの管理人として無理強いはやめるようにと注意した。_PAGE
	WSTR:(K++) = 男性たちは困らせるつもりは無かったと謝り、女性から離れていく。
	LOCALS = %ランダム素材名("贈答アイテム", 2)%
	WSTR:(K++) = 女性は丁寧に%CALLNAME:PLAYER%へ頭を下げ、お礼にと「%LOCALS%」をくれた。
	KSTR:(K++) = %CALLNAME:PLAYER%たちは安心してリゾートで遊ぶ女性と別れ、他の困りごとを探しに行く。
	CALL メッセージ欄表示用関数()
	CALL 素材入手処理(LOCALS, 1)
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 = 依頼完了
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_困りごと解決_遊戯場修理
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = この辺りに困っている人はいないようだ。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	WSTR:(K++) = 遊戯場に入ると、二人の男性が途方に暮れていた。
	WSTR:(K++) = どうやら意図せず、遊戯場の設備を壊してしまったようだ。
	KSTR:(K++) = 少し見てみたが、この程度の損傷なら部品を交換すれば直るだろう。_PAGE
	LOCALS = %ランダム素材名("贈答アイテム", 3)%
	WSTR:(K++) = %CALLNAME:PLAYER%が手早く設備を直すと、男性たちはホッとした様子でお礼に「%LOCALS%」をくれた。
	KSTR:(K++) = %CALLNAME:PLAYER%たちは壊さないようにと優しく設備を扱う男性たちと別れ、他の困りごとを探しに行く。
	CALL メッセージ欄表示用関数()
	CALL 素材入手処理(LOCALS, 1)
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 = 依頼完了
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

@イベント_困りごと解決_疲れたおじいさん
CALL 口上変数初期化
IF 初来訪マス判定用フラグ == 0
	KSTR:(K++) = この辺りに困っている人はいないようだ。
	CALL メッセージ欄表示用関数(,,,0)
ELSE
	;最初に訪れた時
	IF 施設改造度:3:0
		WSTR:(K++) = キャンプ場に行くと、木陰でおじいさんが座り込んでいた。
	ELSE
		WSTR:(K++) = リゾートハウスの庭に行くと、木陰でおじいさんが座り込んでいた。
	ENDIF
	WSTR:(K++) = どうやら散歩している内に気分が悪くなってしまったようだ。
	KSTR:(K++) = %CALLNAME:PLAYER%は水を渡し、タオルでおじいさんの汗を拭う…。_PAGE
	LOCALS = %ランダム素材名("贈答アイテム", 3)%
	WSTR:(K++) = しばらくすると気分が良くなったようで、おじいさんは何度もお礼を言いながら「%LOCALS%」をくれた。
	KSTR:(K++) = %CALLNAME:PLAYER%たちは体調に気をつけてとおじいさんを気遣いながら、他の困りごとを探しに行く。
	CALL メッセージ欄表示用関数()
	CALL 素材入手処理(LOCALS, 1)
	ランダムダンジョン配列:(ダンジョン現在位置:0):(ダンジョン現在位置:1):0 = 依頼完了
	CALL 接続ルート全開放処理((ダンジョン現在位置:0), (ダンジョン現在位置:1))
	CALL 口上変数初期化
ENDIF
CALL 画面再描画

