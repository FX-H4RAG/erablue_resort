@奈落の大空洞_存在判定_2
;推奨レベルを返す
RETURN 35


@奈落の大空洞_エネミーセット_2_1(追加レベル補正)
#DIM 追加レベル補正

敵BATTLE_STATE_STR:0:エネミー名 = Lv10_ボス人食い鳥
敵BATTLE_STATE:0:ランダムエネミーフラグ = 25 + 追加レベル補正 * 2 / 3

敵BATTLE_STATE_STR:4:エネミー名 = Lv6_ストーンイーター
敵BATTLE_STATE:4:ランダムエネミーフラグ = 25 + 追加レベル補正 * 2 / 3


;@奈落の大空洞_エネミー勝利後_2_1


@奈落の大空洞_エネミーセット_2_2(追加レベル補正)
#DIM 追加レベル補正

敵BATTLE_STATE_STR:0:エネミー名 = Lv25_ヒトミダケ
敵BATTLE_STATE:0:ランダムエネミーフラグ = 32 + 追加レベル補正


@奈落の大空洞_エネミー勝利後_2_2
WSTR:(K++) = %CALLNAME:PLAYER%は「魅惑食いの指輪」を手に入れた！
CALL 汎用指輪取得処理("魅惑食いの指輪", 3)
WSTR:(K++) = %CALLNAME:PLAYER%は「誘惑の耳飾り」を手に入れた！
CALL 汎用耳飾り取得処理("誘惑の耳飾り")


@奈落の大空洞_エネミーセット_2_3(追加レベル補正)
#DIM 追加レベル補正

敵BATTLE_STATE_STR:0:エネミー名 = Lv25_真珠貝
敵BATTLE_STATE:0:ランダムエネミーフラグ = 35 + 追加レベル補正


;@奈落の大空洞_エネミー勝利後_2_3


@奈落の大空洞_エネミーセット_2_4(追加レベル補正)
#DIM 追加レベル補正

敵BATTLE_STATE_STR:0:エネミー名 = Lv35_火霊バルトロメウス
敵BATTLE_STATE:0:ランダムエネミーフラグ = 35 + 追加レベル補正


@奈落の大空洞_エネミー勝利後_2_4
WSTR:(K++) = %CALLNAME:PLAYER%は「精霊の指輪・火」を手に入れた！
CALL 汎用指輪取得処理("精霊の指輪・火", 3)
WSTR:(K++) = %CALLNAME:PLAYER%は「精霊の耳飾り・火」を手に入れた！
CALL 汎用耳飾り取得処理("精霊の耳飾り・火")


@奈落の大空洞_エネミーセット_2_5(追加レベル補正)
#DIM 追加レベル補正

敵BATTLE_STATE_STR:0:エネミー名 = Lv18_コロッサス
敵BATTLE_STATE:0:ランダムエネミーフラグ = 38 + 追加レベル補正


@奈落の大空洞_エネミー勝利後_2_5
#DIM コロッサス未招待
#DIM コロマグちゃん未招待
#DIM キャラ番号

WSTR:(K++) = %CALLNAME:PLAYER%は「コロッサスの指輪」を手に入れた！
CALL 汎用指輪取得処理("コロッサスの指輪", 1)

;廃工場のコロッサスをまだ倒してない場合スキップ
SIF GETBIT(ダンジョン構造配列_バルツ地下廃工場:25:5, 4) == 0
	RETURN

;どっちも招待済の場合は招待スキップ
SIF CFLAG:GETCHARA(136):招待不可フラグ == CSVCFLAG(136, GETNUM(CFLAG, "招待不可フラグ"))
	コロッサス未招待 = 1
SIF CFLAG:GETCHARA(137):招待不可フラグ == CSVCFLAG(137, GETNUM(CFLAG, "招待不可フラグ"))
	コロマグちゃん未招待 = 1
SIF !コロッサス未招待 && !コロマグちゃん未招待
	RETURN

CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
WSTR:(K++) = どうやらこのコロッサスは廃工場にいたものとは別個体のようだ。
WSTR:(K++) = ……このコロッサスもリゾートに招いてみようか？
CALL メッセージ欄表示用関数(,,,0)

$選択肢ラベル
CALL 口上変数初期化
KSTR:(K++) = ※星晶獣コロッサスの扱いについて選択出来ます。
KSTR:(K++) = 　任意の選択肢を選んでください。
SIF コロッサス未招待
	KSTR:(K++) = BUTTON_[人間大サイズのコロッサスとして活動する]
IF コロマグちゃん未招待
	KSTR:(K++) = BUTTON_[コロマグちゃんとして活動する]
	KSTR:(K++) = BUTTON_[コロマグちゃんって何？]_（説明を表示。これを選んでもシナリオは進みません）
ENDIF
KSTR:(K++) = BUTTON_[やめておく]
CALL メッセージ欄表示用関数(,,,0)
BINPUTS
SELECTCASE RESULTS
	CASE "[人間大サイズのコロッサスとして活動する]"
		CALL 口上変数初期化
		WSTR:(K++) = 光が収まると、%CALLNAME:PLAYER%の前に2mを超える鋼の鎧が直立している。
		WSTR:(K++) = どうやら人間の施設で活動出来るようなサイズの分体を作ってくれたようだ。
		WSTR:(K++) = リゾートに[鋼鉄の巨人]コロッサスが訪れるようになった！
		CALL メッセージ欄表示用関数(,,,0)
		キャラ番号 = GETCHARA(136)
	CASE "[コロマグちゃんとして活動する]"
		CALL 口上変数初期化
		WSTR:(K++) = 光が収まると、%CALLNAME:PLAYER%の前に120cm程度のドラフ族の少女が立っていた。
		WSTR:(K++) = どうやら人間の施設で活動出来るようなサイズの分体を作ってくれたようだ。
		WSTR:(K++) = リゾートに[こうてつのきょじん]コロマグちゃんが訪れるようになった！
		CALL メッセージ欄表示用関数(,,,0)
		キャラ番号 = GETCHARA(137)
	CASE "[コロマグちゃんって何？]"
		CALL 口上変数初期化
		WSTR:(K++) = ■コロマグちゃん
		WSTR:(K++) = 　二次創作によって生まれたキャラクター。
		KSTR:(K++) = 　「撃破されたコロッサス・マグナのコックピットから見つかり、そのまま騎空団で保護され
		WSTR:(K++) = 　　行動を共にするようになった１０歳前後のドラフ族少女」という設定。
		WSTR:(K++) = 　コロッサスを思わせる模様が入った大角を持ち、長く白い髪を靡かせた無口無表情な娘。
		WSTR:(K++) = 　その正体は不明だが、erablue_resortでは「コロッサスの精神を人化した存在」として扱う。
		CALL メッセージ欄表示用関数(,,,0)
		GOTO 選択肢ラベル
	CASE "[やめておく]"
		CALL 口上変数初期化
		WSTR:(K++) = 空間のねじれた大空洞の、しかも星晶獣を持ち帰った際の影響は未知数だ。
		WSTR:(K++) = %CALLNAME:PLAYER%たちはコロッサスをそのままにして帰還した。
		CALL メッセージ欄表示用関数(,,,0)
		キャラ番号 = -1
ENDSELECT

IF キャラ番号 > 0
	CFLAG:キャラ番号:招待不可フラグ = 0
	CFLAG:キャラ番号:信頼度 = 2000
	CALL FAVOR_LvUPCheck(キャラ番号)
ENDIF
CALL 口上変数初期化
