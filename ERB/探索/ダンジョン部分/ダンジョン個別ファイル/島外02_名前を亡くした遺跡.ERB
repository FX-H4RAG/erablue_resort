

@ダンジョン初期処理_名前を亡くした遺跡
;ダンジョン構造の読み込み
;0でない時、そのマスは存在する
#DIM 深度
#DIM 分岐

ダンジョンマス画像URL_伏せ状態 = 壁_遺跡
ダンジョン現在位置:0 = 0
ダンジョン現在位置:1 = 4

;スタート地点は必ず存在し、同深度に分岐無し
;初期からオープン済みなので3を入れる
;ビット0：存在するか　ビット1：移動できるか　ビット2：マスをオープンするか（ビット１をONにしないと無意味）　ビット3：入れないがオープンする場合に使用
;ビット4以降：自由枠。主に宝箱の開閉やイベント達成フラグなどに使用
IF GETVAR(@"ダンジョン構造配列_%ダンジョン名%:0:4") == 0
	SETVAR @"ダンジョン構造配列_%ダンジョン名%:0:4", 3
ENDIF

FOR 深度, 1, 100
	LOCAL = 0
	FOR 分岐, 0, 9
		IF GETVAR(@"ダンジョン構造配列_%ダンジョン名%:{深度}:{分岐}")
			;既にマス情報が入ってるなら飛ばす
			LOCAL = 1
			CONTINUE
		ELSEIF ENUMFUNCBEGINSWITH(@"ダンジョンマス情報_%ダンジョン名%_{深度}_{分岐}")
			LOCAL = 1
			SETVAR @"ダンジョン構造配列_%ダンジョン名%:深度:分岐", 1
		ENDIF
	NEXT
	IF LOCAL
		ダンジョン最大深度 = 深度
	ENDIF
NEXT


@敵討伐後処理_ダンジョン固有_名前を亡くした遺跡(討伐エネミー数)
;ダンジョン内で戦闘した後に呼ばれる関数、主にダンジョンごとのドロップアイテム取得に使用
;まだ敵データはリセットしていないので敵BATTLE_STATEで色々情報は取得可能
#DIM 討伐エネミー数
#DIM ドロップ確率
#DIMS ドロップアイテム

ドロップ確率 = 10 * 討伐エネミー数
ドロップアイテム = 荒野の石材
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 5 * 討伐エネミー数
ドロップアイテム = 星晶の破片
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

ドロップ確率 = 1 * 討伐エネミー数
ドロップアイテム = 赤銅の古紋
CALL ドロップ判定処理(ドロップ確率, ドロップアイテム)

;---------------------------------------------------------------------
;マップ構造
;---------------------------------------------------------------------
;四角形に配置された４つのマップを巡り、ミニイベをクリアしてゆぐゆぐの信頼度を稼ぐ
;稼ぎ終わったら最初のマップから隠された道が開き、ゆぐゆぐがいる場所に行けるようになる
;ボスは一番奥のマップにおり、ゆぐゆぐルートが開く前に接敵する
;---------------------------------------------------------------------
@ダンジョンマス情報_名前を亡くした遺跡_0_4(ARGS)
#DIM マス深度
#DIM マス分岐
マス深度 = 0
マス分岐 = 4

SELECTCASE ARGS
	CASE "マス画像"
		ダンジョンマス画像URL = 床_遺跡
	CASE "接続先"
		CALL ダンジョン分岐線描画(@"{マス深度}_{マス分岐}", "1_4")
	CASE "マスイベント"
		CALL 口上変数初期化
		IF 初来訪マス判定用フラグ
			;最初に訪れた時
			WSTR:(K++) = %CALLNAME:PLAYER%は名前を亡くした遺跡へと足を踏み入れた。
			WSTR:(K++) = 星晶獣eraが言うには、ここには創世樹と呼ばれる星晶獣が暮らしているそうだ。
			WSTR:(K++) = 心優しいが社交的ではなく、信頼していない相手には姿を見せないのだとか。
			WSTR:(K++) = COLOR_FFD900_木々や動物を尊重し、大切に扱えば信用してもらえるかもしれない。
			WSTR:(K++) = 蒼の少女や星晶獣の友人に案内してもらう手はあるが、%CALLNAME:PLAYER%自身が認められなければロクに話も出来ないだろう。
			WSTR:(K++) = まずは正攻法で、この遺跡をぐるっと回ってみるとしよう。
			CALL 接続ルート全開放処理(マス深度, マス分岐)
			CALL メッセージ欄表示用関数(,,,0)
			CALL 口上変数初期化
		ELSE
			IF 最奥部出現フラグ_名前を亡くした遺跡
				KSTR:(K++) = 既に星晶獣の住処への道は開いている。
			ELSE
				KSTR:(K++) = この遺跡をぐるっと回ってみるとしよう。
			ENDIF
			CALL メッセージ欄表示用関数(,,,0)
		ENDIF
		CALL 画面再描画
ENDSELECT

@ダンジョンマス情報_名前を亡くした遺跡_1_4(ARGS)
#DIM マス深度
#DIM マス分岐
マス深度 = 1
マス分岐 = 4

SELECTCASE ARGS
	CASE "マス画像"
		ダンジョンマス画像URL = 床_遺跡
	CASE "接続先"
		CALL ダンジョン分岐線描画(@"{マス深度}_{マス分岐}", "2_3")
		CALL ダンジョン分岐線描画(@"{マス深度}_{マス分岐}", "2_5")
		SIF 最奥部出現フラグ_名前を亡くした遺跡
			CALL ダンジョン分岐線描画(@"{マス深度}_{マス分岐}", "2_4")
	CASE "マスイベント"
		CALL 口上変数初期化
		IF 初来訪マス判定用フラグ
			;最初に訪れた時
			WSTR:(K++) = 遺跡は古く、植物に侵食されて一部が崩れてさえいる。
			WSTR:(K++) = しかし手入れはされているのか瓦礫などは片付けられており、足場はしっかりとしているようだ。
			WSTR:(K++) = 周囲の魔力は土属性が色濃く出ている。住み着いている魔物も土属性が多いだろう。
			WSTR:(K++) = COLOR_FFD900_仲間には耳飾りを使って風属性になってもらうと有利に戦えそうだ。
			CALL 接続ルート全開放処理(マス深度, マス分岐)
			CALL メッセージ欄表示用関数(,,,0)
			CALL 口上変数初期化
		ELSE
			KSTR:(K++) = 土属性の魔力が色濃い。魔物も土属性が多いだろう。
			CALL メッセージ欄表示用関数(,,,0)
		ENDIF
		CALL 画面再描画
ENDSELECT

@ダンジョンマス情報_名前を亡くした遺跡_2_3(ARGS)
#DIM マス深度
#DIM マス分岐
マス深度 = 2
マス分岐 = 3

SELECTCASE ARGS
	CASE "マス画像"
		ダンジョンマス画像URL = 床_遺跡
		ダンジョンマス画像URL_重ね表示 = エネミーアイコン
	CASE "接続先"
		CALL ダンジョン分岐線描画(@"{マス深度}_{マス分岐}", "3_1")
		CALL ダンジョン分岐線描画(@"{マス深度}_{マス分岐}", "3_2")
	CASE "マスイベント"
		CALL 口上変数初期化
		IF 初来訪マス判定用フラグ
			;最初に訪れた時
			WSTR:(K++) = 遺跡を進むと、巨大な人形のような彫刻が鎮座している。
			WSTR:(K++) = 比較的保存状態が良く、表面に細かいヒビがある以外に損傷は見当たらない。
			WSTR:(K++) = 岩肌を生かした荒削りなデザインは、今にも動き出しそうなほどに迫力がある。
			WSTR:(K++) = ……いや、実際に動き出している！　この彫刻は遺跡を守るゴーレムだ！
			CALL メッセージ欄表示用関数(,,,0)

			敵BATTLE_STATE_STR:3:エネミー名 = Lv9_ゴーレム
			CALL 雑魚敵遭遇汎用処理
			IF RESULT == 0
				;勝利時のみ先へ進む
				CALL 口上変数初期化
				WSTR:(K++) = 君たちはゴーレムを打倒した。
				WSTR:(K++) = しかし、打ち砕いた瓦礫は少しずつ集まり、元に戻ろうとしている。
				WSTR:(K++) = 恐らく周囲に魔力がある限り再生するのだろう。
				WSTR:(K++) = 君たちはゴーレムが再生するまでの隙に道を通り抜け、先へと進む。
				CALL メッセージ欄表示用関数(,,,0)
				CALL 接続ルート全開放処理(マス深度, マス分岐)
			ENDIF
		ELSE
			KSTR:(K++) = 敵を探しますか？
			KSTR:(K++) = BUTTON_[はい]
			KSTR:(K++) = BUTTON_[いいえ]
			CALL メッセージ欄表示用関数(,,,0)
			$LOOP_MESSAGE
			INPUTS
			SELECTCASE RESULTS
				CASE "[はい]"
					敵BATTLE_STATE_STR:3:エネミー名 = Lv9_ゴーレム
					CALL 雑魚敵遭遇汎用処理
				CASE "[いいえ]"
					CALL 口上変数初期化
				CASEELSE
					CLEARLINE 1
					GOTO LOOP_MESSAGE
			ENDSELECT
		ENDIF
		CALL 画面再描画
ENDSELECT

@ダンジョンマス情報_名前を亡くした遺跡_3_1(ARGS)
#DIM マス深度
#DIM マス分岐
マス深度 = 3
マス分岐 = 1

SELECTCASE ARGS
	CASE "マス画像"
		ダンジョンマス画像URL = 床_遺跡
	CASE "接続先"
		CALL ダンジョン分岐線描画(@"{マス深度}_{マス分岐}", "5_0")
	CASE "マスイベント"
		CALL 口上変数初期化
		IF 初来訪マス判定用フラグ
			;最初に訪れた時
			WSTR:(K++) = 遺跡の通路には時折壁画のようなものが描かれている。
			WSTR:(K++) = 恐ろしい怪物と、武器を持った人々が戦っているような絵柄だ。
			WSTR:(K++) = この遺跡は覇空戦争の頃に作られたと聞く。この壁画も戦争のことを描いているのだろう。
			CALL 接続ルート全開放処理(マス深度, マス分岐)
			CALL メッセージ欄表示用関数(,,,0)
			CALL 口上変数初期化
		ELSE
			KSTR:(K++) = 通路には壁画のようなものが描かれている。
			CALL メッセージ欄表示用関数(,,,0)
		ENDIF
		CALL 画面再描画
ENDSELECT

@ダンジョンマス情報_名前を亡くした遺跡_5_0(ARGS)
#DIM マス深度
#DIM マス分岐
マス深度 = 5
マス分岐 = 0

SELECTCASE ARGS
	CASE "マス画像"
		ダンジョンマス画像URL = 床_遺跡
	CASE "接続先"
	CASE "マスイベント"
		CALL 口上変数初期化
		IF 初来訪マス判定用フラグ
			;最初に訪れた時
			WSTR:(K++) = 遺跡を進むと、倉庫のような場所に出た。
			WSTR:(K++) = めぼしい物は無く、ガラクタばかりが残されていたが……唯一価値のありそうな磁器の壺を見つける。
			WSTR:(K++) = 昔の時代に作られたアンティークであり、売れば１万ルピ程度にはなるだろう。
			WSTR:(K++) = しかし、壺の中にはリスのような動物が巣を作っていた。
			KSTR:(K++) = %CALLNAME:PLAYER%は……
			KSTR:(K++) = BUTTON_[リスを追い出して壺を持っていく]
			KSTR:(K++) = BUTTON_[巣があるならしょうがない。壺を諦める]
			CALL メッセージ欄表示用関数(,,,0)
			$LOOP_MESSAGE
			INPUTS
			SELECTCASE RESULTS
				CASE "[リスを追い出して壺を持っていく]"
					CALL 口上変数初期化
					WSTR:(K++) = しっしっ、とリスを追い払って巣を壺から取り出し、軽く磨く。
					WSTR:(K++) = リゾート経営に金はいくらあっても困らない。帰ったらよろず屋経由で好事家に売ってもらおう。
					WSTR:(K++) = %CALLNAME:PLAYER%は10,000ルピを手に入れた！
					CALL メッセージ欄表示用関数(,,,0)
				CASE "[巣があるならしょうがない。壺を諦める]"
					CALL 口上変数初期化
					WSTR:(K++) = ルピは欲しいが、わざわざ巣を壊すほどではない。
					WSTR:(K++) = この壺はそっとしておこう。
					SETBIT ダンジョン構造配列_名前を亡くした遺跡:マス深度:マス分岐, 4
					ゆぐゆぐ信頼度_名前を亡くした遺跡 += 10
					CALL メッセージ欄表示用関数(,,,0)
				CASEELSE
					CLEARLINE 1
					GOTO LOOP_MESSAGE
			ENDSELECT
			CALL 口上変数初期化
		ELSE
			IF GETBIT(ダンジョン構造配列_名前を亡くした遺跡:マス深度:マス分岐, 4)
				;リスを見逃したルート
				KSTR:(K++) = 壺の中には小動物の巣がある。
			ELSE
				;壺を拾ったルート
				KSTR:(K++) = もう価値のありそうな物は何もない。
			ENDIF
			CALL メッセージ欄表示用関数(,,,0)
		ENDIF
		CALL 画面再描画
ENDSELECT
