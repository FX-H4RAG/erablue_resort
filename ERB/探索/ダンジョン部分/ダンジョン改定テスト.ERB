@DUNGEON_MAIN_LOOP(ARGS)

;ダンジョン入場時処理
ダンジョン表示モード = マップ画面
ダンジョン終了フラグ = 0
ダンジョン名 = %ARGS%
ダンジョン背景画像 = 
キャラ画像ボタン化フラグ = 1
選択用変数 = -1

VARSET 探索中カウント:0:0
VARSET 探索中全体カウント

CALLFORM ダンジョン初期処理_%ダンジョン名%

;ステータス代入
CALL 戦闘初期処理_味方(1)
CALL 探索用顔グラフィック生成

DO
	CALL 口上変数初期化
	CALL 画面再描画

	IF ダンジョン表示モード == "マップ画面"
		;マスイベント処理
		CALL 踏破マス開放処理
		TRYCCALLFORM ダンジョンマス情報_%ダンジョン名%_{ダンジョン現在位置:0}_{ダンジョン現在位置:1}("マスイベント")
		CATCH
			PRINTW マスの指定先が間違っています。
			PRINTW ダンジョンの開発者に連絡してください。
		ENDCATCH
	ENDIF

	$描画_LOOP
	IF ダンジョン終了フラグ == 1
		CALL ダンジョン終了時処理
		RETURN
	ENDIF
	CALL 画面再描画
	CALLFORM DUNGEON_操作処理_%ダンジョン表示モード%
	IF RESULT == -1
		GOTO 描画_LOOP
	ENDIF
LOOP 1


@DUNGEON_操作処理_マップ画面
#DIMS 移動先配列, 3
#DIM 探索口上キャラ番号

BINPUTS
LOCALS = %RESULTS%
$切り替え_LOOP
SELECTCASE LOCALS
	CASE "p"
		ダンジョン終了フラグ = 1
		RETURN -1
	CASE "i"
		CLEARLINE 1
		ダンジョン表示モード = アイテム画面
		キャラ画像ボタン化フラグ = 0
		選択用変数 = -1
		RETURN -1
	CASE "m"
		CALL 素材アイテム一覧表示()
		RETURN -1
	CASE "t"
		FOR LOCAL, 0, 4
			IF BATTLE_STATE:LOCAL:キャラ番号 < 1
				BREAK
			ENDIF
		NEXT
		探索口上キャラ番号 = NO:(BATTLE_STATE:(RAND:LOCAL):キャラ番号)
		TRYCCALLFORM KOJO_DUNGEON_仲間と会話_{探索口上キャラ番号}()
		CATCH
			CALL 汎用口上_仲間と会話(BATTLE_STATE:(RAND:LOCAL):キャラ番号)
		ENDCATCH
		RETURN -1
	CASE "0"
		CALL 戦闘能力表示画面(BATTLE_STATE:0:0)
		CALL ダンジョン能力表示キャラ切り替え(0)
		IF RESULT >= 0
			LOCALS = %TOSTR(RESULT)%
			GOTO 切り替え_LOOP
		ENDIF
		RETURN -1
	CASE "1"
		CALL 戦闘能力表示画面(BATTLE_STATE:1:0)
		CALL ダンジョン能力表示キャラ切り替え(1)
		IF RESULT >= 0
			LOCALS = %TOSTR(RESULT)%
			GOTO 切り替え_LOOP
		ENDIF
		RETURN -1
	CASE "2"
		CALL 戦闘能力表示画面(BATTLE_STATE:2:0)
		CALL ダンジョン能力表示キャラ切り替え(2)
		IF RESULT >= 0
			LOCALS = %TOSTR(RESULT)%
			GOTO 切り替え_LOOP
		ENDIF
		RETURN -1
	CASE "3"
		CALL 戦闘能力表示画面(BATTLE_STATE:3:0)
		CALL ダンジョン能力表示キャラ切り替え(3)
		IF RESULT >= 0
			LOCALS = %TOSTR(RESULT)%
			GOTO 切り替え_LOOP
		ENDIF
		RETURN -1
	CASEELSE
		SPLIT LOCALS, "_", 移動先配列
		ダンジョン現在位置:0 = TOINT(移動先配列:1)
		ダンジョン現在位置:1 = TOINT(移動先配列:2)
		RETURN 0
ENDSELECT



@DUNGEON_MAP描画
#DIM 深度
#DIM 分岐
#DIM レイヤー番号
レイヤー番号 = 1

GDISPOSE レイヤー番号
SPRITEDISPOSE ("ダンジョン_1")
GCREATE レイヤー番号, 960, 500
GSETFONT レイヤー番号, GETFONT(), 18
GSETPEN レイヤー番号, 0xFFFFFFFF, 3

LOCALS = 

;マスを並べる
;横に8マス、縦に9マス

FOR 深度, (ダンジョン現在位置:0 / 8) * 8, (ダンジョン現在位置:0 / 8) * 8 + 8
	FOR 分岐, 0, 9
		ダンジョンマス画像URL_重ね表示 = 
		IF GETBIT(GETVAR(@"ダンジョン構造配列_%ダンジョン名%:深度:分岐"), 1)
			IF GETBIT(GETVAR(@"ダンジョン構造配列_%ダンジョン名%:深度:分岐"), 2) && GETBIT(GETVAR(@"ダンジョン構造配列_%ダンジョン名%:深度:分岐"), 3) == 0
				CALLFORM ダンジョンマス情報_%ダンジョン名%_{深度}_{分岐}("マス画像")
				CALLFORM ダンジョンマス情報_%ダンジョン名%_{深度}_{分岐}("接続先")
				LOCALS += @"<div rect='{27 + ((深度 % 8) * 64)}px, {42 + (分岐 * 50)}px, 42px, 42px' border='5px' bcolor='#101010'><button value='move_{深度}_{分岐}'><img src='%ダンジョンマス画像URL%' srcb='自機アイコン' height='32px' width='32px'></button></div>"
				IF ダンジョンマス画像URL_重ね表示 != ""
					LOCALS += @"<div rect='{32 + ((深度 % 8) * 64)}px, {47 + (分岐 * 50)}px, 32px, 32px'><img src='%ダンジョンマス画像URL_重ね表示%' height='32px' width='32px'></div>"
				ENDIF
			ELSE
				ダンジョンマス画像URL = %ダンジョンマス画像URL_伏せ状態%
				LOCALS += @"<div rect='{27 + ((深度 % 8) * 64)}px, {42 + (分岐 * 50)}px, 42px, 42px' border='5px' bcolor='#101010'><button value='move_{深度}_{分岐}'><img src='%ダンジョンマス画像URL%' srcb='自機アイコン' height='32px' width='32px'></button></div>"
			ENDIF
		ELSEIF GETBIT(GETVAR(@"ダンジョン構造配列_%ダンジョン名%:深度:分岐"), 3)
			CALLFORM ダンジョンマス情報_%ダンジョン名%_{深度}_{分岐}("マス画像")
			CALLFORM ダンジョンマス情報_%ダンジョン名%_{深度}_{分岐}("接続先")
			LOCALS += @"<div rect='{27 + ((深度 % 8) * 64)}px, {42 + (分岐 * 50)}px, 42px, 42px' border='5px' bcolor='#101010'><img src='%ダンジョンマス画像URL%' height='32px' width='32px'></div>"
			IF ダンジョンマス画像URL_重ね表示 != ""
				LOCALS += @"<div rect='{32 + ((深度 % 8) * 64)}px, {47 + (分岐 * 50)}px, 32px, 32px'><img src='%ダンジョンマス画像URL_重ね表示%' height='32px' width='32px'></div>"
			ENDIF
		ENDIF
	NEXT
NEXT
;自機表示
LOCALS += @"<div rect='{32 + ((ダンジョン現在位置:0 % 8) * 64)}px, {32 + (ダンジョン現在位置:1 * 50)}px, 32px, 32px'><img src='自機アイコン' height='32px' width='32px'></div>"

HTML文字列受け渡し = %LOCALS%


;基礎情報
GSETPEN レイヤー番号, 0x00FFFFFF, -1
LOCALS = %月算出()%
GDRAWTEXT レイヤー番号, @"{RESULT}年目 %LOCALS% {DAY % 30 + 1}日(%GET_DAY()%)：■%ダンジョン名%", 32, 7

CALL リソース登録(@"ダンジョン_1", レイヤー番号)


@ダンジョン分岐線描画(接続元, 接続先)
#DIM マス数値仮受
#DIMS 接続元
#DIMS 接続先
#DIMS 接続元配列, 2
#DIMS 接続先配列, 2
#DIM レイヤー番号

レイヤー番号 = 1

SPLIT 接続元, "_", 接続元配列
SPLIT 接続先, "_", 接続先配列

;ルート開放処理時
IF ルート開放時フラグ
	マス数値仮受 = GETVAR(@"ダンジョン構造配列_%ダンジョン名%:{TOINT(接続先配列:0)}:{TOINT(接続先配列:1)}")
	SETBIT マス数値仮受, 1
	SETVAR @"ダンジョン構造配列_%ダンジョン名%:{TOINT(接続先配列:0)}:{TOINT(接続先配列:1)}", マス数値仮受

	RETURN 0
ENDIF

;接続先が開いてないなら帰る
LOCAL = GETVAR(@"ダンジョン構造配列_%ダンジョン名%:{TOINT(接続先配列:0)}:{TOINT(接続先配列:1)}")
IF GETBIT(LOCAL, 1) == 0
	RETURN 0
ENDIF

;接続先に従って線が出る場所が変わる
IF TOINT(接続元配列:0) > TOINT(接続先配列:0)
	GSETPEN レイヤー番号, 0xFF101010, 6
	GDRAWLINE レイヤー番号, 32 + ((TOINT(接続元配列:0) % 8) * 64), 62 + (TOINT(接続元配列:1) * 50), 64 + ((TOINT(接続先配列:0) % 8) * 64), 62 + (TOINT(接続先配列:1) * 50)
	GSETPEN レイヤー番号, 0xFFFFFFFF, 3
	GDRAWLINE レイヤー番号, 32 + ((TOINT(接続元配列:0) % 8) * 64), 62 + (TOINT(接続元配列:1) * 50), 64 + ((TOINT(接続先配列:0) % 8) * 64), 62 + (TOINT(接続先配列:1) * 50)
ELSEIF TOINT(接続元配列:0) == TOINT(接続先配列:0)
	IF TOINT(接続元配列:1) > TOINT(接続先配列:1)
		GSETPEN レイヤー番号, 0xFF101010, 6
		GDRAWLINE レイヤー番号, 48 + ((TOINT(接続元配列:0) % 8) * 64), 46 + (TOINT(接続元配列:1) * 50), 48 + ((TOINT(接続先配列:0) % 8) * 64), 78 + (TOINT(接続先配列:1) * 50)
		GSETPEN レイヤー番号, 0xFFFFFFFF, 3
		GDRAWLINE レイヤー番号, 48 + ((TOINT(接続元配列:0) % 8) * 64), 46 + (TOINT(接続元配列:1) * 50), 48 + ((TOINT(接続先配列:0) % 8) * 64), 78 + (TOINT(接続先配列:1) * 50)
	ELSE
		GSETPEN レイヤー番号, 0xFF101010, 6
		GDRAWLINE レイヤー番号, 48 + ((TOINT(接続元配列:0) % 8) * 64), 78 + (TOINT(接続元配列:1) * 50), 48 + ((TOINT(接続先配列:0) % 8) * 64), 46 + (TOINT(接続先配列:1) * 50)
		GSETPEN レイヤー番号, 0xFFFFFFFF, 3
		GDRAWLINE レイヤー番号, 48 + ((TOINT(接続元配列:0) % 8) * 64), 78 + (TOINT(接続元配列:1) * 50), 48 + ((TOINT(接続先配列:0) % 8) * 64), 46 + (TOINT(接続先配列:1) * 50)
	ENDIF
ELSE
	GSETPEN レイヤー番号, 0xFF101010, 6
	GDRAWLINE レイヤー番号, 64 + ((TOINT(接続元配列:0) % 8) * 64), 62 + (TOINT(接続元配列:1) * 50), 32 + ((TOINT(接続先配列:0) % 8) * 64), 62 + (TOINT(接続先配列:1) * 50)
	GSETPEN レイヤー番号, 0xFFFFFFFF, 3
	GDRAWLINE レイヤー番号, 64 + ((TOINT(接続元配列:0) % 8) * 64), 62 + (TOINT(接続元配列:1) * 50), 32 + ((TOINT(接続先配列:0) % 8) * 64), 62 + (TOINT(接続先配列:1) * 50)
ENDIF

@ダンジョン内_SOURCE_CHECK(ARG)


;-------------------------------------------------
;経験加算
;-------------------------------------------------
CALL SOURCE_EXP(ARG)
;-------------------------------------------------
;干渉力による補正処理
;-------------------------------------------------
CALL 干渉補正実処理関数(ARG, "快楽増加時")
;-------------------------------------------------
;絶頂処理
;-------------------------------------------------
CALL ORGASM_ADD(ARG)
;-------------------------------------------------
;調教対象の噴乳チェック
;-------------------------------------------------
CALL TARGET_MILK_CHECK(ARG)
;-------------------------------------------------
;中毒充足チェック
;-------------------------------------------------
;MADA
;-------------------------------------------------
;体力の減少
;-------------------------------------------------
CALL SOURCE_DOWNBASE(ARG)
;-------------------------------------------------
;苦痛快楽経験、奉仕快楽経験のチェック
;-------------------------------------------------
CALL EXP_GOT_CHECK(ARG)
;-------------------------------------------------
;絶頂時の性愛度上昇
;-------------------------------------------------
CALL SOURCE_CALC_好感度要素_性愛度UP(ARG, PLAYER, RCVAR:ARG:絶頂の強度 * 100)

;-------------------------------------------------
;EXP取得表示
;-------------------------------------------------
LOCAL:2 = 0
LOCAL:3 = 0

FOR LOCAL:1, 0, 200
	SIF RCVAR:ARG:表示カット
		CONTINUE
	IF EXP_UP(LOCAL:1,ARG)
		LOCAL:2 ++
		LOCAL:3 ++
		SIF LOCAL:2 == 1
		PRINTL
		LOCALS = %EXPNAME:(LOCAL:1)%
		PRINTFORM %LOCALS,15,LEFT%
		IF LOCAL:1 == 89
			LOCALS = +{EXP_UP(LOCAL:1,ARG)}
			PRINTFORML %LOCALS,4,LEFT%(%CALLNAME:PLAYER% > %CALLNAME:ARG%)
		ELSE
			LOCALS = +{EXP_UP(LOCAL:1,ARG)}
			PRINTFORML %LOCALS,4,LEFT%(%CALLNAME:ARG%)
		ENDIF
	ENDIF
NEXT

SIF LOCAL:3
	WAIT
;-------------------------------------------------
;ABLUP
;-------------------------------------------------
SIF RCVAR:ARG:表示カット == 0
	CALL SOURCE_ABLUP(ARG)
;-------------------------------------------------
;奴隷の絶頂時の口上
;-------------------------------------------------
CALL MESSAGE_PALAMCNG_絶頂強度表示
SIF NOWEX:ARG:多重絶頂
	CALL KOJO_ORGASM_絶頂
;-------------------------------------------------
;奴隷射精時の口上
;-------------------------------------------------
IF NOWEX:ARG:射精
	SIF NOWEX:ARG:射精 > 1
		PRINT 大量
	PRINTFORML 射精(%CALLNAME:ARG%)
	CALL KOJO_ORGASM_射精
ENDIF
;-------------------------------------------------
;調教者射精時
;-------------------------------------------------
IF NOWEX:PLAYER:射精
	SIF NOWEX:PLAYER:射精 > 1
		PRINT 大量
	PRINTFORML 射精(%CALLNAME:PLAYER%)
	RFLAG:フィニッシュフラグ = NOWEX:PLAYER:11
	CALL EVENT_SHOOT_PLAYER
ELSE
	RFLAG:フィニッシュフラグ = 0
ENDIF
;-------------------------------------------------
;屈服・快楽堕ちの上昇による[抵抗]の消滅チェック
;-------------------------------------------------
CALL JUJUN_UP_CHECK(ARG)
;-------------------------------------------------
;調教ソースの表示
;-------------------------------------------------
CALL SHOW_SOURCE(ARG)
;-------------------------------------------------
;関係性ゲージ上昇
;-------------------------------------------------
;反感不快との相殺の関係で先に恋慕度から判定
CALL Love_CALC(ARG)
CALL FAVOR_CALC(ARG)

;-------------------------------------------------
;パラメータの上昇＆表示（DOWNもここで）
;-------------------------------------------------
VARSET LOCAL

LOCAL:3 = 0
FOR LOCAL,0,100
	;コマンド入力前のCUPを追加
	CUP:ARG:LOCAL += TCVAR:ARG:(600 + LOCAL)
	SIF PALAMNAME:LOCAL == ""
		CONTINUE
	SIF CUP:ARG:LOCAL - CDOWN:ARG:LOCAL == 0
		CONTINUE
	IF !LOCAL:3
		PRINTL
		PRINTFORML %CALLNAME:ARG%
	ENDIF
	LOCAL:3 ++
	LOCAL:2 = CUP:ARG:LOCAL - CDOWN:ARG:LOCAL + TCVAR:ARG:(600 + LOCAL)
	PRINTFORM %PALAMNAME:LOCAL,8,LEFT% {PALAM:ARG:LOCAL,10,LEFT} + {CUP:ARG:LOCAL,10,LEFT} =   {PALAM:ARG:LOCAL + CUP:ARG:LOCAL,10,LEFT}
	IF 干渉強化変数:ARG:干渉種類 - 5 == LOCAL
		SETCOLOR カラーパレット("黄")
		PRINT 　干渉効果発動中！
		RESETCOLOR
	ENDIF
	PRINTL
	PALAM:ARG:LOCAL += CUP:ARG:LOCAL - CDOWN:ARG:LOCAL
	PALAM:ARG:LOCAL = MAX(0, PALAM:ARG:LOCAL)
	CUP:ARG:LOCAL = 0
	CDOWN:ARG:LOCAL = 0
NEXT
;干渉ランク上昇判定
IF 干渉強化変数:ARG:干渉ランク < 5
	IF 干渉経験値テーブル:(干渉強化変数:ARG:干渉ランク) <= 干渉強化変数:ARG:干渉経験値
		干渉強化変数:ARG:干渉ランク += 1
		PRINTFORMW %CALLNAME:ARG%の干渉ランクが上昇した！
	ENDIF
ENDIF

FOR LOCAL, 0, 100
	CVARSET TCVAR, 600 + LOCAL, 0
NEXT

;-------------------------------------------------
;ZPの取得量表示
;-------------------------------------------------
IF RFLAG:ZP取得量
	PRINTL
	PRINTFORM ZP:{FLAG:ZP所持量 - RFLAG:ZP取得量}
	IF RCVAR:ARG:ZP取得量
		PRINTFORM  + {RCVAR:ARG:ZP取得量}(%CALLNAME:ARG%)
	ENDIF
	PRINTFORM = {FLAG:ZP所持量}
ENDIF
FLAG:ZP所持量 = RFLAG:ZP取得量 + FLAG:ZP所持量

FOR LOCAL, 0, CHARANUM
	VARSET CUP:LOCAL:0, 0
	VARSET CDOWN:LOCAL:0, 0
	VARSET DOWNBASE:LOCAL:0, 0
	;NOWEXのリセット
	VARSET NOWEX:LOCAL:0, 0
	CFLAG:LOCAL:快楽計算フラグ = 0
	FOR LOCAL:1,0,100
		TCVAR:LOCAL:(LOCAL:1) = 0
	NEXT
NEXT
RFLAG:ZP取得量 = 0

WAIT


@知識技能所持者検索(検索知識, 必要レベル)
#DIMS 検索知識
#DIM 必要レベル
#DIM 選定用配列, 10

VARSET LOCAL
;めくれる服を履いているキャラ
FOR LOCAL, 0, 4
	IF BATTLE_STATE:LOCAL:0 < 1
		BREAK
	ENDIF
	IF 知識素質:(BATTLE_STATE:LOCAL:0):検索知識 >= 必要レベル
		選定用配列:(LOCAL:1) = BATTLE_STATE:LOCAL:0
		LOCAL:1 += 1
	ENDIF
NEXT

IF 知識素質:PLAYER:検索知識 >= 必要レベル
	選定用配列:(LOCAL:1) = PLAYER
	LOCAL:1 += 1
ENDIF

IF LOCAL:1 == 0
	RETURN -1
ELSE
	LOCAL = RAND:(LOCAL:1)
	RETURN 選定用配列:LOCAL
ENDIF

@汎用口上_仲間と会話(ARG)
CALL 口上変数初期化
LOCAL:1 = キャラ隊列検索(ARG)

SELECTCASE RAND:10
	CASE 0
		KSTR:(K++) = %CALLNAME:ARG%は周囲を警戒している。
	CASE 1
		WSTR:(K++) = %CALLNAME:ARG%は懐から小さなお菓子を幾つか取り出した。
		KSTR:(K++) = パーティのみんなにそれを配っている。
	CASE 2
		WSTR:(K++) = %CALLNAME:ARG%は道端に有用な薬草を見つけた！
		KSTR:(K++) = ……と思ったら、よく似た雑草だった。
	CASE 3
		KSTR:(K++) = %CALLNAME:ARG%は楽しげに談笑している。
	CASE 4
		KSTR:(K++) = %CALLNAME:PLAYER%が地図を見ながら進む先に悩んでいると、%CALLNAME:ARG%がアドバイスしてくれた。
	CASE 5
		KSTR:(K++) = 道すがら、%CALLNAME:ARG%が焚き木を拾ってくれている。
	CASE 6
		KSTR:(K++) = %CALLNAME:ARG%は水筒を取り出し、水を飲んでいる。
	CASE 7
		WSTR:(K++) = %CALLNAME:ARG%は道端で魔石を拾った！
		KSTR:(K++) = ……しかし、中の魔力が空っぽのクズ魔石だった。
	CASE 8
		SIF BATTLE_STATE:1:キャラ番号 < 1
			RESTART
		KSTR:(K++) = 探索が終わったら何をしようか、と%CALLNAME:ARG%が仲間たちと談笑している。
	CASE 9
		KSTR:(K++) = %CALLNAME:ARG%は簡単に武器の手入れをしている。
ENDSELECT

CALL メッセージ欄表示用関数(@"探索用_{LOCAL:1}_顔", CALLNAME:ARG,,0)


@探索用顔グラフィック生成
;画像リソースを全解放
SPRITEDISPOSEALL 0
;ダンジョン中の顔グラフィックを生成
FOR LOCAL, 0, 4
	GDISPOSE 2000 + LOCAL
	SIF BATTLE_STATE:LOCAL:キャラ番号 < 1
		BREAK
	CALLF 顔グラ表示用文字列関数(BATTLE_STATE:LOCAL:キャラ番号, 1000 + LOCAL)
	CALLF スプライト作成(@"探索用_{LOCAL}_顔", 2000 + LOCAL, 0)

	;SPRITECREATE @"探索用_{LOCAL}_顔", 2000 + LOCAL
NEXT


@ダンジョン終了時処理
#DIM ループ用
;画像リソースを全解放
SPRITEDISPOSEALL 0

EXP:MASTER:探索経験 ++
FOR ループ用, 0, 4
	LOCAL = BATTLE_STATE:ループ用:キャラ番号
	IF INRANGE(LOCAL, 1, CHARANUM-1)
		EXP:LOCAL:探索経験 ++
		CALL SOURCE_CALC_好感度要素_信頼度UP(LOCAL, PLAYER, MIN(探索中全体カウント:累積勝利数 * 100, 3000) + 探索中全体カウント:追加信頼度)
		
		;衣装を戻す
		;TRYCALLFORM CLOTHES_CHANGE_%CSTR:LOCAL:ダンジョン突入前の服%(LOCAL)
		DEBUGPRINTFORML %CALLNAME:LOCAL%の衣装を%CSTR:LOCAL:ダンジョン突入前の服%にしたよ
		CALL CLOTHES_CHANGE(LOCAL, CSTR:LOCAL:ダンジョン突入前の服)
		CSTR:LOCAL:ダンジョン突入前の服 '= ""
	ENDIF
NEXT
PRINTW あなたとパーティメンバーの探索経験が増加しました

;一時変数のリセット
VARSET ダンジョン内一時変数:0
ダンジョン名 = 

VARSET 探索中カウント:0:0
VARSET 探索中全体カウント

@踏破マス開放処理
#DIM マス数値仮受

初来訪マス判定用フラグ = 0
マス数値仮受 = GETVAR(@"ダンジョン構造配列_%ダンジョン名%:{ダンジョン現在位置:0}:{ダンジョン現在位置:1}")
IF GETBIT(マス数値仮受, 2) == 0
	初来訪マス判定用フラグ = 1
ENDIF
SETBIT マス数値仮受, 2
CLEARBIT マス数値仮受, 3
SETVAR @"ダンジョン構造配列_%ダンジョン名%:{ダンジョン現在位置:0}:{ダンジョン現在位置:1}", マス数値仮受

@接続ルート全開放処理(マス深度, マス分岐)
#DIM マス深度
#DIM マス分岐
ルート開放時フラグ = 1
CALLFORM ダンジョンマス情報_%ダンジョン名%_{マス深度}_{マス分岐}("接続先")
ルート開放時フラグ = 0


