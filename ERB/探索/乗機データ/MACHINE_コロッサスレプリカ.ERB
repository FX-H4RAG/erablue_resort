@乗機関数_コロッサスレプリカ(処理モード)
#DIMS 処理モード
#DIM 武装登録, 6
#DIM 乗機攻撃力
#DIMS 乗機名一時保存
LOCALS = 

SELECTCASE 処理モード
	CASE "乗機登録"	
		乗機名一時保存 = コロッサスレプリカ
		;武装が無い場合は-1を入れる
		;固有武装の場合は0を入れる
		;装備付け替え可能枠は装備種類番号を入れること。1:魔法兵器系 2:機械兵器系 3:援護機能系
		武装登録:0 = 0
		武装登録:1 = 0
		武装登録:2 = -1
		武装登録:3 = -1
		武装登録:4 = -1
		武装登録:5 = -1
		;攻撃系武装で使用する基礎攻撃力を設定する
		乗機攻撃力 = 500

		DT_ROW_ADD "所持乗機データベース", "乗機名", 乗機名一時保存, "乗機攻撃力", 乗機攻撃力, "武装0", 武装登録:0, "武装1", 武装登録:1, "武装2", 武装登録:2, "武装3", 武装登録:3, "武装4", 武装登録:4, "武装5", 武装登録:5
	CASE "フレーバー"
		LOCALS += "バルツの地下廃工場で見つけた図面から起こした機体。<br>"
		LOCALS += "コロッサスの武装と装甲のみを再現した量産機。<br>"
		LOCALS += "星晶獣化はしていないので性能は下位互換である。<br>"
		詳細文文字列受け渡し変数 = %LOCALS%
ENDSELECT


@乗機アビ関数_コロッサスレプリカ_0(処理モード)
;数字部分は武装登録の番号と対応させること
#DIMS 処理モード
#DIM Lv記録
#DIM 武装番号
LOCALS = 

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 = シェルター
	CASE "残弾セット"
		;残弾種別 0:戦闘中回数制限 1:探索中回数制限 2:クールダウンターン発生
		乗機残弾種別:武装番号 = 1
		乗機残弾数:武装番号 = 3
	CASE "アビ説明文"
		LOCALS += "■シェルター<br>"
		LOCALS += @"　%残弾種別名:(乗機残弾種別:武装番号)%制限：残り{乗機残弾数:武装番号, 2}<br>"
		LOCALS += "　効果：味方全体の防御力を３ターン[50%/乗機枠]上昇させる。<br>"
		LOCALS += "　　　　味方全体へ３ターンの再生[味方の最大レベル✕10/乗機枠]効果を与える。<br>"
		LOCALS += "　　　　敵全体へ[味方の最大レベル✕50]の火属性固定ダメージを与える。<br>"
		詳細文文字列受け渡し変数 = %LOCALS%
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 50
		アビ汎用文字列:バフ・デバフ枠 = 防御力_乗機
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		Lv記録 = 0
		FOR LOCAL, 0, 4
			SIF Lv記録 < BATTLE_STATE:LOCAL:Lv
				Lv記録 = BATTLE_STATE:LOCAL:Lv
		NEXT

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果量 = Lv記録 * 10
		アビ汎用文字列:バフ・デバフ枠 = 再生_乗機
		アビ汎用文字列:バフ・デバフ対象能力 = 再生
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果量 = Lv記録 * 50
		CALL アビテンプレート_ダメージ処理_火属性
ENDSELECT
