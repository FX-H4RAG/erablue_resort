@乗機関数_コロッサスレプリカ(処理モード)
#DIMS 処理モード
#DIMS 武装登録, 6
#DIM 乗機攻撃力
#DIM 乗機命中率
#DIMS 乗機名一時保存
LOCALS = 

SELECTCASE 処理モード
	CASE "乗機登録"	
		乗機名一時保存 = コロッサスレプリカ
		;武装が無い場合は「不可」を入れる
		;固有武装の場合は「固有」を入れる
		;装備付け替え可能枠は何入れてもいいけど、わかりやすいように「装備可能」を入れてる
		武装登録:0 = 固有
		武装登録:1 = 固有
		武装登録:2 = 装備可能
		武装登録:3 = 不可
		武装登録:4 = 不可
		武装登録:5 = 不可
		;攻撃系武装で使用する基礎攻撃力を設定する
		乗機攻撃力 = 700
		乗機命中率 = 95

		{
		DT_ROW_ADD "所持乗機データベース", "乗機名", 乗機名一時保存, "乗機攻撃力", 乗機攻撃力, "乗機攻撃力_強化後", 乗機攻撃力, 
			"乗機命中率", 乗機命中率, "乗機命中率_強化後", 乗機命中率, 
			"武装0", 武装登録:0, "武装1", 武装登録:1, "武装2", 武装登録:2, "武装3", 武装登録:3, "武装4", 武装登録:4, "武装5", 武装登録:5
		}
	CASE "フレーバー"
		LOCALS += "バルツの地下廃工場で見つけた図面から起こした機体。<br>"
		LOCALS += "コロッサスの武装と装甲のみを再現した量産機。<br>"
		LOCALS += "星晶獣化はしていないので性能は下位互換である。<br>"
		詳細文文字列受け渡し変数 = %LOCALS%
ENDSELECT

@乗機兵装関数_コロッサスレプリカ_0(処理モード)
;数字部分は武装登録の番号と対応させること
#DIMS 処理モード
#DIM Lv記録
#DIM 武装番号
;武装番号は武装登録の番号と対応させること
武装番号 = 0
LOCALS = 

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 = シェルター
	CASE "残弾セット"
		;残弾種別 0:戦闘中回数制限 1:探索中回数制限 2:クールダウンターン発生
		乗機残弾種別:武装番号 = 1
		乗機残弾数:武装番号 = 3
	CASE "アビ説明文"
		LOCALS += @"　%残弾種別名:(乗機残弾種別:武装番号)%制限：残り{乗機残弾数:武装番号, 2}回<br>"
		LOCALS += "　効果：味方全体の防御力を３ターン[50%/乗機枠]上昇させる。<br>"
		LOCALS += "　　　　味方全体へ３ターンの再生[味方の最大レベル✕10/乗機枠]効果を与える。<br>"
		LOCALS += "　　　　敵全体へ[味方の最大レベル✕25]の火属性固定ダメージを与える。<br>"
		詳細文文字列受け渡し変数 = %LOCALS%
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 50
		アビ汎用文字列:バフ・デバフ枠 = 防御力_乗機
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		Lv記録 = 0
		FOR LOCAL, 0, 4
			SIF Lv記録 < BATTLE_STATE:LOCAL:Lv
				Lv記録 = BATTLE_STATE:LOCAL:Lv
		NEXT

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果量 = Lv記録 * 10
		アビ汎用文字列:バフ・デバフ枠 = 再生_乗機
		アビ汎用文字列:バフ・デバフ対象能力 = 再生
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果量 = Lv記録 * 25
		CALL アビテンプレート_ダメージ処理_火属性
ENDSELECT

@乗機兵装関数_コロッサスレプリカ_1(処理モード)
;数字部分は武装登録の番号と対応させること
#DIMS 処理モード
#DIM 武装番号
;武装番号は武装登録の番号と対応させること
武装番号 = 1
LOCALS = 

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 = プロミネンスリアクター
	CASE "残弾セット"
		;残弾種別 0:戦闘中回数制限 1:探索中回数制限 2:クールダウンターン発生
		乗機残弾種別:武装番号 = 1
		乗機残弾数:武装番号 = 3
	CASE "アビ説明文"
		LOCALS += @"　%残弾種別名:(乗機残弾種別:武装番号)%制限：残り{乗機残弾数:武装番号, 2}回<br>"
		LOCALS += "　効果：味方全体の攻撃力を３ターン[20%/乗機枠]上昇させる。<br>"
		LOCALS += "　　　　敵全体へ[攻撃力✕150%]の火属性ダメージを与える。<br>"
		LOCALS += "　　　　敵全体の防御力を３ターン[15%/乗機枠]低下させる。基礎成功率80%。<br>"
		詳細文文字列受け渡し変数 = %LOCALS%
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 20
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力_乗機
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 150
		CALL アビテンプレート_ダメージ処理_火属性

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 15
		アビ汎用文字列:バフ・デバフ枠 = 防御力_乗機
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 80
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@乗機兵装関数_コロッサスレプリカ_2(処理モード, 兵器id = 0)
;数字部分は武装登録の番号と対応させること
#DIMS 処理モード
#DIM 武装番号
#DIM 兵器id
;武装番号は武装登録の番号と対応させること
武装番号 = 2
LOCALS = 

SELECTCASE 処理モード
	CASE "アビ名", "アビ効果"
		;兵装処理系は装備してる兵装の関数に投げる
		TRYCCALLFORM 搭載兵器関数_%DT_CELL_GETS("所持兵器データベース", 兵器id, "兵器名", 1)%(処理モード, 兵器id)
		CATCH
			PRINTW 処理が不正です
			アビテンプレート用_キャンセルフラグ = 1
			RETURN -1
		ENDCATCH
	CASE "アビ説明文"
		LOCALS += @"　%残弾種別名:(乗機残弾種別:武装番号)%制限：残り{乗機残弾数:武装番号, 2}回<br>"
		TRYCCALLFORM 搭載兵器関数_%DT_CELL_GETS("所持兵器データベース", 兵器id, "兵器名", 1)%(処理モード, 兵器id)
		CATCH
			PRINTW 処理が不正です
			アビテンプレート用_キャンセルフラグ = 1
			RETURN -1
		ENDCATCH
	CASE "残弾セット"
		乗機残弾種別:武装番号 = DT_CELL_GET("所持兵器データベース", 兵器id, "残弾種別", 1)
		乗機残弾数:武装番号 = DT_CELL_GET("所持兵器データベース", 兵器id, "残弾数_強化後", 1)
;---------------------------------------------------------------------------------------------------------
;編集するのはここから
	CASE "装備条件説明文"
		LOCALS += @"　装備可能カテゴリ：機械系・魔法系<br>"
		LOCALS += "　装備可能距離：近距離<br>"
		LOCALS += "　その他条件：なし<br>"
		詳細文文字列受け渡し変数 = %LOCALS%
	CASE "装備条件判定"
		;装備不可だと0、可能だと1を返す
		SIF STRCOUNT(ESCAPE(DT_CELL_GETS("所持兵器データベース", 兵器id, "兵器カテゴリ", 1)), "機械系") == 0 && STRCOUNT(ESCAPE(DT_CELL_GETS("所持兵器データベース", 兵器id, "兵器カテゴリ", 1)), "魔法系") == 0
			RETURN 0
		SIF STRCOUNT(ESCAPE(DT_CELL_GETS("所持兵器データベース", 兵器id, "兵器距離適性", 1)), "近距離") == 0
			RETURN 0
		RETURN 1
;編集するのはここまで
;---------------------------------------------------------------------------------------------------------
ENDSELECT
