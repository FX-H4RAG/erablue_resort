
@BATTLE_エネミー描画
#DIM バフ数
#DIM デバフ数
#DIM バフ・デバフ番号
#DIM レイヤー番号
レイヤー番号 = 10

FOR LOCAL, 0, 10
	GDISPOSE LOCAL + 10
	GDISPOSE LOCAL + 30
	SPRITEDISPOSE (@"エネミー_{LOCAL}")
	SPRITEDISPOSE (@"エネミー_{LOCAL}_ボタン")
NEXT

;ステータス
FOR LOCAL, 0, 10
	SIF 敵BATTLE_STATE_STR:LOCAL:エネミー名 == ""
		CONTINUE
	IF 敵BATTLE_STATE:LOCAL:ボスフラグ
		GCREATE レイヤー番号, 470, 184
		GCREATE レイヤー番号 + 20, 470, 184
	ELSE
		GCREATE レイヤー番号, 235, 92
		GCREATE レイヤー番号 + 20, 235, 92
	ENDIF
	GSETFONT レイヤー番号, GETFONT(), 18
	GSETPEN レイヤー番号, 0x00FFFFFF, -1
	LOCAL:2 = 敵BATTLE_STATE:LOCAL:ＨＰ
	LOCAL:3 = 敵BATTLE_STATE:LOCAL:最大ＨＰ
	SELECTCASE 敵BATTLE_STATE:LOCAL:属性
		CASE 0
			GSETBRUSH レイヤー番号, カラーパレット_透明度込("火属性")
		CASE 1
			GSETBRUSH レイヤー番号, カラーパレット_透明度込("水属性")
		CASE 2
			GSETBRUSH レイヤー番号, カラーパレット_透明度込("風属性")
		CASE 3
			GSETBRUSH レイヤー番号, カラーパレット_透明度込("土属性")
		CASE 4
			GSETBRUSH レイヤー番号, カラーパレット_透明度込("光属性")
		CASE 5
			GSETBRUSH レイヤー番号, カラーパレット_透明度込("闇属性")
	ENDSELECT
	IF LOCAL:2 <= 0
		GSETBRUSH レイヤー番号, カラーパレット_透明度込("戦闘不能")
	ENDIF
	IF 敵BATTLE_STATE:LOCAL:ボスフラグ
		GFILLRECTANGLE レイヤー番号, 0 , 0, 470, 184
		GSETBRUSH レイヤー番号, 0xFF101010
		GFILLRECTANGLE レイヤー番号, 5, 5, 460, 174
	ELSE
		GFILLRECTANGLE レイヤー番号, 0 , 0, 235, 92
		GSETBRUSH レイヤー番号, 0xFF101010
		GFILLRECTANGLE レイヤー番号, 5, 5, 225, 82
	ENDIF
	GSETBRUSH レイヤー番号, 0xFFC0C0C0
	IF LOCAL:2 <= 0
		GSETBRUSH レイヤー番号, カラーパレット_透明度込("戦闘不能")
	ENDIF
	GDRAWTEXT レイヤー番号, @"%敵BATTLE_STATE_STR:LOCAL:エネミー名%", 10, 10
	GDRAWTEXT レイヤー番号, @"HP:", 10, 30
	IF LOCAL:2 < LOCAL:3 / 3
		IF LOCAL:2 > 0
			GSETBRUSH レイヤー番号, 0xFFE60033
			GFILLRECTANGLE レイヤー番号, 42, 35, MAX(90 * LOCAL:2 / LOCAL:3, 5), 9
			IF LOCAL:2 != LOCAL:3
				GSETBRUSH レイヤー番号, 0xFF80001C
				GFILLRECTANGLE レイヤー番号, 42 + MAX(90 * LOCAL:2 / LOCAL:3, 5), 35, MIN(90 - (90 * LOCAL:2 / LOCAL:3), 85), 9
			ENDIF
		ELSE
			GSETBRUSH レイヤー番号, 0xFF80001C
			GFILLRECTANGLE レイヤー番号, 42, 35, 90, 9
		ENDIF
	ELSE
		IF LOCAL:2 > 0
			GSETBRUSH レイヤー番号, 0xFF00E64D
			GFILLRECTANGLE レイヤー番号, 42, 35, MAX(90 * LOCAL:2 / LOCAL:3, 1), 9
		ENDIF
		IF LOCAL:2 != LOCAL:3
			GSETBRUSH レイヤー番号, 0xFF00802B
			GFILLRECTANGLE レイヤー番号, 42 + (90 * LOCAL:2 / LOCAL:3), 35, MAX(90 - (90 * LOCAL:2 / LOCAL:3), 1), 9
		ENDIF
	ENDIF
	SELECTCASE 敵BATTLE_STATE:LOCAL:属性
		CASE 0
			GSETBRUSH レイヤー番号, カラーパレット_透明度込("火属性")
		CASE 1
			GSETBRUSH レイヤー番号, カラーパレット_透明度込("水属性")
		CASE 2
			GSETBRUSH レイヤー番号, カラーパレット_透明度込("風属性")
		CASE 3
			GSETBRUSH レイヤー番号, カラーパレット_透明度込("土属性")
		CASE 4
			GSETBRUSH レイヤー番号, カラーパレット_透明度込("光属性")
		CASE 5
			GSETBRUSH レイヤー番号, カラーパレット_透明度込("闇属性")
	ENDSELECT
	GDRAWTEXT レイヤー番号, @"属性:%属性数値文字列変換(敵BATTLE_STATE:LOCAL:属性)%", 162, 30
	GSETBRUSH レイヤー番号, 0xFFC0C0C0
	LOCAL:2 = 敵BATTLE_STATE:LOCAL:チャージターン
	LOCAL:3 = 敵BATTLE_STATE:LOCAL:最大チャージターン
	GDRAWTEXT レイヤー番号, @"CT:", 10, 50
	FOR LOCAL:10, 0, LOCAL:3
		IF LOCAL:10 < LOCAL:2
			GSETBRUSH レイヤー番号, 0xFFDD0000
		ELSE
			GSETBRUSH レイヤー番号, 0xFF444444
		ENDIF
		GFILLRECTANGLE レイヤー番号, 42 + (15 * LOCAL:10), 50, 10, 18
	NEXT
	
	GSETBRUSH レイヤー番号, 0xFFC0C0C0

	;バフ・デバフマーク表示
	バフ・デバフ番号 = 0
	バフ数 = DT_ROW_LENGTH(@"バフデータベース_{LOCAL + 10}")
	IF バフ数
		FOR バフ・デバフ番号, 0, バフ数
			GDRAWSPRITE レイヤー番号, "バフアイコン", バフ・デバフ番号 * 15, 0,  15, 15
		NEXT
	ENDIF
	デバフ数 = DT_ROW_LENGTH(@"デバフデータベース_{LOCAL + 10}")
	IF デバフ数
		FOR バフ・デバフ番号, バフ・デバフ番号, デバフ数 + バフ数
			GDRAWSPRITE レイヤー番号, "デバフアイコン", バフ・デバフ番号 * 15, 0,  15, 15
		NEXT
	ENDIF

	CALL リソース登録(@"エネミー_{LOCAL}", レイヤー番号)
	IF 敵BATTLE_STATE:(LOCAL):ボスフラグ
		GDRAWG レイヤー番号 + 20, レイヤー番号, 0 , 0, 470, 184, 0 , 0, 470, 184, ボタン化用カラーマトリクス:0:0
		CALL リソース登録(@"エネミー_{LOCAL}_ボタン", レイヤー番号 + 20)
		LOCAL += 3
		レイヤー番号 += 3
	ELSE
		GDRAWG レイヤー番号 + 20, レイヤー番号, 0 , 0, 235, 92, 0 , 0, 235, 92, ボタン化用カラーマトリクス:0:0
		CALL リソース登録(@"エネミー_{LOCAL}_ボタン", レイヤー番号 + 20)
		レイヤー番号 += 1
	ENDIF
NEXT


@戦闘画像表示部分
;表示部分
VARSET LOCALS
VARSET LOCAL
LOCALS = 
LOCALS += "<div rect='540px,158px,400px,800px'>"
FOR LOCAL, 0, 4
	SIF BATTLE_STATE:LOCAL:キャラ番号 < 1
		CONTINUE
	IF キャラ画像ボタン化フラグ
		LOCALS += @"<button value='{LOCAL}' ><img src='キャラ_{LOCAL}' srcb='キャラ_{LOCAL}_ボタン' height='750' width='2462' ypos='{LOCAL * 681}'></button>"
	ELSE
		LOCALS += @"<nonbutton><img src='キャラ_{LOCAL}' height='750' width='2462' ypos='{LOCAL * 681}'></nonbutton>"
	ENDIF
NEXT
LOCALS += "</div>"
FOR LOCAL, 0, 10
	SIF 敵BATTLE_STATE_STR:LOCAL:エネミー名 == ""
		CONTINUE
	LOCAL:2 = 敵BATTLE_STATE:LOCAL:ＨＰ
	IF 敵BATTLE_STATE:(LOCAL):ボスフラグ
		LOCALS += @"<div rect='42px,{180 + (LOCAL / 2) * 92}px,500px,200px'>"
		IF エネミー画像ボタン化フラグ && LOCAL:2 > 0
			LOCALS += @"<button value='{LOCAL + 10}' ><img src='エネミー_{LOCAL}' srcb='エネミー_{LOCAL}_ボタン' height='950' width='2650'></button>"
		ELSE
			LOCALS += @"<nonbutton><img src='エネミー_{LOCAL}' height='950' width='2650'></nonbutton>"
		ENDIF
		LOCALS += "</div>"
		LOCAL += 3
	ELSE
		LOCALS += @"<div rect='{42 + (LOCAL % 2) * 235}px,{180 + (LOCAL / 2) * 92}px,250px,100px'>"
		IF エネミー画像ボタン化フラグ && LOCAL:2 > 0
			LOCALS += @"<button value='{LOCAL + 10}' ><img src='エネミー_{LOCAL}' srcb='エネミー_{LOCAL}_ボタン' height='511' width='1305'></button>"
		ELSE
			LOCALS += @"<nonbutton><img src='エネミー_{LOCAL}' height='511' width='1305'></nonbutton>"
		ENDIF
		LOCALS += "</div>"
	ENDIF
NEXT
LOCALS += "<div rect='32px,680px,906px,270px'><nonbutton><img src='メッセージ欄' height='933' width='5635' ypos='0'></nonbutton></div>"
LOCALS += @"%TSTR:メッセージ欄文字列受け渡し_下側%"
LOCALS += "<div rect='32px,0px,906px,270px'><nonbutton><img src='上メッセージ欄' height='933' width='5635' ypos='0'></nonbutton></div>"
LOCALS += @"%TSTR:メッセージ欄文字列受け渡し_上側%"
HTML_PRINT LOCALS, 1

RETURN 46
