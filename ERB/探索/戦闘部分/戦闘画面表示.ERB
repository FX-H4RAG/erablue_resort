
@BATTLE_エネミー描画
#DIM バフ数
#DIM デバフ数
#DIM バフ・デバフ番号
#DIM レイヤー番号
#DIM 表示位置
レイヤー番号 = 10

FOR LOCAL, 0, 10
	GDISPOSE LOCAL + 10
	GDISPOSE LOCAL + 30
	SPRITEDISPOSE (@"エネミー_{LOCAL}")
	SPRITEDISPOSE (@"エネミー_{LOCAL}_ボタン")
NEXT

;ステータス
FOR LOCAL, 0, 10
	SIF 敵BATTLE_STATE_STR:LOCAL:エネミー名 == ""
		CONTINUE
	IF 敵BATTLE_STATE:LOCAL:ボスフラグ
		GCREATE レイヤー番号, 470, 184
		GCREATE レイヤー番号 + 20, 470, 184
	ELSE
		GCREATE レイヤー番号, 235, 92
		GCREATE レイヤー番号 + 20, 235, 92
	ENDIF
	GSETFONT レイヤー番号, GETFONT(), 18
	GSETPEN レイヤー番号, 0x00FFFFFF, -1
	LOCAL:2 = 敵BATTLE_STATE:LOCAL:ＨＰ
	LOCAL:3 = 敵BATTLE_STATE:LOCAL:最大ＨＰ
	GSETBRUSH レイヤー番号, 属性数値文字色変換_透明度込(敵BATTLE_STATE:LOCAL:属性)
	IF LOCAL:2 <= 0
		GSETBRUSH レイヤー番号, カラーパレット_透明度込("戦闘不能")
	ENDIF
	IF 敵BATTLE_STATE:LOCAL:ボスフラグ
		GFILLRECTANGLE レイヤー番号, 0 , 0, 470, 184
		GSETBRUSH レイヤー番号, 0xFF101010
		GFILLRECTANGLE レイヤー番号, 5, 5, 460, 174
	ELSE
		GFILLRECTANGLE レイヤー番号, 0 , 0, 235, 92
		GSETBRUSH レイヤー番号, 0xFF101010
		GFILLRECTANGLE レイヤー番号, 5, 5, 225, 82
	ENDIF
	GSETBRUSH レイヤー番号, 0xFFC0C0C0
	IF LOCAL:2 <= 0
		GSETBRUSH レイヤー番号, カラーパレット_透明度込("戦闘不能")
	ENDIF
	GDRAWTEXT レイヤー番号, @"%敵BATTLE_STATE_STR:LOCAL:エネミー名%", 10, 10
	GDRAWTEXT レイヤー番号, @"HP:", 10, 30
	IF 敵BATTLE_STATE:LOCAL:ボスフラグ
		GSETBRUSH レイヤー番号, 0xFF808080
		GFILLRECTANGLE レイヤー番号, 41, 42, MAX(325 + 4, 1), 6
		IF LOCAL:2 < LOCAL:3 / 3
			IF LOCAL:2 > 0
				GSETBRUSH レイヤー番号, 0xFFE60033
				GFILLRECTANGLE レイヤー番号, 43, 43, MAX(325 * LOCAL:2 / LOCAL:3, 5), 4
				IF LOCAL:2 != LOCAL:3
					GSETBRUSH レイヤー番号, 0xFF80001C
					GFILLRECTANGLE レイヤー番号, 43 + MAX(325 * LOCAL:2 / LOCAL:3, 5), 43, MAX(325 - (325 * LOCAL:2 / LOCAL:3), 1), 4
				ENDIF
			ELSE
				GSETBRUSH レイヤー番号, 0xFF404040
				GFILLRECTANGLE レイヤー番号, 41, 42, MAX(325 + 4, 1), 6
				GSETBRUSH レイヤー番号, 0xFF80001C
				GFILLRECTANGLE レイヤー番号, 43, 43, 325, 4
			ENDIF
		ELSE
			IF LOCAL:2 > 0
				GSETBRUSH レイヤー番号, 0xFF00E000
				GFILLRECTANGLE レイヤー番号, 43, 43, MAX(325 * LOCAL:2 / LOCAL:3, 1), 4
			ENDIF
			IF LOCAL:2 != LOCAL:3
				GSETBRUSH レイヤー番号, 0xFF006000
				GFILLRECTANGLE レイヤー番号, 43 + (325 * LOCAL:2 / LOCAL:3), 43, MAX(325 - (325 * LOCAL:2 / LOCAL:3), 1), 4
			ENDIF
		ENDIF
		GSETBRUSH レイヤー番号, 属性数値文字色変換_透明度込(敵BATTLE_STATE:LOCAL:属性)
		GDRAWTEXT レイヤー番号, @"属性:%属性数値文字列変換(敵BATTLE_STATE:LOCAL:属性)%", 397, 30
	ELSE
		GSETBRUSH レイヤー番号, 0xFF808080
		GFILLRECTANGLE レイヤー番号, 41, 42, MAX(90 + 4, 1), 6
		IF LOCAL:2 < LOCAL:3 / 3
			IF LOCAL:2 > 0
				GSETBRUSH レイヤー番号, 0xFFE60033
				GFILLRECTANGLE レイヤー番号, 43, 43, MAX(90 * LOCAL:2 / LOCAL:3, 5), 4
				IF LOCAL:2 != LOCAL:3
					GSETBRUSH レイヤー番号, 0xFF80001C
					GFILLRECTANGLE レイヤー番号, 43 + MAX(90 * LOCAL:2 / LOCAL:3, 5), 43, MIN(90 - (90 * LOCAL:2 / LOCAL:3), 85), 4
				ENDIF
			ELSE
				GSETBRUSH レイヤー番号, 0xFF404040
				GFILLRECTANGLE レイヤー番号, 41, 42, MAX(90 + 4, 1), 6
				GSETBRUSH レイヤー番号, 0xFF80001C
				GFILLRECTANGLE レイヤー番号, 43, 43, 88, 4
			ENDIF
		ELSE
			IF LOCAL:2 > 0
				GSETBRUSH レイヤー番号, 0xFF00E000
				GFILLRECTANGLE レイヤー番号, 43, 43, MAX(90 * LOCAL:2 / LOCAL:3, 1), 4
			ENDIF
			IF LOCAL:2 != LOCAL:3
				GSETBRUSH レイヤー番号, 0xFF006000
				GFILLRECTANGLE レイヤー番号, 43 + (90 * LOCAL:2 / LOCAL:3), 43, MAX(90 - (90 * LOCAL:2 / LOCAL:3), 1), 4
			ENDIF
		ENDIF
		GSETBRUSH レイヤー番号, 属性数値文字色変換_透明度込(敵BATTLE_STATE:LOCAL:属性)
		GDRAWTEXT レイヤー番号, @"属性:%属性数値文字列変換(敵BATTLE_STATE:LOCAL:属性)%", 162, 30
	ENDIF
	GSETBRUSH レイヤー番号, 0xFFC0C0C0
	LOCAL:2 = 敵BATTLE_STATE:LOCAL:チャージターン
	LOCAL:3 = 敵BATTLE_STATE:LOCAL:最大チャージターン
	CALL バフ・デバフ測定(LOCAL + 10, "最大ＣＴ増加")
	LOCAL:3 += RESULT
	GDRAWTEXT レイヤー番号, @"CT:", 10, 50
	FOR LOCAL:10, 0, LOCAL:3
		IF LOCAL:10 < LOCAL:2
			GSETBRUSH レイヤー番号, 0xFFDD0000
		ELSE
			GSETBRUSH レイヤー番号, 0xFF444444
		ENDIF
		GFILLRECTANGLE レイヤー番号, 42 + (15 * LOCAL:10), 50, 10, 18
	NEXT
	
	GSETBRUSH レイヤー番号, 0xFFC0C0C0
	TRYCALLFORM 戦闘画面表示_%敵BATTLE_STATE_STR:LOCAL:エネミー関数キー%(レイヤー番号, LOCAL + 10)
	GSETBRUSH レイヤー番号, 0xFFC0C0C0
	
	;バフ・デバフマーク表示
	表示位置 = 0
	バフ数 = DT_ROW_LENGTH(@"バフデータベース_{LOCAL + 10}")
	IF バフ数
		FOR バフ・デバフ番号, 0, バフ数
			SIF DT_CELL_GET(@"バフデータベース_{LOCAL + 10}", バフ・デバフ番号, "特殊表示オプション") == -1
				CONTINUE
			GDRAWSPRITE レイヤー番号, "バフアイコン", 表示位置 * 15, 0,  15, 15
			表示位置 ++
		NEXT
	ENDIF
	デバフ数 = DT_ROW_LENGTH(@"デバフデータベース_{LOCAL + 10}")
	IF デバフ数
		FOR バフ・デバフ番号, 0, デバフ数
			SIF DT_CELL_GET(@"デバフデータベース_{LOCAL + 10}", バフ・デバフ番号, "特殊表示オプション") == -1
				CONTINUE
			GDRAWSPRITE レイヤー番号, "デバフアイコン", 表示位置 * 15, 0,  15, 15
			表示位置 ++
		NEXT
	ENDIF

	CALL リソース登録(@"エネミー_{LOCAL}", レイヤー番号)
	IF 敵BATTLE_STATE:(LOCAL):ボスフラグ
		GDRAWG レイヤー番号 + 20, レイヤー番号, 0 , 0, 470, 184, 0 , 0, 470, 184, ボタン化用カラーマトリクス:0:0
		CALL リソース登録(@"エネミー_{LOCAL}_ボタン", レイヤー番号 + 20)
		LOCAL += 3
		レイヤー番号 += 3
	ELSE
		GDRAWG レイヤー番号 + 20, レイヤー番号, 0 , 0, 235, 92, 0 , 0, 235, 92, ボタン化用カラーマトリクス:0:0
		CALL リソース登録(@"エネミー_{LOCAL}_ボタン", レイヤー番号 + 20)
		レイヤー番号 += 1
	ENDIF
NEXT


@戦闘画像表示部分
;表示部分
VARSET LOCALS
VARSET LOCAL
LOCALS = 
IF 戦闘背景画像 != ""
	LOCALS += @"<div rect='125, 987, 3125, 3125'><img src='%戦闘背景画像%' height='3125'></div>"
ENDIF
FOR LOCAL, 0, 4
	SIF BATTLE_STATE:LOCAL:キャラ番号 < 1
		CONTINUE
	LOCAL:1 = 3375
	SIF LOCAL == 戦闘行動キャラ
		LOCAL:1 = 3175
	LOCALS += @"<div rect='{LOCAL:1},{LOCAL * 800 + 987},2500,5000'>"
	IF キャラ画像ボタン化フラグ
		LOCALS += @"<button value='{LOCAL}' ><img src='キャラ_{LOCAL}' srcb='キャラ_{LOCAL}_ボタン' height='750' width='2462' ypos='0'></button>"
	ELSE
		LOCALS += @"<nonbutton><img src='キャラ_{LOCAL}' height='750' width='2462' ypos='0'></nonbutton>"
	ENDIF
	LOCALS += "</div>"
NEXT
FOR LOCAL, 0, 10
	LOCAL:1 = 262
	SIF LOCAL == 戦闘行動キャラ - 10
		LOCAL:1 = 362
	SIF 敵BATTLE_STATE_STR:LOCAL:エネミー名 == ""
		CONTINUE
	LOCAL:2 = 敵BATTLE_STATE:LOCAL:ＨＰ
	IF 敵BATTLE_STATE:(LOCAL):ボスフラグ
		LOCALS += @"<div rect='{LOCAL:1},{1125 + (LOCAL / 2) * 575},3125,1250'>"
		IF エネミー画像ボタン化フラグ && LOCAL:2 > 0
			LOCALS += @"<button value='{LOCAL + 10}' ><img src='エネミー_{LOCAL}' srcb='エネミー_{LOCAL}_ボタン' height='1037' width='2650'></button>"
		ELSE
			LOCALS += @"<nonbutton><img src='エネミー_{LOCAL}' height='1037' width='2650'></nonbutton>"
		ENDIF
		LOCALS += "</div>"
		LOCAL += 3
	ELSE
	LOCALS += @"<div rect='{LOCAL:1 + (LOCAL % 2) * 1469},{1125 + (LOCAL / 2) * 575},1462,510'>"
		IF エネミー画像ボタン化フラグ && LOCAL:2 > 0
			LOCALS += @"<button value='{LOCAL + 10}' ><img src='エネミー_{LOCAL}' srcb='エネミー_{LOCAL}_ボタン' height='511' width='1305'></button>"
		ELSE
			LOCALS += @"<nonbutton><img src='エネミー_{LOCAL}' height='511' width='1305'></nonbutton>"
		ENDIF
		LOCALS += "</div>"
	ENDIF
NEXT
;LOCALS += "<div rect='200,4250,5662,1687'><nonbutton><img src='メッセージ欄' height='933' width='5635' ypos='0'></nonbutton></div>"
LOCALS += "<div rect='200,0,5662,1687'><nonbutton><img src='上メッセージ欄' height='933' width='5635' ypos='0'></nonbutton></div>"
LOCALS += @"%TSTR:メッセージ欄文字列受け渡し_上側%"
LOCALS += @"%TSTR:メッセージ欄文字列受け渡し_下側%"
HTML_PRINT LOCALS, 1

RETURN 46

