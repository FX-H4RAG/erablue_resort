@汎用固有アビ_1(ARGS, キャラ番号)
#DIM キャラ番号
SELECTCASE 基礎BATTLE_STATE:キャラ番号:ステータスタイプ保存
	CASE 1
		;攻撃
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_ファイア(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_フリーズ(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_ブロウ(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_クエイク(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_シャイニング(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_ダークフラッド(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
		ENDSELECT
	CASE 2
		;防御
		JUMP 汎用アビリティ処理_フォーメーション(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
	CASE 3
		;回復
		JUMP 汎用アビリティ処理_ヒール(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
	CASE 4
		;サポート
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
			CASE 0, 3
				;火, 土
				JUMP 汎用アビリティ処理_フォーメーション(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
			CASE 1, 4
				;水, 光
				JUMP 汎用アビリティ処理_ヒール(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_クイックアップ(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_ダークミスト(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
		ENDSELECT
	CASE 5
		;連撃
		JUMP 汎用アビリティ処理_クイックアップ(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
	CASE 6
		;回避
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_ダークミスト(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
			CASEELSE
				;闇以外
				JUMP 汎用アビリティ処理_アトラクトダンス(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
		ENDSELECT
	CASE 7
		;奥義
		JUMP 汎用アビリティ処理_士気向上(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
ENDSELECT
SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
	CASE 0
		;火
		JUMP 汎用アビリティ処理_ファイア(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
	CASE 1
		;水
		JUMP 汎用アビリティ処理_ヒール(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
	CASE 2
		;風
		JUMP 汎用アビリティ処理_クイックアップ(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
	CASE 3
		;土
		JUMP 汎用アビリティ処理_士気向上(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
	CASE 4
		;光
		JUMP 汎用アビリティ処理_シャイニング(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
	CASE 5
		;闇
		JUMP 汎用アビリティ処理_ダークミスト(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
ENDSELECT

@汎用固有アビ_2(ARGS, キャラ番号)
#DIM キャラ番号
SELECTCASE 基礎BATTLE_STATE:キャラ番号:ステータスタイプ保存
	CASE 1, 5, 7
		;攻撃, 連撃, 奥義
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_エクスプロージョン(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_アクアシュート(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_ダウンバースト(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_ロックブレイク(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_グリッターレイ(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_ディレイカース(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
		ENDSELECT
	CASE 2, 3
		;防御, 回復
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_ブレイズ(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_チルブレイン(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_フォローウィンド(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_フラッシュバン(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 3, 5
				;土, 闇
				JUMP 汎用アビリティ処理_リバースウェイブ(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
		ENDSELECT
	CASE 4
		;サポート
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_リバースウェイブ(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_チルブレイン(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_フォローウィンド(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_ロックブレイク(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_フラッシュバン(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_ディレイカース(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
		ENDSELECT
	CASE 6
		;回避
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
			CASE 2
				;風
				JUMP 汎用アビリティ処理_フォローウィンド(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASEELSE
				;風以外
				JUMP 汎用アビリティ処理_ファストステップ(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
		ENDSELECT
ENDSELECT
SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
	CASE 0
		;火
		JUMP 汎用アビリティ処理_ブレイズ(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
	CASE 1
		;水
		JUMP 汎用アビリティ処理_アクアシュート(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
	CASE 2
		;風
		JUMP 汎用アビリティ処理_ダウンバースト(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
	CASE 3
		;土
		JUMP 汎用アビリティ処理_ロックブレイク(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
	CASE 4
		;光
		JUMP 汎用アビリティ処理_フラッシュバン(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
	CASE 5
		;闇
		JUMP 汎用アビリティ処理_ディレイカース(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
ENDSELECT

@汎用固有アビ_3(ARGS, キャラ番号)
#DIM キャラ番号
SELECTCASE 基礎BATTLE_STATE:キャラ番号:ステータスタイプ保存
	CASE 1, 7
		;攻撃, 奥義
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_ヒートアップ(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_ブリザード(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_ポイズンクラウド(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_ランドスパイク(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_アスピルレイ(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_アベンジャー(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
		ENDSELECT
	CASE 5
		;連撃
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_ヒートアップ(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_ブリザード(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_ランドスパイク(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_アベンジャー(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 2, 4
				;風, 光
				JUMP 汎用アビリティ処理_フォーチュネイト(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
		ENDSELECT
	CASE 2, 3
		;防御, 回復
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_ファイアウォール(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_マウントベール(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_ポイズンクラウド(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_リベンジウォール(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_アスピルレイ(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_チャーム(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
		ENDSELECT
	CASE 6
		;回避
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_ファイアウォール(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_マウントベール(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_ポイズンクラウド(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_リベンジウォール(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_アスピルレイ(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_デッドエンド(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
		ENDSELECT
	CASE 4
		;サポート
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_ファイアウォール(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_チャーム(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASEELSE
				;火, 闇以外
				JUMP 汎用アビリティ処理_フォーチュネイト(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
		ENDSELECT
ENDSELECT
SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
	CASE 0
		;火
		JUMP 汎用アビリティ処理_ヒートアップ(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
	CASE 1
		;水
		JUMP 汎用アビリティ処理_マウントベール(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
	CASE 2
		;風
		JUMP 汎用アビリティ処理_ポイズンクラウド(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
	CASE 3
		;土
		JUMP 汎用アビリティ処理_リベンジウォール(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
	CASE 4
		;光
		JUMP 汎用アビリティ処理_アスピルレイ(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
	CASE 5
		;闇
		JUMP 汎用アビリティ処理_デッドエンド(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
ENDSELECT

@GET_SKILL_LEVEL(アビ番号, キャラ番号)
#FUNCTION
#DIM アビ番号
#DIM キャラ番号
SELECTCASE アビ番号
	CASE 1
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:Lv
			CASE IS >= 20
				RETURNF 3
			CASE IS >= 8
				RETURNF 2
			CASEELSE
				RETURNF 1
		ENDSELECT
	CASE 2
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:Lv
			CASE IS >= 28
				RETURNF 3
			CASE IS >= 16
				RETURNF 2
			CASE IS >= 4
				RETURNF 1
			CASEELSE
				RETURNF 0
		ENDSELECT
	CASEELSE
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:Lv
			CASE IS >= 32
				RETURNF 3
			CASE IS >= 24
				RETURNF 2
			CASE IS >= 12
				RETURNF 1
			CASEELSE
				RETURNF 0
		ENDSELECT
ENDSELECT

@汎用アビリティ処理_ファイア(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ファイア＋＋
	消費ＭＰ = 25
	効果数値 = 250
ELSEIF LEVEL >= 2
	アビ名 = ファイア＋
	消費ＭＰ = 22
	効果数値 = 220
ELSEIF LEVEL >= 1
	アビ名 = ファイア
	消費ＭＰ = 20
	効果数値 = 200
ELSE
	アビ名 = 
ENDIF


SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>敵横列に"
		ELSE
			詳細文文字列受け渡し変数 += "<br>敵単体に"
		ENDIF
		詳細文文字列受け渡し変数 += @"火属性の［攻撃力✕{効果数値}％］ダメージを与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 3
			CALL アビ対象選択テンプレート_敵横列
		ELSE
			CALL アビ対象選択テンプレート_敵単体
		ENDIF
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_火属性
ENDSELECT

@汎用アビリティ処理_ブレイズ(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ブレイズ＋＋
	消費ＭＰ = 30
	効果数値 = 20
ELSEIF LEVEL >= 2
	アビ名 = ブレイズ＋
	消費ＭＰ = 20
	効果数値 = 15
ELSEIF LEVEL >= 1
	アビ名 = ブレイズ
	消費ＭＰ = 15
	効果数値 = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>敵単体に火属性の［攻撃力✕100％］ダメージを与える。<br>対象の攻撃力を3ターン［{効果数値}％/B枠］減少させる。基礎成功率80％。"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>対象に3ターン火傷［150］効果を与える。基礎成功率70％。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_ダメージ処理_火属性
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力B
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 80
		CALL アビテンプレート_不利効果_デバフ効果セット
		IF LEVEL >= 3
			アビ汎用文字列:バフ・デバフ枠 = 火傷
			アビ汎用文字列:バフ・デバフ対象能力 = 火傷
			アビ汎用変数:効果量 = 150
			アビ汎用変数:効果割合 = 0
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 70
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT

@汎用アビリティ処理_ヒートアップ(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
IF LEVEL >= 3
	アビ名 = ヒートアップ＋＋
	消費ＭＰ = 40
ELSEIF LEVEL >= 2
	アビ名 = ヒートアップ＋
	消費ＭＰ = 30
ELSEIF LEVEL >= 1
	アビ名 = ヒートアップ
	消費ＭＰ = 20
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>味方全体の攻撃力を3ターン［30％/A枠］増加させる。"
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 += "<br>味方全体のDA率を3ターン［+20％/A枠］する。"
		ENDIF
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>味方全体の与ダメージを3ターン［+100/A枠］する。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 30
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 2
			アビ汎用文字列:バフ・デバフ枠 = DA率A
			アビ汎用文字列:バフ・デバフ対象能力 = DA率
			アビ汎用変数:効果量 = 20
			アビ汎用変数:効果割合 = 0
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
		IF LEVEL >= 3
			アビ汎用文字列:バフ・デバフ枠 = 与ダメ増加A
			アビ汎用文字列:バフ・デバフ対象能力 = 与ダメ増加
			アビ汎用変数:効果量 = 100
			アビ汎用変数:効果割合 = 0
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@汎用アビリティ処理_ヒール(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ヒール＋＋
	消費ＭＰ = 25
	効果数値 = 100
ELSEIF LEVEL >= 2
	アビ名 = ヒール＋
	消費ＭＰ = 15
	効果数値 = 75
ELSEIF LEVEL >= 1
	アビ名 = ヒール
	消費ＭＰ = 10
	効果数値 = 50
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>味方単体のＨＰを［回復力✕{効果数値}％］回復させる。"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>対象のデバフ効果を1つ消去する。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方単体_生者のみ
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_回復処理
		IF LEVEL >= 3
			CALL アビテンプレート_有利効果_デバフ消去
		ENDIF
ENDSELECT

@汎用アビリティ処理_アクアシュート(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = アクアシュート＋＋
	消費ＭＰ = 30
	効果数値 = 4
ELSEIF LEVEL >= 2
	アビ名 = アクアシュート＋
	消費ＭＰ = 20
	効果数値 = 3
ELSEIF LEVEL >= 1
	アビ名 = アクアシュート
	消費ＭＰ = 15
	効果数値 = 2
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>敵単体に水属性の［攻撃力✕80％］ダメージを{効果数値}回与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 80
		CALL アビテンプレート_ダメージ処理_水属性
		CALL アビテンプレート_ダメージ処理_水属性
		IF LEVEL >= 2
			CALL アビテンプレート_ダメージ処理_水属性
		ENDIF
		IF LEVEL >= 3
			CALL アビテンプレート_ダメージ処理_水属性
		ENDIF
ENDSELECT

@汎用アビリティ処理_マウントベール(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
IF LEVEL >= 3
	アビ名 = マウントベール＋＋
	消費ＭＰ = 30
ELSEIF LEVEL >= 2
	アビ名 = マウントベール＋
	消費ＭＰ = 10
ELSEIF LEVEL >= 1
	アビ名 = マウントベール
	消費ＭＰ = 30
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>味方全体に1回の弱体無効効果を与える。"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>味方全体に3ターン再生［回復力✕30％/A枠］効果を与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 弱体無効
		アビ汎用文字列:バフ・デバフ対象能力 = 弱体無効
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 3
			アビ汎用文字列:バフ・デバフ枠 = 再生A
			アビ汎用文字列:バフ・デバフ対象能力 = 再生
			CALL バフ込み数値算出("回復力")
			アビ汎用変数:効果量 = RESULT * 30 / 100
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:持続回数 = 0
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@汎用アビリティ処理_クイックアップ(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
IF LEVEL >= 3
	アビ名 = クイックアップ＋＋
	消費ＭＰ = 25
	効果数値１ = 30
	効果数値２ = 20
ELSEIF LEVEL >= 2
	アビ名 = クイックアップ＋
	消費ＭＰ = 15
	効果数値１ = 30
	効果数値２ = 20
ELSEIF LEVEL >= 1
	アビ名 = クイックアップ
	消費ＭＰ = 10
	効果数値１ = 20
	効果数値２ = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>味方全体の行動速度を3ターン［{効果数値１}％/A枠］増加させる。<br>味方全体の連続攻撃率を3ターン［+{効果数値２}％/A枠］する。"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>味方全体に3ターン追撃［与ダメージ✕10％/A枠］効果を与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 行動速度A
		アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
		アビ汎用変数:効果割合 = 効果数値１
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		アビ汎用文字列:バフ・デバフ枠 = 連続攻撃率A
		アビ汎用文字列:バフ・デバフ対象能力 = 連続攻撃率
		アビ汎用変数:効果量 = 効果数値２
		アビ汎用変数:効果割合 = 0
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 3
			アビ汎用文字列:バフ・デバフ枠 = 追撃A
			アビ汎用文字列:バフ・デバフ対象能力 = 追撃
			アビ汎用変数:効果量 = 0
			アビ汎用変数:効果割合 = 10
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@汎用アビリティ処理_フォローウィンド(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
IF LEVEL >= 3
	アビ名 = フォローウィンド＋＋
	消費ＭＰ = 30
	効果数値１ = 20
	効果数値２ = 200
ELSEIF LEVEL >= 2
	アビ名 = フォローウィンド＋
	消費ＭＰ = 20
	効果数値１ = 10
	効果数値２ = 100
ELSEIF LEVEL >= 1
	アビ名 = フォローウィンド
	消費ＭＰ = 15
	効果数値１ = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>自身の回避率を3ターン［+{効果数値１}％/A枠］する。"
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 += @"<br>自身に3ターン回避時反撃［攻撃力✕{効果数値２}％］効果を与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 回避率A
		アビ汎用文字列:バフ・デバフ対象能力 = 回避率
		アビ汎用変数:効果量 = 効果数値１
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 2
			アビ汎用文字列:バフ・デバフ枠 = 回避反撃
			アビ汎用文字列:バフ・デバフ対象能力 = 回避反撃
			アビ汎用変数:効果量 = 0
			アビ汎用変数:効果割合 = 効果数値２
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@汎用アビリティ処理_ポイズンクラウド(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果ターン
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ポイズンクラウド＋＋
	消費ＭＰ = 40
	効果ターン = 5
	効果数値 = 200
ELSEIF LEVEL >= 2
	アビ名 = ポイズンクラウド＋
	消費ＭＰ = 30
	効果ターン = 3
	効果数値 = 200
ELSEIF LEVEL >= 1
	アビ名 = ポイズンクラウド
	消費ＭＰ = 15
	効果ターン = 3
	効果数値 = 100
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>敵全体に{効果ターン}ターン毒［攻撃力✕{効果数値}％］効果を与える。基礎成功率60％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 毒
		アビ汎用文字列:バフ・デバフ対象能力 = 毒
		CALL バフ込み数値算出("攻撃力")
		アビ汎用変数:効果量 = RESULT * 効果数値 / 100
		アビ汎用変数:持続ターン = 効果ターン
		アビ汎用変数:基礎成功確率 = 60
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@汎用アビリティ処理_士気向上(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 士気向上＋＋
	消費ＭＰ = 25
	効果数値 = 20
ELSEIF LEVEL >= 2
	アビ名 = 士気向上＋
	消費ＭＰ = 15
	効果数値 = 20
ELSEIF LEVEL >= 1
	アビ名 = 士気向上
	消費ＭＰ = 10
	効果数値 = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}％"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>味方全体のゲージ上昇効率を3ターン［20％/A枠］増加させる。"
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>味方全体の奥義ゲージを［+{効果数値}％］する。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		IF LEVEL >= 3
			アビ汎用文字列:バフ・デバフ枠 = ゲージ上昇効率A
			アビ汎用文字列:バフ・デバフ対象能力 = ゲージ上昇効率
			アビ汎用変数:効果割合 = 20
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
		アビ汎用変数:効果量 = 効果数値
		アビ汎用変数:効果割合 = 0
		CALL アビテンプレート_有利効果_奥義ゲージUP
ENDSELECT

@汎用アビリティ処理_ロックブレイク(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = ロックブレイク＋＋
	消費ＭＰ = 30
	効果数値 = 20
	効果成功率 = 100
ELSEIF LEVEL >= 2
	アビ名 = ロックブレイク＋
	消費ＭＰ = 20
	効果数値 = 15
	効果成功率 = 80
ELSEIF LEVEL >= 1
	アビ名 = ロックブレイク
	消費ＭＰ = 15
	効果数値 = 10
	効果成功率 = 80
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>敵単体に土属性の［攻撃力✕100％］ダメージを与える。<br>対象の防御力を3ターン［{効果数値}％/B枠］減少させる。基礎成功率{効果成功率}％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_ダメージ処理_土属性
		アビ汎用文字列:バフ・デバフ枠 = 防御力B
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@汎用アビリティ処理_リベンジウォール(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果回数
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = リベンジウォール＋＋
	消費ＭＰ = 40
	効果回数 = 4
	効果数値 = 200
ELSEIF LEVEL >= 2
	アビ名 = リベンジウォール＋
	消費ＭＰ = 30
	効果回数 = 3
	効果数値 = 100
ELSEIF LEVEL >= 1
	アビ名 = リベンジウォール
	消費ＭＰ = 20
	効果回数 = 2
	効果数値 = 100
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>自身に{効果回数}回のカウンター［攻撃力✕{効果数値}％］効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = カウンター
		アビ汎用文字列:バフ・デバフ対象能力 = カウンター
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続回数 = 効果回数
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@汎用アビリティ処理_シャイニング(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = シャイニング＋＋
	消費ＭＰ = 25
	効果数値 = 120
ELSEIF LEVEL >= 2
	アビ名 = シャイニング＋
	消費ＭＰ = 15
	効果数値 = 100
ELSEIF LEVEL >= 1
	アビ名 = シャイニング
	消費ＭＰ = 10
	効果数値 = 80
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>敵単体に光属性の［攻撃力✕{効果数値}％］ダメージを与える。<br>このアビリティは相手の防御力を無視する。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:防御無視オプション_割合 = 100
		CALL アビテンプレート_ダメージ処理_光属性
ENDSELECT

@汎用アビリティ処理_フラッシュバン(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = フラッシュバン＋＋
	消費ＭＰ = 30
	効果数値 = 40
	効果成功率 = 80
ELSEIF LEVEL >= 2
	アビ名 = フラッシュバン＋
	消費ＭＰ = 20
	効果数値 = 20
	効果成功率 = 80
ELSEIF LEVEL >= 1
	アビ名 = フラッシュバン
	消費ＭＰ = 15
	効果数値 = 20
	効果成功率 = 60
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>敵縦列に3ターン暗闇［{効果数値}％］効果を与える。基礎成功率{効果成功率}％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵縦列
		アビ汎用文字列:バフ・デバフ枠 = 暗闇
		アビ汎用文字列:バフ・デバフ対象能力 = 暗闇
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@汎用アビリティ処理_アスピルレイ(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = アスピルレイ＋＋
	消費ＭＰ = 40
	効果数値 = 200
ELSEIF LEVEL >= 2
	アビ名 = アスピルレイ＋
	消費ＭＰ = 30
	効果数値 = 150
ELSEIF LEVEL >= 1
	アビ名 = アスピルレイ
	消費ＭＰ = 20
	効果数値 = 150
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>敵単体に光属性の［回復力✕{効果数値}％］ダメージを与える。"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>対象のバフ効果を1つ消去する。"
		ENDIF
		詳細文文字列受け渡し変数 += "<br>自身のＨＰを［与ダメージ✕50％］回復する。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用文字列:攻撃側使用能力値 = 回復力
		CALL アビテンプレート_ダメージ処理_光属性
		;RESULTに与えたダメージ量が入っている
		LOCAL = RESULT
		IF LEVEL >= 3
			CALL アビテンプレート_不利効果_バフ消去
		ENDIF
		IF LOCAL > 0
			;ダメージが発生した場合のみ処理
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:効果量 = LOCAL / 2
			アビ汎用変数:効果割合 = 0
			CALL アビテンプレート_回復処理
		ENDIF
ENDSELECT

@汎用アビリティ処理_ダークミスト(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ダークミスト＋＋
	消費ＭＰ = 25
	効果数値 = 20
ELSEIF LEVEL >= 2
	アビ名 = ダークミスト＋
	消費ＭＰ = 15
	効果数値 = 20
ELSEIF LEVEL >= 1
	アビ名 = ダークミスト
	消費ＭＰ = 10
	効果数値 = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>敵縦列の"
		ELSE
			詳細文文字列受け渡し変数 += "<br>敵単体の"
		ENDIF
		詳細文文字列受け渡し変数 += @"攻撃力を3ターン［{効果数値}％/A枠］減少させる。基礎成功率90％。"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>敵縦列の"
		ELSE
			詳細文文字列受け渡し変数 += "<br>敵単体の"
		ENDIF
		詳細文文字列受け渡し変数 += @"防御力を3ターン［{効果数値}％/A枠］減少させる。基礎成功率90％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 3
			CALL アビ対象選択テンプレート_敵縦列
		ELSE
			CALL アビ対象選択テンプレート_敵単体
		ENDIF
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 90
		CALL アビテンプレート_不利効果_デバフ効果セット
		アビ汎用文字列:バフ・デバフ枠 = 防御力A
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 90
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@汎用アビリティ処理_ディレイカース(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = ディレイカース＋＋
	消費ＭＰ = 30
	効果数値 = 150
	効果成功率 = 100
ELSEIF LEVEL >= 2
	アビ名 = ディレイカース＋
	消費ＭＰ = 20
	効果数値 = 100
	効果成功率 = 100
ELSEIF LEVEL >= 1
	アビ名 = ディレイカース
	消費ＭＰ = 15
	効果数値 = 100
	効果成功率 = 80
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>敵単体に闇属性の［攻撃力✕{効果数値}％］ダメージを与える。<br>対象にスロウ［1］効果を与える。基礎成功率{効果成功率}％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_闇属性
		アビ汎用変数:効果割合 = 0
		アビ汎用変数:効果量 = 1
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_スロウ
ENDSELECT

@汎用アビリティ処理_チャーム(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = チャーム＋＋
	消費ＭＰ = 40
	効果数値 = 35
	効果成功率 = 80
ELSEIF LEVEL >= 2
	アビ名 = チャーム＋
	消費ＭＰ = 30
	効果数値 = 25
	効果成功率 = 60
ELSEIF LEVEL >= 1
	アビ名 = チャーム
	消費ＭＰ = 20
	効果数値 = 25
	効果成功率 = 60
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 += "<br>敵横列に"
		ELSE
			詳細文文字列受け渡し変数 += "<br>敵単体に"
		ENDIF
		詳細文文字列受け渡し変数 += @"3ターン魅了［{効果数値}％］効果を与える。基礎成功率{効果成功率}％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 2
		CALL アビ対象選択テンプレート_敵横列
		ELSE
		CALL アビ対象選択テンプレート_敵単体
		ENDIF
		アビ汎用文字列:バフ・デバフ枠 = 魅了
		アビ汎用文字列:バフ・デバフ対象能力 = 魅了
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@汎用アビリティ処理_フリーズ(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = フリーズ＋＋
	消費ＭＰ = 30
	効果数値 = 30
	効果成功率 = 100
ELSEIF LEVEL >= 2
	アビ名 = フリーズ＋
	消費ＭＰ = 20
	効果数値 = 20
	効果成功率 = 80
ELSEIF LEVEL >= 1
	アビ名 = フリーズ
	消費ＭＰ = 15
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>敵単体に水属性の［攻撃力✕100％］ダメージを与える。"
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 += @"<br>対象の行動速度を3ターン［{効果数値}％/B枠］減少させる。基礎成功率{効果成功率}％。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_ダメージ処理_水属性
		IF LEVEL >= 2
			アビ汎用文字列:バフ・デバフ枠 = 行動速度B
			アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
			アビ汎用変数:効果割合 = 効果数値
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 効果成功率
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT

@汎用アビリティ処理_ブロウ(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ブロウ＋＋
	消費ＭＰ = 40
	効果数値 = 150
ELSEIF LEVEL >= 2
	アビ名 = ブロウ＋
	消費ＭＰ = 32
	効果数値 = 150
ELSEIF LEVEL >= 1
	アビ名 = ブロウ
	消費ＭＰ = 30
	効果数値 = 120
ELSE
	アビ名 = 
ENDIF


SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>敵全体に"
		ELSE
			詳細文文字列受け渡し変数 += "<br>敵横列に"
		ENDIF
		詳細文文字列受け渡し変数 += @"風属性の［攻撃力✕{効果数値}％］ダメージを与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 3
			CALL アビ対象選択テンプレート_敵全体_生者のみ
		ELSE
			CALL アビ対象選択テンプレート_敵横列
		ENDIF
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_風属性
ENDSELECT

@汎用アビリティ処理_クエイク(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = クエイク＋＋
	消費ＭＰ = 35
	効果数値 = 6
ELSEIF LEVEL >= 2
	アビ名 = クエイク＋
	消費ＭＰ = 25
	効果数値 = 5
ELSEIF LEVEL >= 1
	アビ名 = クエイク
	消費ＭＰ = 20
	効果数値 = 4
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>ランダムな敵に土属性の［攻撃力✕50％］ダメージを{効果数値}回与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		FOR LOCAL, 0, 効果数値
			アビ汎用変数:敵対心無視オプション = 1
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			アビ汎用変数:効果割合 = 50
			CALL アビテンプレート_ダメージ処理_土属性
		NEXT
ENDSELECT

@汎用アビリティ処理_ダークフラッド(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ダークフラッド＋＋
	消費ＭＰ = 30
	効果数値 = 180
ELSEIF LEVEL >= 2
	アビ名 = ダークフラッド＋
	消費ＭＰ = 25
	効果数値 = 140
ELSEIF LEVEL >= 1
	アビ名 = ダークフラッド
	消費ＭＰ = 20
	効果数値 = 100
ELSE
	アビ名 = 
ENDIF


SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>敵縦列に闇属性の［攻撃力✕{効果数値}％］ダメージを与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵縦列
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_闇属性
ENDSELECT

@汎用アビリティ処理_フォーメーション(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = フォーメーション＋＋
	消費ＭＰ = 40
	効果数値 = 3
ELSEIF LEVEL >= 2
	アビ名 = フォーメーション＋
	消費ＭＰ = 30
	効果数値 = 2
ELSEIF LEVEL >= 1
	アビ名 = フォーメーション
	消費ＭＰ = 20
	効果数値 = 1
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>味方全体に{効果数値}回のダメージカット［75％］効果を与える。"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>味方全体の防御力を3ターン［10％/A枠］増加させる。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = ダメージカット
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ増減
		アビ汎用変数:効果割合 = 75
		アビ汎用変数:持続回数 = 効果数値
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 3
			アビ汎用文字列:バフ・デバフ枠 = 防御力A
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:効果割合 = 10
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@汎用アビリティ処理_アトラクトダンス(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = アトラクトダンス＋＋
	消費ＭＰ = 50
	効果数値 = 200
ELSEIF LEVEL >= 2
	アビ名 = アトラクトダンス＋
	消費ＭＰ = 40
	効果数値 = 100
ELSEIF LEVEL >= 1
	アビ名 = アトラクトダンス
	消費ＭＰ = 20
	効果数値 = 50
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>自身に対する敵対心を3ターン［{効果数値}％/C枠］増加させる。"
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 += "<br>自身の回避率を3ターン［+15％/C枠］する。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 敵対心C
		アビ汎用文字列:バフ・デバフ対象能力 = 敵対心
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 2
			アビ汎用文字列:バフ・デバフ枠 = 回避率C
			アビ汎用文字列:バフ・デバフ対象能力 = 回避率
			アビ汎用変数:効果量 = 15
			アビ汎用変数:効果割合 = 0
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@汎用アビリティ処理_ファストステップ(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値１
#DIM 効果数値２
IF LEVEL >= 3
	アビ名 = ファストステップ＋＋
	消費ＭＰ = 30
	効果数値１ = 20
	効果数値２ = 30
ELSEIF LEVEL >= 2
	アビ名 = ファストステップ＋
	消費ＭＰ = 20
	効果数値１ = 20
	効果数値２ = 20
ELSEIF LEVEL >= 1
	アビ名 = ファストステップ
	消費ＭＰ = 15
	効果数値１ = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>自身の回避率を3ターン［+{効果数値１}％/A枠］する。"
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 += @"<br>自身の行動速度を3ターン［{効果数値２}％/A枠］増加させる。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 回避率A
		アビ汎用文字列:バフ・デバフ対象能力 = 回避率
		アビ汎用変数:効果量 = 効果数値１
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 2
			アビ汎用文字列:バフ・デバフ枠 = 行動速度A
			アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
			アビ汎用変数:効果量 = 0
			アビ汎用変数:効果割合 = 効果数値２
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@汎用アビリティ処理_エクスプロージョン(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 対象数
IF LEVEL >= 3
	アビ名 = エクスプロージョン＋＋
	消費ＭＰ = 45
	効果数値 = 50
ELSEIF LEVEL >= 2
	アビ名 = エクスプロージョン＋
	消費ＭＰ = 35
	効果数値 = 35
ELSEIF LEVEL >= 1
	アビ名 = エクスプロージョン
	消費ＭＰ = 30
	効果数値 = 25
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>敵全体に火属性の［攻撃力✕敵の数✕{効果数値}％］ダメージを与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		対象数 = 0
		FOR LOCAL, 10, 20
			SIF 敵BATTLE_STATE_STR:(LOCAL - 10):エネミー名 == ""
				CONTINUE
			対象数 += 1
		NEXT
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値 * MAX(対象数, 1)
		CALL アビテンプレート_ダメージ処理_火属性
ENDSELECT

@汎用アビリティ処理_チルブレイン(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = チルブレイン＋＋
	消費ＭＰ = 30
	効果数値 = 200
	効果成功率 = 80
ELSEIF LEVEL >= 2
	アビ名 = チルブレイン＋
	消費ＭＰ = 20
	効果数値 = 100
	効果成功率 = 80
ELSEIF LEVEL >= 1
	アビ名 = チルブレイン
	消費ＭＰ = 15
	効果数値 = 100
	効果成功率 = 60
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>敵縦列に3ターン火傷［回復力✕{効果数値}％］効果を与える。基礎成功率{効果成功率}％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵縦列
		アビ汎用文字列:バフ・デバフ枠 = 火傷
		アビ汎用文字列:バフ・デバフ対象能力 = 火傷
		CALL バフ込み数値算出("回復力")
		アビ汎用変数:効果量 = RESULT * 効果数値 / 100
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@汎用アビリティ処理_ダウンバースト(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ダウンバースト＋＋
	消費ＭＰ = 40
	効果数値 = 260
ELSEIF LEVEL >= 2
	アビ名 = ダウンバースト＋
	消費ＭＰ = 35
	効果数値 = 220
ELSEIF LEVEL >= 1
	アビ名 = ダウンバースト
	消費ＭＰ = 30
	効果数値 = 180
ELSE
	アビ名 = 
ENDIF


SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>敵単体に風属性の［攻撃力✕{効果数値}％］ダメージを与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_風属性
ENDSELECT

@汎用アビリティ処理_グリッターレイ(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値_最小
#DIM 効果数値_最大
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = グリッターレイ＋＋
	消費ＭＰ = 45
	効果数値_最小 = 3
	効果数値_最大 = 6
ELSEIF LEVEL >= 2
	アビ名 = グリッターレイ＋
	消費ＭＰ = 35
	効果数値_最小 = 1
	効果数値_最大 = 5
ELSEIF LEVEL >= 1
	アビ名 = グリッターレイ
	消費ＭＰ = 25
	効果数値_最小 = 1
	効果数値_最大 = 3
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>ランダムな敵に光属性の［攻撃力✕80％］ダメージを{効果数値_最小}～{効果数値_最大}回与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		効果数値 = RAND(効果数値_最小, 効果数値_最大 + 1)
		FOR LOCAL, 0, 効果数値
			アビ汎用変数:敵対心無視オプション = 1
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			アビ汎用変数:効果割合 = 80
			CALL アビテンプレート_ダメージ処理_光属性
		NEXT
ENDSELECT

@汎用アビリティ処理_リバースウェイブ(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = リバースウェイブ＋＋
	消費ＭＰ = 40
	効果数値 = 3
ELSEIF LEVEL >= 2
	アビ名 = リバースウェイブ＋
	消費ＭＰ = 20
	効果数値 = 2
ELSEIF LEVEL >= 1
	アビ名 = リバースウェイブ
	消費ＭＰ = 10
	効果数値 = 1
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>敵横列の"
		ELSE
			詳細文文字列受け渡し変数 += "<br>敵単体の"
		ENDIF
		詳細文文字列受け渡し変数 += @"バフ効果を{効果数値}つ消去する。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 3
			CALL アビ対象選択テンプレート_敵横列
		ELSE
			CALL アビ対象選択テンプレート_敵単体
		ENDIF
		FOR LOCAL, 0, 効果数値
			CALL アビテンプレート_不利効果_バフ消去
		NEXT
ENDSELECT

@汎用アビリティ処理_ファイアウォール(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果回数
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ファイアウォール＋＋
	消費ＭＰ = 50
	効果回数 = 2
	効果数値 = 100
ELSEIF LEVEL >= 2
	アビ名 = ファイアウォール＋
	消費ＭＰ = 40
	効果回数 = 1
	効果数値 = 100
ELSEIF LEVEL >= 1
	アビ名 = ファイアウォール
	消費ＭＰ = 30
	効果回数 = 1
	効果数値 = 80
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>味方全体に{効果回数}回のカウンター［攻撃力✕{効果数値}％］効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = カウンター
		アビ汎用文字列:バフ・デバフ対象能力 = カウンター
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続回数 = 効果回数
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@汎用アビリティ処理_ブリザード(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ブリザード＋＋
	消費ＭＰ = 30
	効果数値 = 150
ELSEIF LEVEL >= 2
	アビ名 = ブリザード＋
	消費ＭＰ = 20
	効果数値 = 100
ELSEIF LEVEL >= 1
	アビ名 = ブリザード
	消費ＭＰ = 15
	効果数値 = 75
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>敵全体に水属性の［攻撃力✕{効果数値}％］ダメージを与える。<br>対象の防御力を3ターン［20％/B枠］減少させる。基礎成功率80％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_水属性
		アビ汎用文字列:バフ・デバフ枠 = 防御力B
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 20
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 80
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@汎用アビリティ処理_ランドスパイク(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ランドスパイク＋＋
	消費ＭＰ = 35
	効果数値 = 150
ELSEIF LEVEL >= 2
	アビ名 = ランドスパイク＋
	消費ＭＰ = 32
	効果数値 = 150
ELSEIF LEVEL >= 1
	アビ名 = ランドスパイク
	消費ＭＰ = 30
	効果数値 = 120
ELSE
	アビ名 = 
ENDIF


SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>敵横列に土属性の［攻撃力✕{効果数値}％］ダメージを与える。"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>このアビリティは相手の防御力を無視する。"
		ELSE
			詳細文文字列受け渡し変数 += "<br>このアビリティは相手の防御力を50％無視する。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵横列
		アビ汎用変数:効果割合 = 効果数値
		IF LEVEL >= 3
			アビ汎用変数:防御無視オプション_割合 = 100
		ELSE
			アビ汎用変数:防御無視オプション_割合 = 50
		ENDIF
		CALL アビテンプレート_ダメージ処理_土属性
ENDSELECT

@汎用アビリティ処理_アベンジャー(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = アベンジャー＋＋
	消費ＭＰ = 40
	効果数値 = 5
ELSEIF LEVEL >= 2
	アビ名 = アベンジャー＋
	消費ＭＰ = 30
	効果数値 = 3
ELSEIF LEVEL >= 1
	アビ名 = アベンジャー
	消費ＭＰ = 20
	効果数値 = 1
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>自身に{効果数値}ターン、カウンター［攻撃力✕100％］効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = カウンター
		アビ汎用文字列:バフ・デバフ対象能力 = カウンター
		アビ汎用変数:効果割合 = 100
		アビ汎用変数:持続ターン = 効果数値
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT


@汎用アビリティ処理_フォーチュネイト(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = フォーチュネイト＋＋
	消費ＭＰ = 40
	効果数値 = 50
ELSEIF LEVEL >= 2
	アビ名 = フォーチュネイト＋
	消費ＭＰ = 30
	効果数値 = 25
ELSEIF LEVEL >= 1
	アビ名 = フォーチュネイト
	消費ＭＰ = 20
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}<br>味方全体のクリティカル率を3ターン［+40％/A枠］する。"
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 += @"<br>味方全体のクリティカルダメージ率を3ターン［+{効果数値}％/A枠］する。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = クリティカル率A
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
		アビ汎用変数:効果量 = 40
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 2
			アビ汎用文字列:バフ・デバフ枠 = クリティカルダメージ率A
			アビ汎用文字列:バフ・デバフ対象能力 = クリティカルダメージ率
			アビ汎用変数:効果量 = 効果数値
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@汎用アビリティ処理_デッドエンド(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
IF LEVEL >= 3
	アビ名 = デッドエンド＋＋
	消費ＭＰ = 45
ELSEIF LEVEL >= 2
	アビ名 = デッドエンド＋
	消費ＭＰ = 30
ELSEIF LEVEL >= 1
	アビ名 = デッドエンド
	消費ＭＰ = 15
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"消費ＭＰ：{消費ＭＰ}"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>敵全体に"
		ELSEIF LEVEL >= 2
			詳細文文字列受け渡し変数 += "<br>敵縦列に"
		ELSE
			詳細文文字列受け渡し変数 += "<br>敵単体に"
		ENDIF
		詳細文文字列受け渡し変数 += "即死効果を与える。基礎成功率30％。"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += @"<br>敵全体の回避率を2ターン［-30％/B枠］する。基礎成功率90％。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 3
			CALL アビ対象選択テンプレート_敵全体_生者のみ
		ELSEIF LEVEL >= 2
			CALL アビ対象選択テンプレート_敵縦列
		ELSE
			CALL アビ対象選択テンプレート_敵単体
		ENDIF
		アビ汎用変数:基礎成功確率 = 30
		CALL アビテンプレート_不利効果_即死
		IF LEVEL >= 3
			アビ汎用文字列:バフ・デバフ枠 = 回避率B
			アビ汎用文字列:バフ・デバフ対象能力 = 回避率
			アビ汎用変数:効果量 = 30
			アビ汎用変数:持続ターン = 2
			アビ汎用変数:基礎成功確率 = 90
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT
