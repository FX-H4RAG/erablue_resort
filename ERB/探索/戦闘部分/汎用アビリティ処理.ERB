@汎用固有アビ_1(ARGS, キャラ番号)
#DIM キャラ番号
SELECTCASE 基礎BATTLE_STATE:キャラ番号:ステータスタイプ保存
	CASE 1
		;攻撃
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_ファイア(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_フリーズ(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_ブロウ(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_クエイク(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_シャイニング(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_ダークフラッド(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
		ENDSELECT
	CASE 2
		;防御
		JUMP 汎用アビリティ処理_フォーメーション(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
	CASE 3
		;回復
		JUMP 汎用アビリティ処理_ヒール(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
	CASE 4, 6
		;サポート, 回避
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
			CASE 0, 3
				;火, 土
				JUMP 汎用アビリティ処理_フォーメーション(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
			CASE 1, 4
				;水, 光
				JUMP 汎用アビリティ処理_ヒール(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_クイックアップ(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_ダークミスト(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
		ENDSELECT
	CASE 5
		;連撃
		JUMP 汎用アビリティ処理_クイックアップ(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
	CASE 7
		;奥義
		JUMP 汎用アビリティ処理_士気向上(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
ENDSELECT
SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
	CASE 0
		;火
		JUMP 汎用アビリティ処理_ファイア(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
	CASE 1
		;水
		JUMP 汎用アビリティ処理_ヒール(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
	CASE 2
		;風
		JUMP 汎用アビリティ処理_クイックアップ(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
	CASE 3
		;土
		JUMP 汎用アビリティ処理_士気向上(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
	CASE 4
		;光
		JUMP 汎用アビリティ処理_シャイニング(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
	CASE 5
		;闇
		JUMP 汎用アビリティ処理_ダークミスト(ARGS, GET_SKILL_LEVEL(1, キャラ番号), キャラ番号)
ENDSELECT

@汎用固有アビ_2(ARGS, キャラ番号)
#DIM キャラ番号
SELECTCASE 基礎BATTLE_STATE:キャラ番号:ステータスタイプ保存
	CASE 1, 5, 7
		;攻撃, 連撃, 奥義
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_ブレイズ(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_アクアシュート(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_ダウンバースト(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_ロックブレイク(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_グリッターレイ(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_ディレイカース(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
		ENDSELECT
	CASE 2, 3, 4
		;防御, 回復, サポート
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_ブレイズ(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_チルブレイン(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_フォローウィンド(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_ロックブレイク(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_フラッシュバン(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_ディレイカース(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
		ENDSELECT
	CASE 6
		;回避
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
			CASE 2
				;風
				JUMP 汎用アビリティ処理_フォローウィンド(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
			CASEELSE
				;風以外
				JUMP 汎用アビリティ処理_ファストステップ(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
		ENDSELECT
ENDSELECT
SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
	CASE 0
		;火
		JUMP 汎用アビリティ処理_ブレイズ(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
	CASE 1
		;水
		JUMP 汎用アビリティ処理_アクアシュート(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
	CASE 2
		;風
		JUMP 汎用アビリティ処理_ダウンバースト(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
	CASE 3
		;土
		JUMP 汎用アビリティ処理_ロックブレイク(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
	CASE 4
		;光
		JUMP 汎用アビリティ処理_フラッシュバン(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
	CASE 5
		;闇
		JUMP 汎用アビリティ処理_ディレイカース(ARGS, GET_SKILL_LEVEL(2, キャラ番号), キャラ番号)
ENDSELECT

@汎用固有アビ_3(ARGS, キャラ番号)
#DIM キャラ番号
SELECTCASE 基礎BATTLE_STATE:キャラ番号:ステータスタイプ保存
	CASE 1, 5, 7
		;攻撃, 連撃, 奥義
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_ヒートアップ(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_ブリザード(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_ポイズンクラウド(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_アーススパイク(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_アスピルレイ(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_アベンジャー(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
		ENDSELECT
	CASE 2, 3, 4, 6
		;防御, 回復, サポート, 回避
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
			CASE 0
				;火
				JUMP 汎用アビリティ処理_ファイアウォール(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 1
				;水
				JUMP 汎用アビリティ処理_マウントベール(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 2
				;風
				JUMP 汎用アビリティ処理_ポイズンクラウド(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 3
				;土
				JUMP 汎用アビリティ処理_リベンジウォール(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 4
				;光
				JUMP 汎用アビリティ処理_アスピルレイ(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
			CASE 5
				;闇
				JUMP 汎用アビリティ処理_チャーム(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
		ENDSELECT
ENDSELECT
SELECTCASE 基礎BATTLE_STATE:キャラ番号:属性
	CASE 0
		;火
		JUMP 汎用アビリティ処理_ヒートアップ(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
	CASE 1
		;水
		JUMP 汎用アビリティ処理_マウントベール(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
	CASE 2
		;風
		JUMP 汎用アビリティ処理_ポイズンクラウド(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
	CASE 3
		;土
		JUMP 汎用アビリティ処理_リベンジウォール(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
	CASE 4
		;光
		JUMP 汎用アビリティ処理_アスピルレイ(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
	CASE 5
		;闇
		JUMP 汎用アビリティ処理_チャーム(ARGS, GET_SKILL_LEVEL(3, キャラ番号), キャラ番号)
ENDSELECT

@GET_SKILL_LEVEL(アビ番号, キャラ番号)
#FUNCTION
#DIM アビ番号
#DIM キャラ番号
SELECTCASE アビ番号
	CASE 1
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:Lv
			CASE IS >= 20
				RETURNF 3
			CASE IS >= 8
				RETURNF 2
			CASEELSE
				RETURNF 1
		ENDSELECT
	CASE 2
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:Lv
			CASE IS >= 28
				RETURNF 3
			CASE IS >= 16
				RETURNF 2
			CASE IS >= 4
				RETURNF 1
			CASEELSE
				RETURNF 0
		ENDSELECT
	CASEELSE
		SELECTCASE 基礎BATTLE_STATE:キャラ番号:Lv
			CASE IS >= 32
				RETURNF 3
			CASE IS >= 24
				RETURNF 2
			CASE IS >= 12
				RETURNF 1
			CASEELSE
				RETURNF 0
		ENDSELECT
ENDSELECT

@汎用アビリティ処理_ファイア(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ファイア＋＋
	効果数値 = 250
	消費ＭＰ = 25
ELSEIF LEVEL >= 2
	アビ名 = ファイア＋
	効果数値 = 220
	消費ＭＰ = 22
ELSEIF LEVEL >= 1
	アビ名 = ファイア
	効果数値 = 200
	消費ＭＰ = 20
ELSE
	アビ名 = 
ENDIF


SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵横列に火属性の［攻撃力✕{効果数値}％］ダメージを与える。
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵単体に火属性の［攻撃力✕{効果数値}％］ダメージを与える。
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 3
			CALL アビ対象選択テンプレート_敵横列
		ELSE
			CALL アビ対象選択テンプレート_敵単体
		ENDIF
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_火属性
ENDSELECT

@汎用アビリティ処理_ブレイズ(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ブレイズ＋＋
	効果数値 = 20
	消費ＭＰ = 30
ELSEIF LEVEL >= 2
	アビ名 = ブレイズ＋
	効果数値 = 15
	消費ＭＰ = 20
ELSEIF LEVEL >= 1
	アビ名 = ブレイズ
	効果数値 = 10
	消費ＭＰ = 15
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵単体に火属性の［攻撃力✕100％］ダメージを与える。<br>対象の攻撃力を3ターン［{効果数値}％/B枠］減少させる。基礎成功率80％。
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>対象に3ターン火傷デバフ［150］を与える。基礎成功率70％。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_ダメージ処理_火属性
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力B
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 80
		CALL アビテンプレート_不利効果_デバフ効果セット
		IF LEVEL >= 3
			アビ汎用文字列:バフ・デバフ枠 = 火傷
			アビ汎用文字列:バフ・デバフ対象能力 = 火傷
			アビ汎用変数:効果量 = 150
			アビ汎用変数:効果割合 = 0
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 70
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT

@汎用アビリティ処理_ヒートアップ(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
IF LEVEL >= 3
	アビ名 = ヒートアップ＋＋
	消費ＭＰ = 40
ELSEIF LEVEL >= 2
	アビ名 = ヒートアップ＋
	消費ＭＰ = 30
ELSEIF LEVEL >= 1
	アビ名 = ヒートアップ
	消費ＭＰ = 20
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>
		詳細文文字列受け渡し変数 += "味方全体の攻撃力を3ターン［30％/A枠］増加させる。<br>"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "味方全体の与ダメージを3ターン［100/A枠］増加させる。<br>"
		ELSEIF LEVEL >= 2
			詳細文文字列受け渡し変数 += "味方全体のDA率を3ターン［20％/A枠］増加させる。<br>"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 30
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 2
			アビ汎用変数:効果割合 = 20
			アビ汎用変数:効果量 = 0
			アビ汎用文字列:バフ・デバフ枠 = DA率A
			アビ汎用文字列:バフ・デバフ対象能力 = DA率
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
		IF LEVEL >= 3
			アビ汎用変数:効果割合 = 0
			アビ汎用変数:効果量 = 100
			アビ汎用文字列:バフ・デバフ枠 = 与ダメ増加A
			アビ汎用文字列:バフ・デバフ対象能力 = 与ダメ増加
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@汎用アビリティ処理_ヒール(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ヒール＋＋
	効果数値 = 100
	消費ＭＰ = 25
ELSEIF LEVEL >= 2
	アビ名 = ヒール＋
	効果数値 = 75
	消費ＭＰ = 15
ELSEIF LEVEL >= 1
	アビ名 = ヒール
	効果数値 = 50
	消費ＭＰ = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>味方単体のＨＰを［回復力✕{効果数値}％］回復させる。
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>対象のデバフを一つ削除する。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方単体_生者のみ
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_回復処理
		IF LEVEL >= 3
			CALL アビテンプレート_有利効果_デバフ消去
		ENDIF
ENDSELECT

@汎用アビリティ処理_アクアシュート(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = アクアシュート＋＋
	効果数値 = 4
	消費ＭＰ = 30
ELSEIF LEVEL >= 2
	アビ名 = アクアシュート＋
	効果数値 = 3
	消費ＭＰ = 20
ELSEIF LEVEL >= 1
	アビ名 = アクアシュート
	効果数値 = 2
	消費ＭＰ = 15
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵単体に水属性の［攻撃力✕80％］ダメージを{効果数値}回与える。
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 80
		CALL アビテンプレート_ダメージ処理_水属性
		CALL アビテンプレート_ダメージ処理_水属性
		IF LEVEL >= 2
			CALL アビテンプレート_ダメージ処理_水属性
		ENDIF
		IF LEVEL >= 3
			CALL アビテンプレート_ダメージ処理_水属性
		ENDIF
ENDSELECT

@汎用アビリティ処理_マウントベール(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
IF LEVEL >= 3
	アビ名 = マウントベール＋＋
	消費ＭＰ = 30
ELSEIF LEVEL >= 2
	アビ名 = マウントベール＋
	消費ＭＰ = 10
ELSEIF LEVEL >= 1
	アビ名 = マウントベール
	消費ＭＰ = 30
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>味方全体に1回弱体効果無効［A枠］効果を与える。
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += @"<br>味方全体に3ターン再生［回復力✕30％/A枠］効果を与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 弱体無効
		アビ汎用文字列:バフ・デバフ対象能力 = 弱体無効
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 3
			アビ汎用文字列:バフ・デバフ枠 = 再生
			アビ汎用文字列:バフ・デバフ対象能力 = 再生
			CALL バフ込み数値算出("回復力")
			アビ汎用変数:効果量 = RESULT * 30 / 100
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:持続回数 = 0
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@汎用アビリティ処理_クイックアップ(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = クイックアップ＋＋
	消費ＭＰ = 25
ELSEIF LEVEL >= 2
	アビ名 = クイックアップ＋
	消費ＭＰ = 15
ELSEIF LEVEL >= 1
	アビ名 = クイックアップ
	消費ＭＰ = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>味方全体の行動速度を3ターン［30％/A枠］増加させる。<br>味方全体の連続攻撃率を3ターン［20％/A枠］増加させる。
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>味方全体の行動速度を3ターン［20％/A枠］増加させる。<br>味方全体の連続攻撃率を3ターン［10％/A枠］増加させる。
		ENDIF
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += @"<br>味方全体に3ターン追撃［10％/A枠］効果を与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		IF LEVEL >= 2
			アビ汎用変数:効果割合 = 30
		ELSE
			アビ汎用変数:効果割合 = 20
		ENDIF
		アビ汎用文字列:バフ・デバフ枠 = 行動速度A
		アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 2
			アビ汎用変数:効果量 = 20
		ELSE
			アビ汎用変数:効果量 = 10
		ENDIF
		アビ汎用変数:効果割合 = 0
		アビ汎用文字列:バフ・デバフ枠 = 連続攻撃率A
		アビ汎用文字列:バフ・デバフ対象能力 = 連続攻撃率
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 3
			アビ汎用変数:効果量 = 0
			アビ汎用変数:効果割合 = 10
			アビ汎用文字列:バフ・デバフ枠 = 追撃A
			アビ汎用文字列:バフ・デバフ対象能力 = 追撃
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@汎用アビリティ処理_フォローウィンド(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = フォローウィンド＋＋
	消費ＭＰ = 30
ELSEIF LEVEL >= 2
	アビ名 = フォローウィンド＋
	消費ＭＰ = 20
ELSEIF LEVEL >= 1
	アビ名 = フォローウィンド
	消費ＭＰ = 15
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>自身の回避率を3ターン[20％/A枠]増加させる。
			詳細文文字列受け渡し変数 += @"<br>自身に3ターン回避時攻撃［攻撃力✕200％/A枠］効果を与える。"
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>自身の回避率を3ターン[10％/A枠]増加させる。
			IF LEVEL >= 2
				詳細文文字列受け渡し変数 += @"<br>自身に3ターン回避時反撃［攻撃力✕100％/A枠］効果を与える。"
			ENDIF
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		IF LEVEL >= 3
			アビ汎用変数:効果量 = 20
		ELSE
			アビ汎用変数:効果量 = 10
		ENDIF
		アビ汎用文字列:バフ・デバフ枠 = 回避率A
		アビ汎用文字列:バフ・デバフ対象能力 = 回避率
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 2
			アビ汎用変数:効果量 = 0
			IF LEVEL >= 3
				アビ汎用変数:効果割合 = 100
			ELSE
				アビ汎用変数:効果割合 = 200
			ENDIF
			アビ汎用文字列:バフ・デバフ枠 = 回避反撃
			アビ汎用文字列:バフ・デバフ対象能力 = 回避反撃
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@汎用アビリティ処理_ポイズンクラウド(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果ターン
IF LEVEL >= 3
	アビ名 = ポイズンクラウド＋＋
	消費ＭＰ = 40
	効果ターン = 5
	効果数値 = 200
ELSEIF LEVEL >= 2
	アビ名 = ポイズンクラウド＋
	消費ＭＰ = 30
	効果ターン = 3
	効果数値 = 200
ELSEIF LEVEL >= 1
	アビ名 = ポイズンクラウド
	消費ＭＰ = 15
	効果ターン = 3
	効果数値 = 100
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵全体に3ターン毒［攻撃力✕{効果数値}％］効果を与える。基礎成功率60％。
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 毒
		アビ汎用文字列:バフ・デバフ対象能力 = 毒
		CALL バフ込み数値算出("攻撃力")
		アビ汎用変数:効果量 = RESULT * 効果数値 / 100
		アビ汎用変数:持続ターン = 効果ターン
		アビ汎用変数:基礎成功確率 = 60
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@汎用アビリティ処理_士気向上(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = 士気向上＋＋
	消費ＭＰ = 25
	効果数値 = 20
ELSEIF LEVEL >= 2
	アビ名 = 士気向上＋
	消費ＭＰ = 15
	効果数値 = 20
ELSEIF LEVEL >= 1
	アビ名 = 士気向上
	消費ＭＰ = 10
	効果数値 = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%％
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += @"<br>味方全体のゲージ上昇効率を3ターン［20％/A枠］増加させる。"
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>味方全体の奥義ゲージを［{効果数値}％］増加させる。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		IF LEVEL >= 3
			アビ汎用変数:効果割合 = 20
			アビ汎用文字列:バフ・デバフ枠 = ゲージ上昇効率A
			アビ汎用文字列:バフ・デバフ対象能力 = ゲージ上昇効率
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
		アビ汎用変数:効果割合 = 0
		アビ汎用変数:効果量 = 効果数値
		CALL アビテンプレート_有利効果_奥義ゲージUP
ENDSELECT

@汎用アビリティ処理_ロックブレイク(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = ロックブレイク＋＋
	効果数値 = 20
	効果成功率 = 100
	消費ＭＰ = 30
ELSEIF LEVEL >= 2
	アビ名 = ロックブレイク＋
	効果数値 = 15
	効果成功率 = 80
	消費ＭＰ = 20
ELSEIF LEVEL >= 1
	アビ名 = ロックブレイク
	効果数値 = 10
	効果成功率 = 80
	消費ＭＰ = 15
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵単体に土属性の［攻撃力✕100％］ダメージを与える。<br>対象の防御力を3ターン［{効果数値}％/B枠］減少させる。基礎成功率{効果成功率}％。
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_ダメージ処理_土属性
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用文字列:バフ・デバフ枠 = 防御力B
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@汎用アビリティ処理_リベンジウォール(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果回数

IF LEVEL >= 3
	アビ名 = リベンジウォール＋＋
	消費ＭＰ = 40
	効果回数 = 4
	効果数値 = 200
ELSEIF LEVEL >= 2
	アビ名 = リベンジウォール＋
	消費ＭＰ = 30
	効果回数 = 3
	効果数値 = 100
ELSEIF LEVEL >= 1
	アビ名 = リベンジウォール
	消費ＭＰ = 20
	効果回数 = 2
	効果数値 = 100
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>自身に{効果回数}回のカウンター［攻撃力✕{効果数値}％］効果を与える。
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = カウンター
		アビ汎用文字列:バフ・デバフ対象能力 = カウンター
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続回数 = 効果回数
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@汎用アビリティ処理_シャイニング(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = シャイニング＋＋
	消費ＭＰ = 25
	効果数値 = 120
ELSEIF LEVEL >= 2
	アビ名 = シャイニング＋
	消費ＭＰ = 15
	効果数値 = 100
ELSEIF LEVEL >= 1
	アビ名 = シャイニング
	消費ＭＰ = 10
	効果数値 = 80
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵単体に光属性の［攻撃力✕{効果数値}％］ダメージを与える。<br>このアビリティは相手の防御力を無視する。
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:防御無視オプション_割合 = 100
		CALL アビテンプレート_ダメージ処理_光属性
ENDSELECT

@汎用アビリティ処理_フラッシュバン(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = フラッシュバン＋＋
	効果数値 = 40
	効果成功率 = 80
	消費ＭＰ = 30
ELSEIF LEVEL >= 2
	アビ名 = フラッシュバン＋
	効果数値 = 20
	効果成功率 = 80
	消費ＭＰ = 20
ELSEIF LEVEL >= 1
	アビ名 = フラッシュバン
	効果数値 = 20
	効果成功率 = 60
	消費ＭＰ = 15
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵縦列に3ターン暗闇［{効果数値}％］効果を与える。基礎成功率{効果成功率}％。
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵縦列
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用文字列:バフ・デバフ枠 = 暗闇
		アビ汎用文字列:バフ・デバフ対象能力 = 暗闇
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@汎用アビリティ処理_アスピルレイ(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = アスピルレイ＋＋
	消費ＭＰ = 40
	効果数値 = 200
ELSEIF LEVEL >= 2
	アビ名 = アスピルレイ＋
	消費ＭＰ = 30
	効果数値 = 150
ELSEIF LEVEL >= 1
	アビ名 = アスピルレイ
	消費ＭＰ = 20
	効果数値 = 150
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵単体に光属性の［回復力✕{効果数値}％］のダメージを与える。
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += @"<br>対象のバフ効果を一つ消去する。"
		ENDIF
		詳細文文字列受け渡し変数 += @"<br>与えたダメージの半分、自身のＨＰを回復する。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用文字列:攻撃側使用能力値 = 回復力
		CALL アビテンプレート_ダメージ処理_光属性
		;RESULTに与えたダメージ量が入っている
		LOCAL = RESULT
		IF LEVEL >= 3
			CALL アビテンプレート_不利効果_バフ消去
		ENDIF
		IF LOCAL > 0
			;ダメージが発生した場合のみ処理
			CALL アビ対象選択テンプレート_自己のみ
			アビ汎用変数:効果割合 = 0
			アビ汎用変数:効果量 = LOCAL / 2
			CALL アビテンプレート_回復処理
		ENDIF
ENDSELECT

@汎用アビリティ処理_ダークミスト(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ダークミスト＋＋
	消費ＭＰ = 25
	効果数値 = 20
ELSEIF LEVEL >= 2
	アビ名 = ダークミスト＋
	消費ＭＰ = 15
	効果数値 = 20
ELSEIF LEVEL >= 1
	アビ名 = ダークミスト
	消費ＭＰ = 10
	効果数値 = 10
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵縦列の
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵単体の
		ENDIF
		詳細文文字列受け渡し変数 += @"攻撃力を3ターン［{効果数値}％/A枠］減少させる。基礎成功率90％。<br>敵単体の防御力を3ターン［{効果数値}％/A枠］減少させる。基礎成功率90％"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 3
			CALL アビ対象選択テンプレート_敵縦列
		ELSE
			CALL アビ対象選択テンプレート_敵単体
		ENDIF
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 90
		CALL アビテンプレート_不利効果_デバフ効果セット
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用文字列:バフ・デバフ枠 = 防御力A
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 90
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@汎用アビリティ処理_ディレイカース(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = ディレイカース＋＋
	効果数値 = 150
	効果成功率 = 100
	消費ＭＰ = 30
ELSEIF LEVEL >= 2
	アビ名 = ディレイカース＋
	効果数値 = 100
	効果成功率 = 100
	消費ＭＰ = 20
ELSEIF LEVEL >= 1
	アビ名 = ディレイカース
	効果数値 = 100
	効果成功率 = 80
	消費ＭＰ = 15
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵単体に闇属性の［攻撃力✕{効果数値}］ダメージを与える。<br>対象にスロウ［1］効果を与える。基礎成功率{効果成功率}％。
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_闇属性
		アビ汎用変数:効果割合 = 0
		アビ汎用変数:効果量 = 1
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_スロウ
ENDSELECT

@汎用アビリティ処理_チャーム(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = チャーム＋＋
	消費ＭＰ = 40
	効果数値 = 35
	効果成功率 = 80
ELSEIF LEVEL >= 2
	アビ名 = チャーム＋
	消費ＭＰ = 30
	効果数値 = 25
	効果成功率 = 60
ELSEIF LEVEL >= 1
	アビ名 = チャーム
	消費ＭＰ = 20
	効果数値 = 25
	効果成功率 = 60
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵横列に
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵単体に
		ENDIF
		詳細文文字列受け渡し変数 += @"3ターン魅了［{効果数値}％］効果を与える。基礎成功率{効果成功率}％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 2
		CALL アビ対象選択テンプレート_敵横列
		ELSE
		CALL アビ対象選択テンプレート_敵単体
		ENDIF
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用文字列:バフ・デバフ枠 = 魅了
		アビ汎用文字列:バフ・デバフ対象能力 = 魅了
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@汎用アビリティ処理_フリーズ(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = フリーズ＋＋
	効果数値 = 30
	効果成功率 = 100
	消費ＭＰ = 30
ELSEIF LEVEL >= 2
	アビ名 = フリーズ＋
	効果数値 = 20
	効果成功率 = 80
	消費ＭＰ = 20
ELSEIF LEVEL >= 1
	アビ名 = フリーズ
	消費ＭＰ = 15
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵単体に水属性の［攻撃力✕100％］ダメージを与える。
		IF LEVEL >= 2
			詳細文文字列受け渡し変数 += @"<br>対象の行動速度を3ターン［{効果数値}％/B枠］減少させる。基礎成功率{効果成功率}％。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_ダメージ処理_水属性
		IF LEVEL >= 2
			アビ汎用変数:効果割合 = 効果数値
			アビ汎用文字列:バフ・デバフ枠 = 行動速度B
			アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 効果成功率
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT

@汎用アビリティ処理_ブロウ(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ブロウ＋＋
	効果数値 = 150
	消費ＭＰ = 40
ELSEIF LEVEL >= 2
	アビ名 = ブロウ＋
	効果数値 = 150
	消費ＭＰ = 32
ELSEIF LEVEL >= 1
	アビ名 = ブロウ
	効果数値 = 120
	消費ＭＰ = 30
ELSE
	アビ名 = 
ENDIF


SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵全体に風属性の［攻撃力✕{効果数値}％］ダメージを与える。
		ELSE
			詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵横列に風属性の［攻撃力✕{効果数値}％］ダメージを与える。
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 3
			CALL アビ対象選択テンプレート_敵全体_生者のみ
		ELSE
			CALL アビ対象選択テンプレート_敵横列
		ENDIF
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_風属性
ENDSELECT

@汎用アビリティ処理_クエイク(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = クエイク＋＋
	効果数値 = 6
	消費ＭＰ = 35
ELSEIF LEVEL >= 2
	アビ名 = クエイク＋
	効果数値 = 5
	消費ＭＰ = 25
ELSEIF LEVEL >= 1
	アビ名 = クエイク
	効果数値 = 4
	消費ＭＰ = 20
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>ランダムな敵に土属性の［攻撃力✕50％］ダメージを{効果数値}回与える。
		RETURN 消費ＭＰ
	CASE "アビ効果"
		FOR LOCAL, 0, 効果数値
			アビ汎用変数:敵対心無視オプション = 1
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			アビ汎用変数:効果割合 = 50
			CALL アビテンプレート_ダメージ処理_土属性
		NEXT
ENDSELECT

@汎用アビリティ処理_ダークフラッド(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ダークフラッド＋＋
	効果数値 = 180
	消費ＭＰ = 30
ELSEIF LEVEL >= 2
	アビ名 = ダークフラッド＋
	効果数値 = 140
	消費ＭＰ = 25
ELSEIF LEVEL >= 1
	アビ名 = ダークフラッド
	効果数値 = 100
	消費ＭＰ = 20
ELSE
	アビ名 = 
ENDIF


SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵縦列に闇属性の［攻撃力✕{効果数値}％］ダメージを与える。
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵縦列
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_闇属性
ENDSELECT

@汎用アビリティ処理_フォーメーション(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = フォーメーション＋＋
	効果数値 = 3
	消費ＭＰ = 40
ELSEIF LEVEL >= 2
	アビ名 = フォーメーション＋
	効果数値 = 2
	消費ＭＰ = 30
ELSEIF LEVEL >= 1
	アビ名 = フォーメーション
	効果数値 = 1
	消費ＭＰ = 20
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>味方全体にダメージカット［75％/{効果数値}回］を付与する。
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>味方全体の防御力を3ターン［10％/A枠］増加させる。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 75
		アビ汎用文字列:バフ・デバフ枠 = ダメージカット
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ増減
		アビ汎用変数:持続回数 = 効果数値
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 3
			アビ汎用変数:効果割合 = 10
			アビ汎用文字列:バフ・デバフ枠 = 防御力A
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@汎用アビリティ処理_ファストステップ(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ファストステップ＋＋
	効果数値 = 20
	消費ＭＰ = 30
ELSEIF LEVEL >= 2
	アビ名 = ファストステップ＋
	効果数値 = 20
	消費ＭＰ = 20
ELSEIF LEVEL >= 1
	アビ名 = ファストステップ
	効果数値 = 10
	消費ＭＰ = 15
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>自身の回避率を3ターン[{効果数値}％/A枠]増加させる。
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>自身の行動速度を3ターン［30％/A枠］増加させる。"
		ELSEIF LEVEL >= 2
			詳細文文字列受け渡し変数 += "<br>自身の行動速度を3ターン［20％/A枠］増加させる。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 効果数値
		アビ汎用文字列:バフ・デバフ枠 = 回避率A
		アビ汎用文字列:バフ・デバフ対象能力 = 回避率
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		IF LEVEL >= 2
			IF LEVEL >= 3
				アビ汎用変数:効果割合 = 30
			ELSE
				アビ汎用変数:効果割合 = 20
			ENDIF
			アビ汎用変数:効果量 = 0
			アビ汎用文字列:バフ・デバフ枠 = 行動速度A
			アビ汎用文字列:バフ・デバフ対象能力 = 行動速度
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@汎用アビリティ処理_チルブレイン(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果成功率
IF LEVEL >= 3
	アビ名 = チルブレイン＋＋
	効果数値 = 200
	効果成功率 = 80
	消費ＭＰ = 30
ELSEIF LEVEL >= 2
	アビ名 = チルブレイン＋
	効果数値 = 100
	効果成功率 = 80
	消費ＭＰ = 20
ELSEIF LEVEL >= 1
	アビ名 = チルブレイン
	効果数値 = 100
	効果成功率 = 60
	消費ＭＰ = 15
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵縦列に3ターン火傷［回復力×{効果数値}％］効果を与える。基礎成功率{効果成功率}％。
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵縦列
		アビ汎用文字列:バフ・デバフ枠 = 火傷
		アビ汎用文字列:バフ・デバフ対象能力 = 火傷
		CALL バフ込み数値算出("回復力")
		アビ汎用変数:効果量 = RESULT * 効果数値 / 100
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果成功率
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@汎用アビリティ処理_ダウンバースト(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ダウンバースト＋＋
	効果数値 = 260
	消費ＭＰ = 40
ELSEIF LEVEL >= 2
	アビ名 = ダウンバースト＋
	効果数値 = 220
	消費ＭＰ = 35
ELSEIF LEVEL >= 1
	アビ名 = ダウンバースト
	効果数値 = 180
	消費ＭＰ = 30
ELSE
	アビ名 = 
ENDIF


SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵単体に風属性の［攻撃力✕{効果数値}％］ダメージを与える。
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_風属性
ENDSELECT

@汎用アビリティ処理_グリッターレイ(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = グリッターレイ＋＋
	消費ＭＰ = 45
ELSEIF LEVEL >= 2
	アビ名 = グリッターレイ＋
	消費ＭＰ = 35
ELSEIF LEVEL >= 1
	アビ名 = グリッターレイ
	消費ＭＰ = 25
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>ランダムな敵に光属性の［攻撃力✕80％］ダメージを
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "3～6回与える。"
		ELSEIF LEVEL >= 2
			詳細文文字列受け渡し変数 += "1～5回与える。"
		ELSE
			詳細文文字列受け渡し変数 += "1～3回与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		IF LEVEL >= 3
			効果数値 = RAND(3, 7)
		ELSEIF LEVEL >= 2
			効果数値 = RAND(1, 6)
		ELSE
			効果数値 = RAND(1, 4)
		ENDIF
		FOR LOCAL, 0, 効果数値
			アビ汎用変数:敵対心無視オプション = 1
			CALL アビ対象選択テンプレート_敵ランダムＸ体(1)
			アビ汎用変数:効果割合 = 80
			CALL アビテンプレート_ダメージ処理_光属性
		NEXT
ENDSELECT

@汎用アビリティ処理_ファイアウォール(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
#DIM 効果回数

IF LEVEL >= 3
	アビ名 = ファイアウォール＋＋
	消費ＭＰ = 50
	効果回数 = 2
	効果数値 = 100
ELSEIF LEVEL >= 2
	アビ名 = ファイアウォール＋
	消費ＭＰ = 40
	効果回数 = 1
	効果数値 = 100
ELSEIF LEVEL >= 1
	アビ名 = ファイアウォール
	消費ＭＰ = 30
	効果回数 = 1
	効果数値 = 80
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>味方全体に{効果回数}回のカウンター［攻撃力✕{効果数値}％］効果を与える。
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = カウンター
		アビ汎用文字列:バフ・デバフ対象能力 = カウンター
		アビ汎用変数:効果割合 = 効果数値
		アビ汎用変数:持続回数 = 効果回数
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@汎用アビリティ処理_ブリザード(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = ブリザード＋＋
	効果数値 = 150
	消費ＭＰ = 30
ELSEIF LEVEL >= 2
	アビ名 = ブリザード＋
	効果数値 = 100
	消費ＭＰ = 20
ELSEIF LEVEL >= 1
	アビ名 = ブリザード
	効果数値 = 75
	消費ＭＰ = 15
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵全体に水属性の［攻撃力✕{効果数値}％］ダメージを与える。<br>対象の防御力を3ターン［20％/B枠］減少させる。基礎成功率80％。
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 効果数値
		CALL アビテンプレート_ダメージ処理_水属性
		アビ汎用変数:効果割合 = 20
		アビ汎用文字列:バフ・デバフ枠 = 防御力B
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 80
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@汎用アビリティ処理_アーススパイク(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値
IF LEVEL >= 3
	アビ名 = アーススパイク＋＋
	効果数値 = 150
	消費ＭＰ = 35
ELSEIF LEVEL >= 2
	アビ名 = アーススパイク＋
	効果数値 = 150
	消費ＭＰ = 32
ELSEIF LEVEL >= 1
	アビ名 = アーススパイク
	効果数値 = 120
	消費ＭＰ = 30
ELSE
	アビ名 = 
ENDIF


SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>敵横列に土属性の［攻撃力✕{効果数値}％］ダメージを与える。
		IF LEVEL >= 3
			詳細文文字列受け渡し変数 += "<br>このアビリティは相手の防御力を無視する。"
		ELSE
			詳細文文字列受け渡し変数 += "<br>このアビリティは相手の防御力を50％無視する。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵横列
		アビ汎用変数:効果割合 = 効果数値
		IF LEVEL >= 3
			アビ汎用変数:防御無視オプション_割合 = 100
		ELSE
			アビ汎用変数:防御無視オプション_割合 = 50
		ENDIF
		CALL アビテンプレート_ダメージ処理_土属性
ENDSELECT

@汎用アビリティ処理_アベンジャー(ARGS, LEVEL, キャラ番号)
#DIM LEVEL
#DIM キャラ番号
#DIM 消費ＭＰ
#DIMS アビ名
#DIM 効果数値

IF LEVEL >= 3
	アビ名 = アベンジャー＋＋
	消費ＭＰ = 40
	効果数値 = 5
ELSEIF LEVEL >= 2
	アビ名 = アベンジャー＋
	消費ＭＰ = 30
	効果数値 = 3
ELSEIF LEVEL >= 1
	アビ名 = アベンジャー
	消費ＭＰ = 20
	効果数値 = 1
ELSE
	アビ名 = 
ENDIF

SELECTCASE ARGS
	CASE "アビ名"
		TSTR:コマンド名受渡 = %アビ名%
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 = 消費ＭＰ：%TOFULL(TOSTR(消費ＭＰ))%<br>自身に{効果数値}ターンの間カウンター［攻撃力✕100％］効果を与える。
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = カウンター
		アビ汎用文字列:バフ・デバフ対象能力 = カウンター
		アビ汎用変数:効果割合 = 100
		アビ汎用変数:持続ターン = 効果数値
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT
