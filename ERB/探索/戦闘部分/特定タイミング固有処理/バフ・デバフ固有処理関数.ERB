@バフ・デバフ測定_特殊補正処理(隊列番号, バフ・デバフ種類, 補正前数値)
#DIM 隊列番号
#DIM 補正前数値
#DIM 特殊補正後数値
#DIMS バフ・デバフ種類
#DIM バフ番号
;特定のキャラや装備などの固有効果へのCALLをここにまとめる
;バフ・デバフが掛かった時ではなく、バフ・デバフの効果が発揮される時に呼ばれるので注意

特殊補正後数値 = 補正前数値

;フィールドを見る
IF 展開中フィールド名格納 != ""
	TRYCCALLFORM 固有バフ・デバフ処理_フィールド_%展開中フィールド名格納%_%バフ・デバフ種類%(隊列番号, 特殊補正後数値)
		特殊補正後数値 = RESULT
	CATCH
	ENDCATCH
ENDIF

IF 隊列番号 < 10
	;味方専用の効果

	;指輪を見る
	TRYCCALLFORM 固有バフ・デバフ処理_指輪_%DT_CELL_GETS("所持指輪データベース", 装備ステータス補正保存:(BATTLE_STATE:隊列番号:キャラ番号):装備指輪番号, "指輪名", 1)%_%バフ・デバフ種類%(隊列番号, 特殊補正後数値)
		特殊補正後数値 = RESULT
	CATCH
	ENDCATCH

	;ジョブ証を見る
	TRYCCALLFORM 固有バフ・デバフ処理_ジョブ証_%DT_CELL_GETS("所持ジョブ証データベース", 装備ステータス補正保存:(BATTLE_STATE:隊列番号:キャラ番号):装備証番号, "ジョブ証名", 1)%_%バフ・デバフ種類%(隊列番号, 特殊補正後数値)
		特殊補正後数値 = RESULT
	CATCH
	ENDCATCH

	;キャラを見る
	TRYCCALLFORM 固有バフ・デバフ処理_キャラ_{NO:(BATTLE_STATE:隊列番号:キャラ番号)}_%バフ・デバフ種類%(隊列番号, 特殊補正後数値)
		特殊補正後数値 = RESULT
	CATCH
	ENDCATCH
ELSE
	;敵専用の効果

	;エネミーを見る
	TRYCCALLFORM 固有バフ・デバフ処理_エネミー_%敵BATTLE_STATE_STR:(隊列番号 - 10):エネミー関数キー%_%バフ・デバフ種類%(隊列番号, 特殊補正後数値)
		特殊補正後数値 = RESULT
	CATCH
	ENDCATCH
ENDIF

;バフを見る
FOR バフ番号, 0, DT_ROW_LENGTH(@"バフデータベース_{隊列番号}")
	TRYCCALLFORM 固有バフ・デバフ処理_バフ_%DT_CELL_GETS(@"バフデータベース_{隊列番号}", バフ番号, "バフ枠")%_%バフ・デバフ種類%(隊列番号, バフ番号, 特殊補正後数値)
		特殊補正後数値 = RESULT
	CATCH
	ENDCATCH
NEXT
;デバフを見る
FOR バフ番号, 0, DT_ROW_LENGTH(@"デバフデータベース_{隊列番号}")
	TRYCCALLFORM 固有バフ・デバフ処理_デバフ_%DT_CELL_GETS(@"デバフデータベース_{隊列番号}", バフ番号, "デバフ枠")%_%バフ・デバフ種類%(隊列番号, バフ番号, 特殊補正後数値)
		特殊補正後数値 = RESULT
	CATCH
	ENDCATCH
NEXT

RETURN 特殊補正後数値


@固有バフ・デバフ処理_バフ_ブレイクアサシン_与ダメージ(隊列番号, バフ・デバフ行数, ダメージ値)
#DIM 隊列番号
#DIM バフ・デバフ行数
#DIM ダメージ値

;与ダメバフ処理時なので、戦闘行動対象で対象が取れる
;敵が行動キャラの場合はスルー
SIF 隊列番号 >= 10
	RETURN ダメージ値
;敵対象じゃない時はスルー
SIF 戦闘行動対象 < 10
	RETURN ダメージ値

;チャージ０の時ダメージ2.5倍
IF 敵BATTLE_STATE:(戦闘行動対象 - 10):チャージターン == 0
	RETURN ダメージ値 * 5 / 2
ENDIF
RETURN ダメージ値
