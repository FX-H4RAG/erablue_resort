@戦闘不能時処理関数(隊列番号)
#DIM バフ番号
#DIM デバフ番号
#DIM 隊列番号
#DIM 生存チェック_味方
#DIM 生存チェック_敵
生存チェック_味方 = 0
生存チェック_敵 = 0

CALL 口上変数初期化
CALL アビテンプレ変数リセット
;戦闘不能時に呼ぶ関数

;フィールドを見る
IF 展開中フィールド名格納 != ""
	TRYCALLFORM 固有戦闘不能時処理_フィールド_%展開中フィールド名格納%()
	;基本的に表示処理まで全て関数の中でやること
	;（戦闘行動キャラが存在しないのでアビテンプレートは使用しない）
ENDIF

IF 隊列番号 < 10
	;指輪を見る
	TRYCCALLFORM 固有戦闘不能時処理_指輪_%DT_CELL_GETS("所持指輪データベース", 装備ステータス補正保存:(BATTLE_STATE:隊列番号:キャラ番号):装備指輪番号, "指輪名", 1)%(隊列番号)
		;表示処理
		CALL アビテンプレート用_表示メッセージ変換処理
		SIF K > 0
			CALL メッセージ欄表示用関数(,,,0)
		CALL 口上変数初期化
		CALL アビテンプレ変数リセット
	CATCH
	ENDCATCH

	;衣装を見る
	TRYCCALLFORM 固有戦闘不能時処理_探索衣装_%DT_CELL_GETS("所持衣装データベース", 装備ステータス補正保存:(BATTLE_STATE:隊列番号:キャラ番号):装備衣装番号, "衣装名", 1)%(隊列番号)
		;表示処理
		CALL アビテンプレート用_表示メッセージ変換処理
		SIF K > 0
			CALL メッセージ欄表示用関数(,,,0)
		CALL 口上変数初期化
		CALL アビテンプレ変数リセット
	CATCH
	ENDCATCH

	;キャラを見る
	TRYCCALLFORM 固有戦闘不能時処理_キャラ_{NO:(BATTLE_STATE:隊列番号:キャラ番号)}(隊列番号)
		;表示処理
		CALL アビテンプレート用_表示メッセージ変換処理
		SIF K > 0
			CALL メッセージ欄表示用関数(,,,0)
		CALL 口上変数初期化
		CALL アビテンプレ変数リセット
	CATCH
	ENDCATCH

	;バフ・デバフを見る
	FOR バフ番号, 0, DT_ROW_LENGTH(@"バフデータベース_{隊列番号}")
		TRYCCALLFORM 固有戦闘不能時処理_バフ_%DT_CELL_GETS(@"バフデータベース_{隊列番号}", バフ番号, "バフ枠")%(隊列番号)
			;表示処理
			CALL アビテンプレート用_表示メッセージ変換処理
			SIF K > 0
				CALL メッセージ欄表示用関数(,,,0)
			CALL 口上変数初期化
			CALL アビテンプレ変数リセット
		CATCH
		ENDCATCH
	NEXT
	FOR デバフ番号, 0, DT_ROW_LENGTH(@"デバフデータベース_{隊列番号}")
		TRYCCALLFORM 固有戦闘不能時処理_デバフ_%DT_CELL_GETS(@"デバフデータベース_{隊列番号}", デバフ番号, "デバフ枠")%(隊列番号)
			;表示処理
			CALL アビテンプレート用_表示メッセージ変換処理
			SIF K > 0
				CALL メッセージ欄表示用関数(,,,0)
			CALL 口上変数初期化
			CALL アビテンプレ変数リセット
		CATCH
		ENDCATCH
	NEXT
ELSE
	;キャラを見る
	TRYCCALLFORM 固有戦闘不能時処理_エネミー_%敵BATTLE_STATE_STR:(隊列番号 - 10):エネミー名%(隊列番号)
		;表示処理
		CALL アビテンプレート用_表示メッセージ変換処理
		SIF K > 0
			CALL メッセージ欄表示用関数(,,,0)
		CALL 口上変数初期化
		CALL アビテンプレ変数リセット
	CATCH
	ENDCATCH
ENDIF

;全滅チェック
FOR LOCAL, 0, 4
	SIF BATTLE_STATE:LOCAL:キャラ番号 < 1
		CONTINUE
	SIF BATTLE_STATE:LOCAL:ＨＰ < 1
		CONTINUE
	生存チェック_味方 = 1
NEXT

IF 生存チェック_味方 == 0
	RETURN 1
ENDIF

FOR LOCAL, 0, 10
	SIF 敵BATTLE_STATE_STR:LOCAL:エネミー名 == ""
		CONTINUE
	SIF 敵BATTLE_STATE:LOCAL:ＨＰ < 1
		CONTINUE
	生存チェック_敵 = 1
NEXT

IF 生存チェック_敵 == 0
	RETURN 1
ENDIF

RETURN 0
