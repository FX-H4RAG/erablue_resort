
@指輪個別文章表示_ゴブリン殺しの指輪
LOCALS = 
LOCALS += @"■ゴブリン殺しの指輪<br><br>"
LOCALS += "対ゴブリンに特化した指輪。<br>"
LOCALS += "ゴブリンに対して攻撃した時、威力が増加する。<br>"
LOCALS += "使い道が限られているためあまり人気がないタイプの指輪。<br>"
LOCALS += "<br>"

詳細文文字列受け渡し変数 = %LOCALS%

@指輪基礎効果文章表示_ゴブリン殺しの指輪(指輪番号)
#DIM 指輪番号
LOCALS = 
LOCALS += "【基礎効果】<br>"
LOCALS += "最大ＨＰ+100 最大ＭＰ+30 攻撃力+80 DA率+10% 対ゴブリン特攻<br>"

詳細文文字列受け渡し変数 = %LOCALS%

@指輪ステータス決定_ゴブリン殺しの指輪(オプション数, 情報取得フラグ = 0)
#DIM オプション数
#DIM 情報取得フラグ
#DIMS 基本補正文字列
#DIMS 特殊補正文字列

基本補正文字列 = 最大ＨＰ_150_最大ＭＰ_30_攻撃力_100_防御力_20_行動速度_50
特殊補正文字列 = _対ゴブリン特攻

IF 情報取得フラグ
	詳細文文字列受け渡し変数 = %基本補正文字列%
	詳細文文字列受け渡し変数 += @"%特殊補正文字列%"
	RETURN 0
ENDIF

;オプション数に応じてオプション効果をランダムで付加
;基礎指輪なのでLv1のオプションしか付かない
;例外作りたいときは例外の関数作ってもろて

オプション補正文字列 = 
IF オプション数
	FOR LOCAL, 0, オプション数
		CALL オプション効果決定(1)
	NEXT
ENDIF

LOCAL = DT_ROW_ADD("所持指輪データベース", "指輪名", "ゴブリン殺しの指輪", "基礎ステータス補正", 基本補正文字列, "特殊ステータス補正", 特殊補正文字列, "オプションステータス補正", オプション補正文字列)


@固有バフ・デバフ処理_指輪_ゴブリン殺しの指輪_与ダメ増加(隊列番号, ダメージ値)
#DIM 隊列番号
#DIM ダメージ値

;対象の種族にゴブリンがあるなら
IF STRFIND(敵BATTLE_STATE_STR:(戦闘行動対象 - 10):種族, "ゴブリン") > -1
	;ダメージ１．５倍
	RETURN ダメージ値 * 150 / 100
ENDIF

;上に引っかからなかったらダメージをそのまま返す
RETURN ダメージ値



