
@指輪個別文章表示_栄冠の指輪・力
LOCALS = 
LOCALS += @"■栄冠の指輪・力<br><br>"
LOCALS += "魔力を引き出すありふれた指輪。<br>"
LOCALS += "特に攻撃する性能に優れた調整がされており、火力重視の騎空士に人気。<br>"
LOCALS += "汎用品であるため全空で広く使われており、ダンジョンにも良く落ちている。<br>"
LOCALS += "<br>"

詳細文文字列受け渡し変数 = %LOCALS%

@指輪基礎効果文章表示_栄冠の指輪・力(指輪番号, 効果番号)
#DIM 効果番号
#DIM 指輪番号
#DIMS オプション効果一時保存, 20
LOCALS = 
LOCALS += "【基礎効果】<br>"
LOCALS += "最大ＨＰ+150 最大ＭＰ+30 攻撃力+100 防御力+20 行動速度+50<br>"

LOCALS:1 = %DT_CELL_GETS("所持指輪データベース", 指輪番号, "オプションステータス補正")%
IF LOCALS:1 != ""
	LOCALS += "<br>【オプション効果】<br>"
	SPLIT LOCALS:1, "_", オプション効果一時保存
	FOR LOCAL, 0, 20
		IF オプション効果一時保存:LOCAL == ""
			BREAK
		ENDIF
		LOCALS += @"%オプション効果一時保存:LOCAL%"
		IF STRCOUNT(オプション効果一時保存:LOCAL, "率")
			LOCAL += 1
			IF TOINT(オプション効果一時保存:LOCAL) > 0
				LOCALS += "+"
			ENDIF
			LOCALS += @"%オプション効果一時保存:LOCAL%"
			LOCALS += "% "
		ELSE
			LOCAL += 1
			IF TOINT(オプション効果一時保存:LOCAL) > 0
				LOCALS += "+"
			ENDIF
			LOCALS += @"%オプション効果一時保存:LOCAL% "
		ENDIF
	NEXT
ENDIF

詳細文文字列受け渡し変数 = %LOCALS%

@指輪ステータス決定_栄冠の指輪・力(オプション数, 情報取得フラグ = 0, 効果指定 = -1)
#DIM 効果指定
#DIM オプション数
#DIM 情報取得フラグ
#DIMS 基本補正文字列

基本補正文字列 = 最大ＨＰ_150_最大ＭＰ_30_攻撃力_100_防御力_20_行動速度_50

IF 情報取得フラグ
	詳細文文字列受け渡し変数 = %基本補正文字列%
	RETURN 0
ENDIF

;オプション数に応じてオプション効果をランダムで付加
;基礎指輪なのでLv1のオプションしか付かない
;例外作りたいときは例外の関数作ってもろて

オプション補正文字列 = 
IF オプション数
	FOR LOCAL, 0, オプション数
		CALL オプション効果決定(1)
	NEXT
ENDIF

LOCAL = DT_ROW_ADD("所持指輪データベース", "指輪名", "栄冠の指輪・力", "基礎ステータス補正", 基本補正文字列, "オプションステータス補正", オプション補正文字列)





