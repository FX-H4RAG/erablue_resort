;NEWGAMEに突っ込む用


@汎用指輪取得処理(指輪名, オプション数)
#DIMS 指輪名
#DIM オプション数

;個別の指輪ERBに飛んでステータス補正を入れ込む
TRYCCALLFORM 指輪ステータス決定_%指輪名%(オプション数)
CATCH
	PRINTW 指定の指輪が見つかりません。
	RETURN -1
ENDCATCH

@指輪付け替え処理画面(キャラ番号)
#DIM キャラ番号
#DIM ページ番号
;表示部分
;装備中指輪表示
DRAWLINE
詳細文文字列受け渡し変数 = 
TRYCALLFORM 指輪基礎効果文章表示_%DT_CELL_GETS("所持指輪データベース", 装備ステータス補正保存:キャラ番号:装備指輪番号, "指輪名")%(装備ステータス補正保存:キャラ番号:装備指輪番号)
LOCALS = <div rect='32px,0px,906px,200px' border='5px' bcolor='#C0C0C0'></div>
LOCALS += @"<div rect='42px, 10px, 180px, 180px'>%顔グラ表示用文字列関数(キャラ番号)%</div>"
LOCALS += @"<div rect='222px,10px,886px,180px'>■%CALLNAME:キャラ番号%が装備中の指輪<br>"
IF 詳細文文字列受け渡し変数 == ""
	LOCALS += "なし</div>"
ELSE
	LOCALS += @"%DT_CELL_GETS("所持指輪データベース", 装備ステータス補正保存:キャラ番号:装備指輪番号, "指輪名")%<br>%詳細文文字列受け渡し変数%</div>"
ENDIF
;所持指輪リスト表示
LOCALS += "<div rect='32px,210px,906px,600px' border='5px' bcolor='#C0C0C0'></div>"
CALL 所持指輪リスト表示(キャラ番号, ページ番号)

;戻るボタンと前次ページボタン
LOCALS += "<div rect='42px,780px,886px,580px'>"
IF ページ番号 > 0
	LOCALS += "<button value='1000'>[1000]前のページ</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF 表示用ステータス格納配列:30:0 > 0
	LOCALS += "<button value='1001'>[1001]次のページ</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF 装備ステータス補正保存:キャラ番号:装備指輪番号 > 0
	LOCALS += "　　　　　　　　　　　　　<button value='1002'>[1002]装備を外す</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　　　　　　　　　　　　　　"
ENDIF
LOCALS += "　　<button value='9999'>[9999]戻る</button></div>"

HTML_PRINT LOCALS, 1

FOR LOCAL, 0, 45
	PRINTL
NEXT

DO
BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 9999
		VARSET 表示用ステータス格納配列:0:0
		ページ番号 = 0
		RETURN 0
	CASE 1002
		DT_CELL_SET "所持指輪データベース", 装備ステータス補正保存:キャラ番号:装備指輪番号, "装備者キャラ番号", 0
		装備ステータス補正保存:キャラ番号:装備指輪番号 = 0
		CALL 装備再計算処理(キャラ番号)
		PRINTFORMW %CALLNAME:キャラ番号%は指輪を外した。
		VARSET 表示用ステータス格納配列:0:0
		ページ番号 = 0
		RETURN 0
	CASE IS < 500
		IF 装備ステータス補正保存:キャラ番号:装備指輪番号 > 0
			DT_CELL_SET "所持指輪データベース", 装備ステータス補正保存:キャラ番号:装備指輪番号, "装備者キャラ番号", 0
		ENDIF
		LOCAL:1 = DT_CELL_GET("所持指輪データベース", LOCAL, "装備者キャラ番号")
		IF LOCAL:1 > 0
			装備ステータス補正保存:(LOCAL:1):装備指輪番号 = 0
			CALL 装備再計算処理(LOCAL:1)
		ENDIF
		装備ステータス補正保存:キャラ番号:装備指輪番号 = LOCAL
		DT_CELL_SET "所持指輪データベース", LOCAL, "装備者キャラ番号", キャラ番号
		CALL 装備再計算処理(キャラ番号)
		PRINTFORMW %CALLNAME:キャラ番号%は%DT_CELL_GETS("所持指輪データベース", LOCAL, "指輪名")%を装備した。
		VARSET 表示用ステータス格納配列:0:0
		ページ番号 = 0
		RETURN 0
	CASE IS < 1000
		TRYCALLFORM 指輪個別文章表示_%DT_CELL_GETS("所持指輪データベース", LOCAL - 500, "指輪名")%
		LOCALS = %詳細文文字列受け渡し変数%
		TRYCALLFORM 指輪基礎効果文章表示_%DT_CELL_GETS("所持指輪データベース", LOCAL - 500, "指輪名")%(LOCAL - 500)
		LOCALS += @"%詳細文文字列受け渡し変数%<br>"
		HTML_PRINT LOCALS
		WAIT
		RESTART
ENDSELECT
LOOP 1

@所持指輪リスト表示(キャラ番号, ページ番号)
#DIM キャラ番号
#DIM ページ番号

;まず指輪のステータス補正を全部変数になおして放り込む
IF 表示用ステータス格納配列:1:0 == 0
	FOR LOCAL, 1, 500
		VARSET LOCALS
		IF DT_CELL_GETS("所持指輪データベース", LOCAL, "指輪名") == ""
			BREAK
		ENDIF
		表示用ステータス格納配列:LOCAL:0 = LOCAL
		SPLIT DT_CELL_GETS("所持指輪データベース", LOCAL, "基礎ステータス補正"), "_", LOCALS
		FOR LOCAL:1, 0, 8
			IF LOCALS:(LOCAL:1 * 2) != ""
				SELECTCASE LOCALS:(LOCAL:1 * 2)
					CASE "最大ＨＰ"
						表示用ステータス格納配列:LOCAL:1 += TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "最大ＭＰ"
						表示用ステータス格納配列:LOCAL:2 += TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "攻撃力"
						表示用ステータス格納配列:LOCAL:3 += TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "防御力"
						表示用ステータス格納配列:LOCAL:4 += TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "回復力"
						表示用ステータス格納配列:LOCAL:5 += TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "行動速度"
						表示用ステータス格納配列:LOCAL:6 += TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "DA率"
						表示用ステータス格納配列:LOCAL:7 += TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "TA率"
						表示用ステータス格納配列:LOCAL:8 += TOINT(LOCALS:(LOCAL:1 * 2 + 1))
				ENDSELECT
			ENDIF
		NEXT
		VARSET LOCALS
		SPLIT DT_CELL_GETS("所持指輪データベース", LOCAL, "オプションステータス補正"), "_", LOCALS
		FOR LOCAL:1, 0, 20
			IF LOCALS:(LOCAL:1 * 2) != ""
				SELECTCASE LOCALS:(LOCAL:1 * 2)
					CASE "最大ＨＰ"
						表示用ステータス格納配列:LOCAL:1 += TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "最大ＭＰ"
						表示用ステータス格納配列:LOCAL:2 += TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "攻撃力"
						表示用ステータス格納配列:LOCAL:3 += TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "防御力"
						表示用ステータス格納配列:LOCAL:4 += TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "回復力"
						表示用ステータス格納配列:LOCAL:5 += TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "行動速度"
						表示用ステータス格納配列:LOCAL:6 += TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "DA率"
						表示用ステータス格納配列:LOCAL:7 += TOINT(LOCALS:(LOCAL:1 * 2 + 1))
					CASE "TA率"
						表示用ステータス格納配列:LOCAL:8 += TOINT(LOCALS:(LOCAL:1 * 2 + 1))
				ENDSELECT
			ENDIF
		NEXT
		表示用ステータス格納配列:LOCAL:9 = DT_CELL_GET("所持指輪データベース", LOCAL, "装備者キャラ番号")
	NEXT
ENDIF

VARSET LOCALS
LOCALS = <div rect='42px,220px,886px,580px'><nobr>
LOCALS += "　　　 指輪名　　　　　　　　　　ＨＰ　　ＭＰ　　攻撃力　防御力　回復力　行動速度　DA率　TA率　装備者<br>"
LOCALS += "--------------------------------------------------------------------------------------------------------------<br>"
IF キャラ番号 > 0
	LOCAL:1 = 装備ステータス補正保存:キャラ番号:装備指輪番号
	FOR LOCAL, 1, 500
		IF 表示用ステータス格納配列:LOCAL:0 == 0
			BREAK
		ENDIF
		LOCALS += @"<button value='{LOCAL + 500}'>[詳細]</button> "
		IF LOCAL:1 == LOCAL
			LOCALS += "<font color='#CCCC00'>"
		ENDIF
		LOCALS += @"<button value='{LOCAL}'>"
		LOCALS += @"%DT_CELL_GETS("所持指輪データベース", LOCAL, "指輪名"), 26, LEFT%"
		FOR LOCAL:2, 1, 9
			IF 表示用ステータス格納配列:LOCAL:(LOCAL:2) > 表示用ステータス格納配列:(LOCAL:1):(LOCAL:2)
				SELECTCASE LOCAL:2
					CASE 1 TO 2
						LOCALS += @"<font color='#55CC55'>{表示用ステータス格納配列:LOCAL:(LOCAL:2), 4}　　</font>"
					CASE 3 TO 5
						LOCALS += @"<font color='#55CC55'>{表示用ステータス格納配列:LOCAL:(LOCAL:2), 6}　</font>"
					CASE 6
						LOCALS += @"<font color='#55CC55'>{表示用ステータス格納配列:LOCAL:(LOCAL:2), 8}　</font>"
					CASE 7 TO 8
						LOCALS += @"<font color='#55CC55'>{表示用ステータス格納配列:LOCAL:(LOCAL:2), 4}　</font>"
				ENDSELECT
			ELSEIF 表示用ステータス格納配列:LOCAL:(LOCAL:2) < 表示用ステータス格納配列:(LOCAL:1):(LOCAL:2)
				SELECTCASE LOCAL:2
					CASE 1 TO 2
						LOCALS += @"<font color='#CC5555'>{表示用ステータス格納配列:LOCAL:(LOCAL:2), 4}　　</font>"
					CASE 3 TO 5
						LOCALS += @"<font color='#CC5555'>{表示用ステータス格納配列:LOCAL:(LOCAL:2), 6}　</font>"
					CASE 6
						LOCALS += @"<font color='#CC5555'>{表示用ステータス格納配列:LOCAL:(LOCAL:2), 8}　</font>"
					CASE 7 TO 8
						LOCALS += @"<font color='#CC5555'>{表示用ステータス格納配列:LOCAL:(LOCAL:2), 4}　</font>"
				ENDSELECT
			ELSE
				SELECTCASE LOCAL:2
					CASE 1 TO 2
						LOCALS += @"{表示用ステータス格納配列:LOCAL:(LOCAL:2), 4}　　"
					CASE 3 TO 5
						LOCALS += @"{表示用ステータス格納配列:LOCAL:(LOCAL:2), 6}　"
					CASE 6
						LOCALS += @"{表示用ステータス格納配列:LOCAL:(LOCAL:2), 8}　"
					CASE 7 TO 8
						LOCALS += @"{表示用ステータス格納配列:LOCAL:(LOCAL:2), 4}　"
				ENDSELECT
			ENDIF
		NEXT
		IF 表示用ステータス格納配列:LOCAL:9 > 0
			LOCALS += @"%CALLNAME:(表示用ステータス格納配列:LOCAL:9)%"
		ENDIF
		LOCALS += "</button>"
		IF LOCAL:1 == LOCAL
			LOCALS += "</font>"
		ENDIF
		LOCALS += "<br>"
	NEXT
ELSE
	LOCAL:1 = 0
	FOR LOCAL, 1, 500
		IF 表示用ステータス格納配列:LOCAL:0 == 0
			BREAK
		ENDIF
		LOCALS += @"<button value='{LOCAL + 500}'>[詳細]</button> "
		LOCALS += @"<button value='{LOCAL}'>"
		LOCALS += @"%DT_CELL_GETS("所持指輪データベース", LOCAL, "指輪名"), 26, LEFT%"
		LOCALS += @"{表示用ステータス格納配列:LOCAL:1, 8, LEFT}"
		LOCALS += @"{表示用ステータス格納配列:LOCAL:2, 8, LEFT}"
		LOCALS += @"{表示用ステータス格納配列:LOCAL:3, 8, LEFT}"
		LOCALS += @"{表示用ステータス格納配列:LOCAL:4, 8, LEFT}"
		LOCALS += @"{表示用ステータス格納配列:LOCAL:5, 8, LEFT}"
		LOCALS += @"{表示用ステータス格納配列:LOCAL:6, 10, LEFT}"
		LOCALS += @"{表示用ステータス格納配列:LOCAL:7, 6, LEFT}"
		LOCALS += @"{表示用ステータス格納配列:LOCAL:8, 8, LEFT}"
		IF 表示用ステータス格納配列:LOCAL:9 > 0
			LOCALS += @"%CALLNAME:(表示用ステータス格納配列:LOCAL:9)%"
		ENDIF
		LOCALS += "</button><br>"
	NEXT
ENDIF
LOCALS += "</div>"

HTML_PRINT LOCALS, 1


@所持指輪リスト表示_USERCOM
#DIM ページ番号
VARSET 表示用ステータス格納配列:0:0

DO
LOCALS = 
LOCALS += "<div rect='32px,210px,906px,600px' border='5px' bcolor='#C0C0C0'></div>"
CALL 所持指輪リスト表示(0, ページ番号)
HTML_PRINT LOCALS, 1
LOCALS = 
;戻るボタンと前次ページボタン
LOCALS += "<div rect='42px,780px,886px,580px'>"
IF ページ番号 > 0
	LOCALS += "<button value='1000'>[1000]前のページ</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF 表示用ステータス格納配列:30:0 > 0
	LOCALS += "<button value='1001'>[1001]次のページ</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
LOCALS += "　　　　　　　　　　　　　　　　　　　　　　　"
LOCALS += "　　<button value='9999'>[9999]戻る</button></div>"
HTML_PRINT LOCALS, 1
FOR LOCAL, 0, 45
	PRINTL
NEXT
BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 9999
		VARSET 表示用ステータス格納配列:0:0
		ページ番号 = 0
		RETURN 1
	CASE IS < 500
		TRYCALLFORM 指輪個別文章表示_%DT_CELL_GETS("所持指輪データベース", LOCAL, "指輪名")%
		LOCALS = %詳細文文字列受け渡し変数%
		TRYCALLFORM 指輪基礎効果文章表示_%DT_CELL_GETS("所持指輪データベース", LOCAL, "指輪名")%(LOCAL)
		LOCALS += @"%詳細文文字列受け渡し変数%<br>"
		HTML_PRINT LOCALS
		WAIT
	CASE IS < 1000
		TRYCALLFORM 指輪個別文章表示_%DT_CELL_GETS("所持指輪データベース", LOCAL - 500, "指輪名")%
		LOCALS = %詳細文文字列受け渡し変数%
		TRYCALLFORM 指輪基礎効果文章表示_%DT_CELL_GETS("所持指輪データベース", LOCAL - 500, "指輪名")%(LOCAL - 500)
		LOCALS += @"%詳細文文字列受け渡し変数%<br>"
		HTML_PRINT LOCALS
		WAIT
ENDSELECT
LOOP 1