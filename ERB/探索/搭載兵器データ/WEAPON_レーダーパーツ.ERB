@搭載兵器関数_レーダーパーツ(処理モード, 兵器id)
#DIMS 処理モード
#DIM 兵器id
#DIM 兵器_攻撃力補正
#DIM 兵器_命中率補正
#DIM 兵器_攻撃力変動
#DIM 兵器_命中率変動
#DIM 兵器_残弾種別
#DIM 兵器_残弾数
#DIMS 兵器_カテゴリ
#DIMS 兵器_距離適性
#DIMS 兵器名一時保存
LOCALS = 

SELECTCASE 処理モード
	CASE "兵器登録"
		兵器名一時保存 = レーダーパーツ
		;攻撃力補正、命中率補正は「その兵器を使用した時に補正を掛ける数値」
		;攻撃力変動、命中率変動は「その兵器を装備しているだけで乗機のステータスを変える数値」
		兵器_攻撃力補正 = 0
		兵器_命中率補正 = 0
		兵器_攻撃力変動 = 0
		兵器_命中率変動 = 10
		;残弾種別 0:戦闘中回数制限 1:探索中回数制限 2:クールダウンターン発生
		兵器_残弾種別 = 2
		兵器_残弾数 = 5
		;カテゴリ 基本は「機械系」と「魔法系」の二択。自由文字列なので任意のカテゴリ名を突っ込んでも良い
		;距離適性 基本は「近距離」と「遠距離」と「味方支援」の三択。自由文字列なので任意のカテゴリ名を突っ込んでも良い
		;複数カテゴリをもたせる場合は「近距離_遠距離」のように記載
		兵器_カテゴリ = 機械系
		兵器_距離適性 = 味方支援

		{
		DT_ROW_ADD "所持兵器データベース", "兵器名", 兵器名一時保存, 
			"攻撃力補正", 兵器_攻撃力補正, "攻撃力補正_強化後", 兵器_攻撃力補正, 
			"命中率補正", 兵器_命中率補正, "命中率補正_強化後", 兵器_命中率補正, 
			"攻撃力変動値", 兵器_攻撃力変動, "攻撃力変動値_強化後", 兵器_攻撃力変動, 
			"命中率変動値", 兵器_命中率変動, "命中率変動値_強化後", 兵器_命中率変動, 
			"残弾種別", 兵器_残弾種別, "残弾数", 兵器_残弾数, "残弾数_強化後", 兵器_残弾数, 
			"兵器カテゴリ", 兵器_カテゴリ, "兵器距離適性", 兵器_距離適性,
		}
	CASE "フレーバー"
		LOCALS += "味方全体の命中率を上げるレーダー。<br>"
		LOCALS += "再使用まではやや長いが、十分な効果時間を持つ。<br>"
		LOCALS += "回避力の高い相手や、命中が不安な攻撃を振る際に有効。<br>"
		詳細文文字列受け渡し変数 = %LOCALS%
	CASE "作成素材"
		作成素材登録:0 = 古びた歯車_3
	CASE "アビ名"
		TSTR:コマンド名受渡 = レーダーパーツ
	CASE "アビ説明文"
		;残弾などの説明文字列は機体側で記述
		LOCALS += "　効果：味方全体の命中率を３ターン[30%/乗機枠]上昇させる。<br>"
		LOCALS += "　　　　サブメンバー搭載時、更にクリティカル率を３ターン[20%/乗機枠]上昇させる。<br>"
		詳細文文字列受け渡し変数 = %LOCALS%
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果量 = 30
		アビ汎用文字列:バフ・デバフ枠 = 命中率_乗機
		アビ汎用文字列:バフ・デバフ対象能力 = 命中率
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
		IF サブメンバーステータス取得("Lv") > 0
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 20
			アビ汎用文字列:バフ・デバフ枠 = クリティカル率_乗機
			アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

;兵装開発では出現しない（入手法：初期から所持）
;@兵器開発対象_レーダーパーツ(処理モード)
