@搭載兵器関数_近接用ヒートアクス(処理モード, 兵器id)
#DIMS 処理モード
#DIM 兵器id
#DIM 兵器_攻撃力補正
#DIM 兵器_命中率補正
#DIM 兵器_攻撃力変動
#DIM 兵器_命中率変動
#DIM 兵器_残弾種別
#DIM 兵器_残弾数
#DIMS 兵器_カテゴリ
#DIMS 兵器_距離適性
#DIMS 兵器名一時保存
LOCALS = 

SELECTCASE 処理モード
	CASE "兵器登録"
		兵器名一時保存 = 近接用ヒートアクス
		;攻撃力補正、命中率補正は「その兵器を使用した時に補正を掛ける数値」
		;攻撃力変動、命中率変動は「その兵器を装備しているだけで乗機のステータスを変える数値」
		兵器_攻撃力補正 = 0
		兵器_命中率補正 = 10
		兵器_攻撃力変動 = 0
		兵器_命中率変動 = 0
		;残弾種別 0:戦闘中回数制限 1:探索中回数制限 2:クールダウンターン発生
		兵器_残弾種別 = 2
		兵器_残弾数 = 5
		;カテゴリ 基本は「機械系」と「魔法系」の二択。自由文字列なので任意のカテゴリ名を突っ込んでも良い
		;距離適性 基本は「近距離」と「遠距離」と「味方支援」の三択。自由文字列なので任意のカテゴリ名を突っ込んでも良い
		;複数カテゴリをもたせる場合は「近距離_遠距離」のように記載
		兵器_カテゴリ = 機械系
		兵器_距離適性 = 近距離

		{
		DT_ROW_ADD "所持兵器データベース", "兵器名", 兵器名一時保存, 
			"攻撃力補正", 兵器_攻撃力補正, "攻撃力補正_強化後", 兵器_攻撃力補正, 
			"命中率補正", 兵器_命中率補正, "命中率補正_強化後", 兵器_命中率補正, 
			"攻撃力変動値", 兵器_攻撃力変動, "攻撃力変動値_強化後", 兵器_攻撃力変動, 
			"命中率変動値", 兵器_命中率変動, "命中率変動値_強化後", 兵器_命中率変動, 
			"残弾種別", 兵器_残弾種別, "残弾数", 兵器_残弾数, "残弾数_強化後", 兵器_残弾数, 
			"兵器カテゴリ", 兵器_カテゴリ, "兵器距離適性", 兵器_距離適性,
		}
	CASE "フレーバー"
		LOCALS += "信頼性の高い近距離用のアクス。<br>"
		LOCALS += "刃の部分を高熱化させる機構により、高いダメージを与えることが出来る。<br>"
		LOCALS += "再使用までの時間はやや長い。<br>"
		詳細文文字列受け渡し変数 = %LOCALS%
	CASE "作成素材"
		作成素材登録:0 = 鉄鉱石_3
		作成素材登録:1 = 赤熱鉱_3
	CASE "アビ名"
		TSTR:コマンド名受渡 = 近接用ヒートアクス
	CASE "アビ説明文"
		;残弾などの説明文字列は機体側で記述
		LOCALS += "　効果：敵単体へ[攻撃力✕300%]の火属性ダメージを与える。<br>"
		LOCALS += "　　　　サブメンバー搭載時、ダメージ係数をサブメンバーの最大Lv✕2だけ増加させる。<br>"
		詳細文文字列受け渡し変数 = %LOCALS%
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 300
		アビ汎用変数:効果割合 += サブメンバーステータス取得("Lv") * 2
		CALL アビテンプレート_ダメージ処理_火属性
ENDSELECT

;兵装開発では出現しない（入手法：初期から所持）
;@兵器開発対象_近接用ヒートアクス(処理モード)
