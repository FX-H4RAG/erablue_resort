@CLOTHES_CHANGE_ウィザードの服(キャラ番号)
#DIM キャラ番号

;一個前の服装を保持
SIF CSTR:キャラ番号:着せ替え服 != "ウィザードの服"
	CSTR:キャラ番号:脱ぐ前の服 = %CSTR:キャラ番号:着せ替え服%

CSTR:キャラ番号:着せ替え服 = ウィザードの服
CSTR:キャラ番号:着せ替え服追加名 = 
TEQUIP:キャラ番号:上半身鎧あり = 0
TEQUIP:キャラ番号:上半身服あり = 1
TEQUIP:キャラ番号:脇出し = 0
TEQUIP:キャラ番号:へそ出し = 0
TEQUIP:キャラ番号:はだけ可 = 0
TEQUIP:キャラ番号:手差し込み可 = 0
TEQUIP:キャラ番号:パイズリ可 = 0
TEQUIP:キャラ番号:上タイツあり = 0
TEQUIP:キャラ番号:上半身下着あり = 1
TEQUIP:キャラ番号:上半身下着抜き取り可 = 0
TEQUIP:キャラ番号:下腹部後鎧あり = 0
TEQUIP:キャラ番号:下腹部前鎧あり = 0
TEQUIP:キャラ番号:下半身服あり = 1
TEQUIP:キャラ番号:スカートめくり可 = 0
TEQUIP:キャラ番号:ずらし可 = 0
TEQUIP:キャラ番号:下タイツあり = 0
TEQUIP:キャラ番号:下半身下着あり = 1
TEQUIP:キャラ番号:下半身下着抜き取り可 = 0
TEQUIP:キャラ番号:上下一体服 = 0
TEQUIP:キャラ番号:上下一体タイツ = 0
TEQUIP:キャラ番号:上タイツ破れ = 0
TEQUIP:キャラ番号:下タイツ破れ = 0

@衣装文章表示_ウィザードの服(ARGS)
LOCALS = 
SELECTCASE ARGS
	CASE "フレーバー"
		LOCALS += "敵をまとめて攻撃する魔術師の服。<br>"
		LOCALS += "全体攻撃や縦列攻撃など範囲魔法が得意。また毒を扱うことも可能。<br>"
		LOCALS += "着ると頭が良くなったような気がする。気がするだけ。<br>"
	CASE "追加アビ_1"
		LOCALS += "■エーテルブラスト<br>"
		LOCALS += "　消費MP：60<br>"
		LOCALS += "　効果：敵全体に攻撃力の100%自属性ダメージを与える。"
	CASE "追加アビ_2"
		LOCALS += "■ポイズン（装備者が短剣得意の時）<br>"
		LOCALS += "　消費MP：12<br>"
		LOCALS += "　効果：敵単体に攻撃力の180%闇属性ダメージを与える。<br>"
		LOCALS += "　　　　攻撃後、毒/3Tを与える。基礎成功率50%。"
	CASE "追加アビ_3"
		LOCALS += "■メガファイア（装備者が杖得意の時）<br>"
		LOCALS += "　消費MP：20<br>"
		LOCALS += "　効果：敵縦列に攻撃力の150%火属性ダメージを与える。"
ENDSELECT
詳細文文字列受け渡し変数 = %LOCALS%

@衣装関数_ウィザードの服(処理モード, 装備キャラ番号)
#DIM 装備キャラ番号
#DIMS 処理モード

SELECTCASE 処理モード
	CASE "衣装種別"
		衣装関連文字列受け渡し = 通常衣装
	CASE "アビ名_1"
		TSTR:コマンド名受渡 = エーテルブラスト
	CASE "消費MP_1"
		RETURN 40
	CASE "効果処理_1"
		CALL 口上変数初期化
		アビ汎用変数:攻撃倍率 = 100
		WSTR:(K++) = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ番号)%のエーテルブラスト！
		CALL アビ汎用_通常ダメージ処理_全体
		IF RESULT == -1
			CALL 口上変数初期化
			RETURN -1
		ENDIF
		BATTLE_STATE:戦闘行動キャラ:ＭＰ -= 60
	CASE "アビ名_2"
		IF 基礎BATTLE_STATE:装備キャラ番号:得意武器 == 得意武器文字列数値変換("短剣")
			TSTR:コマンド名受渡 = ポイズン
		ENDIF
	CASE "消費MP_2"
		RETURN 12
	CASE "効果処理_2"
		CALL 口上変数初期化
		アビ汎用変数:攻撃倍率 = 180
		WSTR:(K++) = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ番号)%のポイズン！
		CALL 効果使用先設定_敵()
		IF RESULT == -1
			CALL 口上変数初期化
			RETURN -1
		ENDIF
		LOCAL = BATTLE_STATE:戦闘行動キャラ:属性
		BATTLE_STATE:戦闘行動キャラ:属性 = 属性文字列数値変換("闇")
		CALL アビ汎用_通常ダメージ処理_単体
		BATTLE_STATE:戦闘行動キャラ:属性 = LOCAL
		IF RESULT == -1
			CALL 口上変数初期化
			RETURN -1
		ENDIF
		BATTLE_STATE:戦闘行動キャラ:ＭＰ -= 12
		アビ汎用文字列:バフ・デバフ枠 = 毒A
		アビ汎用文字列:バフ・デバフ対象能力 = 毒
		アビ汎用変数:効果量 = 50
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 50
		CALL アビ汎用_デバフセット("ポイズン")
		IF RESULT == 1
			KSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%は毒になった！
		ENDIF

	CASE "アビ名_3"
		IF 基礎BATTLE_STATE:装備キャラ番号:得意武器 == 得意武器文字列数値変換("杖")
			TSTR:コマンド名受渡 = メガファイア
		ENDIF
	CASE "消費MP_3"
		RETURN 20
	CASE "効果処理_3"
		CALL 口上変数初期化
		アビ汎用変数:攻撃倍率 = 150
		WSTR:(K++) = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ番号)%のメガファイア！
		LOCAL = BATTLE_STATE:戦闘行動キャラ:属性
		BATTLE_STATE:戦闘行動キャラ:属性 = 属性文字列数値変換("火")
		;全体かばうは有効だが、かばう者が死んだ後の列指定のためにかばう無視で使用先を決める
		CALL 効果使用先設定_敵("かばう無視")
		IF RESULT == -1
			CALL 口上変数初期化
			RETURN -1
		ENDIF
		CALL アビ汎用_通常ダメージ処理_縦列
		BATTLE_STATE:戦闘行動キャラ:属性 = LOCAL
		IF RESULT == -1
			CALL 口上変数初期化
			RETURN -1
		ENDIF
		BATTLE_STATE:戦闘行動キャラ:ＭＰ -= 20
ENDSELECT

