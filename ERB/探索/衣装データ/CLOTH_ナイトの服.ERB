@CLOTHES_CHANGE_ナイトの服(キャラ番号)
#DIM キャラ番号

;一個前の服装を保持
SIF CSTR:キャラ番号:着せ替え服 != "ナイトの服"
	CSTR:キャラ番号:脱ぐ前の服 = %CSTR:キャラ番号:着せ替え服%

CSTR:キャラ番号:着せ替え服 = ナイトの服
CSTR:キャラ番号:着せ替え服追加名 = 
TEQUIP:キャラ番号:上半身鎧あり = 1
TEQUIP:キャラ番号:上半身服あり = 1
TEQUIP:キャラ番号:脇出し = 0
TEQUIP:キャラ番号:へそ出し = 0
TEQUIP:キャラ番号:はだけ可 = 0
TEQUIP:キャラ番号:手差し込み可 = 0
TEQUIP:キャラ番号:パイズリ可 = 0
TEQUIP:キャラ番号:上タイツあり = 0
TEQUIP:キャラ番号:上半身下着あり = 1
TEQUIP:キャラ番号:上半身下着抜き取り可 = 0
TEQUIP:キャラ番号:下腹部後鎧あり = 1
TEQUIP:キャラ番号:下腹部前鎧あり = 1
TEQUIP:キャラ番号:下半身服あり = 1
TEQUIP:キャラ番号:スカートめくり可 = 0
TEQUIP:キャラ番号:ずらし可 = 0
TEQUIP:キャラ番号:下タイツあり = 0
TEQUIP:キャラ番号:下半身下着あり = 1
TEQUIP:キャラ番号:下半身下着抜き取り可 = 0
TEQUIP:キャラ番号:上下一体服 = 0
TEQUIP:キャラ番号:上下一体タイツ = 0
TEQUIP:キャラ番号:上タイツ破れ = 0
TEQUIP:キャラ番号:下タイツ破れ = 0

@衣装文章表示_ナイトの服(ARGS)
LOCALS = 
SELECTCASE ARGS
	CASE "フレーバー"
		LOCALS += "守りを重視した全身鎧の服。<br>"
		LOCALS += "味方を守り、相手の隙を付く技に長けている。<br>"
		LOCALS += "金属鎧を全身に纏うので脱ぎ着はとても大変。<br>"
	CASE "追加アビ_1"
		LOCALS += "■ファランクス<br>"
		LOCALS += "　消費MP：30<br>"
		LOCALS += "　効果：味方全体にダメージカット50%/1回を付与する"
	CASE "追加アビ_2"
		LOCALS += "■ドライブバースト（装備者が剣得意の時）<br>"
		LOCALS += "　消費MP：15<br>"
		LOCALS += "　効果：敵横列に攻撃力の100%自属性ダメージを与える。<br>"
		LOCALS += "　　　　対象のチャージターン１つにつき30%威力が上昇する。"
	CASE "追加アビ_3"
		LOCALS += "■かばう（装備者が槍得意の時）<br>"
		LOCALS += "　消費MP：5<br>"
		LOCALS += "　効果：次の行動時まで、自身が全ての単体攻撃を受け持つ"
ENDSELECT
詳細文文字列受け渡し変数 = %LOCALS%

@衣装関数_ナイトの服(処理モード, 装備キャラ番号)
#DIM 装備キャラ番号
#DIMS 処理モード

SELECTCASE 処理モード
	CASE "衣装種別"
		衣装関連文字列受け渡し = 通常衣装
	CASE "アビ名_1"
		TSTR:コマンド名受渡 = ファランクス
	CASE "消費MP_1"
		RETURN 40
	CASE "効果処理_1"
		CALL 口上変数初期化
		WSTR:(K++) = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ番号)%のファランクス！
		BATTLE_STATE:戦闘行動キャラ:ＭＰ -= 30

		FOR LOCAL, 0, 4
			IF BATTLE_STATE:LOCAL:0
				アビ汎用変数:効果対象 = LOCAL
				アビ汎用変数:効果割合 = 50
				アビ汎用文字列:バフ・デバフ枠 = ダメージカット
				アビ汎用文字列:バフ・デバフ対象能力 = ダメージ増減
				アビ汎用変数:持続回数 = 1
				CALL アビ汎用_バフセット("ファランクス")
				IF RESULT == 1
					KSTR:(K++) = %CALLNAME:(BATTLE_STATE:LOCAL:キャラ番号)%にダメージカット50\%/1回を付与！
				ELSEIF RESULT == -1
					KSTR:(K++) = しかし%CALLNAME:(BATTLE_STATE:LOCAL:キャラ番号)%に効果はなかった…
				ENDIF
			ENDIF
		NEXT
	CASE "アビ名_2"
		IF 基礎BATTLE_STATE:装備キャラ番号:得意武器 == 得意武器文字列数値変換("剣")
			TSTR:コマンド名受渡 = ドライブバースト
		ENDIF
	CASE "消費MP_2"
		RETURN 20
	CASE "効果処理_2"
		CALL 口上変数初期化
		;本来「CALL アビ汎用_通常ダメージ処理_横列」を使うべきだが、相手によって威力が変動するため
		;手動で処理を行う
		CALL 効果使用先設定_敵("単体かばう無視")
		IF RESULT == -1
			RETURN -1
		ENDIF
		アビ汎用変数:攻撃倍率 = 100 + (敵BATTLE_STATE:(戦闘行動対象 - 10):チャージターン * 30)
		WSTR:(K++) = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ番号)%のドライブバースト！
		BATTLE_STATE:戦闘行動キャラ:ＭＰ -= 15

		CALL アビ汎用_通常ダメージ処理

		KSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%に{RESULT}のダメージ！
		IF アビ汎用変数:死亡フラグ
			KSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%は倒れた！
		ENDIF
		
		IF 敵BATTLE_STATE:(戦闘行動対象 - 10):ボスフラグ == 0
			IF 戦闘行動対象 % 2 == 0
				戦闘行動対象 += 1
			ELSE
				戦闘行動対象 -= 1
			ENDIF
			IF 敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名 != ""
				アビ汎用変数:死亡フラグ = 0
				アビ汎用変数:効果対象 = 戦闘行動対象
				アビ汎用変数:攻撃倍率 = 100 + (敵BATTLE_STATE:(戦闘行動対象 - 10):チャージターン * 30)
				CALL アビ汎用_通常ダメージ処理

				KSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%に{RESULT}のダメージ！
				IF アビ汎用変数:死亡フラグ
					KSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%は倒れた！
				ENDIF
			ENDIF
		ENDIF
	CASE "アビ名_3"
		IF 基礎BATTLE_STATE:装備キャラ番号:得意武器 == 得意武器文字列数値変換("槍")
			TSTR:コマンド名受渡 = かばう
		ENDIF
	CASE "消費MP_3"
		RETURN 15
	CASE "効果処理_3"
		CALL 口上変数初期化
		WSTR:(K++) = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ番号)%のかばう！
		BATTLE_STATE:戦闘行動キャラ:ＭＰ -= 5

		アビ汎用変数:効果対象 = 戦闘行動キャラ
		アビ汎用変数:持続行動回数 = 1
		アビ汎用文字列:バフ・デバフ枠 = 単体かばう
		アビ汎用文字列:バフ・デバフ対象能力 = 単体かばう
		CALL アビ汎用_バフセット("かばう")
		KSTR:(K++) = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:キャラ番号)%は全ての単体攻撃を受け持つ構えだ！
ENDSELECT


