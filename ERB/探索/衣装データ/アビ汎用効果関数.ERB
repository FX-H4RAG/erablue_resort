@アビ汎用_通常ダメージ処理_単体(ARGS)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用変数:効果対象
;アビ汎用変数:攻撃倍率
;アビ汎用変数:数値乱数幅
;アビ汎用変数:防御無視率
;アビ汎用文字列:攻撃側使用能力値
;アビ汎用文字列:防御側使用能力値
;アビ汎用変数:死亡フラグ
;------------------------------------------------------------------------
;返り値-1:対象が不正
;------------------------------------------------------------------------
IF アビ汎用変数:かばうフラグ
	WSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%が攻撃をかばった！
ENDIF
CALL アビ汎用_通常ダメージ処理
IF RESULT == -1
	CALL 口上変数初期化
	RETURN -1
ENDIF
KSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%に{RESULT}のダメージ！
IF アビ汎用変数:死亡フラグ
	KSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%は倒れた！
	RETURN 1
ENDIF

@アビ汎用_通常ダメージ処理_横列(ARGS)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用変数:効果対象
;アビ汎用変数:攻撃倍率
;アビ汎用変数:数値乱数幅
;アビ汎用変数:防御無視率
;アビ汎用文字列:攻撃側使用能力値
;アビ汎用文字列:防御側使用能力値
;アビ汎用変数:死亡フラグ
;------------------------------------------------------------------------
;返り値-1:対象が不正
;------------------------------------------------------------------------
CALL アビ汎用_通常ダメージ処理
IF RESULT == -1
	CALL 口上変数初期化
	RETURN -1
ENDIF
IF アビ汎用変数:かばうフラグ
	WSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%が攻撃をかばった！
ENDIF
KSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%に{RESULT}のダメージ！
IF アビ汎用変数:死亡フラグ
	KSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%は倒れた！
ENDIF
LOCAL = アビ汎用変数:死亡フラグ

IF 敵BATTLE_STATE:(戦闘行動対象 - 10):ボスフラグ == 0
	IF アビ汎用変数:かばうフラグ && アビ汎用変数:死亡フラグ == 0
		WSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%が攻撃をかばった！
	ELSE
		IF 戦闘行動対象 % 2 == 0
			戦闘行動対象 += 1
		ELSE
			戦闘行動対象 -= 1
		ENDIF
	ENDIF
	IF 敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名 == ""
		;対象いないならスキップ
	ELSEIF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ == 0
		;対象がすでに死んでるならスキップ
	ELSE
		アビ汎用変数:死亡フラグ = 0
		アビ汎用変数:効果対象 = 戦闘行動対象
		CALL アビ汎用_通常ダメージ処理
		IF RESULT == -1
			CALL 口上変数初期化
			RETURN -1
		ENDIF
		KSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%に{RESULT}のダメージ！
		IF アビ汎用変数:死亡フラグ
			KSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%は倒れた！
		ENDIF
		アビ汎用変数:死亡フラグ += LOCAL
	ENDIF
ENDIF


@アビ汎用_通常ダメージ処理_縦列(ARGS)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用変数:効果対象
;アビ汎用変数:攻撃倍率
;アビ汎用変数:数値乱数幅
;アビ汎用変数:防御無視率
;アビ汎用文字列:攻撃側使用能力値
;アビ汎用文字列:防御側使用能力値
;アビ汎用変数:死亡フラグ
;------------------------------------------------------------------------
;返り値-1:対象が不正
;------------------------------------------------------------------------
VARSET LOCAL

;全体かばうが死んだ時用に対象を保存
LOCAL:10 = 戦闘行動対象

IF 戦闘行動対象 % 2 == 0
	LOCAL:1 = 10
ELSE
	LOCAL:1 = 11
ENDIF

FOR LOCAL, LOCAL:1, LOCAL:1 + 10, 2
	アビ汎用変数:かばうフラグ = 0
	CALL かばうチェック_敵("全体かばう")

	IF 敵BATTLE_STATE_STR:(LOCAL - 10):エネミー名 == ""
		CONTINUE
	ENDIF
	IF 敵BATTLE_STATE:(LOCAL - 10):ＨＰ == 0
		CONTINUE
	ENDIF

	IF アビ汎用変数:かばうフラグ && アビ汎用変数:死亡フラグ == 0
		WSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%が攻撃をかばった！
	ELSE
		戦闘行動対象 = LOCAL
		アビ汎用変数:効果対象 = LOCAL
	ENDIF
	アビ汎用変数:死亡フラグ = 0
	CALL アビ汎用_通常ダメージ処理
	IF RESULT == -1
		CALL 口上変数初期化
		RETURN -1
	ENDIF
	KSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%に{RESULT}のダメージ！
	IF アビ汎用変数:死亡フラグ
		KSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%は倒れた！
		IF アビ汎用変数:かばうフラグ
			;かばう役が死んだら対象列を調整
			IF LOCAL:10 % 2 != LOCAL % 2
				;偶数の時は＋１、奇数の時は－１なのでINVERTBITで調整可能
				INVERTBIT LOCAL, 0
			ENDIF
		ENDIF
	ENDIF
	LOCAL += アビ汎用変数:死亡フラグ

	IF 敵BATTLE_STATE:(戦闘行動対象 - 10):ボスフラグ
		LOCAL += 2
	ENDIF
NEXT


@アビ汎用_通常ダメージ処理_全体(ARGS)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用変数:効果対象
;アビ汎用変数:攻撃倍率
;アビ汎用変数:数値乱数幅
;アビ汎用変数:防御無視率
;アビ汎用文字列:攻撃側使用能力値
;アビ汎用文字列:防御側使用能力値
;アビ汎用変数:死亡フラグ
;------------------------------------------------------------------------
;返り値-1:対象が不正
;------------------------------------------------------------------------

FOR LOCAL, 10, 20
	アビ汎用変数:かばうフラグ = 0
	CALL かばうチェック_敵("全体かばう")

	IF 敵BATTLE_STATE_STR:(LOCAL - 10):エネミー名 == ""
		CONTINUE
	ENDIF
	IF 敵BATTLE_STATE:(LOCAL - 10):ＨＰ == 0
		CONTINUE
	ENDIF

	IF アビ汎用変数:かばうフラグ && アビ汎用変数:死亡フラグ == 0
		WSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%が攻撃をかばった！
	ELSE
		戦闘行動対象 = LOCAL
		アビ汎用変数:効果対象 = LOCAL
	ENDIF
	アビ汎用変数:死亡フラグ = 0
	CALL アビ汎用_通常ダメージ処理
	IF RESULT == -1
		CALL 口上変数初期化
		RETURN -1
	ENDIF
	KSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%に{RESULT}のダメージ！
	IF アビ汎用変数:死亡フラグ
		KSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%は倒れた！
	ENDIF
	LOCAL += アビ汎用変数:死亡フラグ

	IF 敵BATTLE_STATE:(戦闘行動対象 - 10):ボスフラグ
		LOCAL += 3
	ENDIF
NEXT

@アビ汎用_通常回復処理_味方全体(ARGS)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用変数:効果対象
;アビ汎用変数:回復倍率
;アビ汎用変数:数値乱数幅
;アビ汎用文字列:回復側使用能力値
;アビ汎用変数:蘇生可能フラグ
;------------------------------------------------------------------------
;返り値-1:対象が不正 それ以外:与えた回復量
;------------------------------------------------------------------------

FOR LOCAL, 0, 4
	戦闘行動対象 = LOCAL
	アビ汎用変数:効果対象 = LOCAL
	IF BATTLE_STATE:戦闘行動対象:0 < 1
		CONTINUE
	ENDIF
	IF BATTLE_STATE:戦闘行動対象:ＨＰ == 0
		IF アビ汎用変数:蘇生可能フラグ == 0
			CONTINUE
		ELSE
			KSTR:(K++) = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ番号)%が起き上がった！
		ENDIF
	ENDIF
	CALL アビ汎用_通常回復処理
	IF RESULT == -1
		CALL 口上変数初期化
		RETURN -1
	ENDIF
	KSTR:(K++) = %CALLNAME:(BATTLE_STATE:戦闘行動対象:キャラ番号)%のＨＰが{RESULT}回復！
NEXT

@アビ汎用_通常回復処理_敵全体(ARGS)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用変数:効果対象
;アビ汎用変数:回復倍率
;アビ汎用変数:数値乱数幅
;アビ汎用文字列:回復側使用能力値
;アビ汎用変数:蘇生可能フラグ
;------------------------------------------------------------------------
;返り値-1:対象が不正 それ以外:与えた回復量
;------------------------------------------------------------------------

FOR LOCAL, 10, 20
	戦闘行動対象 = LOCAL
	アビ汎用変数:効果対象 = LOCAL
	IF 敵BATTLE_STATE:(戦闘行動対象 - 10):0 < 1
		CONTINUE
	ENDIF
	IF 敵BATTLE_STATE:(戦闘行動対象 - 10):ＨＰ == 0
		IF アビ汎用変数:蘇生可能フラグ == 0
			CONTINUE
		ELSE
			KSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%が起き上がった！
		ENDIF
	ENDIF
	CALL アビ汎用_通常回復処理
	IF RESULT == -1
		CALL 口上変数初期化
		RETURN -1
	ENDIF
	KSTR:(K++) = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%のＨＰが{RESULT}回復！
NEXT

@アビ汎用_通常ダメージ処理
;------------------------------------------------------------------------
;・使用変数
;アビ汎用変数:効果対象
;アビ汎用変数:攻撃倍率
;アビ汎用変数:数値乱数幅
;アビ汎用変数:防御無視率
;アビ汎用文字列:攻撃側使用能力値
;アビ汎用文字列:防御側使用能力値
;アビ汎用変数:死亡フラグ
;------------------------------------------------------------------------
;返り値-1:対象が不正 それ以外:与えたダメージ量
;------------------------------------------------------------------------
#DIM 実ダメージ量

LOCAL = アビ汎用変数:効果対象
IF LOCAL < 10 && BATTLE_STATE:LOCAL:キャラ番号 < 1
	PRINTW 対象が不正です。
	RETURN -1
ELSEIF LOCAL >= 10 && 敵BATTLE_STATE_STR:(LOCAL - 10):エネミー名 == ""
	PRINTW 対象が不正です。
	RETURN -1
ENDIF

IF アビ汎用文字列:攻撃側使用能力値 == ""
	アビ汎用文字列:攻撃側使用能力値 = 攻撃力
ENDIF
IF アビ汎用文字列:防御側使用能力値 == ""
	アビ汎用文字列:防御側使用能力値 = 防御力
ENDIF
IF アビ汎用変数:数値乱数幅 == -1
	アビ汎用変数:数値乱数幅 = 5
ENDIF


CALL ダメージ計算処理(アビ汎用変数:攻撃倍率, アビ汎用変数:防御無視率, アビ汎用文字列:攻撃側使用能力値, アビ汎用文字列:防御側使用能力値)

実ダメージ量 = RESULT
;最低ダメージ１保証
IF 実ダメージ量 < 1
	実ダメージ量 = 1
ENDIF
IF LOCAL < 10
	BATTLE_STATE:LOCAL:ＨＰ = MAX(BATTLE_STATE:LOCAL:ＨＰ - 実ダメージ量, 0)
	IF BATTLE_STATE:LOCAL:ＨＰ < 1
		CALL 戦闘不能時処理(LOCAL)
		アビ汎用変数:死亡フラグ = 1
	ENDIF
ELSEIF LOCAL >= 10
	敵BATTLE_STATE:(LOCAL - 10):ＨＰ = MAX(敵BATTLE_STATE:(LOCAL - 10):ＨＰ - 実ダメージ量, 0)
	IF 敵BATTLE_STATE:(LOCAL - 10):ＨＰ < 1
		CALL 戦闘不能時処理(LOCAL)
		アビ汎用変数:死亡フラグ = 1
	ENDIF
ENDIF
RETURN 実ダメージ量

@アビ汎用_通常回復処理
;------------------------------------------------------------------------
;・使用変数
;アビ汎用変数:効果対象
;アビ汎用変数:回復倍率
;アビ汎用変数:数値乱数幅
;アビ汎用文字列:回復側使用能力値
;アビ汎用変数:蘇生可能フラグ
;------------------------------------------------------------------------
;返り値-1:対象が不正 それ以外:与えた回復量
;------------------------------------------------------------------------
#DIM 実回復量

LOCAL = アビ汎用変数:効果対象
IF LOCAL < 10
	IF BATTLE_STATE:LOCAL:キャラ番号 < 1
		PRINTW 対象が不正です。
		RETURN -1
	ENDIF
	IF BATTLE_STATE:LOCAL:ＨＰ == 0 && アビ汎用変数:蘇生可能フラグ == 0
		PRINTW この効果では蘇生出来ません。
		RETURN -1
	ENDIF
ELSEIF LOCAL >= 10 && 敵BATTLE_STATE_STR:(LOCAL - 10):エネミー名 == ""
	PRINTW 対象が不正です。
	RETURN -1
	IF 敵BATTLE_STATE:(LOCAL - 10):ＨＰ == 0 && アビ汎用変数:蘇生可能フラグ == 0
		PRINTW この効果では蘇生出来ません。
		RETURN -1
	ENDIF
ENDIF


IF アビ汎用文字列:回復側使用能力値 == ""
	アビ汎用文字列:回復側使用能力値 = 回復力
ENDIF
IF アビ汎用変数:数値乱数幅 == -1
	アビ汎用変数:数値乱数幅 = 5
ENDIF

CALL 回復量計算処理(アビ汎用変数:回復倍率, アビ汎用文字列:回復側使用能力値, アビ汎用変数:数値乱数幅)

実回復量 = RESULT
;最低回復１保証
IF 実回復量 < 1
	実回復量 = 1
ENDIF
IF LOCAL < 10
	実回復量 = MIN(実回復量, BATTLE_STATE:LOCAL:最大ＨＰ - BATTLE_STATE:LOCAL:ＨＰ)
	BATTLE_STATE:LOCAL:ＨＰ = MIN(BATTLE_STATE:LOCAL:ＨＰ + 実回復量, BATTLE_STATE:LOCAL:最大ＨＰ)
ELSEIF LOCAL >= 10
	実回復量 = MIN(実回復量, 敵BATTLE_STATE:(LOCAL - 10):最大ＨＰ - 敵BATTLE_STATE:(LOCAL - 10):ＨＰ)
	敵BATTLE_STATE:(LOCAL - 10):ＨＰ = MIN(敵BATTLE_STATE:(LOCAL - 10):ＨＰ + 実回復量, 敵BATTLE_STATE:(LOCAL - 10):最大ＨＰ)
ENDIF

RETURN 実回復量

@アビ汎用_ポンバ
;------------------------------------------------------------------------
;・使用変数
;アビ汎用変数:効果対象
;------------------------------------------------------------------------
;返り値0:対象が不正 -1:ゲージ100%以上 1:成功
;------------------------------------------------------------------------
LOCAL = アビ汎用変数:効果対象
IF LOCAL < 10
	IF BATTLE_STATE:LOCAL:キャラ番号 < 1
		PRINTW 対象が不正です。
		RETURN 0
	ENDIF
	IF BATTLE_STATE:LOCAL:奥義ゲージ < 100
		BATTLE_STATE:LOCAL:奥義ゲージ = 100
		RETURN 1
	ELSE
		RETURN -1
	ENDIF
ELSEIF LOCAL >= 10
	IF 敵BATTLE_STATE_STR:(LOCAL - 10):エネミー名 == ""
		PRINTW 対象が不正です。
		RETURN 0
	ENDIF
	敵BATTLE_STATE:(LOCAL - 10):チャージターン = 敵BATTLE_STATE:(LOCAL - 10):最大チャージターン
	RETURN 1
ENDIF


@アビ汎用_バフセット(アビ名)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用変数:効果対象
;アビ汎用文字列:バフ・デバフ枠
;アビ汎用文字列:バフ・デバフ対象能力
;アビ汎用変数:効果割合
;アビ汎用変数:効果量
;アビ汎用変数:持続ターン
;アビ汎用変数:持続回数
;------------------------------------------------------------------------
;◯強化、◯耐性、連続攻撃率については初期値が０のため効果割合を入れても効果がないことに注意
;------------------------------------------------------------------------
;返り値-1:対象が不正 0:同じ枠で効果高いバフがすでにある 1:成功
;------------------------------------------------------------------------
#DIMS アビ名
#DIM ループ用
#DIM データベース行数
VARSET LOCAL

LOCAL = アビ汎用変数:効果対象
IF LOCAL < 10 && BATTLE_STATE:LOCAL:キャラ番号 < 1
	PRINTW 対象が不正です。
	RETURN -1
ELSEIF LOCAL >= 10 && 敵BATTLE_STATE_STR:(LOCAL - 10):エネミー名 == ""
	PRINTW 対象が不正です。
	RETURN -1
ENDIF

;既存のバフと枠が被ってないかチェック
データベース行数 = DT_ROW_LENGTH(@"バフデータベース_{LOCAL}")
FOR ループ用, 0, データベース行数
	IF DT_CELL_GETS(@"バフデータベース_{LOCAL}", ループ用, "バフ枠") == アビ汎用文字列:バフ・デバフ枠
		;すでに同じ枠のバフがある場合
		IF DT_CELL_GET(@"バフデータベース_{LOCAL}", ループ用, "バフ効果量_割合") == 0 && アビ汎用変数:効果割合 == 0
			;大本バフも追加バフも割合が０の時、固定値を比較
			IF DT_CELL_GET(@"バフデータベース_{LOCAL}", ループ用, "バフ効果量_固定値") <= アビ汎用変数:効果量
				;追加する方の割合が高いか同じ場合は消して新しく追加
				DT_ROW_REMOVE @"バフデータベース_{LOCAL}", ループ用
			ELSE
				;そうでない場合は終了
				RETURN 0
			ENDIF
		ELSEIF DT_CELL_GET(@"バフデータベース_{LOCAL}", ループ用, "バフ効果量_割合") <= アビ汎用変数:効果割合
			;追加する方の割合が高いか同じ場合は消して新しく追加
			DT_ROW_REMOVE @"バフデータベース_{LOCAL}", ループ用
		ELSE
			;そうでない場合は終了
			RETURN 0
		ENDIF
		BREAK
	ENDIF
NEXT

DT_ROW_ADD @"バフデータベース_{LOCAL}", "バフ枠", アビ汎用文字列:バフ・デバフ枠, "バフ対象能力", アビ汎用文字列:バフ・デバフ対象能力, "バフ効果量_割合", アビ汎用変数:効果割合, "バフ効果量_固定値", アビ汎用変数:効果量, "バフ名", アビ名, "持続ターン", アビ汎用変数:持続ターン, "持続回数", アビ汎用変数:持続回数, "持続行動回数", アビ汎用変数:持続行動回数

RETURN 1

@アビ汎用_デバフセット(アビ名)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用変数:効果対象
;アビ汎用文字列:バフ・デバフ枠
;アビ汎用文字列:バフ・デバフ対象能力
;アビ汎用変数:効果割合
;アビ汎用変数:効果量
;アビ汎用変数:持続ターン
;アビ汎用変数:持続回数
;アビ汎用変数:基礎成功確率
;------------------------------------------------------------------------
;◯強化、◯耐性、連続攻撃率については初期値が０のため効果割合を入れても効果がないことに注意
;------------------------------------------------------------------------
;返り値-1:対象が不正 0:同じ枠で効果高いデバフがすでにある 1:成功
;------------------------------------------------------------------------
#DIMS アビ名
#DIM ループ用
#DIM データベース行数
VARSET LOCAL

LOCAL = アビ汎用変数:効果対象
IF LOCAL < 10 && BATTLE_STATE:LOCAL:キャラ番号 < 1
	PRINTW 対象が不正です。
	RETURN -1
ELSEIF LOCAL >= 10 && 敵BATTLE_STATE_STR:(LOCAL - 10):エネミー名 == ""
	PRINTW 対象が不正です。
	RETURN -1
ENDIF

;確率チェック(0の場合は判定しない)
IF アビ汎用変数:基礎成功確率
	LOCAL = RAND:100
	IF アビ汎用変数:基礎成功確率 < RAND:100
		RETURN 0
	ENDIF
ENDIF

;既存のバフと枠が被ってないかチェック
データベース行数 = DT_ROW_LENGTH(@"デバフデータベース_{LOCAL}")
FOR ループ用, 0, データベース行数
	IF DT_CELL_GETS(@"デバフデータベース_{LOCAL}", ループ用, "デバフ枠") == アビ汎用文字列:バフ・デバフ枠
		;すでに同じ枠のデバフがある場合
		IF DT_CELL_GET(@"デバフデータベース_{LOCAL}", ループ用, "デバフ効果量_割合") == 0 && アビ汎用変数:効果割合 == 0
			;大本デバフも追加デバフも割合が０の時、固定値を比較
			IF DT_CELL_GET(@"デバフデータベース_{LOCAL}", ループ用, "デバフ効果量_固定値") <= アビ汎用変数:効果量
				;追加する方の割合が高いか同じ場合は消して新しく追加
				DT_ROW_REMOVE @"デバフデータベース_{LOCAL}", ループ用
			ELSE
				;そうでない場合は終了
				RETURN 0
			ENDIF
		ELSEIF DT_CELL_GET(@"デバフデータベース_{LOCAL}", ループ用, "デバフ効果量_割合") <= アビ汎用変数:効果割合
			;追加する方の割合が高いか同じ場合は消して新しく追加
			DT_ROW_REMOVE @"デバフデータベース_{LOCAL}", ループ用
		ELSE
			;そうでない場合は終了
			RETURN 0
		ENDIF
	ENDIF
NEXT

DT_ROW_ADD @"デバフデータベース_{LOCAL}", "デバフ枠", アビ汎用文字列:バフ・デバフ枠, "デバフ対象能力", アビ汎用文字列:バフ・デバフ対象能力, "デバフ効果量_割合", アビ汎用変数:効果割合, "デバフ効果量_固定値", アビ汎用変数:効果量, "デバフ名", アビ名, "持続ターン", アビ汎用変数:持続ターン, "持続回数", アビ汎用変数:持続回数

RETURN 1


@アビ汎用_バフ消去_敵単体(ARGS)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用変数:効果対象
;------------------------------------------------------------------------
;返り値-1:対象が不正
;------------------------------------------------------------------------
#DIM データベース行数
#DIMS 対象名
VARSET LOCAL

CALL 効果使用先設定_敵("かばう無視")
IF RESULT == -1
	CALL 口上変数初期化
	RETURN -1
ENDIF

LOCAL = アビ汎用変数:効果対象
IF LOCAL < 10
	IF BATTLE_STATE:LOCAL:キャラ番号 < 1
		PRINTW 対象が不正です。
		RETURN -1
	ENDIF
	対象名 = %CALLNAME:(BATTLE_STATE:LOCAL:キャラ番号)%
ELSEIF LOCAL >= 10
	IF 敵BATTLE_STATE_STR:(LOCAL - 10):エネミー名 == ""
		PRINTW 対象が不正です。
		RETURN -1
	ENDIF
	対象名 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
ENDIF

データベース行数 = DT_ROW_LENGTH(@"バフデータベース_{アビ汎用変数:効果対象}")
IF データベース行数
	DT_ROW_REMOVE @"バフデータベース_{アビ汎用変数:効果対象}", データベース行数 - 1
	KSTR:(K++) = %対象名%の強化効果が消去された！
ELSE
	KSTR:(K++) = %対象名%には効果がなかった…
ENDIF

@アビ汎用_デバフ消去_味方単体(ARGS)
;------------------------------------------------------------------------
;・使用変数
;アビ汎用変数:効果対象
;------------------------------------------------------------------------
;返り値-1:対象が不正
;------------------------------------------------------------------------
#DIM データベース行数
#DIMS 対象名
VARSET LOCAL

LOCAL = アビ汎用変数:効果対象
IF LOCAL < 10
	IF BATTLE_STATE:LOCAL:キャラ番号 < 1
		PRINTW 対象が不正です。
		RETURN -1
	ENDIF
	対象名 = %CALLNAME:(BATTLE_STATE:LOCAL:キャラ番号)%
ELSEIF LOCAL >= 10
	IF 敵BATTLE_STATE_STR:(LOCAL - 10):エネミー名 == ""
		PRINTW 対象が不正です。
		RETURN -1
	ENDIF
	対象名 = %敵BATTLE_STATE_STR:(戦闘行動対象 - 10):エネミー名%
ENDIF

データベース行数 = DT_ROW_LENGTH(@"デバフデータベース_{アビ汎用変数:効果対象}")
IF データベース行数
	DT_ROW_REMOVE @"デバフデータベース_{アビ汎用変数:効果対象}", データベース行数 - 1
	KSTR:(K++) = %対象名%の弱体効果が消去された！
ELSE
	KSTR:(K++) = %対象名%には効果がなかった…
ENDIF



@アビ汎用変数文字列リセット
VARSET アビ汎用変数:0
VARSET アビ汎用文字列:0
アビ汎用変数:数値乱数幅 = -1


@効果使用先設定_味方単体
CALL 口上変数初期化
KSTR:(K++) = 誰に使用しますか？
KSTR:(K++) = 　
KSTR:(K++) = 　
KSTR:(K++) = 　
KSTR:(K++) = 　
KSTR:(K++) = 　
KSTR:(K++) = <button value='999'>[999]戻る</button>
キャラ画像ボタン化フラグ = 1
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 0 TO 3
		IF BATTLE_STATE:RESULT:キャラ番号 < 1
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		戦闘行動対象 = RESULT
		アビ汎用変数:効果対象 = 戦闘行動対象
	CASE 999
		RETURN -1
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT

@効果使用先設定_敵(処理オプション)
#DIMS 処理オプション
アビ汎用変数:かばうフラグ = 0

CALL 口上変数初期化
KSTR:(K++) = 誰に使用しますか？
KSTR:(K++) = 　
KSTR:(K++) = 　
KSTR:(K++) = 　
KSTR:(K++) = 　
KSTR:(K++) = 　
KSTR:(K++) = <button value='999'>[999]戻る</button>
エネミー画像ボタン化フラグ = 1
CALL メッセージ欄表示用関数(,,,0)
CALL 口上変数初期化
$INPUT_LOOP
INPUT
SELECTCASE RESULT
	CASE 10 TO 19
		IF 敵BATTLE_STATE_STR:(RESULT - 10):エネミー名 == ""
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ELSEIF 敵BATTLE_STATE:(RESULT - 10):ＨＰ < 1
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		戦闘行動対象 = RESULT
		アビ汎用変数:効果対象 = 戦闘行動対象
	CASE 999
		RETURN -1
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT

;かばうチェック
IF STRCOUNT(処理オプション, "かばう無視")
ELSE
	IF STRCOUNT(処理オプション, "単体かばう無視")
		CALL かばうチェック_敵("全体かばう")
	ELSE
		CALL かばうチェック_敵()
	ENDIF
ENDIF


