

@汎用衣装取得処理(衣装名一時保存)
#DIMS 衣装名一時保存
	
;アンロック式なのでフラグ立てるだけ
;ERDいじったりしなくていいようにデータベース管理
ENUMFUNCBEGINSWITH @"衣装関数_%衣装名一時保存%"
IF RESULT
	FOR LOCAL, 1, DT_ROW_LENGTH("所持衣装データベース")
		SELECTCASE DT_CELL_GETS("所持衣装データベース", LOCAL, "衣装名") 
			CASE 衣装名一時保存
				;すでに追加済みの場合は-2を返す
				RETURN -2
		ENDSELECT
	NEXT
	;抜けてきたら登録
	CALLFORM 衣装関数_%衣装名一時保存%("衣装種別")
	DT_ROW_ADD "所持衣装データベース", "衣装名", 衣装名一時保存, "衣装種別", 衣装関連文字列受け渡し
	RETURN 1
ELSE
	;そもそも衣装の関数そのものが無い時は-1を返す
	PRINTW 指定の衣装が見つかりません。
	RETURN -1
ENDIF

@衣装付け替え処理画面(キャラ番号)
#DIM キャラ番号
#DIM ページ番号
#DIMS 表示衣装種別
#DIM 表示アビ番号
#DIM 関数総数

IF 表示衣装種別 == ""
	表示衣装種別 = 通常衣装
ENDIF

;表示部分
;装備中衣装表示
DRAWLINE
詳細文文字列受け渡し変数 = 
TRYCALLFORM 衣装関数_%DT_CELL_GETS("所持衣装データベース", 装備ステータス補正保存:キャラ番号:装備衣装番号, "衣装名", 1)%("フレーバー")
LOCALS = <div rect='200,0,5662,1250' border='31' bcolor='#C0C0C0'></div>
LOCALS += @"<div rect='262, 62, 1125, 1125'>%顔グラ表示用文字列関数(キャラ番号)%</div>"
LOCALS += @"<div rect='1387,62,5537,1125'>■%CALLNAME:キャラ番号%が装備中の探索衣装<br>"
IF 詳細文文字列受け渡し変数 == ""
	LOCALS += "なし</div>"
ELSE
	LOCALS += @"%DT_CELL_GETS("所持衣装データベース", 装備ステータス補正保存:キャラ番号:装備衣装番号, "衣装名", 1)%<br>%詳細文文字列受け渡し変数%</div>"
ENDIF
;所持衣装リスト表示
LOCALS += "<div rect='200,1312,5662,3750' border='31' bcolor='#C0C0C0'></div>"
CALL 所持衣装リスト表示(キャラ番号, ページ番号, 表示衣装種別)

;戻るボタンと前次ページボタン
LOCALS += "<div rect='262,4875,5537,3625'>"
IF ページ番号 > 0
	LOCALS += "<button value='1000'>[1000]前のページ</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF 表示用ステータス格納配列:30:0 > 0
	LOCALS += "<button value='1001'>[1001]次のページ</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF 装備ステータス補正保存:キャラ番号:装備衣装番号 > 0
	LOCALS += "　　　　　　　　　　　　　<button value='1002'>[1002]装備を外す</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　　　　　　　　　　　　　　"
ENDIF
LOCALS += "　　<button value='9999'>[9999]戻る</button></div>"

HTML_PRINT LOCALS, 1

FOR LOCAL, 0, 45
	PRINTL
NEXT

DO
BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 9999
		ページ番号 = 0
		表示衣装種別 = 通常衣装
		RETURN 0
	CASE 1002
		装備ステータス補正保存:キャラ番号:装備衣装番号 = 0
		PRINTFORMW %CALLNAME:キャラ番号%は探索衣装を外した。
		ページ番号 = 0
		RETURN 0
	CASE IS < 500
		装備ステータス補正保存:キャラ番号:装備衣装番号 = DT_CELL_GET("所持衣装データベース", LOCAL, "id")
		PRINTFORMW %CALLNAME:キャラ番号%は%DT_CELL_GETS("所持衣装データベース", LOCAL, "衣装名")%を装備した。
		表示衣装種別 = 通常衣装
		ページ番号 = 0
		RETURN 0
	CASE IS < 1000
		TRYCALLFORM 衣装関数_%DT_CELL_GETS("所持衣装データベース", LOCAL - 500, "衣装名")%("フレーバー")
		LOCALS = %詳細文文字列受け渡し変数%<br>
		LOCALS += @"・追加アビ<br>"
		VARSET RESULTS
		関数総数 = ENUMFUNCBEGINSWITH(@"衣装アビ関数_%DT_CELL_GETS("所持衣装データベース", LOCAL - 500, "衣装名")%_")
		FOR 表示アビ番号, 0, 関数総数
			詳細文文字列受け渡し変数 = 
			TRYCALLFORM %RESULTS:表示アビ番号%("アビ説明文")
			LOCALS += @"%詳細文文字列受け渡し変数%<br>"
		NEXT
		LOCALS += "<br>"
		HTML_PRINT LOCALS
		WAIT
		RESTART
ENDSELECT
LOOP 1

@所持衣装リスト表示(キャラ番号, ページ番号, 表示衣装種別 = "通常衣装")
#DIM キャラ番号
#DIM ページ番号
#DIM 装備中衣装番号
#DIMS 表示衣装種別

;衣装はアンロック式なので一覧だと名前だけ表示でいいはず
装備中衣装番号 = 0
;まず装備している衣装番号取得
IF キャラ番号 > 0
	装備中衣装番号 = 装備ステータス補正保存:キャラ番号:装備衣装番号
ENDIF

VARSET LOCAL
VARSET LOCALS
LOCALS = <div rect='262,1375,5537,3625'><nobr>
FOR LOCAL, 1, 200
	DT_CELL_GETS "所持衣装データベース", LOCAL, "衣装名"
	IF RESULTS == ""
		BREAK
	ENDIF
	;衣装名を一旦保存
	LOCALS:10 = %RESULTS%
	DT_CELL_GETS "所持衣装データベース", LOCAL, "衣装種別"
	IF RESULTS == 表示衣装種別
		;表示種別が合うなら表示
		IF 装備中衣装番号 == DT_CELL_GET("所持衣装データベース", LOCAL, "id")
			LOCALS += "<font color='#CCCC00'>"
		ENDIF
		LOCALS += @"<button value='{LOCAL}'>"
		LOCALS += @"%LOCALS:10, 30, LEFT%</button>"
		IF 装備中衣装番号 == DT_CELL_GET("所持衣装データベース", LOCAL, "id")
			LOCALS += "</font>"
		ENDIF
		LOCALS += @"<button value='{LOCAL + 500}'>"
		LOCALS += "[詳細]</button>"
		LOCAL:1 += 1
		IF LOCAL:1 % 2 == 0
			LOCALS += "<br>"
		ELSE
			LOCALS += "　　　"
		ENDIF
	ENDIF
NEXT
LOCALS += "</div>"

HTML_PRINT LOCALS, 1

@所持衣装リスト表示_USERCOM
#DIM ページ番号
#DIM 表示アビ番号
#DIM 関数総数

LOCALS = 
LOCALS += "<div rect='200,1312,5662,3750' border='31' bcolor='#C0C0C0'></div>"
HTML_PRINT LOCALS, 1
CALL 所持衣装リスト表示(0, ページ番号)
LOCALS = 
;戻るボタンと前次ページボタン
LOCALS += "<div rect='262,4875,5537,3625'>"
IF ページ番号 > 0
	LOCALS += "<button value='1000'>[1000]前のページ</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
IF 表示用ステータス格納配列:30:0 > 0
	LOCALS += "<button value='1001'>[1001]次のページ</button>　　"
ELSE
	LOCALS += "　　　　　　　　　　"
ENDIF
LOCALS += "　　　　　　　　　　　　　　　　　　　　　　　"
LOCALS += "　　<button value='9999'>[9999]戻る</button></div>"
HTML_PRINT LOCALS, 1
FOR LOCAL, 0, 45
	PRINTL
NEXT
BINPUTS
IF ISNUMERIC(RESULTS) == 0
	VARSET 表示用ステータス格納配列:0:0
	表示切り替え文字列 = %RESULTS%
	ページ番号 = 0
	RETURN 0
ENDIF
LOCAL = TOINT(RESULTS)
SELECTCASE LOCAL
	CASE 9999
		VARSET 表示用ステータス格納配列:0:0
		ページ番号 = 0
		RETURN -1
	CASE IS < 500
		TRYCALLFORM 衣装関数_%DT_CELL_GETS("所持衣装データベース", LOCAL, "衣装名")%("フレーバー")
		LOCALS = %詳細文文字列受け渡し変数%<br>

		LOCALS += @"・追加アビ<br>"
		VARSET RESULTS
		関数総数 = ENUMFUNCBEGINSWITH(@"衣装アビ関数_%DT_CELL_GETS("所持衣装データベース", LOCAL, "衣装名")%_")
		FOR 表示アビ番号, 0, 関数総数
			詳細文文字列受け渡し変数 = 
			TRYCALLFORM %RESULTS:表示アビ番号%("アビ説明文")
			LOCALS += @"%詳細文文字列受け渡し変数%<br>"
		NEXT
		HTML_PRINT LOCALS
		WAIT
	CASE IS < 1000
		TRYCALLFORM 衣装関数_%DT_CELL_GETS("所持衣装データベース", LOCAL - 500, "衣装名")%("フレーバー")
		LOCALS = %詳細文文字列受け渡し変数%<br>

		LOCALS += @"・追加アビ<br>"
		VARSET RESULTS
		関数総数 = ENUMFUNCBEGINSWITH(@"衣装アビ関数_%DT_CELL_GETS("所持衣装データベース", LOCAL - 500, "衣装名")%_")
		FOR 表示アビ番号, 0, 関数総数
			詳細文文字列受け渡し変数 = 
			TRYCALLFORM %RESULTS:表示アビ番号%("アビ説明文")
			LOCALS += @"%詳細文文字列受け渡し変数%<br>"
		NEXT
		HTML_PRINT LOCALS
		WAIT
ENDSELECT
