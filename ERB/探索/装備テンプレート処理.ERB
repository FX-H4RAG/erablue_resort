@装備テンプレート処理()
#DIM 編成番号

LOCALS = <div rect='200,0,5662,500' border='31' bcolor='#C0C0C0' padding='50,25,50'>
LOCALS += "適用テンプレートを選んでください。<br>"
LOCALS += "※現在の編成にキャラがいない枠、及び未設定の装備枠は変更されません。<br>"
LOCALS += "　　　　　　　　　　　　　　　　　　　　　　　　<button value='998'>[998] 別のテンプレートをコピー</button>　　<button value='999'>[999] 戻る</button>"
LOCALS += "</div>"
HTML_PRINT LOCALS, 1
CALL 編成セットボタン表示(編成番号, 200, 450)
FOR LOCAL, 0, 6
	PRINTL
NEXT
DRAWLINE

CALL 装備テンプレート表示(編成番号)
FOR LOCAL, 0, 20
	PRINTL
NEXT

DRAWLINE
BINPUT
SELECTCASE RESULT
	CASE 999
	CASE 998
	CASEELSE
		編成番号 = RESULT
ENDSELECT
RESTART


@装備テンプレート表示(編成番号 = 0)
#DIM 編成番号
#DIM キャラ順番
#DIMS 指輪名保存
#DIMS 耳飾り名保存
#DIMS 証名保存

PRINTL ・メイン枠　　　　　　　　　　　　　　　　　　　　　　　　　　　・サブ枠
LOCALS = 
FOR キャラ順番, 0, 14
	IF キャラ順番 > 3 && キャラ順番 < 10
		CONTINUE
	ENDIF
	LOCALS += @"<div rect='{(キャラ順番 / 10) * 3200 + 30},{(キャラ順番 % 10) * 540},2462,500' border='31' bcolor='#C0C0C0' padding='50,25,50'>"
	指輪名保存 = 
	耳飾り名保存 = 
	証名保存 = 
	SIF 装備テンプレ記録配列:編成番号:(キャラ順番 * 10)
		指輪名保存 = %DT_CELL_GETS("所持指輪データベース", 装備テンプレ記録配列:編成番号:(キャラ順番 * 10), "指輪名", 1)%
	SIF 装備テンプレ記録配列:編成番号:(キャラ順番 * 10 + 1)
		耳飾り名保存 = %DT_CELL_GETS("所持耳飾りデータベース", 装備テンプレ記録配列:編成番号:(キャラ順番 * 10 + 1), "耳飾り名", 1)%
	SIF 装備テンプレ記録配列:編成番号:(キャラ順番 * 10 + 2)
		証名保存 = %DT_CELL_GETS("所持ジョブ証データベース", 装備テンプレ記録配列:編成番号:(キャラ順番 * 10 + 2), "ジョブ証名", 1)%

	IF 指輪名保存 != ""
		LOCALS += @"<button value='{キャラ順番 * 10}'>[指輪　　：%指輪名保存%]</button><br>"
	ELSE
		LOCALS += @"<button value='{キャラ順番 * 10}'>[指輪　　：未設定]</button><br>"
	ENDIF
	IF 耳飾り名保存 != ""
		LOCALS += @"<button value='{キャラ順番 * 10 + 1}'>[耳飾り　：%耳飾り名保存%]</button><br>"
	ELSE
		LOCALS += @"<button value='{キャラ順番 * 10 + 1}'>[耳飾り　：未設定]</button><br>"
	ENDIF
	IF 証名保存 != ""
		LOCALS += @"<button value='{キャラ順番 * 10 + 2}'>[ジョブ証：%証名保存%]</button><br>"
	ELSE
		LOCALS += @"<button value='{キャラ順番 * 10 + 2}'>[ジョブ証：未設定]</button><br>"
	ENDIF
	LOCALS += @"</div>"
NEXT
HTML_PRINT LOCALS, 1

