@証関数_シーフの証(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード

SELECTCASE 処理モード
	CASE "ジョブ証種別"
		証関連文字列受け渡し = 通常ジョブ
	CASE "証名"
		TSTR:コマンド名受渡 = シーフの証
	CASE "クラス名"
		SIF 対象キャラ <= 0
			RETURN 0
		SELECTCASE GET_JOBSKILL_LEVEL(対象キャラ)
			CASE IS >= 3
				RESULTS = ホークアイ
				RESULTS:1 = Class.III
				RETURN 1
			CASE 2
				RESULTS = レイダー
				RESULTS:1 = Class.II
				RETURN 1
			CASEELSE
				RESULTS = シーフ
				RESULTS:1 = Class.I
				RETURN 1
		ENDSELECT
	CASE "フレーバー"
		詳細文文字列受け渡し変数 '= "トリッキーなアビと独自の動きで戦う証。<br>"
		詳細文文字列受け渡し変数 += "特定条件で大火力を出したり、アイテムを手に入れやすくなる技を扱う。<br>"
		詳細文文字列受け渡し変数 += "付けると手先が器用になった気分になる。<br>"
ENDSELECT

@証出現率_シーフの証(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 2
	RETURN 0
SELECTCASE 入手タイプ
	CASE "輸入"
		RETURN 装備レアリティ_Ｎ
ENDSELECT

@証アビ関数_シーフの証_1(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM バフID
#DIM LEVEL
#DIM 消費ＭＰ
#DIM 効果数値, 1
#DIMS CONST アビ名 = "ブレイクアサシン"
#DIMS CONST 数値フォーマット = "#,.0##"
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		消費ＭＰ = 50
		効果数値:0 = 2750
	CASE 2
		消費ＭＰ = 50
		効果数値:0 = 2500
	CASEELSE
		消費ＭＰ = 100
		効果数値:0 = 2500
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　効果：通常攻撃を行う。<br>"
		詳細文文字列受け渡し変数 += @"　　　　対象のCTが0の時、この通常攻撃のダメージは%TOSTR(効果数値:0, 数値フォーマット)%倍になる。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = ブレイクアサシン
		アビ汎用文字列:バフ・デバフ対象能力 = ブレイクアサシン
		アビ汎用変数:効果割合 = 効果数値:0
		アビ汎用変数:持続行動回数 = 1
		アビ汎用変数:消去不可オプション = 1
		アビ汎用変数:特殊表示オプション = -1
		CALL アビテンプレート_有利効果_バフ効果セット
		;ダメージ計算や攻撃への反応でバフが増減したときのためにIDで保持する
		バフID = DT_CELL_GET("戦闘効果データベース", バフ番号取得_枠("ブレイクアサシン", 戦闘行動キャラ), "id")
		; 選択時メッセージより前にメッセージを出さないための処理
		CALL アビテンプレ変数リセット

		TRYCCALLFORM 固有通常攻撃_{NO:(BATTLE_STATE:戦闘行動キャラ:キャラ登録番号)}
		CATCH
			アビ汎用文字列:選択時メッセージ１ = 誰を攻撃しますか？
			CALL アビ対象選択テンプレート_敵単体
			アビ汎用変数:効果割合 = 100
			CALL アビテンプレート_通常攻撃処理
		ENDCATCH

		CALL 戦闘効果削除(バフID, 1)
ENDSELECT

@証アビ関数_シーフの証_2(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM ループ用
#DIM LEVEL
#DIM 効果数値, 3
#DIMS CONST アビ名 = "トレジャーハント"
#DIM CONST 消費ＭＰ = 15
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 得意武器判定(対象キャラ, "短剣")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("サポート")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 3
		効果数値:1 = 4
		効果数値:2 = 5
	CASE 2
		効果数値:0 = 3
		効果数値:1 = 3
		効果数値:2 = 4
	CASEELSE
		効果数値:0 = 3
		効果数値:1 = 3
		効果数値:2 = 3
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%（装備者が短剣得意orサポートタイプの時）<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		IF 効果数値:0 == 効果数値:2
			詳細文文字列受け渡し変数 += @"　効果：敵単体にドロップ率UP効果を{効果数値:0}回与える。"
		ELSE
			詳細文文字列受け渡し変数 += @"　効果：敵単体にドロップ率UP効果を{効果数値:0}～{効果数値:2}回与える。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		FOR ループ用, 0, 効果数値:RAND(3)
			CALL アビテンプレート_不利効果_トレハン
		NEXT
ENDSELECT

@証アビ関数_シーフの証_3(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 2
#DIMS CONST アビ名 = "デッドリーエイム"
#DIM CONST 消費ＭＰ = 20
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 得意武器判定(対象キャラ, "銃")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("連撃")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 4
		効果数値:1 = 60
	CASE 2
		効果数値:0 = 3
		効果数値:1 = 55
	CASEELSE
		効果数値:0 = 3
		効果数値:1 = 50
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0 && LEVEL != 2
			詳細文文字列受け渡し変数 '= @"■%アビ名%（装備者が銃得意or連撃タイプの時）<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　効果：自身のTA率を{効果数値:0}ターン［+{効果数値:1}％/A枠］する。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = TA率A
		アビ汎用文字列:バフ・デバフ対象能力 = TA率
		アビ汎用変数:効果量 = 効果数値:1
		アビ汎用変数:持続ターン = 効果数値:0
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@証アビ関数_シーフの証_4(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM トレハン行数
#DIM トレハンLv
#DIM LEVEL
#DIM 効果数値, 2
#DIMS CONST アビ名 = "ブレイクキープ"
#DIM CONST 消費ＭＰ = 25
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ) - 1
ELSE
	LEVEL = 0
ENDIF
SELECTCASE LEVEL
	CASE IS >= 2
		効果数値:0 = 30
		効果数値:1 = 10
	CASEELSE
		効果数値:0 = 20
		効果数値:1 = 10
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　効果：CTが0の敵単体に1ターンの恐怖効果を与える。基礎成功率{効果数値:0}％。<br>"
		詳細文文字列受け渡し変数 += "　　　　対象のトレハンLvが高いほど成功率が高くなり、効果ターンも長くなる。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		IF アビテンプレート用_キャンセルフラグ
			RETURN
		ELSEIF 敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 10):チャージターン > 0
			CALL 口上変数初期化
			WSTR:(K++) = 対象のチャージターンが0ではありません
			CALL メッセージ欄表示用関数(,,,0)
			アビテンプレート用_キャンセルフラグ = 1
			RETURN
		ENDIF
		トレハン行数 = デバフ番号取得("ドロップ率UP", アビテンプレート用_対象保存:0)

		アビ汎用文字列:バフ・デバフ枠 = 恐怖
		アビ汎用文字列:バフ・デバフ対象能力 = 恐怖
		アビ汎用変数:持続ターン = 1
		アビ汎用変数:基礎成功確率 = 効果数値:0
		IF トレハン行数 >= 0
			トレハンLv = DT_CELL_GET("戦闘効果データベース", トレハン行数, "効果量_固定値")
			アビ汎用変数:持続ターン += MAX(1, トレハンLv / 2)
			アビ汎用変数:基礎成功確率 += 効果数値:1 * トレハンLv
		ENDIF
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@証アビ関数_シーフの証_10(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIMS CONST アビ名 = "不意打ち"
#DIM CONST 消費ＭＰ = 0

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　効果：敵単体に自属性の［行動速度✕100％］ダメージを与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 100
		アビ汎用文字列:攻撃側使用能力値 = 行動速度
		CALL アビテンプレート_ダメージ処理_自属性
ENDSELECT
