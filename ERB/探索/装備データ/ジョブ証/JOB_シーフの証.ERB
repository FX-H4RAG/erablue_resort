@証関数_シーフの証(処理モード)
#DIMS 処理モード
LOCALS = 

SELECTCASE 処理モード
	CASE "ジョブ証種別"
		証関連文字列受け渡し = 通常ジョブ
	CASE "フレーバー"
		LOCALS += "トリッキーなアビと独自の動きで戦う証。<br>"
		LOCALS += "特定条件で大火力を出したり、アイテムを手に入れやすくなる技を扱う。<br>"
		LOCALS += "付けると手先が器用になった気分になる。<br>"
		詳細文文字列受け渡し変数 = %LOCALS%
ENDSELECT

@証アビ関数_シーフの証_1(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM 消費ＭＰ = 100
LOCALS = 

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 = ブレイクアサシン
	CASE "アビ説明文"
		LOCALS += "■ブレイクアサシン<br>"
		LOCALS += @"　消費ＭＰ：{消費ＭＰ}<br>"
		LOCALS += "　効果：通常攻撃を行う。<br>"
		LOCALS += "　　　　対象のCTが0の時、この通常攻撃のダメージは2.5倍になる。"
		詳細文文字列受け渡し変数 = %LOCALS%
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = ブレイクアサシン
		アビ汎用文字列:バフ・デバフ対象能力 = ブレイクアサシン
		アビ汎用変数:持続行動回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
		TRYCCALLFORM 固有通常攻撃_{NO:(BATTLE_STATE:戦闘行動キャラ:キャラ番号)}
		CATCH
			アビ汎用文字列:選択時メッセージ１ = 誰を攻撃しますか？
			CALL アビ対象選択テンプレート_敵単体
			アビテンプレート用_アビ名 = 攻撃
			アビ汎用変数:効果割合 = 100
			CALL アビテンプレート_通常攻撃処理
		ENDCATCH
		IF アビテンプレート用_キャンセルフラグ
			アビテンプレート用_キャンセルフラグ = 0
			CALL アビ対象選択テンプレート_自己のみ
			CALL アビテンプレート_不利効果_バフ消去("ブレイクアサシン")
			アビテンプレート用_キャンセルフラグ = 1
		ENDIF
ENDSELECT

@証アビ関数_シーフの証_2(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM 消費ＭＰ = 15
LOCALS = 

SELECTCASE 処理モード
	CASE "アビ名"
		IF 得意武器判定(対象キャラ, "短剣")
			TSTR:コマンド名受渡 = トレジャーハント
		ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("サポート")
			TSTR:コマンド名受渡 = トレジャーハント
		ENDIF
	CASE "アビ説明文"
		LOCALS += "■トレジャーハント（装備者が短剣得意orサポートタイプの時）<br>"
		LOCALS += @"　消費ＭＰ：{消費ＭＰ}<br>"
		LOCALS += "　効果：敵単体にドロップ率UP効果を3回与える。"
		詳細文文字列受け渡し変数 = %LOCALS%
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		CALL アビテンプレート_不利効果_トレハン
		CALL アビテンプレート_不利効果_トレハン
		CALL アビテンプレート_不利効果_トレハン
ENDSELECT

@証アビ関数_シーフの証_3(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM 消費ＭＰ = 20
LOCALS = 

SELECTCASE 処理モード
	CASE "アビ名"
		IF 得意武器判定(対象キャラ, "銃")
			TSTR:コマンド名受渡 = デッドリーエイム
		ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("連撃")
			TSTR:コマンド名受渡 = デッドリーエイム
		ENDIF
	CASE "アビ説明文"
		LOCALS += "■デッドリーエイム（装備者が銃得意or連撃タイプの時）<br>"
		LOCALS += @"　消費ＭＰ：{消費ＭＰ}<br>"
		LOCALS += "　効果：自身のTA率を3ターン［+50%/A枠］する。"
		詳細文文字列受け渡し変数 = %LOCALS%
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 50
		アビ汎用文字列:バフ・デバフ枠 = TA率A
		アビ汎用文字列:バフ・デバフ対象能力 = TA率
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@証アビ関数_シーフの証_4(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM 消費ＭＰ = 0
LOCALS = 

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 = 不意打ち
	CASE "アビ説明文"
		LOCALS += "■不意打ち<br>"
		LOCALS += @"　消費ＭＰ：{消費ＭＰ}<br>"
		LOCALS += "　効果：敵単体に自属性の［行動速度✕100％］ダメージを与える。"
		詳細文文字列受け渡し変数 = %LOCALS%
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 100
		アビ汎用文字列:攻撃側使用能力値 = 行動速度
		CALL アビテンプレート_ダメージ処理_自属性
ENDSELECT


