@証関数_ナイトの証(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード

SELECTCASE 処理モード
	CASE "ジョブ証種別"
		証関連文字列受け渡し = 通常ジョブ
	CASE "証名"
		TSTR:コマンド名受渡 = ナイトの証
	CASE "クラス名"
		SIF 対象キャラ <= 0
			RETURN 0
		SELECTCASE GET_JOBSKILL_LEVEL(対象キャラ)
			CASE IS >= 3
				RESULTS = ホーリーセイバー
				RESULTS:1 = Class.III
				RETURN 1
			CASE 2
				RESULTS = フォートレス
				RESULTS:1 = Class.II
				RETURN 1
			CASEELSE
				RESULTS = ナイト
				RESULTS:1 = Class.I
				RETURN 1
		ENDSELECT
	CASE "フレーバー"
		詳細文文字列受け渡し変数 '= "守りを重視したタンク向けの証。<br>"
		詳細文文字列受け渡し変数 += "味方を守り、相手の隙を付く技に長けている。<br>"
		詳細文文字列受け渡し変数 += "人気は低いが重要な立ち回りが出来る。<br>"
ENDSELECT

;ランダム抽選では出現しない（入手法：ダンジョン「人食い鳥の島」のイベントで入手）
;@証出現率_ナイトの証(入手タイプ, 装備ランク)

@証アビ関数_ナイトの証_1(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 1
#DIMS CONST アビ名 = "ファランクス"
#DIM CONST 消費ＭＰ = 45
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 70
	CASE 2
		効果数値:0 = 60
	CASEELSE
		効果数値:0 = 50
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　効果：味方全体に1回のダメージカット［{効果数値:0}％］効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = ダメージカット
		アビ汎用文字列:バフ・デバフ対象能力 = 被ダメージ
		アビ汎用変数:効果割合 = 効果数値:0
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@証アビ関数_ナイトの証_2(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM 攻撃対象
#DIM LEVEL
#DIM 効果数値, 2
#DIMS CONST アビ名 = "ドライブバースト"
#DIM CONST 消費ＭＰ = 15
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 得意武器判定(対象キャラ, "剣")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("バランス")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 200
		効果数値:1 = 70
	CASE 2
		効果数値:0 = 150
		効果数値:1 = 50
	CASEELSE
		効果数値:0 = 100
		効果数値:1 = 30
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%（装備者が剣得意orバランスタイプの時）<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　効果：敵横列に自属性の［攻撃力✕{効果数値:0}％］ダメージを与える。<br>"
		詳細文文字列受け渡し変数 += @"　　　　対象のチャージターン1つにつき威力が［{効果数値:1}％］上昇する。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		アビ汎用文字列:選択時メッセージ１ = どの横列に使用しますか？
		CALL アビ対象選択テンプレート_敵単体
		;本来「CALL アビ対象選択テンプレート_敵横列」を使うべきだが、相手によって威力が変動するため
		;手動で処理を行う
		SIF アビテンプレート用_キャンセルフラグ
			RETURN -1
		攻撃対象 = アビテンプレート用_対象保存:0
		アビ汎用変数:効果割合 = 効果数値:0 + (敵BATTLE_STATE:(攻撃対象 - 10):チャージターン * 効果数値:1)
		CALL アビテンプレート_ダメージ処理_自属性
		IF 攻撃対象 % 2
			攻撃対象 -= 1
		ELSE
			攻撃対象 += 1
		ENDIF
		IF 敵BATTLE_STATE_STR:(攻撃対象 - 10):エネミー名 != "" && 敵BATTLE_STATE:(攻撃対象 - 10):ＨＰ > 0
			CALL アビ対象選択テンプレート_指定(攻撃対象)
			アビ汎用変数:効果割合 = 効果数値:0 + (敵BATTLE_STATE:(攻撃対象 - 10):チャージターン * 効果数値:1)
			CALL アビテンプレート_ダメージ処理_自属性
		ENDIF
ENDSELECT

@証アビ関数_ナイトの証_3(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 1
#DIMS CONST アビ名 = "かばう"
#DIM CONST 消費ＭＰ = 20
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 得意武器判定(対象キャラ, "槍")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("防御")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 35
	CASE 2
		効果数値:0 = 25
	CASEELSE
		効果数値:0 = 0
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%（装備者が槍得意or防御タイプの時）<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　効果：自身に次の行動時まで、全ての単体攻撃を味方に代わって受ける効果を与える。"
		IF 効果数値:0 > 0
			詳細文文字列受け渡し変数 += "<br>"
			詳細文文字列受け渡し変数 += @"　　　　自身の防御力を次の行動時まで［{効果数値:0}％/独自枠］増加させる。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 単体かばう
		アビ汎用文字列:バフ・デバフ対象能力 = 単体かばう
		;1にするとかばう使った直後に消えてしまうので2で設定
		アビ汎用変数:持続行動回数 = 2
		CALL アビテンプレート_有利効果_バフ効果セット
		IF 効果数値:0 > 0
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = かばう_防御力
			アビ汎用文字列:バフ・デバフ対象能力 = 防御力
			アビ汎用変数:効果割合 = 効果数値:0
			アビ汎用変数:持続行動回数 = 2
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@証アビ関数_ナイトの証_4(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIMS ガード属性
#DIM LEVEL
#DIM 効果数値, 1
#DIMS CONST アビ名 = "エレメンタルガード"
#DIM CONST 消費ＭＰ = 40
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ) - 2
	IF 装備ステータス補正保存:対象キャラ:属性
		ガード属性 '= 属性数値文字列変換(有利属性変換(装備ステータス補正保存:対象キャラ:属性 - 10))
	ELSE
		ガード属性 '= 属性数値文字列変換(有利属性変換(基礎BATTLE_STATE:対象キャラ:属性))
	ENDIF
ELSE
	LEVEL = 0
ENDIF
SELECTCASE LEVEL
	CASEELSE
		効果数値:0 = 50
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 += @"　効果：アビリティ使用者の有利属性に対し、味方全体の耐性を3ターン［+{効果数値:0}/A枠］する。"
		ELSE
			詳細文文字列受け渡し変数 += @"　効果：味方全体の%ガード属性%耐性を3ターン［+{効果数値:0}/A枠］する。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = %ガード属性%耐性A
		アビ汎用文字列:バフ・デバフ対象能力 = %ガード属性%耐性
		アビ汎用変数:効果量 = 効果数値:0
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@証アビ関数_ナイトの証_10(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIMS CONST アビ名 = "シールドバッシュ"
#DIM CONST 消費ＭＰ = 0

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　効果：敵単体に自属性の［防御力✕100％］ダメージを与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 100
		アビ汎用文字列:攻撃側使用能力値 = 防御力
		CALL アビテンプレート_ダメージ処理_自属性
ENDSELECT
