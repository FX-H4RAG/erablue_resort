@証関数_ハーピストの証(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード

SELECTCASE 処理モード
	CASE "ジョブ証種別"
		証関連文字列受け渡し = 通常ジョブ
	CASE "証名"
		TSTR:コマンド名受渡 = ハーピストの証
	CASE "クラス名"
		SIF 対象キャラ <= 0
			RETURN 0
		SELECTCASE GET_JOBSKILL_LEVEL(対象キャラ)
			CASE IS >= 3
				RESULTS = スーパースター
				RESULTS:1 = Class.III
				RETURN 1
			CASE 2
				RESULTS = ミンストレル
				RESULTS:1 = Class.II
				RETURN 1
			CASEELSE
				RESULTS = ハーピスト
				RESULTS:1 = Class.I
				RETURN 1
		ENDSELECT
	CASE "フレーバー"
		詳細文文字列受け渡し変数 '= "さまざまな詩曲で味方を支援する楽器使いの証。<br>"
		詳細文文字列受け渡し変数 += "美しい歌声や佇まいから、モテるためにこの職の門を叩いた者は多い。<br>"
		詳細文文字列受け渡し変数 += "そのほとんどがほどなく去ったことは言うまでもない。<br>"
ENDSELECT

@証出現率_ハーピストの証(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 2
	RETURN 0
SELECTCASE 入手タイプ
	CASE "輸入"
		RETURN 装備レアリティ_Ｎ
ENDSELECT

@証アビ関数_ハーピストの証_1(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 楽器フラグ
#DIM 効果数値, 2
#DIMS CONST アビ名 = "オーヴァチュア"
#DIM CONST 消費ＭＰ = 20
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	楽器フラグ = 0
	IF 得意武器判定(対象キャラ, "楽器")
		楽器フラグ = 1
	ENDIF
ELSE
	LEVEL = 1
	楽器フラグ = 0
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 40
		効果数値:1 = 80
	CASE 2
		効果数値:0 = 30
		効果数値:1 = 0
	CASEELSE
		効果数値:0 = 10
		効果数値:1 = 0
ENDSELECT
SIF 効果数値:0 < 30 && 楽器フラグ
	効果数値:0 = 30
SIF 効果数値:1 > 0 && 効果数値:1 < 100 && 楽器フラグ
	効果数値:1 = 100

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　効果：味方全体の回復力を3ターン［{効果数値:0}％/独自枠］増加させる。"
		IF 効果数値:0 < 30 && 対象キャラ <= 0
			詳細文文字列受け渡し変数 += "<br>"
			詳細文文字列受け渡し変数 += "　　　　楽器が得意武器の場合、バフ倍率が［30％］になる。"
		ENDIF
		IF 効果数値:1 > 0
			詳細文文字列受け渡し変数 += "<br>"
			詳細文文字列受け渡し変数 += @"　　　　この回復力増加は［{効果数値:1}％］まで累積できる。"
			IF 効果数値:1 < 100 && 対象キャラ <= 0
				詳細文文字列受け渡し変数 += "<br>"
				詳細文文字列受け渡し変数 += "　　　　楽器が得意武器の場合、累積上限が［100％］になる。"
			ENDIF
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = オーヴァーチュア_回復力
		アビ汎用文字列:バフ・デバフ対象能力 = 回復力
		アビ汎用変数:効果割合 = 効果数値:0
		アビ汎用変数:持続ターン = 3
		IF 効果数値:1 > 0
			アビ汎用変数:累積上限_割合 = 効果数値:1
			CALL アビテンプレート_有利効果_累積バフ効果セット
		ELSE
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@証アビ関数_ハーピストの証_2(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 楽器フラグ
#DIM 効果数値, 2
#DIMS CONST アビ名 = "チャームボイス"
#DIM CONST 消費ＭＰ = 30
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	楽器フラグ = 0
	IF 得意武器判定(対象キャラ, "楽器")
		楽器フラグ = 1
	ELSEIF 得意武器判定(対象キャラ, "短剣")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("サポート")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
	楽器フラグ = 0
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 45
		効果数値:1 = 75
	CASE 2
		効果数値:0 = 45
		効果数値:1 = 70
	CASEELSE
		効果数値:0 = 35
		効果数値:1 = 60
ENDSELECT
SIF 効果数値:1 < 90 && 楽器フラグ
	効果数値:1 = 90

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%（装備者が楽器得意or短剣得意orサポートタイプの時）<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　効果：敵全体に3ターン魅了［{効果数値:0}％］効果を与える。基礎成功率{効果数値:1}％。"
		IF 効果数値:1 < 90 && 対象キャラ <= 0
			詳細文文字列受け渡し変数 += "<br>"
			詳細文文字列受け渡し変数 += "　　　　楽器が得意武器の場合、基礎成功率が90％になる。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 魅了
		アビ汎用文字列:バフ・デバフ対象能力 = 魅了
		アビ汎用変数:効果割合 = 効果数値:0
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 効果数値:1
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@証アビ関数_ハーピストの証_3(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM 楽器フラグ
#DIM LEVEL
#DIM 効果数値, 1
#DIMS CONST アビ名 = "ミンストレルソング"
#DIM CONST 消費ＭＰ = 25
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	楽器フラグ = 0
	IF 得意武器判定(対象キャラ, "楽器")
		楽器フラグ = 1
	ELSEIF 得意武器判定(対象キャラ, "短剣")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("サポート")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
	楽器フラグ = 0
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 35
	CASE 2
		効果数値:0 = 25
	CASEELSE
		効果数値:0 = 10
ENDSELECT
SIF 楽器フラグ
	効果数値:0 = MAX(効果数値:0 + 5, 30)

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%（装備者が楽器得意or短剣得意orサポートタイプの時）<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　効果：味方全体に5ターン再生［回復力✕{効果数値:0}％/A枠］効果を与える。"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 += "<br>"
			詳細文文字列受け渡し変数 += @"　　　　楽器が得意武器の場合、回復倍率が［{MAX(効果数値:0 + 5, 30)}％］になる。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 再生A
		アビ汎用文字列:バフ・デバフ対象能力 = 再生
		CALL バフ込み数値算出("回復力")
		アビ汎用変数:効果量 = RESULT * 効果数値:0 / 100
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@証アビ関数_ハーピストの証_4(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM 楽器フラグ
#DIM LEVEL
#DIM 効果数値, 1
#DIMS CONST アビ名 = "ソング・オブ・グランデ"
#DIM CONST 消費ＭＰ = 40
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ) - 2
	楽器フラグ = 0
	IF 得意武器判定(対象キャラ, "楽器")
		楽器フラグ = 1
	ENDIF
ELSE
	LEVEL = 0
	楽器フラグ = 0
ENDIF
SELECTCASE LEVEL
	CASEELSE
		効果数値:0 = 45
ENDSELECT
SIF 楽器フラグ
	効果数値:0 = MAX(効果数値:0 + 5, 65)

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　効果：味方全体のDA率を3ターン［+{効果数値:0}％/A枠］する。"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 += "<br>"
			詳細文文字列受け渡し変数 += @"　　　　楽器が得意武器の場合、上昇値が［+{MAX(効果数値:0 + 5, 65)}％］になる。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = DA率A
		アビ汎用文字列:バフ・デバフ対象能力 = DA率
		アビ汎用変数:効果量 = 効果数値:0
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT
