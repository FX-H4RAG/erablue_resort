@証関数_ファイターの証(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード

SELECTCASE 処理モード
	CASE "ジョブ証種別"
		証関連文字列受け渡し = 通常ジョブ
	CASE "証名"
		TSTR:コマンド名受渡 = ファイターの証
	CASE "クラス名"
		SIF 対象キャラ <= 0
			RETURN 0
		SELECTCASE GET_JOBSKILL_LEVEL(対象キャラ)
			CASE IS >= 3
				RESULTS = ウェポンマスター
				RESULTS:1 = Class.III
				RETURN 1
			CASE 2
				RESULTS = ウォーリア
				RESULTS:1 = Class.II
				RETURN 1
			CASEELSE
				RESULTS = ファイター
				RESULTS:1 = Class.I
				RETURN 1
		ENDSELECT
	CASE "フレーバー"
		詳細文文字列受け渡し変数 '= "駆け出しの戦士がよく使う前衛用の証。<br>"
		詳細文文字列受け渡し変数 += "シンプルで使いやすいアビリティが多くの戦士に愛用されている。<br>"
		詳細文文字列受け渡し変数 += "ベテランになっても使い続けている戦士も多いとか。<br>"
ENDSELECT

;ランダム抽選では出現しない（入手法：ダンジョン「人食い鳥の島」のイベントで入手）
;@証出現率_ファイターの証(入手タイプ, 装備ランク)

@証アビ関数_ファイターの証_1(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 1
#DIMS CONST アビ名 = "ウェポンバースト"
#DIM CONST 消費ＭＰ = 100
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 25
	CASE 2
		効果数値:0 = 15
	CASEELSE
		効果数値:0 = 0
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　効果：自身の奥義ゲージを［100％］にする。"
		IF 効果数値:0 > 0
			詳細文文字列受け渡し変数 += "<br>"
			詳細文文字列受け渡し変数 += @"　　　　自身の攻撃力を次の行動時まで［{効果数値:0}％/独自枠］増加させる。"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		CALL アビテンプレート_有利効果_ウェポンバースト
		IF 効果数値:0 > 0
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = ウェポンバースト_攻撃力
			アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
			アビ汎用変数:効果割合 = 効果数値:0
			アビ汎用変数:持続行動回数 = 2
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
ENDSELECT

@証アビ関数_ファイターの証_2(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 1
#DIMS CONST アビ名 = "レイジ"
#DIM CONST 消費ＭＰ = 15
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 得意武器判定(対象キャラ, "剣")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("サポート")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 25
	CASE 2
		効果数値:0 = 22
	CASEELSE
		効果数値:0 = 20
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%（装備者が剣得意orサポートタイプの時）<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　効果：味方全体の攻撃力を3ターン［{効果数値:0}％/A枠］増加させる。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
		アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
		アビ汎用変数:効果割合 = 効果数値:0
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@証アビ関数_ファイターの証_3(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 2
#DIMS CONST アビ名 = "アーマーブレイク"
#DIM CONST 消費ＭＰ = 25
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 得意武器判定(対象キャラ, "斧")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("バランス")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 250
		効果数値:1 = 20
	CASE 2
		効果数値:0 = 200
		効果数値:1 = 15
	CASEELSE
		効果数値:0 = 150
		効果数値:1 = 15
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%（装備者が斧得意orバランスタイプの時）<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　効果：敵単体に自属性の［攻撃力✕{効果数値:0}％］ダメージを与える。<br>"
		詳細文文字列受け渡し変数 += @"　　　　対象の防御力を3ターン［{効果数値:1}％/B枠］減少させる。基礎成功率100％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値:0
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 防御力B
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果割合 = 効果数値:1
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 100
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@証アビ関数_ファイターの証_4(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 2
#DIMS CONST アビ名 = "パワーレイズ"
#DIM CONST 消費ＭＰ = 20
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ) - 2
ELSE
	LEVEL = 0
ENDIF
SELECTCASE LEVEL
	CASEELSE
		効果数値:0 = 70
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　効果：自身のクリティカル率を3ターン［+50％/A枠］する。<br>"
		詳細文文字列受け渡し変数 += @"　　　　自身のクリティカルダメージ率を3ターン［+{効果数値:0}％/A枠］する。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = クリティカル率A
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカル率
		アビ汎用変数:効果割合 = 50
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = クリティカルダメージ率A
		アビ汎用文字列:バフ・デバフ対象能力 = クリティカルダメージ率
		アビ汎用変数:効果割合 = 効果数値:0
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT
