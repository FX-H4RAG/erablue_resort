@証関数_メカニックの証(処理モード)
#DIMS 処理モード
LOCALS = 

SELECTCASE 処理モード
	CASE "ジョブ証種別"
		証関連文字列受け渡し = 通常ジョブ
	CASE "フレーバー"
		LOCALS += "機械を扱うことに特化した証<br>"
		LOCALS += "ＨＰを犠牲にして得たエネルギーで様々な行動が可能<br>"
		LOCALS += "装備するとダンジョン探索中に「機械知識Lv1」と同じ効果が得られる。<br>"
		詳細文文字列受け渡し変数 = %LOCALS%
ENDSELECT

;ランダム抽選では出現しない（入手法：ダンジョン「バルツ地下廃工場」のイベントで入手）
;@証出現率_メカニックの証(入手タイプ, 装備ランク)

@証アビ関数_メカニックの証_1(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM バフ番号
#DIM 消費ＭＰ = 100
LOCALS = 

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 = エナジーゲイン
	CASE "アビ説明文"
		LOCALS += "■エナジーゲイン<br>"
		LOCALS += @"　消費ＭＰ：{消費ＭＰ}<br>"
		LOCALS += "　効果：自身のＨＰを現在値の半分にし、エネルギーを［１０］得る。（最大１０）<br>"
		LOCALS += "　　　　このアビリティは行動を消費しない。"
		詳細文文字列受け渡し変数 = %LOCALS%
		RETURN 消費ＭＰ
	CASE "アビ効果"
		アビ汎用文字列:実行時メッセージ１ = %CALLNAME:(BATTLE_STATE:戦闘行動キャラ:0)%は自身のＨＰをエネルギーに変換した！
		BATTLE_STATE:戦闘行動キャラ:ＨＰ = MAX(1, BATTLE_STATE:戦闘行動キャラ:ＨＰ / 2)
		CALL アビ対象選択テンプレート_自己のみ
		バフ番号 = バフ存在チェック_枠("エネルギー_メカニック", 戦闘行動キャラ)
		IF バフ番号 == -1
			アビ汎用変数:効果量 = 10
			アビ汎用文字列:バフ・デバフ枠 = エネルギー_メカニック
			アビ汎用文字列:バフ・デバフ対象能力 = エネルギー_メカニック
			アビ汎用変数:持続ターン = -1
			CALL アビテンプレート_有利効果_バフ効果セット
		ELSE
			CALL アビテンプレート_効果共通処理
			DT_CELL_SET @"バフデータベース_{戦闘行動キャラ}", バフ番号, "バフ効果量_固定値", 10 
		ENDIF
		アビテンプレート用_行動消費無しフラグ = 1
ENDSELECT

@証アビ関数_メカニックの証_2(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM 消費ＭＰ = 0
LOCALS = 

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 = アタックマニューバ
	CASE "アビ説明文"
		LOCALS += "■アタックマニューバ<br>"
		LOCALS += @"　消費ＭＰ：{消費ＭＰ}<br>"
		LOCALS += "　効果：エネルギーを［２］消費する。<br>"
		LOCALS += "　　　　敵単体に自属性の［攻撃力✕200％］ダメージを与える。"
		詳細文文字列受け渡し変数 = %LOCALS%
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL エネルギー_メカニック消費(2)
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 200
		CALL アビテンプレート_ダメージ処理_自属性
ENDSELECT

@証アビ関数_メカニックの証_3(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM 消費ＭＰ = 0
LOCALS = 

SELECTCASE 処理モード
	CASE "アビ名"
		IF 知識素質:対象キャラ:機械知識 > 1
			TSTR:コマンド名受渡 = プロテクトマニューバ
		ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("防御")
			TSTR:コマンド名受渡 = プロテクトマニューバ
		ENDIF
	CASE "アビ説明文"
		LOCALS += "■プロテクトマニューバ（装備者が機械知識Lv2以上or防御タイプの時）<br>"
		LOCALS += @"　消費ＭＰ：{消費ＭＰ}<br>"
		LOCALS += "　効果：エネルギーを［４］消費する。<br>"
		LOCALS += "　　　　味方全体に1回のダメージカット［50％］効果を与える。"
		詳細文文字列受け渡し変数 = %LOCALS%
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL エネルギー_メカニック消費(4)
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		アビ汎用変数:効果割合 = 50
		アビ汎用文字列:バフ・デバフ枠 = ダメージカット
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ増減
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@証アビ関数_メカニックの証_4(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM 消費ＭＰ = 0
LOCALS = 

SELECTCASE 処理モード
	CASE "アビ名"
		IF 知識素質:対象キャラ:機械知識 > 1
			TSTR:コマンド名受渡 = オーラマニューバ
		ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("回復")
			TSTR:コマンド名受渡 = オーラマニューバ
		ENDIF
	CASE "アビ説明文"
		LOCALS += "■オーラマニューバ（装備者が機械知識Lv2以上or回復タイプの時）<br>"
		LOCALS += @"　消費ＭＰ：{消費ＭＰ}<br>"
		LOCALS += "　効果：エネルギーを［３］消費する。<br>"
		LOCALS += "　　　　味方全体に3ターン再生［回復力✕10％（上限500）/B枠］効果を与える。"
		詳細文文字列受け渡し変数 = %LOCALS%
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL エネルギー_メカニック消費(3)
		CALL アビ対象選択テンプレート_味方全体_生者のみ
		CALL バフ込み数値算出("回復力")
		アビ汎用変数:効果量 = MIN(RESULT * 10 / 100, 500)
		アビ汎用文字列:バフ・デバフ枠 = 再生B
		アビ汎用文字列:バフ・デバフ対象能力 = 再生
		アビ汎用変数:持続ターン = 3
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@証アビ関数_メカニックの証_5(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM バフ番号
#DIM 消費エネルギー
#DIM 消費ＭＰ = 0
LOCALS = 

SELECTCASE 処理モード
	CASE "アビ名"
		IF 知識素質:対象キャラ:機械知識 > 1
			TSTR:コマンド名受渡 = エナジーマニューバ
		ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("奥義")
			TSTR:コマンド名受渡 = エナジーマニューバ
		ENDIF
	CASE "アビ説明文"
		LOCALS += "■エナジーマニューバ（装備者が機械知識Lv2以上or奥義タイプの時）<br>"
		LOCALS += @"　消費ＭＰ：{消費ＭＰ}<br>"
		LOCALS += "　効果：エネルギーを全て消費する。<br>"
		LOCALS += "　　　　自身に1回の奥義威力増加［消費したエネルギー✕10％/A枠］を付与する。"
		詳細文文字列受け渡し変数 = %LOCALS%
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		バフ番号 = バフ存在チェック_枠("エネルギー_メカニック", 戦闘行動キャラ)
		消費エネルギー = DT_CELL_GET(@"バフデータベース_{戦闘行動キャラ}", バフ番号, "バフ効果量_固定値")
		DT_CELL_SET @"バフデータベース_{戦闘行動キャラ}", バフ番号, "バフ効果量_固定値", 0
		アビ汎用変数:効果量 = 消費エネルギー * 10
		アビ汎用文字列:バフ・デバフ枠 = 奥義ダメージ補正_割合A
		アビ汎用文字列:バフ・デバフ対象能力 = 奥義ダメージ補正_割合
		アビ汎用変数:持続回数 = 1
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@証アビ関数_メカニックの証_6(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM 消費ＭＰ = 0
LOCALS = 

SELECTCASE 処理モード
	CASE "アビ名"
		IF 知識素質:対象キャラ:機械知識 > 1
			TSTR:コマンド名受渡 = ボムマニューバ
		ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("攻撃")
			TSTR:コマンド名受渡 = ボムマニューバ
		ENDIF
	CASE "アビ説明文"
		LOCALS += "■ボムマニューバ（装備者が機械知識Lv2以上or攻撃タイプの時）<br>"
		LOCALS += @"　消費ＭＰ：{消費ＭＰ}<br>"
		LOCALS += "　効果：エネルギーを［３］消費する。<br>"
		LOCALS += "　　　　敵単体に自属性の［自身のレベル✕50］固定ダメージを与える。"
		詳細文文字列受け渡し変数 = %LOCALS%
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL エネルギー_メカニック消費(3)
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果量 = BATTLE_STATE:戦闘行動キャラ:Lv * 50
		CALL アビテンプレート_ダメージ処理_自属性
ENDSELECT

@エネルギー_メカニック消費(消費エネルギー)
#DIM 消費エネルギー
#DIM バフ番号
#DIM エネルギー不足

エネルギー不足 = 0
バフ番号 = バフ存在チェック_枠("エネルギー_メカニック", 戦闘行動キャラ)
IF バフ番号 == -1
	エネルギー不足 = 1
ELSE
	IF DT_CELL_GET(@"バフデータベース_{戦闘行動キャラ}", バフ番号, "バフ効果量_固定値") < 消費エネルギー
		エネルギー不足 = 1
	ELSE
		DT_CELL_SET @"バフデータベース_{戦闘行動キャラ}", バフ番号, "バフ効果量_固定値", DT_CELL_GET(@"バフデータベース_{戦闘行動キャラ}", バフ番号, "バフ効果量_固定値") - 消費エネルギー
	ENDIF
ENDIF


IF エネルギー不足
	CALL 口上変数初期化
	WSTR:(K++) = エネルギーが足りません。
	CALL メッセージ欄表示用関数(@"探索用_{戦闘行動キャラ}_顔",,,0,1)
	アビテンプレート用_キャンセルフラグ = 1
ENDIF
