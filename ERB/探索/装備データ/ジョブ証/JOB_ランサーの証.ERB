@証関数_ランサーの証(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード

SELECTCASE 処理モード
	CASE "ジョブ証種別"
		証関連文字列受け渡し = 通常ジョブ
	CASE "証名"
		TSTR:コマンド名受渡 = ランサーの証
	CASE "クラス名"
		SIF 対象キャラ <= 0
			RETURN 0
		SELECTCASE GET_JOBSKILL_LEVEL(対象キャラ)
			CASE IS >= 3
				RESULTS = ヴァルキュリア（未実装）
				RESULTS:1 = Class.III
				RETURN 1
			CASE 2
				RESULTS = ドラグーン（未実装）
				RESULTS:1 = Class.II
				RETURN 1
			CASEELSE
				RESULTS = ランサー
				RESULTS:1 = Class.I
				RETURN 1
		ENDSELECT
	CASE "フレーバー"
		詳細文文字列受け渡し変数 '= "槍や斧を巧みに振るい、柔軟に戦う槍術士の証。<br>"
		詳細文文字列受け渡し変数 += "得意武器によって効果の変化するアビリティを持つ。<br>"
		詳細文文字列受け渡し変数 += "手に入れると一粒で二度おいしい気分になれる。<br>"
ENDSELECT

@証出現率_ランサーの証(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 2
	RETURN 0
SELECTCASE 入手タイプ
	CASE "輸入"
		RETURN 装備レアリティ_Ｎ
ENDSELECT

@証アビ関数_ランサーの証_1(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 武器フラグ
#DIM 効果数値, 2
#DIMS CONST アビ名 = "リードドライブ"
#DIM CONST 消費ＭＰ = 25
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	武器フラグ = 0
	SIF 得意武器判定(対象キャラ, "槍")
		SETBIT 武器フラグ, 0
	SIF 得意武器判定(対象キャラ, "斧")
		SETBIT 武器フラグ, 1
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 250
		効果数値:1 = 3
	CASE 2
		効果数値:0 = 200
		効果数値:1 = 5
	CASEELSE
		効果数値:0 = 150
		効果数値:1 = 6
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　効果：敵単体に自属性の［攻撃力✕{効果数値:0}％］ダメージを与える。<br>"
		IF 対象キャラ <= 0 || !GETBIT(武器フラグ, 0)
			詳細文文字列受け渡し変数 += @"　　　　自身のバフ効果数が{効果数値:1}以上かつ"
		ELSE
			詳細文文字列受け渡し変数 += "　　　　"
		ENDIF
		詳細文文字列受け渡し変数 += "対象のCTが0の場合、対象に1回の恐怖効果を与える。基礎成功率100％。<br>"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 += "　　　　（槍が得意武器の場合、常に発動）<br>"
		ENDIF
		IF 対象キャラ <= 0 || !GETBIT(武器フラグ, 1)
			詳細文文字列受け渡し変数 += @"　　　　対象のデバフ効果数が{効果数値:1}以上の場合、"
		ELSE
			詳細文文字列受け渡し変数 += "　　　　"
		ENDIF
		詳細文文字列受け渡し変数 += "対象にスロウ［2］効果を与える。基礎成功率60％。"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 += "<br>"
			詳細文文字列受け渡し変数 += "　　　　（斧が得意武器の場合、常に発動）"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 効果数値:0
		CALL アビテンプレート_ダメージ処理_自属性

		SIF アビテンプレート用_キャンセルフラグ
			RETURN
		SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
			RETURN
		IF (GETBIT(武器フラグ, 0) || 強化数算出(戦闘行動キャラ) >= 効果数値:1) && 敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 10):チャージターン == 0
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 恐怖
			アビ汎用文字列:バフ・デバフ対象能力 = 恐怖
			アビ汎用変数:持続回数 = 1
			アビ汎用変数:基礎成功確率 = 100
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
		IF GETBIT(武器フラグ, 1) || 弱体数算出(アビテンプレート用_対象保存:0) >= 効果数値:1
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 2
			アビ汎用変数:基礎成功確率 = 60
			CALL アビテンプレート_不利効果_スロウ
		ENDIF
ENDSELECT

@証アビ関数_ランサーの証_2(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM 敵番号
#DIM 対象数
#DIM LEVEL
#DIM 武器フラグ
#DIMS 能力名_自己
#DIMS 能力名_追加
#DIM 効果数値, 1
#DIMS CONST アビ名 = "デュアルインパルス"
#DIM CONST 消費ＭＰ = 15
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	武器フラグ = 0
	IF 得意武器判定(対象キャラ, "槍")
		SETBIT 武器フラグ, 0
		SIF 得意武器判定(対象キャラ, "斧")
			SETBIT 武器フラグ, 1
	ELSEIF 得意武器判定(対象キャラ, "斧")
		SETBIT 武器フラグ, 1
		SIF 得意武器判定(対象キャラ, "槍")
			SETBIT 武器フラグ, 0
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("連撃")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		能力名_自己 '= "TA率"
		能力名_追加 '= "連続攻撃率"
		効果数値:0 = 15
	CASE 2
		能力名_自己 '= "DA率"
		能力名_追加 '= "DA率"
		効果数値:0 = 25
	CASEELSE
		能力名_自己 '= "DA率"
		能力名_追加 '= "DA率"
		効果数値:0 = 20
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%（装備者が槍得意or斧得意or連撃タイプの時）<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　効果：自身の%能力名_自己%を2ターン［+100％/C枠］する。<br>"
		IF 対象キャラ <= 0 || !GETBIT(武器フラグ, 0)
			詳細文文字列受け渡し変数 += "　　　　自身のバフ効果数が4以上の場合、"
		ELSE
			詳細文文字列受け渡し変数 += "　　　　"
		ENDIF
		詳細文文字列受け渡し変数 += @"味方全体の%能力名_追加%を3ターン［+{効果数値:0}％/A枠］する。<br>"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 += "　　　　（槍が得意武器の場合、常に発動）<br>"
		ENDIF
		IF 対象キャラ <= 0 || !GETBIT(武器フラグ, 1)
			詳細文文字列受け渡し変数 += "　　　　デバフ効果数が4以上の敵の"
		ELSE
			詳細文文字列受け渡し変数 += "　　　　敵全体の"
		ENDIF
		詳細文文字列受け渡し変数 += @"%能力名_追加%を3ターン［-{効果数値:0}％/B枠］する。基礎成功率90％。"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 += "<br>"
			詳細文文字列受け渡し変数 += "　　　　（斧が得意武器の場合、常に発動）"
		ENDIF
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = %能力名_自己%C
		アビ汎用文字列:バフ・デバフ対象能力 = %能力名_自己%
		アビ汎用変数:効果量 = 100
		アビ汎用変数:持続ターン = 2
		CALL アビテンプレート_有利効果_バフ効果セット

		IF GETBIT(武器フラグ, 0) || 強化数算出(戦闘行動キャラ) >= 4
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用文字列:バフ・デバフ枠 = %能力名_追加%A
			アビ汎用文字列:バフ・デバフ対象能力 = %能力名_追加%
			アビ汎用変数:効果量 = 効果数値:0
			アビ汎用変数:持続ターン = 3
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF

		CALL アビ汎用変数文字列リセット
		対象数 = 0
		FOR 敵番号, 10, 20
			SIF 敵BATTLE_STATE_STR:(敵番号 - 10):エネミー名 == ""
				CONTINUE
			SIF 敵BATTLE_STATE:(敵番号 - 10):ＨＰ < 1
				CONTINUE
			IF GETBIT(武器フラグ, 1) || 弱体数算出(敵番号) >= 4
				CALL アビ対象選択テンプレート_指定(敵番号, 対象数)
				対象数 += 1
			ENDIF
		NEXT
		IF 対象数 > 0
			アビ汎用文字列:バフ・デバフ枠 = %能力名_追加%B
			アビ汎用文字列:バフ・デバフ対象能力 = %能力名_追加%
			アビ汎用変数:効果量 = 効果数値:0
			アビ汎用変数:持続ターン = 3
			アビ汎用変数:基礎成功確率 = 90
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT

@証アビ関数_ランサーの証_3(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 1
#DIMS CONST アビ名 = "ランドグリース"
#DIM CONST 消費ＭＰ = 20
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)
	IF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("攻撃")
	ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("奥義")
	ELSE
		LEVEL --
	ENDIF
ELSE
	LEVEL = 1
ENDIF
SELECTCASE LEVEL
	CASE IS >= 3
		効果数値:0 = 25
	CASE 2
		効果数値:0 = 22
	CASEELSE
		効果数値:0 = 20
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		IF 対象キャラ <= 0
			詳細文文字列受け渡し変数 '= @"■%アビ名%（装備者が攻撃タイプor奥義タイプの時）<br>"
		ELSE
			詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		ENDIF
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += @"　効果：敵全体の防御力を3ターン［{効果数値:0}％/B枠］減少させる。基礎成功率80％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 防御力B
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果量 = 効果数値:0
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 80
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT

@証アビ関数_ランサーの証_4(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM 効果数値, 3
#DIMS CONST アビ名 = "レゾナンスサージ"
#DIM CONST 消費ＭＰ = 25
IF 対象キャラ > 0
	LEVEL = GET_JOBSKILL_LEVEL(対象キャラ) - 2
ELSE
	LEVEL = 0
ENDIF
SELECTCASE LEVEL
	CASEELSE
		効果数値:0 = 15
		効果数値:1 = 135
ENDSELECT

SELECTCASE 処理モード
	CASE "アビ名"
		SIF LEVEL > 0
			TSTR:コマンド名受渡 '= アビ名
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= @"■%アビ名%<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　効果：敵全体に自属性の［攻撃力✕100％］ダメージを与える。<br>"
		詳細文文字列受け渡し変数 += @"　　　　自身のアビリティダメージを6ターン［{効果数値:0}％/独自枠］増加させる。<br>"
		詳細文文字列受け渡し変数 += @"　　　　このアビリティダメージ増加は［{効果数値:1}％］まで累積できる。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:基礎成功確率 = 100
		CALL アビテンプレート_ダメージ処理_自属性

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = レゾナンスサージ_アビリティダメージ補正
		アビ汎用文字列:バフ・デバフ対象能力 = ダメージ補正_アビリティダメージ
		アビ汎用変数:効果割合 = 効果数値:0
		アビ汎用変数:累積上限_割合 = 効果数値:1
		アビ汎用変数:持続ターン = 6
		CALL アビテンプレート_有利効果_累積バフ効果セット
ENDSELECT
