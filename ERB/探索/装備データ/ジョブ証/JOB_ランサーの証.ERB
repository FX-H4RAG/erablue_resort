@証関数_ランサーの証(処理モード)
#DIMS 処理モード

SELECTCASE 処理モード
	CASE "ジョブ証種別"
		証関連文字列受け渡し = 通常ジョブ
	CASE "証名"
		TSTR:コマンド名受渡 = ランサーの証
	CASE "フレーバー"
		詳細文文字列受け渡し変数 '= "槍や斧を巧みに振るい、柔軟に戦う槍術士の証。<br>"
		詳細文文字列受け渡し変数 += "得意武器によって効果の変化するアビリティを持つ。<br>"
		詳細文文字列受け渡し変数 += "手に入れると一粒で二度おいしい気分になれる。<br>"
ENDSELECT

@証出現率_ランサーの証(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 2
	RETURN 0
SELECTCASE 入手タイプ
	CASE "輸入"
		RETURN 装備レアリティ_Ｎ
ENDSELECT

@証アビ関数_ランサーの証_1(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM CONST 消費ＭＰ = 25
LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 = リードドライブ
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= "■リードドライブ<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　効果：敵単体に自属性の［攻撃力✕150％］ダメージを与える。<br>"
		詳細文文字列受け渡し変数 += "　　　　槍が得意武器かつ対象のCTが0の場合、対象に1回の恐怖効果を与える。基礎成功率100％。<br>"
		詳細文文字列受け渡し変数 += "　　　　斧が得意武器の場合、対象にスロウ［2］効果を与える。基礎成功率60％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用変数:効果割合 = 150
		CALL アビテンプレート_ダメージ処理_自属性

		SIF アビテンプレート用_キャンセルフラグ
			RETURN
		SIF アビテンプレート用_味方全滅フラグ || アビテンプレート用_敵全滅フラグ
			RETURN
		IF 得意武器判定(対象キャラ, "槍") && 敵BATTLE_STATE:(アビテンプレート用_対象保存:0 - 10):チャージターン == 0
			CALL アビ汎用変数文字列リセット
			アビ汎用文字列:バフ・デバフ枠 = 恐怖
			アビ汎用文字列:バフ・デバフ対象能力 = 恐怖
			アビ汎用変数:持続回数 = 1
			アビ汎用変数:基礎成功確率 = 100
			CALL アビテンプレート_不利効果_デバフ効果セット
		ELSEIF 得意武器判定(対象キャラ, "斧")
			CALL アビ汎用変数文字列リセット
			アビ汎用変数:効果量 = 2
			アビ汎用変数:基礎成功確率 = 60
			CALL アビテンプレート_不利効果_スロウ
		ENDIF
ENDSELECT

@証アビ関数_ランサーの証_2(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM CONST 消費ＭＰ = 15
LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)

SELECTCASE 処理モード
	CASE "アビ名"
		IF 得意武器判定(対象キャラ, "槍")
			TSTR:コマンド名受渡 = デュアルインパルス
		ELSEIF 得意武器判定(対象キャラ, "斧")
			TSTR:コマンド名受渡 = デュアルインパルス
		ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("連撃")
			TSTR:コマンド名受渡 = デュアルインパルス
		ENDIF
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= "■デュアルインパルス（装備者が槍得意or斧得意or連撃タイプの時）<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　効果：自身のDA率を2ターン［+100%/C枠］する。<br>"
		詳細文文字列受け渡し変数 += "　　　　槍が得意武器の場合、味方全体のDA率を2ターン［+20%/A枠］する。<br>"
		詳細文文字列受け渡し変数 += "　　　　斧が得意武器の場合、敵全体のDA率を2ターン［-20%/B枠］する。基礎成功率90％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = DA率C
		アビ汎用文字列:バフ・デバフ対象能力 = DA率
		アビ汎用変数:効果量 = 100
		アビ汎用変数:持続ターン = 2
		CALL アビテンプレート_有利効果_バフ効果セット

		IF 得意武器判定(対象キャラ, "槍")
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_味方全体_生者のみ
			アビ汎用文字列:バフ・デバフ枠 = DA率A
			アビ汎用文字列:バフ・デバフ対象能力 = DA率
			アビ汎用変数:効果量 = 20
			アビ汎用変数:持続ターン = 2
			CALL アビテンプレート_有利効果_バフ効果セット
		ENDIF
		IF 得意武器判定(対象キャラ, "斧")
			CALL アビ汎用変数文字列リセット
			CALL アビ対象選択テンプレート_敵全体_生者のみ
			アビ汎用文字列:バフ・デバフ枠 = DA率B
			アビ汎用文字列:バフ・デバフ対象能力 = DA率
			アビ汎用変数:効果量 = 20
			アビ汎用変数:持続ターン = 2
			アビ汎用変数:基礎成功確率 = 90
			CALL アビテンプレート_不利効果_デバフ効果セット
		ENDIF
ENDSELECT

@証アビ関数_ランサーの証_3(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM CONST 消費ＭＰ = 20
LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)

SELECTCASE 処理モード
	CASE "アビ名"
		IF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("攻撃")
			TSTR:コマンド名受渡 = ランドグリース
		ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("奥義")
			TSTR:コマンド名受渡 = ランドグリース
		ENDIF
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= "■ランドグリース（装備者が攻撃タイプor奥義タイプの時）<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　効果：敵全体の防御力を3ターン［20%/B枠］減少させる。基礎成功率80％。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用文字列:バフ・デバフ枠 = 防御力B
		アビ汎用文字列:バフ・デバフ対象能力 = 防御力
		アビ汎用変数:効果量 = 20
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:基礎成功確率 = 80
		CALL アビテンプレート_不利効果_デバフ効果セット
ENDSELECT
