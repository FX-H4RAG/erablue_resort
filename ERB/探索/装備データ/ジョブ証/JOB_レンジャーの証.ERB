@証関数_レンジャーの証(処理モード)
#DIMS 処理モード

SELECTCASE 処理モード
	CASE "ジョブ証種別"
		証関連文字列受け渡し = 通常ジョブ
	CASE "証名"
		TSTR:コマンド名受渡 = レンジャーの証
	CASE "フレーバー"
		詳細文文字列受け渡し変数 '= "俊敏な動きで攻撃を掻い潜り敵を狙う射手の証。<br>"
		詳細文文字列受け渡し変数 += "攻撃を躱したり、弱った敵への追い撃ちを得意とする。<br>"
		詳細文文字列受け渡し変数 += "そのため、敵の弱体化が得意な仲間との相性が良い。<br>"
ENDSELECT

@証出現率_レンジャーの証(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 2
	RETURN 0
SELECTCASE 入手タイプ
	CASE "輸入"
		RETURN 装備レアリティ_Ｎ
ENDSELECT

@証アビ関数_レンジャーの証_1(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM CONST 消費ＭＰ = 100
LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)

SELECTCASE 処理モード
	CASE "アビ名"
		TSTR:コマンド名受渡 = ライトウェイト
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= "■ライトウェイト<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　効果：自身の回避率を次の行動時まで［+1000％/独自枠］する。<br>"
		詳細文文字列受け渡し変数 += "　　　　自身に次の行動時までの弱体無効効果を与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用文字列:バフ・デバフ枠 = 回避率A
		アビ汎用文字列:バフ・デバフ対象能力 = 回避率
		アビ汎用変数:効果量 = 1000
		アビ汎用変数:持続行動回数 = 2
		CALL アビテンプレート_有利効果_バフ効果セット

		CALL アビ汎用変数文字列リセット
		アビ汎用文字列:バフ・デバフ枠 = 弱体無効
		アビ汎用文字列:バフ・デバフ対象能力 = 弱体無効
		アビ汎用変数:持続行動回数 = 2
		CALL アビテンプレート_有利効果_バフ効果セット
ENDSELECT

@証アビ関数_レンジャーの証_2(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM CONST 消費ＭＰ = 20
LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)

SELECTCASE 処理モード
	CASE "アビ名"
		IF 得意武器判定(対象キャラ, "弓")
			TSTR:コマンド名受渡 = アローレイン
		ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("攻撃")
			TSTR:コマンド名受渡 = アローレイン
		ENDIF
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= "■アローレイン（装備者が弓得意or攻撃タイプの時）<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　効果：敵全体に自属性の［攻撃力✕100％］ダメージを与える。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵全体_生者のみ
		アビ汎用変数:効果割合 = 100
		CALL アビテンプレート_ダメージ処理_自属性
ENDSELECT

@証アビ関数_レンジャーの証_3(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM CONST 消費ＭＰ = 15
LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)

SELECTCASE 処理モード
	CASE "アビ名"
		IF 得意武器判定(対象キャラ, "銃")
			TSTR:コマンド名受渡 = ハードライン
		ELSEIF 基礎BATTLE_STATE:対象キャラ:ステータスタイプ保存 == タイプ文字列数値変換("バランス")
			TSTR:コマンド名受渡 = ハードライン
		ENDIF
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= "■ハードライン（装備者が銃得意orバランスタイプの時）<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　効果：敵単体に自属性の［攻撃力✕100％］ダメージを与える。<br>"
		詳細文文字列受け渡し変数 += "　　　　相手が弱体状態の場合、ダメージ倍率が［250％］になる。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		SIF アビテンプレート用_キャンセルフラグ
			RETURN
		IF DT_ROW_LENGTH(@"デバフデータベース_{アビテンプレート用_対象保存:0}") > 0
			アビ汎用変数:効果割合 = 250
		ELSE
			アビ汎用変数:効果割合 = 100
		ENDIF
		CALL アビテンプレート_ダメージ処理_自属性
ENDSELECT

@証アビ関数_レンジャーの証_4(処理モード, 対象キャラ)
#DIM 対象キャラ
#DIMS 処理モード
#DIM LEVEL
#DIM CONST 消費ＭＰ = 10
LEVEL = GET_JOBSKILL_LEVEL(対象キャラ)

SELECTCASE 処理モード
	CASE "アビ名"
		IF 得意武器判定(対象キャラ, "弓")
			TSTR:コマンド名受渡 = スタディエイム
		ELSEIF 得意武器判定(対象キャラ, "銃")
			TSTR:コマンド名受渡 = スタディエイム
		ENDIF
	CASE "アビ説明文"
		詳細文文字列受け渡し変数 '= "■スタディエイム（装備者が弓得意or銃得意の時）<br>"
		詳細文文字列受け渡し変数 += @"　消費ＭＰ：{消費ＭＰ}<br>"
		詳細文文字列受け渡し変数 += "　効果：敵単体に3ターン、弱体状態として扱われるスタディエイム効果を与える。<br>"
		詳細文文字列受け渡し変数 += "　　　　自身の奥義ゲージを［+20％］する。"
		RETURN 消費ＭＰ
	CASE "アビ効果"
		CALL アビ対象選択テンプレート_敵単体
		アビ汎用文字列:バフ・デバフ枠 = スタディエイム
		アビ汎用文字列:バフ・デバフ対象能力 = スタディエイム
		アビ汎用変数:持続ターン = 3
		アビ汎用変数:弱体耐性貫通オプション = 1
		CALL アビテンプレート_不利効果_デバフ効果セット

		CALL アビ汎用変数文字列リセット
		CALL アビ対象選択テンプレート_自己のみ
		アビ汎用変数:効果量 = 20
		CALL アビテンプレート_有利効果_奥義ゲージUP
ENDSELECT
