
@指輪個別文章表示_魅惑食いの指輪
LOCALS = 
LOCALS += @"■魅惑食いの指輪<br><br>"
LOCALS += "魅了されている相手へ大きな効果を発揮する指輪<br>"
LOCALS += "魅了のデバフを受けているキャラクターに攻撃する時、威力が増加する。<br>"
LOCALS += "強敵「ヒトミダケ」の力が籠められた非常に珍しい指輪。<br>"
LOCALS += "<br>"

詳細文文字列受け渡し変数 = %LOCALS%
基礎販売額 = 50000

@指輪基礎効果文章表示_魅惑食いの指輪(指輪番号, 効果番号)
#DIM 効果番号
#DIM 指輪番号
LOCALS = 
LOCALS += "【基礎効果】<br>"
LOCALS += "最大ＨＰ+600 最大ＭＰ+30 攻撃力+100 防御力+250 回復力+200 対魅了特攻<br>"

LOCALS:1 = %DT_CELL_GETS("所持指輪データベース", 指輪番号, "オプションステータス補正", 1)%
IF LOCALS:1 != ""
	LOCALS += @"<br>【オプション効果】<br>%オプション効果表示文字列作成(LOCALS:1)%"
ENDIF

詳細文文字列受け渡し変数 = %LOCALS%

;ランダム抽選では出現しない（入手法：ダンジョン「名前を亡くした遺跡」でボス「ヒトミダケ」を初回撃破）
;@指輪出現率_魅惑食いの指輪(入手タイプ, 装備ランク)

@指輪ステータス決定_魅惑食いの指輪(オプション数, 情報取得フラグ = 0, 効果指定 = -1)
#DIM 効果指定
#DIM オプション数
#DIM 情報取得フラグ
#DIMS 基本補正文字列
#DIMS 特殊補正文字列

基本補正文字列 = 最大ＨＰ_600_最大ＭＰ_30_攻撃力_100_防御力_250_回復力_200
特殊補正文字列 = 対魅了特攻

IF 情報取得フラグ
	詳細文文字列受け渡し変数 = %基本補正文字列%
	詳細文文字列受け渡し変数 += @"%特殊補正文字列%"
	;オプション固定用にオプションランクを返す
	RETURN 3
ENDIF

;オプション数に応じてオプション効果をランダムで付加
;基礎指輪なのでLv1のオプションしか付かない
;例外作りたいときは例外の関数作ってもろて

オプション補正文字列 = 
IF オプション数
	FOR LOCAL, 0, オプション数
		CALL オプション効果決定(3)
	NEXT
ENDIF

LOCAL = DT_ROW_ADD("所持指輪データベース", "指輪名", "魅惑食いの指輪", "基礎ステータス補正", 基本補正文字列, "特殊ステータス補正", 特殊補正文字列, "オプションステータス補正", オプション補正文字列)


@固有バフ・デバフ処理_指輪_魅惑食いの指輪_与ダメージ(隊列番号, ダメージ値)
#DIM 隊列番号
#DIM ダメージ値

;対象が魅了状態なら
IF デバフ番号取得("魅了", 戦闘行動対象) > -1
	;ダメージ２倍
	RETURN ダメージ値 * 2
ENDIF

;上に引っかからなかったらダメージをそのまま返す
RETURN ダメージ値
