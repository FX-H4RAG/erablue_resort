
@指輪個別文章表示_来賓の指輪
LOCALS = 
LOCALS += @"■来賓の指輪<br><br>"
LOCALS += "「来訪者が領地を楽しめますように」という、古の領主の願いが込められた指輪。<br>"
LOCALS += "その願いが魔力となり、リゾートの観光客に力を与えてくれる。<br>"
LOCALS += "性能は回復性能に特化しており、サポート用途では心強い。<br>"
LOCALS += "<br>"

詳細文文字列受け渡し変数 = %LOCALS%
基礎販売額 = 30000

@指輪基礎効果文章表示_来賓の指輪(指輪番号, 効果番号)
#DIM 効果番号
#DIM 指輪番号
LOCALS = 
LOCALS += "【基礎効果】<br>"
LOCALS += "最大ＨＰ+300 最大ＭＰ+240 防御力+150 回復力+300 行動速度+100 ※条件あり<br>"

LOCALS:1 = %DT_CELL_GETS("所持指輪データベース", 指輪番号, "オプションステータス補正", 1)%
IF LOCALS:1 != ""
	LOCALS += @"<br>【オプション効果】<br>%オプション効果表示文字列作成(LOCALS:1)%"
ENDIF

詳細文文字列受け渡し変数 = %LOCALS%

@指輪出現率_来賓の指輪(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 2
	RETURN 0
SELECTCASE 入手タイプ
	CASE "古ぼけた装備"
		RETURN 装備レアリティ_ＳＲ
ENDSELECT

@指輪基礎効果適用判定_来賓の指輪(対象キャラ)
#DIM 対象キャラ
SIF TALENT:対象キャラ:従業員 > 0
	RETURN 0
SIF TALENT:対象キャラ:定住者 > 0
	RETURN 0
RETURN 1

@指輪ステータス決定_来賓の指輪(オプション数, 情報取得フラグ = 0, 効果指定 = -1)
#DIM 効果指定
#DIM オプション数
#DIM 情報取得フラグ
#DIMS 基本補正文字列

基本補正文字列 = 最大ＨＰ_300_最大ＭＰ_240_防御力_150_回復力_300_行動速度_100

IF 情報取得フラグ
	詳細文文字列受け渡し変数 = %基本補正文字列%
	;オプション固定用にオプションランクを返す
	RETURN 2
ENDIF

;オプション数に応じてオプション効果をランダムで付加
;基礎指輪なのでLv2のオプションしか付かない
;例外作りたいときは例外の関数作ってもろて

オプション補正文字列 = 
IF オプション数
	FOR LOCAL, 0, オプション数
		CALL オプション効果決定(2)
	NEXT
ENDIF

LOCAL = DT_ROW_ADD("所持指輪データベース", "指輪名", "来賓の指輪", "基礎ステータス補正", 基本補正文字列, "オプションステータス補正", オプション補正文字列)
