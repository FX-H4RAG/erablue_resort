
@指輪個別文章表示_征伐の指輪・狩
LOCALS = 
LOCALS += @"■征伐の指輪・狩<br><br>"
LOCALS += "魔力を引き出す性能の良い指輪。<br>"
LOCALS += "討伐対象を想定した調整がされており、事前に作戦を立てる騎空士に人気。<br>"
LOCALS += "汎用品であるため全空で広く使われており、ダンジョンにも良く落ちている。<br>"
LOCALS += "<br>"

詳細文文字列受け渡し変数 = %LOCALS%
基礎販売額 = 25000

@指輪基礎効果文章表示_征伐の指輪・狩(指輪番号, 効果番号)
#DIM 効果番号
#DIM 指輪番号
#DIMS オプション効果一時保存, 20

LOCALS = 
LOCALS += "【基礎効果】<br>"
LOCALS += "最大ＨＰ+400 最大ＭＰ+120 攻撃力+120 防御力+120 回復力+120<br>"
LOCALS += "行動速度+120 DA率+10% TA率+5% 対獣・鳥・ゴブリン特攻<br>"

LOCALS:1 = %DT_CELL_GETS("所持指輪データベース", 指輪番号, "オプションステータス補正", 1)%
IF LOCALS:1 != ""
	LOCALS += @"<br>【オプション効果】<br>%オプション効果表示文字列作成(LOCALS:1)%"
ENDIF

詳細文文字列受け渡し変数 = %LOCALS%

@指輪出現率_征伐の指輪・狩(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 3
	RETURN 0
SELECTCASE 入手タイプ
	CASE "輸入"
		RETURN 装備レアリティ_Ｎ
	CASE "古ぼけた装備"
		RETURN 装備レアリティ_Ｎ
ENDSELECT

@指輪ステータス決定_征伐の指輪・狩(オプション数, 情報取得フラグ = 0, 効果指定 = -1)
#DIM 効果指定
#DIM オプション数
#DIM 情報取得フラグ
#DIMS 基本補正文字列

基本補正文字列 = 最大ＨＰ_400_最大ＭＰ_120_攻撃力_120_防御力_120_回復力_120_行動速度_120_DA率_10_TA率_5

IF 情報取得フラグ
	詳細文文字列受け渡し変数 = %基本補正文字列%
	;オプション固定用にオプションランクを返す
	RETURN 2
ENDIF

;オプション数に応じてオプション効果をランダムで付加
;基礎指輪なのでLv2のオプションしか付かない
;例外作りたいときは例外の関数作ってもろて

オプション補正文字列 = 
IF オプション数
	FOR LOCAL, 0, オプション数
		CALL オプション効果決定(2)
	NEXT
ENDIF

LOCAL = DT_ROW_ADD("所持指輪データベース", "指輪名", "征伐の指輪・狩", "基礎ステータス補正", 基本補正文字列, "オプションステータス補正", オプション補正文字列)


@固有バフ・デバフ処理_指輪_征伐の指輪・狩_与ダメ増加(隊列番号, ダメージ値)
#DIM 隊列番号
#DIM ダメージ値

;対象の種族がいずれかの特攻条件を満たすなら
IF REGEXPMATCH(敵BATTLE_STATE_STR:(戦闘行動対象 - 10):種族, "獣類|鳥類|ゴブリン") > 0
	;ダメージ１．３倍
	RETURN ダメージ値 * 130 / 100
ENDIF

;上に引っかからなかったらダメージをそのまま返す
RETURN ダメージ値

