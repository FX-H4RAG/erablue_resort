
@指輪個別文章表示_濃霧の指輪
LOCALS = 
LOCALS += @"■濃霧の指輪<br><br>"
LOCALS += "伝説の殺人鬼が装着していたとされる曰く付きの希少な指輪。<br>"
LOCALS += "人型の相手に攻撃した時、威力が増加する。<br>"
LOCALS += "夜になると、装備者の周りに霧が発生している気がする……<br>"
LOCALS += "<br>"

詳細文文字列受け渡し変数 = %LOCALS%
基礎販売額 = 100000

@指輪基礎効果文章表示_濃霧の指輪(指輪番号, 効果番号)
#DIM 効果番号
#DIM 指輪番号
#DIMS オプション効果一時保存, 20

LOCALS = 
LOCALS += "【基礎効果】<br>"
LOCALS += "最大ＨＰ+400 最大ＭＰ+120 攻撃力+240 防御力+240 回復力+240<br>"
LOCALS += "行動速度+240 DA率+10% TA率+5% 対人型特攻<br>"

LOCALS:1 = %DT_CELL_GETS("所持指輪データベース", 指輪番号, "オプションステータス補正", 1)%
IF LOCALS:1 != ""
	LOCALS += @"<br>【オプション効果】<br>%オプション効果表示文字列作成(LOCALS:1)%"
ENDIF

詳細文文字列受け渡し変数 = %LOCALS%

@指輪出現率_濃霧の指輪(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 3
	RETURN 0
SELECTCASE 入手タイプ
	CASE "古ぼけた装備"
		RETURN 装備レアリティ_ＳＳＲ
ENDSELECT

@指輪ステータス決定_濃霧の指輪(オプション数, 情報取得フラグ = 0, 効果指定 = -1)
#DIM 効果指定
#DIM オプション数
#DIM 情報取得フラグ
#DIMS 基本補正文字列

基本補正文字列 = 最大ＨＰ_400_最大ＭＰ_120_攻撃力_240_防御力_240_回復力_240_行動速度_240_DA率_10_TA率_5

IF 情報取得フラグ
	詳細文文字列受け渡し変数 = %基本補正文字列%
	;オプション固定用にオプションランクを返す
	RETURN 3
ENDIF

;オプション数に応じてオプション効果をランダムで付加
;基礎指輪なのでLv3のオプションしか付かない
;例外作りたいときは例外の関数作ってもろて

オプション補正文字列 = 
IF オプション数
	FOR LOCAL, 0, オプション数
		CALL オプション効果決定(3)
	NEXT
ENDIF

LOCAL = DT_ROW_ADD("所持指輪データベース", "指輪名", "濃霧の指輪", "基礎ステータス補正", 基本補正文字列, "オプションステータス補正", オプション補正文字列)


@固有バフ・デバフ処理_指輪_濃霧の指輪_与ダメ増加(隊列番号, ダメージ値)
#DIM 隊列番号
#DIM ダメージ値

;対象の種族に人型があるなら
IF STRFIND(敵BATTLE_STATE_STR:(戦闘行動対象 - 10):種族, "人型") > -1
	;ダメージ１．５倍
	RETURN ダメージ値 * 150 / 100
ENDIF

;上に引っかからなかったらダメージをそのまま返す
RETURN ダメージ値




