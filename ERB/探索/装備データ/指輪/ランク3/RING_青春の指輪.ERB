
@指輪個別文章表示_青春の指輪
LOCALS = 
LOCALS += @"■青春の指輪<br><br>"
LOCALS += "ある国の巫女が木行の気を込めて作った指輪。<br>"
LOCALS += "春に装備することで、速攻向きの性能を発揮する。<br>"
LOCALS += "衝動的で血気盛んな若者の装備にも適しそうだ。<br>"
LOCALS += "<br>"

詳細文文字列受け渡し変数 = %LOCALS%
基礎販売額 = 50000

@指輪基礎効果文章表示_青春の指輪(指輪番号, 効果番号)
#DIM 効果番号
#DIM 指輪番号
LOCALS = 
LOCALS += "【基礎効果】<br>"
LOCALS += "最大ＨＰ+150 最大ＭＰ+200 攻撃力+150 行動速度+400 DA率+30% TA率+20% ※条件あり<br>"

LOCALS:1 = %DT_CELL_GETS("所持指輪データベース", 指輪番号, "オプションステータス補正", 1)%
IF LOCALS:1 != ""
	LOCALS += @"<br>【オプション効果】<br>%オプション効果表示文字列作成(LOCALS:1)%"
ENDIF

詳細文文字列受け渡し変数 = %LOCALS%

@指輪出現率_青春の指輪(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 3
	RETURN 0
SELECTCASE 入手タイプ
	CASE "古ぼけた装備"
		RETURN 装備レアリティ_ＳＲ
ENDSELECT

@指輪基礎効果適用判定_青春の指輪(対象キャラ)
#DIM 対象キャラ
;春なら無条件でOK
SIF (DAY / 30) % 4 == 0
	RETURN 1
;耳飾りとのセット効果
SIF GROUPMATCH(DT_CELL_GETS("所持耳飾りデータベース", 装備ステータス補正保存:対象キャラ:装備耳飾り番号, "耳飾り名", 1), "桜花の耳飾り", "朝顔の耳飾り", "紅葉の耳飾り", "水仙の耳飾り", "蓮華の耳飾り", "菊花の耳飾り") > 0
	RETURN 1
;若い・成人
RETURN (TALENT:対象キャラ:年齢層 == 年齢層_若い || TALENT:対象キャラ:年齢層 == 年齢層_成人)

@指輪ステータス決定_青春の指輪(オプション数, 情報取得フラグ = 0, 効果指定 = -1)
#DIM 効果指定
#DIM オプション数
#DIM 情報取得フラグ
#DIMS 基本補正文字列

基本補正文字列 = 最大ＨＰ_150_最大ＭＰ_200_攻撃力_150_行動速度_400_DA率_30_TA率_20

IF 情報取得フラグ
	詳細文文字列受け渡し変数 = %基本補正文字列%
	;オプション固定用にオプションランクを返す
	RETURN 2
ENDIF

;オプション数に応じてオプション効果をランダムで付加
;基礎指輪なのでLv2のオプションしか付かない
;例外作りたいときは例外の関数作ってもろて

オプション補正文字列 = 
IF オプション数
	FOR LOCAL, 0, オプション数
		CALL オプション効果決定(2)
	NEXT
ENDIF

LOCAL = DT_ROW_ADD("所持指輪データベース", "指輪名", "青春の指輪", "基礎ステータス補正", 基本補正文字列, "オプションステータス補正", オプション補正文字列)
