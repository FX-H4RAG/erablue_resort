
@耳飾り個別文章表示_火の耳飾り
LOCALS = 
LOCALS += @"■火の耳飾り<br><br>"
LOCALS += "火の力を活性化させるありふれた耳飾り。<br>"
LOCALS += "奥義への追加効果も基礎的なものばかり。<br>"
LOCALS += "汎用品であるため全空で広く使われており、ダンジョンにも良く落ちている。<br>"
LOCALS += "<br>"

詳細文文字列受け渡し変数 = %LOCALS%
基礎販売額 = 10000

@耳飾りショップ簡易文章表示_火の耳飾り
詳細文文字列受け渡し変数 = 奥義威力+30\% + ランダム効果１つ

@耳飾り基礎効果文章表示_火の耳飾り(耳飾り番号 = 0, 効果番号 = 0)
#DIM 耳飾り番号
#DIM 効果番号

LOCALS = 
LOCALS += "【変更属性】<br>"
LOCALS += "火<br><br>"
LOCALS += "【奥義補正】<br>"
LOCALS += "奥義威力+30%"
IF 耳飾り番号
	IF STRCOUNT(DT_CELL_GETS("所持耳飾りデータベース", 耳飾り番号, "奥義補正", 1), "自己回復")
		効果番号 = 1
	ELSEIF STRCOUNT(DT_CELL_GETS("所持耳飾りデータベース", 耳飾り番号, "奥義補正", 1), "自己攻撃力強化")
		効果番号 = 2
	ELSEIF STRCOUNT(DT_CELL_GETS("所持耳飾りデータベース", 耳飾り番号, "奥義補正", 1), "自己奥義ゲージ増加")
		効果番号 = 3
	ENDIF
ENDIF
IF 効果番号
	SELECTCASE 効果番号
		CASE 1
			LOCALS += "　自己回復 回復力x50%<br>"
			詳細文文字列受け渡し変数２ = 奥義威力+30\%　自己回復 回復力x50\%
		CASE 2
			LOCALS += "　自己攻撃力強化 Ａ枠+30% ３ターン<br>"
			詳細文文字列受け渡し変数２ = 奥義威力+30\%　自己攻撃力強化 Ａ枠+30\% ３ターン
		CASE 3
			LOCALS += "　自己奥義ゲージ増加 +20%<br>"
			詳細文文字列受け渡し変数２ = 奥義威力+30\%　自己奥義ゲージ増加 +20\%
	ENDSELECT
ELSE
	LOCALS += "<br>以下の効果から１つ。取得時にランダム決定<br>"
	LOCALS += "・自己回復 回復力x50%<br>"
	LOCALS += "・自己攻撃力強化 Ａ枠+30% ３ターン<br>"
	LOCALS += "・自己奥義ゲージ増加 +20%<br>"
	詳細文文字列受け渡し変数２ = 奥義威力+30\%　ランダム効果１つ
ENDIF

詳細文文字列受け渡し変数 = %LOCALS%
;ランダム効果数を返す
RETURN 3

@耳飾り出現率_火の耳飾り(入手タイプ, 装備ランク)
#DIMS 入手タイプ
#DIM 装備ランク

SIF 装備ランク < 1
	RETURN 0
SELECTCASE 入手タイプ
	CASE "輸入"
		RETURN 装備レアリティ_Ｎ
	CASE "古ぼけた装備"
		RETURN 装備レアリティ_Ｎ
ENDSELECT

@耳飾りステータス決定_火の耳飾り(ARG = 0)

LOCALS = 奥義威力_
IF ARG
	LOCAL = ARG
ELSE
	LOCAL = RAND:3 + 1
ENDIF
SELECTCASE LOCAL
	CASE 1
		LOCALS += "自己回復"
	CASE 2
		LOCALS += "自己攻撃力強化"
	CASE 3
		LOCALS += "自己奥義ゲージ増加"
ENDSELECT

LOCAL = DT_ROW_ADD("所持耳飾りデータベース", "耳飾り名", "火の耳飾り", "変更属性", "火", "奥義補正", LOCALS)

@奥義威力_効果量_火の耳飾り
RETURN 30

;@奥義補正処理関数_(奥義補正名)_(耳飾り名)
;ここに効果処理を記述する

@奥義補正処理関数_自己回復_火の耳飾り
CALL アビ対象選択テンプレート_自己のみ
;回復力x50%
アビ汎用変数:効果割合 = 50
CALL アビテンプレート_回復処理_MAXでも回復

@奥義補正処理関数_自己攻撃力強化_火の耳飾り
CALL アビ対象選択テンプレート_自己のみ
アビ汎用変数:効果割合 = 30
アビ汎用文字列:バフ・デバフ枠 = 攻撃力A
アビ汎用文字列:バフ・デバフ対象能力 = 攻撃力
アビ汎用変数:持続ターン = 3
CALL アビテンプレート_有利効果_バフ効果セット

@奥義補正処理関数_自己奥義ゲージ増加_火の耳飾り
CALL アビ対象選択テンプレート_自己のみ
アビ汎用変数:効果量 = 20
CALL アビテンプレート_有利効果_奥義ゲージUP
