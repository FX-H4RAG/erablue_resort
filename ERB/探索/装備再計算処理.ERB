@装備再計算処理(キャラ番号)
#DIM キャラ番号
#DIM 装備指輪
#DIM 装備耳飾り
#DIM 装備衣装
#DIM ステータス一時計算
VARSET LOCALS

装備指輪 = 装備ステータス補正保存:キャラ番号:装備指輪番号
装備耳飾り = 装備ステータス補正保存:キャラ番号:装備耳飾り番号
装備衣装 = 装備ステータス補正保存:キャラ番号:装備衣装番号
;装備アイテム番号だけ退避させて一端全部消す
VARSET 装備ステータス補正保存:キャラ番号:0

装備ステータス補正保存:キャラ番号:装備指輪番号 = 装備指輪
SPLIT DT_CELL_GETS("所持指輪データベース", 装備指輪, "基礎ステータス補正", 1), "_", LOCALS
FOR LOCAL:1, 0, 30, 2
	IF LOCALS:(LOCAL:1) == ""
		BREAK
	ENDIF
	装備ステータス補正保存:キャラ番号:(LOCALS:(LOCAL:1)) += TOINT(LOCALS:(LOCAL:1 + 1))
NEXT
SPLIT DT_CELL_GETS("所持指輪データベース", 装備指輪, "オプションステータス補正", 1), "_", LOCALS
FOR LOCAL:1, 0, 30, 2
	IF LOCALS:(LOCAL:1) == ""
		BREAK
	ENDIF
	装備ステータス補正保存:キャラ番号:(LOCALS:(LOCAL:1)) += TOINT(LOCALS:(LOCAL:1 + 1))
NEXT

装備ステータス補正保存:キャラ番号:装備耳飾り番号 = 装備耳飾り
IF DT_CELL_GETS("所持耳飾りデータベース", 装備耳飾り, "変更属性", 1) != ""
	;0を火属性にしちゃったので数値そのままだと属性変更してるかどうか判別がつかないため、装備による属性変更は属性番号+10で保存
	装備ステータス補正保存:キャラ番号:属性 = 属性文字列数値変換(DT_CELL_GETS("所持耳飾りデータベース", 装備耳飾り, "変更属性", 1)) + 10
ENDIF

装備ステータス補正保存:キャラ番号:装備衣装番号 = 装備衣装
;たぶん現状衣装でステータスが変化することはないので触らない

;その他ステータス補正処理
IF CFLAG:キャラ番号:滞在期間 > 0
	;最大HP
	ステータス一時計算 = 基礎BATTLE_STATE:キャラ番号:最大ＨＰ + 装備ステータス補正保存:キャラ番号:最大ＨＰ
	SIF 基礎BATTLE_STATE:キャラ番号:属性 == 3 && 料理セット判定("１ポンドステーキ")
		装備ステータス補正保存:キャラ番号:最大ＨＰ += ステータス一時計算 * 10 / 100
	;最大MP
	ステータス一時計算 = 基礎BATTLE_STATE:キャラ番号:最大ＭＰ + 装備ステータス補正保存:キャラ番号:最大ＭＰ
	SIF 基礎BATTLE_STATE:キャラ番号:属性 == 3 && 料理セット判定("闇鍋")
		装備ステータス補正保存:キャラ番号:最大ＭＰ += ステータス一時計算 * 10 / 100
	;攻撃力
	ステータス一時計算 = 基礎BATTLE_STATE:キャラ番号:攻撃力 + 装備ステータス補正保存:キャラ番号:攻撃力
	SIF 料理セット判定("焼肉セット")
		装備ステータス補正保存:キャラ番号:攻撃力 += ステータス一時計算 * 5 / 100
	;防御力
	ステータス一時計算 = 基礎BATTLE_STATE:キャラ番号:防御力 + 装備ステータス補正保存:キャラ番号:防御力
	SIF 基礎BATTLE_STATE:キャラ番号:属性 == 1 && 料理セット判定("海鮮焼きそば")
		装備ステータス補正保存:キャラ番号:防御力 += ステータス一時計算 * 5 / 100
	;行動速度
	ステータス一時計算 = 基礎BATTLE_STATE:キャラ番号:行動速度 + 装備ステータス補正保存:キャラ番号:行動速度
	SIF 基礎BATTLE_STATE:キャラ番号:属性 == 3 && 料理セット判定("温玉サラダ")
		装備ステータス補正保存:キャラ番号:行動速度 += ステータス一時計算 * 5 / 100
	;命中率
	ステータス一時計算 = 基礎BATTLE_STATE:キャラ番号:命中率 + 装備ステータス補正保存:キャラ番号:命中率
	SIF 料理セット判定("レンズ豆煮込み")
		装備ステータス補正保存:キャラ番号:命中率 += ステータス一時計算 * 5 / 100
	;DA率
	ステータス一時計算 = 基礎BATTLE_STATE:キャラ番号:DA率 + 装備ステータス補正保存:キャラ番号:DA率
	SIF 基礎BATTLE_STATE:キャラ番号:属性 == 0 && 料理セット判定("ドリア")
		装備ステータス補正保存:キャラ番号:DA率 += 10
	;TA率
	ステータス一時計算 = 基礎BATTLE_STATE:キャラ番号:TA率 + 装備ステータス補正保存:キャラ番号:TA率
	SIF 基礎BATTLE_STATE:キャラ番号:属性 == 0 && 料理セット判定("温玉サラダ")
		装備ステータス補正保存:キャラ番号:TA率 += 5
	;ゲージ上昇効率
	ステータス一時計算 = 基礎BATTLE_STATE:キャラ番号:ゲージ上昇効率 + 装備ステータス補正保存:キャラ番号:ゲージ上昇効率
	SIF 基礎BATTLE_STATE:キャラ番号:属性 == 4 && 料理セット判定("出汁鍋")
		装備ステータス補正保存:キャラ番号:ゲージ上昇効率 += 10
ENDIF
