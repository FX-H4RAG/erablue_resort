
;********************************************************************************************
;2023/6/xx 作成 rsp暇人
;関数が増えてきたので別ファイル化
;********************************************************************************************

;********************************************************************************************
;@スプライト作成(ARGS, ARG, mode = 0)
;  グラフィックID(ARG)からARGSという名前のスプライトを、アスペクト比不適な画像処理の設定に応じた方法で作成する
;  mode 0：正方形 1；縦長
;********************************************************************************************

@スプライト作成(ARGS, ARG, mode = 0)
#FUNCTION
#DIM mode;0:正方形 1:縦長
#DIM 縦幅
#DIM 横幅
#DIM 横始点

SIF ARGS == ""
	THROW スプライト名が指定されていません
SIF !GCREATED(ARG)
	THROW グラフィックID{ARG}は作成されていません

横幅 = GWIDTH(ARG)
縦幅 = GHEIGHT(ARG)
mode ++

IF 画像サイズ切り替えOPTION記憶:1
	;拡大縮小処理
	横始点 = 0
	GDISPOSE TEMP_gID
	IF 横幅 * mode > 縦幅
		;横長の場合、縦にサイズを広げて再描画
		GCREATE TEMP_gID, 横幅, 横幅 * mode
		GDRAWG TEMP_gID, ARG, 0, (横幅 * mode - 縦幅) / 2, 横幅, 縦幅, 0, 0, 横幅, 縦幅
		横幅 = GWIDTH(TEMP_gID)
		縦幅 = GHEIGHT(TEMP_gID)
		GDISPOSE ARG
		GCREATE ARG, 横幅, 縦幅
		GDRAWG ARG,TEMP_gID, 0, 0, 横幅, 縦幅, 0, 0, 横幅, 縦幅
		GDISPOSE TEMP_gID
	ELSEIF 横幅 * mode < 縦幅
		;縦長の場合、横にサイズを広げて再描画
		GCREATE TEMP_gID, 縦幅 / mode, 縦幅
		GDRAWG TEMP_gID, ARG, (縦幅 - 横幅 * mode) / (2 * mode), 0, 横幅, 縦幅, 0, 0, 横幅, 縦幅
		横幅 = GWIDTH(TEMP_gID)
		縦幅 = GHEIGHT(TEMP_gID)
		GDISPOSE ARG
		GCREATE ARG, 横幅, 縦幅
		GDRAWG ARG,TEMP_gID, 0, 0, 横幅, 縦幅, 0, 0, 横幅, 縦幅
		GDISPOSE TEMP_gID
	ENDIF
ELSE
	;切り抜き処理
	IF 横幅 * 100 * mode > 縦幅 * 105
		;横長の場合、はみ出した左右を切る
		横始点 = (横幅 - 縦幅 / mode) / 2
		横幅 = 縦幅 / mode
	ELSEIF 横幅 * 105 * mode < 縦幅 * 100
		;縦長の場合、はみ出した下を切るだけ
		横始点 = 0
		縦幅 = 横幅 * mode
	ELSE
		横始点 = 0
	ENDIF
ENDIF

SPRITECREATE ARGS, ARG, 横始点, 0, 横幅, 縦幅
RETURNF 1

;********************************************************************************************
;@CLOTHES_TODAY(ARG)
;  キャラARGがその日着る普段着をデフォルトの中から設定する。好感度レベルか恋慕レベルが高いとおしゃれ着を含む
;********************************************************************************************

@CLOTHES_TODAY(ARG)
#DIM CNT
#DIM C_CNT
#DIMS C_CHOICE, 22

;滞在していないキャラは着替えない
SIF CFLAG:ARG:滞在期間 == -1
	RETURN

VARSET LOCALS
LOCALS = デフォルト
SIF MAX(CFLAG:ARG:好感度レベル, CFLAG:ARG:恋慕レベル) > 関係閾値:4
	LOCALS:1 = おしゃれ着

CSTR:ARG:今日の衣装 = %GRAP_RAND(ARG, "顔_", LOCALS, LOCALS:1)%

VARSET LOCALS
LOCALS = 水着
IF ENUMFILES("resources\\" + CSTR:ARG:画像フォルダ, "顔_水着-*-?.png")
	LOCALS:1 = 水着-*-?
ENDIF

CSTR:ARG:今日の水着 = %GRAP_RAND(ARG, "顔_", LOCALS, LOCALS:1)%

;********************************************************************************************
;@GRAP_RAND(ARG, TYPE, PATRN, PATRN:1)
;  キャラARGのTYPE+PATRNで始まるファイル名を持つ画像を抽出し、その中からランダムで１つ選び
;  TYPEと.pngを除いたファイル名を返す
;  TYPE "顔_" または "短冊_"
;********************************************************************************************

@GRAP_RAND(ARG, TYPE, PATRN, PATRN:1)
#FUNCTIONS
#DIMS FOLDER
#DIMS TYPE
#DIMS PATRN, 2
#DIM L_CNT

SIF !INRANGE(ARG, 0, CHARANUM-1)
	RETURNF

VARSET LOCAL
VARSET LOCALS
VARSET RESULTS

SIF CSTR:ARG:画像フォルダ == ""
	CSTR:ARG:画像フォルダ = {NO:ARG}%CALLNAME:ARG%


FOLDER '= "resources\\" + CSTR:ARG:画像フォルダ
LOCAL = ENUMFILES(FOLDER, TYPE + PATRN + "*.png")
FOR L_CNT, 0, LOCAL
	IF STRCOUNT(RESULTS:L_CNT, "差分") == 0 || STRCOUNT(PATRN, "差分")
		LOCALS:(LOCAL:10) '= RESULTS:L_CNT
		LOCAL:10 += 1
	ELSE
	ENDIF
NEXT
LOCAL:11 = LOCAL:10
IF PATRN:1 != ""
	LOCAL:1 = ENUMFILES(FOLDER, TYPE + PATRN:1 + "*.png")
	FOR L_CNT, LOCAL, LOCAL + LOCAL:1
		IF STRCOUNT(RESULTS:(L_CNT - LOCAL), "差分") == 0 || STRCOUNT(PATRN:1, "差分")
			LOCALS:(LOCAL:11) '= RESULTS:(L_CNT - LOCAL)
			LOCAL:11 += 1
		ELSE
		ENDIF
	NEXT
ENDIF
LOCAL = LOCAL:10
LOCAL:1 = LOCAL:11 - LOCAL:10
SIF LOCAL + LOCAL:1 < 1
	RETURNF "デフォルト"
L_CNT = RAND(LOCAL + LOCAL:1)
RESULTS '= LOCALS:L_CNT

IF !STRCOUNT(PATRN, "差分") && !STRCOUNT(PATRN:1, "差分") 
	CSTR:ARG:表示グラフィックカテゴリ '= PATRN
	SIF L_CNT >= LOCAL
		CSTR:ARG:表示グラフィックカテゴリ '= PATRN:1
ENDIF

SUBSTRING RESULTS, 1 +STRLENS(FOLDER) + STRLENS(TYPE), -1
REPLACE RESULTS, "\.png", ""

RETURNF RESULTS

;********************************************************************************************
;@画像フォルダ選択(ARG)
;  キャラARGの画像フォルダ内で"顔_デフォルト.png"という名前の画像を含むフォルダ（親フォルダ、サブフォルダ含む）を
;  探して、そのフォルダ内の画像ファイルを一覧で表示し、どのフォルダの画像を使用するかを選択させる
;********************************************************************************************

@画像フォルダ選択(ARG)
#DIM L_CNT, 3
#DIM LNCOUNT
#DIM G_NUM
#DIM C_NUM
#DIM F_NUM
#DIMS G_NAME
#DIMS FOLDER, 10

LNCOUNT = LINECOUNT
VARSET LOCAL
VARSET LOCALS
VARSET FOLDER
SPRITEDISPOSEALL 0

LOCALS '= "resources\\" + TOSTR(NO:ARG) + CALLNAME:ARG
F_NUM = ENUMFILES(LOCALS, "顔_デフォルト.png", 1)
IF F_NUM < 2
	PRINTW 有効な画像フォルダは1つ以下しかありません
	RETURN
ELSEIF F_NUM > 10
	PRINTL 有効な画像フォルダが多すぎます。最初に見つかった10個のみが有効となります。
	F_NUM = 10
ENDIF

FOR L_CNT, 0, F_NUM
	FOLDER:L_CNT '= SUBSTRING(RESULTS:L_CNT, 10, STRLENS(RESULTS:L_CNT)-28)
NEXT

DRAWLINE
PRINTL 使用する画像フォルダを選択してください
PRINTL ※例としてランダムな画像を５つ表示中
DRAWLINE
TEMP_HTML = 

FOR L_CNT, 0, F_NUM
	PRINTFORML [{L_CNT}] %FOLDER:L_CNT%
	G_NUM = ENUMFILES("resources\\" + FOLDER:L_CNT, "*.png")
	PRINTFORM 総画像数：{G_NUM, 3}枚
	C_NUM = ENUMFILES("resources\\" + FOLDER:L_CNT, "*カットイン.png")
	IF C_NUM
		PRINTL 　　カットインあり
	ELSE
		PRINTL 　　カットインなし
	ENDIF
	LOCALS = 短冊_
	VARSET RESULTS
	G_NUM = ENUMFILES("resources\\" + FOLDER:L_CNT, LOCALS + "*.png")
	IF G_NUM
		TEMP_HTML_MAX_HEIGHT = 1500
		短冊フラグ = 1
	ELSE
		LOCALS = 顔_
		VARSET RESULTS
		G_NUM = ENUMFILES("resources\\" + FOLDER:L_CNT, LOCALS + "*.png")
		TEMP_HTML_MAX_HEIGHT = 750
		短冊フラグ = 0
	ENDIF
	FOR L_CNT:1, 0, 5
		G_NAME '= SUBSTRING(RESULTS:(RAND:G_NUM), 10, -1)
		LOCAL = TEMP_gID + L_CNT * 10 + L_CNT:1
		GCREATEFROMFILE LOCAL, G_NAME
		SIF スプライト作成(TOSTR(LOCAL), LOCAL, 短冊フラグ) == 0
			CONTINUE
		TEMP_HTML_MAX_HEIGHT = 1500
		LOCALS = {(L_CNT:1 % 8) * 750}, 0, 750, {TEMP_HTML_MAX_HEIGHT}
		TEMP_HTML += @"<div rect='%LOCALS%'><button value='{L_CNT}' title='%ESCAPE(G_NAME)%'><img src='{LOCAL}' width='750' height='{TEMP_HTML_MAX_HEIGHT}'></button></div>"
		TEMP_HTML_MAX_HEIGHT = 240
	NEXT
	描画開始行数 = LINECOUNT
	CALL 画像一斉表示("左", 0, 1)
NEXT


PRINTL [200] 画像のランダム表示をリロール
PRINTL [100] 戻る

BINPUT
LOCAL = RESULT
IF INRANGE(RESULT, 0, F_NUM)
	CSTR:ARG:画像フォルダ '= FOLDER:RESULT
	PRINTFORMW 画像フォルダを%CSTR:ARG:画像フォルダ%に変更しました
	SUBSTRING CSTR:ARG:画像フォルダ, STRLENS(TOSTR(NO:ARG) + CALLNAME:ARG), -1
ENDIF

FOR L_CNT, 0, (F_NUM+1)*2000
	GDISPOSE TEMP_gID + L_CNT
NEXT
CLEARLINE LINECOUNT - LNCOUNT
SIF LOCAL == 200
	RESTART
PRINTFORML 現在の画像フォルダは%CSTR:ARG:画像フォルダ%です
CALL CLOTHES_TODAY(ARG)
RETURN

;********************************************************************************************
;@グラ表示用文字列算出(ARG, TYPE="顔_")
;  キャラARGのパラメータなどの条件を元に、差分ファイルがあればそれを返す
;  こちらは条件式による振り分けのみ行い、差分ファイルのサーチ自体は@差分抽出で行う
;  条件設定は上にあるほど優先度が高い
;  TYPE "顔_" または "短冊_"
;********************************************************************************************

@グラ表示用文字列算出(ARG, TYPE="顔_")
#FUNCTIONS
#DIMS TYPE
#DIMS CONST V_FRONT = "正常位","対面座位", "側位","屈曲位", "駅弁", "対面立位", "騎乗位", "同時挿入"
#DIMS CONST V_BACK = "後背位","寝バック", "側位", "背面座位", "逆駅弁", "立ちバック", "同時挿入"
#DIMS CONST A_FRONT = "Ａ正常位","Ａ対面座位", "Ａ側位","Ａ屈曲位", "Ａ駅弁", "Ａ対面立位", "Ａ騎乗位", "Ａ同時挿入"
#DIMS CONST A_BACK = "Ａ後背位","Ａ寝バック", "Ａ側位", "Ａ背面座位", "Ａ逆駅弁", "Ａ立ちバック", "Ａ同時挿入"
#DIMS D_NAME

VARSET LOCAL
VARSET 差分エフェクト

D_NAME '= ""
LOCALS '= CSTR:ARG:顔グラ表示切り替え用文字列
SIF TYPE == "短冊_"
	LOCALS '= CSTR:ARG:短冊グラ表示切り替え用文字列

IF LOCALS == ""
	SIF CSTR:ARG:表示グラフィックカテゴリ == ""
		CSTR:ARG:表示グラフィックカテゴリ = デフォルト
	LOCALS = %TYPE + CSTR:ARG:表示グラフィックカテゴリ%
ENDIF

;設定グラの差分を見る
IF MODE_確認_ターゲット側("Ｖ挿入系", ARG) > -1
	;各体位の挿入差分があればそれを表示する
	FOR LOCAL, 0, VARSIZE("V_FRONT")
		IF MODE_確認_ターゲット側(V_FRONT:LOCAL, ARG) > -1
			LOCAL:1 ++
			SIF TCVAR:ARG:前ターン被射精位置 == 1 && 差分抽出(ARG, TYPE, LOCALS, "膣内射精", "膣内射精") && D_NAME == ""
				D_NAME '= RESULTS
			SIF 差分抽出(ARG, TYPE, LOCALS, V_FRONT:LOCAL, "挿入") && D_NAME == ""
				D_NAME '= RESULTS
		ENDIF
	NEXT
	FOR LOCAL, 0, VARSIZE("V_BACK")
		IF MODE_確認_ターゲット側(V_BACK:LOCAL, ARG) > -1
			LOCAL:2 ++
			SIF TCVAR:ARG:前ターン被射精位置 == 1 && 差分抽出(ARG, TYPE, LOCALS, "膣内射精", "膣内射精") && D_NAME == ""
				D_NAME '= RESULTS
			SIF 差分抽出(ARG, TYPE, LOCALS, V_BACK:LOCAL, "挿入") && D_NAME == ""
				D_NAME '= RESULTS
		ENDIF
	NEXT
	;なければ正面体位は正常位を、背面体位は後背位を表示する
	IF LOCAL:1 && D_NAME == ""
		VARSET 差分エフェクト
		IF TCVAR:ARG:前ターン被射精位置 == 1 && 差分抽出(ARG, TYPE, LOCALS, "膣内射精", "膣内射精")
				D_NAME '= RESULTS
		ELSEIF 差分抽出(ARG, TYPE, LOCALS, "正常位", "挿入")
			D_NAME '= RESULTS
		ENDIF
	ELSEIF LOCAL:2 && D_NAME == ""
		VARSET 差分エフェクト
		IF TCVAR:ARG:前ターン被射精位置 == 1 && 差分抽出(ARG, TYPE, LOCALS, "膣内射精", "膣内射精")
				D_NAME '= RESULTS
		ELSEIF 差分抽出(ARG, TYPE, LOCALS, "後背位", "挿入")
			D_NAME '= RESULTS
		ENDIF
	ENDIF
ENDIF
IF MODE_確認_ターゲット側("Ａ挿入系", ARG) > -1
	;各体位の挿入差分があればそれを表示する
	FOR LOCAL, 0, VARSIZE("A_FRONT")
		IF MODE_確認_ターゲット側(A_FRONT:LOCAL, ARG) > -1
			LOCAL:1 ++
			SIF TCVAR:ARG:前ターン被射精位置 == 2 && 差分抽出(ARG, TYPE, LOCALS, "腸内射精", "") && D_NAME == ""
				D_NAME '= RESULTS
			SIF 差分抽出(ARG, TYPE, LOCALS, A_FRONT:LOCAL, "Ａ挿入") && D_NAME == ""
				D_NAME '= RESULTS
		ENDIF
	NEXT
	FOR LOCAL, 0, VARSIZE("A_BACK")
		IF MODE_確認_ターゲット側(A_BACK:LOCAL, ARG) > -1
			LOCAL:2 ++
			SIF TCVAR:ARG:前ターン被射精位置 == 2 && 差分抽出(ARG, TYPE, LOCALS, "腸内射精", "") && D_NAME == ""
				D_NAME '= RESULTS
			SIF 差分抽出(ARG, TYPE, LOCALS, A_BACK:LOCAL, "Ａ挿入") && D_NAME == ""
				D_NAME '= RESULTS
		ENDIF
	NEXT
	;なければ正面体位はA正常位を、背面体位はA後背位を表示する
	SIF LOCAL:1 && 差分抽出(ARG, TYPE, LOCALS, "Ａ正常位", "") && D_NAME == ""
		D_NAME '= RESULTS
	SIF LOCAL:2 && 差分抽出(ARG, TYPE, LOCALS, "Ａ後背位", "") && D_NAME == ""
			D_NAME '= RESULTS
ENDIF
IF MAX(MODE_確認_プレイヤー側("フェラチオ系", ARG), MODE_確認_ターゲット側("シックスナイン", ARG), MODE_確認_プレイヤー側("シックスナイン", ARG), MODE_確認_プレイヤー側("パイズリ系", ARG)) > -1
	SIF TCVAR:ARG:前ターン被射精位置 == 3 && 差分抽出(ARG, TYPE, LOCALS, "口内射精", "口内射精") && D_NAME == ""
		D_NAME '= RESULTS
	SIF TCVAR:ARG:前ターン被射精位置 == 4 && 差分抽出(ARG, TYPE, LOCALS, "乳内射精", @"乳内射精{LIMIT(TALENT:ARG:バストサイズ, 0, 4)}") && D_NAME == ""
		D_NAME '= RESULTS
	SIF TCVAR:ARG:前ターン被射精位置 == 5 && 差分抽出(ARG, TYPE, LOCALS, "顔射") && D_NAME == ""
		D_NAME '= RESULTS
	SIF MODE_確認_プレイヤー側("パイズリ系", ARG) > -1 && 差分抽出(ARG, TYPE, LOCALS, "パイズリ", @"パイズリ{LIMIT(TALENT:ARG:バストサイズ, 0, 4)}") && D_NAME == ""
		D_NAME '= RESULTS
	SIF (MODE_確認_プレイヤー側("フェラチオ系", ARG) > -1 || MODE_確認_プレイヤー側("シックスナイン", ARG) > -1 || MODE_確認_ターゲット側("シックスナイン", ARG) > -1) && 差分抽出(ARG, TYPE, LOCALS, "フェラチオ", "フェラチオ") && D_NAME == ""
		D_NAME '= RESULTS
ENDIF

IF TCVAR:ARG:前ターン絶頂
	;前ターン絶頂時
	SIF 差分抽出(ARG, TYPE, LOCALS, "絶頂", , "絶頂") && D_NAME == ""
		D_NAME '= RESULTS
ENDIF
IF GETPALAMLV(PALAM:ARG:欲情,15) > 3
	;欲情時
	SIF 差分抽出(ARG, TYPE, LOCALS, "欲情", , "欲情") && D_NAME == ""
		D_NAME '= RESULTS
ENDIF

SIF !RAND:2 && 差分抽出(ARG, TYPE, LOCALS, "表情") && D_NAME == ""
	D_NAME '= RESULTS

;差分が見つかればそれを、なければ差分無しを返す
SIF D_NAME != ""
	RETURNF D_NAME
RETURNF LOCALS

;********************************************************************************************
;@差分抽出(ARG, TYPE="顔_", TYPE:1, DIFF, DIFF:1="", DIFF:2="")
;  @グラ表示用文字列算出から呼び出されて条件に合うファイルからランダムに抽出する
;  TYPE:0 "顔_" または "短冊_"
;  TYPE:1 基本的にはその時のグラ表示切り替え用文字列。空欄なら表示グラフィックカテゴリまたはデフォルトが入っているはず
;  DIFF:0 サーチする差分区分名。「顔_デフォルト_◯◯.png」の◯◯部分
;  DIFF:1、2 見つからなかった場合に表示する差分エフェクト名。1と2でレイヤー別で重ねて表示可能
;  TYPE:2 間に挟むキーワード、「顔_デフォルト[_差分_]〇〇.png」の[]内の文字列。省略した場合「_差分_」が使用される
;********************************************************************************************

@差分抽出(ARG, TYPE="顔_", TYPE:1, DIFF, DIFF:1="", DIFF:2="", TYPE:2="_差分_")
#FUNCTION
#DIMS TYPE, 3
#DIMS DIFF, 3

IF ENUMFILES(@"resources/%CSTR:ARG:画像フォルダ%/", @"%TYPE:1 + TYPE:2 +DIFF%*.png")
	;現在表示してるグラの差分があるか見る
	RESULTS '= TYPE:1 + GRAP_RAND(ARG, TYPE:1, TYPE:2 + DIFF)
	RETURNF 1
;ELSEIF ENUMFILES(@"resources/%CSTR:ARG:画像フォルダ%/", @"%TYPE + CSTR:ARG:表示グラフィックカテゴリ + TYPE:2 + DIFF%*.png")
;	;現在表示してるグラの、基本画像の差分があるか見る
;	RESULTS '= TYPE + GRAP_RAND(ARG, TYPE, CSTR:ARG:表示グラフィックカテゴリ + TYPE:2 + DIFF)
;	RETURNF 1
ENDIF
RESULTS '= TYPE:1

;差分画像がなく、エフェクトが既に自邸されていなければエフェクトを追加
SIF 差分エフェクト:0 == "" && DIFF:1 != ""
	差分エフェクト:0 '= "effect_" + TYPE + DIFF:1
SIF 差分エフェクト:1 == "" && DIFF:2 != ""
	差分エフェクト:1 '= "effect_" + TYPE + DIFF:2

RETURNF 0

;********************************************************************************************
;スプライト合成
;指定した元スプライトにリソース名のスプライトを重ねて合成する関数
;横幅か縦幅に0が入っている場合には自動で元スプライトのサイズを取得してサイズを合わせる
;********************************************************************************************
@スプライト合成(元スプライト, グラフィックID, リソース名, POS_X = 0, POS_Y = 0, 横幅 = 0, 縦幅 = 0)
#FUNCTION
#DIM グラフィックID		;合成先のグラフィックID
#DIMS 元スプライト		;合成先のスプライト名
#DIMS リソース名			;合成元のリソースID
#DIM POS_X					;リソースを配置するX座標
#DIM POS_Y					;リソースを配置するY座標
#DIM 元横幅					;合成する元スプライトの横幅
#DIM 元縦幅					;合成する元スプライトの縦幅
#DIM 横幅					;合成するリソースの横幅
#DIM 縦幅					;合成するリソースの縦幅

SIF !SPRITECREATED(リソース名)
	;リソースが存在しない場合0を返す
	RETURNF 0
SIF !SPRITECREATED(元スプライト)
	;元スプライトが存在しない場合0を返す
	RETURNF 0

元横幅 = SPRITEWIDTH(元スプライト)
元縦幅 = SPRITEHEIGHT(元スプライト)

IF 横幅 == 0 || 縦幅 == 0
	横幅 = SPRITEWIDTH(元スプライト)
	縦幅 = SPRITEHEIGHT(元スプライト)
ENDIF

GDISPOSE TEMP_gID

CBGCLEAR

;元スプライトをグラフィック化
GCREATE TEMP_gID, 元横幅, 元縦幅
GDRAWSPRITE TEMP_gID, 元スプライト, 0, 0, 元横幅, 元縦幅
;CBGSETG TEMP_gID, 0, -1*元縦幅, 1

;グラフィックIDに転写
GDISPOSE グラフィックID
GCREATE グラフィックID, 元横幅, 元縦幅
GDRAWG グラフィックID, TEMP_gID, 0, 0, 元横幅, 元縦幅, 0, 0, 元横幅, 元縦幅
;CBGSETG グラフィックID, 元横幅, -1*元縦幅, 1

GDISPOSE TEMP_gID
SPRITEDISPOSE 元スプライト
GDRAWSPRITE グラフィックID, リソース名, POS_X, POS_Y,  横幅, 縦幅
;CBGSETG グラフィックID, 0, 0, 1
SPRITECREATE 元スプライト, グラフィックID, 0, 0, 元横幅, 元縦幅
;CBGSETSPRITE  元スプライト, 元横幅, 0, 1

RETURNF

@画像フォルダ有効チェック(対象キャラ)
#DIM 対象キャラ
#DIM F_NUM
#DIMS FOLDER

IF CSTR:対象キャラ:画像フォルダ != ""
	;CSTRに入ってるフォルダ名は有効か？
	LOCALS '= "resources\\" + CSTR:対象キャラ:画像フォルダ
	F_NUM = ENUMFILES(LOCALS, "顔_デフォルト.png")
	IF F_NUM
		;デフォルト画像があるなら有効と見なす
		RETURN 1
	ENDIF
ENDIF

;この下はCSTRに文字列が入ってないor入ってるフォルダ名が有効ではないパターン
;仮に一番上のフォルダを見に行く
CSTR:対象キャラ:画像フォルダ = {NO:対象キャラ}%CALLNAME:対象キャラ%

LOCALS '= "resources\\" + CSTR:対象キャラ:画像フォルダ
F_NUM = ENUMFILES(LOCALS, "顔_デフォルト.png")
IF F_NUM
	;デフォルト画像があるなら有効と見なしてこのまま帰る
	RETURN 1
ENDIF

;ないならサブフォルダを見に行く
LOCALS '= "resources\\" + TOSTR(NO:対象キャラ) + CALLNAME:対象キャラ
F_NUM = ENUMFILES(LOCALS, "顔_デフォルト.png", 1)
IF F_NUM < 1
	;そもそも画像がないので帰る、CSTRはそのまま
	RETURN 0
ENDIF
;見つかったら一番最初のフォルダを選択しておく
FOLDER '= SUBSTRING(RESULTS:0, 10, STRLENS(RESULTS:0)-28)
CSTR:対象キャラ:画像フォルダ '= FOLDER
