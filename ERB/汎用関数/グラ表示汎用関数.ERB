
;********************************************************************************************
;2023/6/xx 作成 rsp暇人
;関数が増えてきたので別ファイル化
;********************************************************************************************

;********************************************************************************************
;@スプライト作成(ARGS, ARG, mode = 0)
;  グラフィックID(ARG)からARGSという名前のスプライトを、アスペクト比不適な画像処理の設定に応じた方法で作成する
;  mode 0：正方形 1；縦長
;  ※ グラフィックID(ARG)にはTEMP_gID以外の値を指定すること
;********************************************************************************************

@スプライト作成(ARGS, ARG, mode = 0)
#FUNCTION
#DIM mode;0:正方形 1:縦長
#DIM 縦幅
#DIM 横幅
#DIM 横始点

SIF ARGS == ""
	THROW スプライト名が指定されていません
SIF !GCREATED(ARG)
	THROW グラフィックID{ARG}は作成されていません
; グラフィックID(ARG) == TEMP_gIDの場合、拡大縮小処理が誤動作するのでエラー通知
; 切り抜き処理の場合は問題なく動作するが、設定で使用可能なグラフィックIDが変わるのもおかしいので設定に関わらず通知する
SIF ARG == TEMP_gID
	THROW グラフィックIDにはTEMP_gID({TEMP_gID})以外の数値を指定してください

横幅 = GWIDTH(ARG)
縦幅 = GHEIGHT(ARG)
mode ++

IF 画像サイズ切り替えOPTION記憶:1
	;拡大縮小処理
	横始点 = 0
	GDISPOSE TEMP_gID
	IF 横幅 * mode > 縦幅
		;横長の場合、縦にサイズを広げて再描画
		GCREATE TEMP_gID, 横幅, 横幅 * mode
		GDRAWG TEMP_gID, ARG, 0, (横幅 * mode - 縦幅) / 2, 横幅, 縦幅, 0, 0, 横幅, 縦幅
		横幅 = GWIDTH(TEMP_gID)
		縦幅 = GHEIGHT(TEMP_gID)
		GDISPOSE ARG
		GCREATE ARG, 横幅, 縦幅
		GDRAWG ARG,TEMP_gID, 0, 0, 横幅, 縦幅, 0, 0, 横幅, 縦幅
		GDISPOSE TEMP_gID
	ELSEIF 横幅 * mode < 縦幅
		;縦長の場合、横にサイズを広げて再描画
		GCREATE TEMP_gID, 縦幅 / mode, 縦幅
		GDRAWG TEMP_gID, ARG, (縦幅 - 横幅 * mode) / (2 * mode), 0, 横幅, 縦幅, 0, 0, 横幅, 縦幅
		横幅 = GWIDTH(TEMP_gID)
		縦幅 = GHEIGHT(TEMP_gID)
		GDISPOSE ARG
		GCREATE ARG, 横幅, 縦幅
		GDRAWG ARG,TEMP_gID, 0, 0, 横幅, 縦幅, 0, 0, 横幅, 縦幅
		GDISPOSE TEMP_gID
	ENDIF
ELSE
	;切り抜き処理
	IF 横幅 * 100 * mode > 縦幅 * 105
		;横長の場合、はみ出した左右を切る
		横始点 = (横幅 - 縦幅 / mode) / 2
		横幅 = 縦幅 / mode
	ELSEIF 横幅 * 105 * mode < 縦幅 * 100
		;縦長の場合、はみ出した下を切るだけ
		横始点 = 0
		縦幅 = 横幅 * mode
	ELSE
		横始点 = 0
	ENDIF
ENDIF

SPRITECREATE ARGS, ARG, 横始点, 0, 横幅, 縦幅
RETURNF 1

;********************************************************************************************
;@CLOTHES_TODAY(ARG)
;  キャラARGがその日着る普段着をデフォルトの中から設定する。好感度レベルか恋慕レベルが高いとおしゃれ着を含む
;********************************************************************************************

@CLOTHES_TODAY(ARG)
#DIM CNT
#DIM C_CNT
#DIMS C_CHOICE, 22

;滞在していないキャラは着替えない
SIF CFLAG:ARG:滞在期間 == -1
	RETURN

VARSET LOCALS
LOCALS = デフォルト
SIF MAX(CFLAG:ARG:好感度レベル, CFLAG:ARG:恋慕レベル) > 関係閾値:4
	LOCALS:1 = おしゃれ着

CSTR:ARG:今日の衣装 = %GRAP_RAND(ARG, "顔_", LOCALS, LOCALS:1)%

VARSET LOCALS
LOCALS = 水着
IF ENUMFILES("resources\\" + CSTR:ARG:画像フォルダ, "顔_水着-*-?.png")
	LOCALS:1 = 水着-*-?
ENDIF

CSTR:ARG:今日の水着 = %GRAP_RAND(ARG, "顔_", LOCALS, LOCALS:1)%

;********************************************************************************************
;@GRAP_RAND(ARG, TYPE, PATRN, PATRN:1)
;  キャラARGのTYPE+PATRNで始まるファイル名を持つ画像を抽出し、その中からランダムで１つ選び
;  TYPEと.pngを除いたファイル名を返す
;  TYPE "顔_" または "短冊_"
;********************************************************************************************

@GRAP_RAND(ARG, TYPE, PATRN, PATRN:1)
#FUNCTIONS
#DIMS FOLDER
#DIMS TYPE
#DIMS PATRN, 2
#DIMS 妊娠用PATRN保存
#DIM L_CNT
#DIMS 画像配列保存, 2000

SIF !INRANGE(ARG, 0, CHARANUM-1)
	RETURNF

SIF CSTR:ARG:画像フォルダ == ""
	CSTR:ARG:画像フォルダ = {NO:ARG}%CALLNAME:ARG%

妊娠用PATRN保存 = 
IF TALENT:ARG:妊娠 && PREGNANCY_SIZE(ARG) >= 2
	妊娠用PATRN保存 '= PATRN
	PATRN '= "妊娠" + PATRN
	SIF PATRN:1 != ""
		PATRN:1 '= "妊娠" + PATRN:1
ENDIF

$妊娠差分用_LOOP

VARSET LOCAL
VARSET 画像配列保存
VARSET RESULTS

FOLDER '= "resources\\" + CSTR:ARG:画像フォルダ
LOCAL = ENUMFILES(FOLDER, TYPE + PATRN + "*.png")
FOR L_CNT, 0, LOCAL
	IF STRCOUNT(RESULTS:L_CNT, "差分") == 0 || STRCOUNT(PATRN, "差分")
		画像配列保存:(LOCAL:10) '= RESULTS:L_CNT
		LOCAL:10 += 1
	ELSE
	ENDIF
NEXT
LOCAL:11 = LOCAL:10
IF PATRN:1 != ""
	LOCAL:1 = ENUMFILES(FOLDER, TYPE + PATRN:1 + "*.png")
	FOR L_CNT, LOCAL, LOCAL + LOCAL:1
		IF STRCOUNT(RESULTS:(L_CNT - LOCAL), "差分") == 0 || STRCOUNT(PATRN:1, "差分")
			画像配列保存:(LOCAL:11) '= RESULTS:(L_CNT - LOCAL)
			LOCAL:11 += 1
		ELSE
		ENDIF
	NEXT
ENDIF
LOCAL = LOCAL:10
LOCAL:1 = LOCAL:11 - LOCAL:10
IF LOCAL + LOCAL:1 < 1
	IF PATRN == "妊娠デフォルト"
		;妊娠デフォルトが無いならデフォルトに戻してもういっかい
		PATRN = デフォルト
		;分岐用に一時保存にも入れておく
		妊娠用PATRN保存 = デフォルト
		GOTO 妊娠差分用_LOOP
	ELSEIF STRCOUNT(PATRN, "妊娠") > 0
		;着せ替えの妊娠差分がないなら戻して最初から
		PATRN '= 妊娠用PATRN保存
		妊娠用PATRN保存 = 
		GOTO 妊娠差分用_LOOP
	ELSE
		IF TALENT:ARG:妊娠 && PREGNANCY_SIZE(ARG) >= 2 && 妊娠用PATRN保存 == ""
			;妊娠しててPATRN保存が空っぽ = 現在衣装のグラが妊娠も通常も無いパターン
			;なので通常妊娠画像を見に行く
			PATRN = 妊娠デフォルト
			GOTO 妊娠差分用_LOOP
		ELSE
			RETURNF "デフォルト"
		ENDIF
	ENDIF
ENDIF
L_CNT = RAND(LOCAL + LOCAL:1)
RESULTS '= 画像配列保存:L_CNT

IF !STRCOUNT(PATRN, "差分") && !STRCOUNT(PATRN:1, "差分") 
	CSTR:ARG:表示グラフィックカテゴリ '= PATRN
	SIF L_CNT >= LOCAL
		CSTR:ARG:表示グラフィックカテゴリ '= PATRN:1
ENDIF

SUBSTRING RESULTS, 1 +STRLENS(FOLDER) + STRLENS(TYPE), -1
REPLACE RESULTS, "\.png", ""

RETURNF RESULTS

;********************************************************************************************
;@画像フォルダ選択(対象キャラ)
;  対象キャラの画像フォルダ内で"顔_デフォルト.png"という名前の画像を含むフォルダ（親フォルダ、サブフォルダ含む）を
;  探して、そのフォルダ内の画像ファイルを一覧で表示し、どのフォルダの画像を使用するかを選択させる
;********************************************************************************************

@画像フォルダ選択(対象キャラ)
#DIM 対象キャラ
#DIM L_CNT, 3
#DIM LNCOUNT
#DIM G_NUM
#DIM C_NUM
#DIM F_NUM
#DIM T_REDRAW
#DIMS G_NAME
#DIMS FOLDER, 10
#DIM CONST FOLDER_NUM_MAX = 10

LNCOUNT = LINECOUNT
VARSET LOCAL
VARSET FOLDER
T_REDRAW = CURRENTREDRAW()
REDRAW 0

; 画像フォルダ数を取得
F_NUM = GET_ALL_RESOURCES_FOLDERS(対象キャラ)
; 取得した結果を退避
FOR L_CNT, 0, MIN(F_NUM, FOLDER_NUM_MAX)
	FOLDER:L_CNT '= RESULTS:L_CNT
NEXT

SELECTCASE F_NUM
	CASE IS < 1
		PRINTL 有効な画像フォルダがありません
		; 画像フォルダ設定の修正
		CALL FIX_RESOURCES_FOLDER(対象キャラ)
		SIF RESULT
			PRINTFORMW 画像フォルダの設定を%CSTR:対象キャラ:画像フォルダ%に変更しました
		; 表示するものがないので終了
		RETURN
	CASE 1
		; 画像フォルダ設定の修正
		CALL FIX_RESOURCES_FOLDER(対象キャラ)
		IF RESULT
			PRINTL 現在設定されている画像フォルダが無効なため、
			PRINTFORMW 画像フォルダの設定を%CSTR:対象キャラ:画像フォルダ%に変更しました
		ENDIF
	CASE 2 TO FOLDER_NUM_MAX
		; 処理なし
	CASE IS > FOLDER_NUM_MAX
		; 画像フォルダが多すぎるので一部のみ表示
		PRINTFORML 有効な画像フォルダが多すぎます。最初に見つかった{FOLDER_NUM_MAX}個のみが有効となります。
		F_NUM = FOLDER_NUM_MAX
ENDSELECT

PRINTL
DRAWLINE
PRINTL 使用する画像フォルダを選択してください
PRINTL ※例としてデフォルト画像＋ランダムな画像を４つ表示中
DRAWLINE
CALL ランダム画像表示(FOLDER, F_NUM)
G_NUM = RESULT

; 5枚以上画像がある場合はリロールボタンを表示
SIF G_NUM > 5
	PRINTL [200] 画像のランダム表示をリロール
PRINTL [100] 戻る
BINPUT
LOCAL = RESULT
IF INRANGE(LOCAL, 0, F_NUM)
	CSTR:対象キャラ:画像フォルダ '= FOLDER:LOCAL
	PRINTFORMW 画像フォルダを%CSTR:対象キャラ:画像フォルダ%に変更しました
	SUBSTRING CSTR:対象キャラ:画像フォルダ, STRLENS(TOSTR(NO:対象キャラ) + CALLNAME:対象キャラ), -1
ENDIF

CALL ランダム画像表示_リソース解放(F_NUM)

CLEARLINE LINECOUNT - LNCOUNT
REDRAW T_REDRAW
SIF LOCAL == 200
	RESTART
PRINTFORML 現在の画像フォルダは%CSTR:対象キャラ:画像フォルダ%です
CALL CLOTHES_TODAY(対象キャラ)
RETURN

;********************************************************************************************
;@グラ表示用文字列算出(ARG, TYPE="顔_")
;  キャラARGのパラメータなどの条件を元に、差分ファイルがあればそれを返す
;  こちらは条件式による振り分けのみ行い、差分ファイルのサーチ自体は@差分抽出で行う
;  条件設定は上にあるほど優先度が高い
;  TYPE "顔_" または "短冊_"
;********************************************************************************************

@グラ表示用文字列算出(ARG, TYPE="顔_")
#FUNCTIONS
#DIMS TYPE
#DIMS 文字列記憶
#DIMS CONST V_FRONT = "正常位","対面座位", "側位","屈曲位", "駅弁", "対面立位", "騎乗位", "同時挿入"
#DIMS CONST V_BACK = "後背位","寝バック", "側位", "背面座位", "背面駅弁", "立ちバック", "同時挿入"
#DIMS CONST A_FRONT = "Ａ正常位","Ａ対面座位", "Ａ側位","Ａ屈曲位", "Ａ駅弁", "Ａ対面立位", "Ａ騎乗位", "Ａ同時挿入"
#DIMS CONST A_BACK = "Ａ後背位","Ａ寝バック", "Ａ側位", "Ａ背面座位", "Ａ背面駅弁", "Ａ立ちバック", "Ａ同時挿入"
#DIMS D_NAME

VARSET LOCAL
VARSET 差分エフェクト

D_NAME '= ""
LOCALS '= CSTR:ARG:顔グラ表示切り替え用文字列
SIF TYPE == "短冊_"
	LOCALS '= CSTR:ARG:短冊グラ表示切り替え用文字列

IF LOCALS == ""
	SIF CSTR:ARG:表示グラフィックカテゴリ == ""
		CSTR:ARG:表示グラフィックカテゴリ = デフォルト
	LOCALS = %TYPE + CSTR:ARG:表示グラフィックカテゴリ%
ENDIF

;設定グラの差分を見る
IF MODE_存在判定_ターゲット側("Ｖ挿入系", ARG)
	;各体位の挿入差分があればそれを表示する
	FOR LOCAL, 0, VARSIZE("V_FRONT")
		IF MODE_存在判定_ターゲット側(V_FRONT:LOCAL, ARG)
			LOCAL:1 ++
			IF TCVAR:ARG:前ターン被射精位置 == 1 && D_NAME == ""
				文字列記憶 = 
				IF 差分抽出(ARG, TYPE, LOCALS, @"%V_FRONT:LOCAL%膣内射精", "膣内射精")
					文字列記憶 '= RESULTS
				ELSE
					IF 差分抽出(ARG, TYPE, LOCALS, @"膣内射精", "膣内射精")
						文字列記憶 '= RESULTS
					ENDIF
				ENDIF
				IF 文字列記憶 != ""
					IF OPTION変数:射精後の挿入系立ち絵制御
						IF RAND:2
							D_NAME '= 文字列記憶
						ELSE
							IF 差分抽出(ARG, TYPE, LOCALS, V_FRONT:LOCAL, "挿入")
								D_NAME '= RESULTS
							ELSE
								LOCAL = 差分抽出(ARG, TYPE, LOCALS, "膣内射精", "膣内射精")
								D_NAME '= RESULTS
							ENDIF
						ENDIF
					ELSE
						D_NAME '= 文字列記憶
					ENDIF
				ENDIF
			ENDIF
			SIF 差分抽出(ARG, TYPE, LOCALS, V_FRONT:LOCAL, "挿入") && D_NAME == ""
				D_NAME '= RESULTS
		ENDIF
	NEXT
	FOR LOCAL, 0, VARSIZE("V_BACK")
		IF MODE_存在判定_ターゲット側(V_BACK:LOCAL, ARG)
			LOCAL:2 ++
			IF TCVAR:ARG:前ターン被射精位置 == 1 && D_NAME == ""
				文字列記憶 = 
				IF 差分抽出(ARG, TYPE, LOCALS, @"%V_BACK:LOCAL%膣内射精", "膣内射精")
					文字列記憶 '= RESULTS
				ELSE
					IF 差分抽出(ARG, TYPE, LOCALS, @"膣内射精", "膣内射精")
						文字列記憶 '= RESULTS
					ENDIF
				ENDIF
				IF 文字列記憶 != ""
					IF OPTION変数:射精後の挿入系立ち絵制御
						IF RAND:2
							D_NAME '= 文字列記憶
						ELSE
							IF 差分抽出(ARG, TYPE, LOCALS, V_BACK:LOCAL, "挿入")
								D_NAME '= RESULTS
							ELSE
								LOCAL = 差分抽出(ARG, TYPE, LOCALS, "膣内射精", "膣内射精")
								D_NAME '= RESULTS
							ENDIF
						ENDIF
					ELSE
						D_NAME '= 文字列記憶
					ENDIF
				ENDIF
			ENDIF
			SIF 差分抽出(ARG, TYPE, LOCALS, V_BACK:LOCAL, "挿入") && D_NAME == ""
				D_NAME '= RESULTS
		ENDIF
	NEXT
	;なければ正面体位は正常位を、背面体位は後背位を表示する
	IF LOCAL:1 && D_NAME == ""
		VARSET 差分エフェクト
		IF TCVAR:ARG:前ターン被射精位置 == 1
			文字列記憶 = 
			IF 差分抽出(ARG, TYPE, LOCALS, @"正常位膣内射精", "膣内射精")
				文字列記憶 '= RESULTS
			ELSE
				IF 差分抽出(ARG, TYPE, LOCALS, @"膣内射精", "膣内射精")
					文字列記憶 '= RESULTS
				ENDIF
			ENDIF
			IF 文字列記憶 != ""
				IF OPTION変数:射精後の挿入系立ち絵制御
					IF RAND:2
						D_NAME '= 文字列記憶
					ELSE
						IF 差分抽出(ARG, TYPE, LOCALS, "正常位", "挿入")
							D_NAME '= RESULTS
						ELSE
							LOCAL = 差分抽出(ARG, TYPE, LOCALS, "膣内射精", "膣内射精")
							D_NAME '= RESULTS
						ENDIF
					ENDIF
				ELSE
					D_NAME '= 文字列記憶
				ENDIF
			ENDIF
		ENDIF
		IF 差分抽出(ARG, TYPE, LOCALS, "正常位", "挿入") && D_NAME == ""
			D_NAME '= RESULTS
		ENDIF
	ELSEIF LOCAL:2 && D_NAME == ""
		VARSET 差分エフェクト
		IF TCVAR:ARG:前ターン被射精位置 == 1
			文字列記憶 = 
			IF 差分抽出(ARG, TYPE, LOCALS, @"後背位膣内射精", "膣内射精")
				文字列記憶 '= RESULTS
			ELSE
				IF 差分抽出(ARG, TYPE, LOCALS, @"膣内射精", "膣内射精")
					文字列記憶 '= RESULTS
				ENDIF
			ENDIF
			IF 文字列記憶 != ""
				IF OPTION変数:射精後の挿入系立ち絵制御
					IF RAND:2
						D_NAME '= 文字列記憶
					ELSE
						IF 差分抽出(ARG, TYPE, LOCALS, "後背位", "挿入")
							D_NAME '= RESULTS
						ELSE
							LOCAL = 差分抽出(ARG, TYPE, LOCALS, "膣内射精", "膣内射精")
							D_NAME '= RESULTS
						ENDIF
					ENDIF
				ELSE
					D_NAME '= 文字列記憶
				ENDIF
			ENDIF
		ENDIF
		IF 差分抽出(ARG, TYPE, LOCALS, "後背位", "挿入") && D_NAME == ""
			D_NAME '= RESULTS
		ENDIF
	ENDIF
ENDIF
IF MODE_存在判定_ターゲット側("Ａ挿入系", ARG)
	;各体位の挿入差分があればそれを表示する
	FOR LOCAL, 0, VARSIZE("A_FRONT")
		IF MODE_存在判定_ターゲット側(A_FRONT:LOCAL, ARG)
			LOCAL:1 ++
			IF TCVAR:ARG:前ターン被射精位置 == 2 && D_NAME == ""
				文字列記憶 = 
				IF 差分抽出(ARG, TYPE, LOCALS, @"%A_FRONT:LOCAL%腸内射精", "")
					文字列記憶 '= RESULTS
				ELSE
					IF 差分抽出(ARG, TYPE, LOCALS, @"腸内射精", "")
						文字列記憶 '= RESULTS
					ENDIF
				ENDIF
				IF 文字列記憶 != ""
					IF OPTION変数:射精後の挿入系立ち絵制御
						IF RAND:2
							D_NAME '= 文字列記憶
						ELSE
							IF 差分抽出(ARG, TYPE, LOCALS, A_FRONT:LOCAL, "Ａ挿入")
								D_NAME '= RESULTS
							ELSE
								LOCAL = 差分抽出(ARG, TYPE, LOCALS, "腸内射精", "腸内射精")
								D_NAME '= RESULTS
							ENDIF
						ENDIF
					ELSE
						D_NAME '= 文字列記憶
					ENDIF
				ENDIF
			ENDIF
			SIF 差分抽出(ARG, TYPE, LOCALS, A_FRONT:LOCAL, "Ａ挿入") && D_NAME == ""
				D_NAME '= RESULTS
		ENDIF
	NEXT
	FOR LOCAL, 0, VARSIZE("A_BACK")
		IF MODE_存在判定_ターゲット側(A_BACK:LOCAL, ARG)
			LOCAL:2 ++
			IF TCVAR:ARG:前ターン被射精位置 == 2 && D_NAME == ""
				文字列記憶 = 
				IF 差分抽出(ARG, TYPE, LOCALS, @"%A_BACK:LOCAL%腸内射精", "")
					文字列記憶 '= RESULTS
				ELSE
					IF 差分抽出(ARG, TYPE, LOCALS, @"腸内射精", "")
						文字列記憶 '= RESULTS
					ENDIF
				ENDIF
				IF 文字列記憶 != ""
					IF OPTION変数:射精後の挿入系立ち絵制御
						IF RAND:2
							D_NAME '= 文字列記憶
						ELSE
							IF 差分抽出(ARG, TYPE, LOCALS, A_BACK:LOCAL, "Ａ挿入")
								D_NAME '= RESULTS
							ELSE
								LOCAL = 差分抽出(ARG, TYPE, LOCALS, "腸内射精", "腸内射精")
								D_NAME '= RESULTS
							ENDIF
						ENDIF
					ELSE
						D_NAME '= 文字列記憶
					ENDIF
				ENDIF
			ENDIF
			SIF 差分抽出(ARG, TYPE, LOCALS, A_BACK:LOCAL, "Ａ挿入") && D_NAME == ""
				D_NAME '= RESULTS
		ENDIF
	NEXT
	;なければ正面体位はA正常位を、背面体位はA後背位を表示する
	SIF LOCAL:1 && 差分抽出(ARG, TYPE, LOCALS, "Ａ正常位", "") && D_NAME == ""
		D_NAME '= RESULTS
	SIF LOCAL:2 && 差分抽出(ARG, TYPE, LOCALS, "Ａ後背位", "") && D_NAME == ""
		D_NAME '= RESULTS
ENDIF
IF MODE_存在判定_プレイヤー側("フェラチオ系", ARG) || MODE_存在判定_プレイヤー側("パイズリ系", ARG)
	IF TCVAR:ARG:前ターン被射精位置 == 3 && 差分抽出(ARG, TYPE, LOCALS, "口内射精", "口内射精") && D_NAME == ""
		IF OPTION変数:射精後のモード時立ち絵制御
			IF RAND:2
				D_NAME '= RESULTS
			ELSE
				SIF MODE_存在判定_プレイヤー側("パイズリ系", ARG) && 差分抽出(ARG, TYPE, LOCALS, "パイズリ", @"パイズリ{LIMIT(TALENT:ARG:バストサイズ, 0, 4)}")
					D_NAME '= RESULTS
				SIF MODE_存在判定_プレイヤー側("フェラチオ系", ARG) && 差分抽出(ARG, TYPE, LOCALS, "フェラチオ", "フェラチオ") && D_NAME == ""
					D_NAME '= RESULTS
			ENDIF
		ELSE
			D_NAME '= RESULTS
		ENDIF
	ENDIF
	IF TCVAR:ARG:前ターン被射精位置 == 4 && 差分抽出(ARG, TYPE, LOCALS, "乳内射精", @"乳内射精{LIMIT(TALENT:ARG:バストサイズ, 0, 4)}") && D_NAME == ""
		IF OPTION変数:射精後のモード時立ち絵制御
			IF RAND:2
				D_NAME '= RESULTS
			ELSE
				SIF MODE_存在判定_プレイヤー側("パイズリ系", ARG) && 差分抽出(ARG, TYPE, LOCALS, "パイズリ", @"パイズリ{LIMIT(TALENT:ARG:バストサイズ, 0, 4)}")
					D_NAME '= RESULTS
				SIF MODE_存在判定_プレイヤー側("フェラチオ系", ARG) && 差分抽出(ARG, TYPE, LOCALS, "フェラチオ", "フェラチオ") && D_NAME == ""
					D_NAME '= RESULTS
			ENDIF
		ELSE
			D_NAME '= RESULTS
		ENDIF
	ENDIF
	IF TCVAR:ARG:前ターン被射精位置 == 5 && 差分抽出(ARG, TYPE, LOCALS, "顔射") && D_NAME == ""
		IF OPTION変数:射精後のモード時立ち絵制御
			IF RAND:2
				D_NAME '= RESULTS
			ELSE
				SIF MODE_存在判定_プレイヤー側("パイズリ系", ARG) && 差分抽出(ARG, TYPE, LOCALS, "パイズリ", @"パイズリ{LIMIT(TALENT:ARG:バストサイズ, 0, 4)}")
					D_NAME '= RESULTS
				SIF MODE_存在判定_プレイヤー側("フェラチオ系", ARG) && 差分抽出(ARG, TYPE, LOCALS, "フェラチオ", "フェラチオ") && D_NAME == ""
					D_NAME '= RESULTS
			ENDIF
		ELSE
			D_NAME '= RESULTS
		ENDIF
	ENDIF
	SIF MODE_存在判定_プレイヤー側("パイズリ系", ARG) && 差分抽出(ARG, TYPE, LOCALS, "パイズリ", @"パイズリ{LIMIT(TALENT:ARG:バストサイズ, 0, 4)}") && D_NAME == ""
		D_NAME '= RESULTS
	SIF MODE_存在判定_プレイヤー側("フェラチオ系", ARG) && 差分抽出(ARG, TYPE, LOCALS, "フェラチオ", "フェラチオ") && D_NAME == ""
		D_NAME '= RESULTS
ENDIF

IF MODE_存在判定_プレイヤー側("手コキ", ARG)
	IF TCVAR:ARG:前ターン被射精位置 == 9 && 差分抽出(ARG, TYPE, LOCALS, "手に射精", "手に射精") && D_NAME == ""
		IF OPTION変数:射精後のモード時立ち絵制御
			IF RAND:2
				D_NAME '= RESULTS
			ELSE
				SIF MODE_存在判定_プレイヤー側("手コキ", ARG) && 差分抽出(ARG, TYPE, LOCALS, "手コキ", "手コキ") && D_NAME == ""
					D_NAME '= RESULTS
			ENDIF
		ELSE
			D_NAME '= RESULTS
		ENDIF
	ENDIF
ENDIF

IF MODE_存在判定_プレイヤー側("授乳手コキ", ARG)
	IF TCVAR:ARG:前ターン被射精位置 == 9 && 差分抽出(ARG, TYPE, LOCALS, "手に射精", "手に射精") && D_NAME == ""
		IF OPTION変数:射精後のモード時立ち絵制御
			IF RAND:2
				D_NAME '= RESULTS
			ELSE
				SIF MODE_存在判定_プレイヤー側("授乳手コキ", ARG) && 差分抽出(ARG, TYPE, LOCALS, "授乳手コキ", "授乳手コキ") && D_NAME == ""
					D_NAME '= RESULTS
			ENDIF
		ELSE
			D_NAME '= RESULTS
		ENDIF
	ENDIF
ENDIF

IF TCVAR:ARG:前ターン絶頂
	;前ターン絶頂時
	SIF 差分抽出(ARG, TYPE, LOCALS, "絶頂", , "絶頂") && D_NAME == ""
		D_NAME '= RESULTS
ENDIF

IF TCVAR:ARG:トランス残り時間 > 0
	;トランス時
	SIF 差分抽出(ARG, TYPE, LOCALS, "トランス", , "トランス") && D_NAME == ""
		D_NAME '= RESULTS
ENDIF

IF GETPALAMLV(PALAM:ARG:欲情,15) > 3
	;欲情時
	SIF 差分抽出(ARG, TYPE, LOCALS, "欲情", , "欲情") && D_NAME == ""
		D_NAME '= RESULTS
ENDIF

IF TALENT:ARG:妊娠 && PREGNANCY_SIZE(ARG) >= 2 && !STRCOUNT(LOCALS, "妊娠") && FLAG_妊娠エフェクトフィルタ
	;妊娠時
	LOCALS:1 = 妊娠{FLAG_妊娠エフェクトフィルタ}
	SIF FLAG_妊娠エフェクトフィルタ == 3
		LOCALS:1 += @"_{LIMIT(PREGNANCY_SIZE(ARG), 2, 5)}"
	IF 差分抽出(ARG, TYPE, LOCALS, "妊娠", , , , LOCALS:1) && D_NAME == ""
		D_NAME '= RESULTS
	ENDIF
ENDIF

SIF !RAND:2 && 差分抽出(ARG, TYPE, LOCALS, "表情") && D_NAME == ""
	D_NAME '= RESULTS

;差分が見つかればそれを、なければ差分無しを返す
SIF D_NAME != ""
	RETURNF D_NAME
RETURNF LOCALS

;********************************************************************************************
;@差分抽出(ARG, TYPE="顔_", TYPE:1, DIFF, DIFF:1="", DIFF:2="")
;  @グラ表示用文字列算出から呼び出されて条件に合うファイルからランダムに抽出する
;  TYPE:0 "顔_" または "短冊_"
;  TYPE:1 基本的にはその時のグラ表示切り替え用文字列。空欄なら表示グラフィックカテゴリまたはデフォルトが入っているはず
;  DIFF:0 サーチする差分区分名。「顔_デフォルト_◯◯.png」の◯◯部分
;  DIFF:1、2 見つからなかった場合に表示する差分エフェクト名。1と2でレイヤー別で重ねて表示可能
;  TYPE:2 間に挟むキーワード、「顔_デフォルト[_差分_]〇〇.png」の[]内の文字列。省略した場合「_差分_」が使用される
;********************************************************************************************

@差分抽出(ARG, TYPE="顔_", TYPE:1, DIFF, DIFF:1="", DIFF:2="", TYPE:2="_差分_", DIFF:3="")
#FUNCTION
#DIMS TYPE, 3
#DIMS DIFF, 4

IF ENUMFILES(@"resources/%CSTR:ARG:画像フォルダ%/", @"%TYPE:1 + TYPE:2 +DIFF%*.png")
	;現在表示してるグラの差分があるか見る
	RESULTS '= TYPE:1 + GRAP_RAND(ARG, TYPE:1, TYPE:2 + DIFF)
	RETURNF 1
;ELSEIF ENUMFILES(@"resources/%CSTR:ARG:画像フォルダ%/", @"%TYPE + CSTR:ARG:表示グラフィックカテゴリ + TYPE:2 + DIFF%*.png")
;	;現在表示してるグラの、基本画像の差分があるか見る
;	RESULTS '= TYPE + GRAP_RAND(ARG, TYPE, CSTR:ARG:表示グラフィックカテゴリ + TYPE:2 + DIFF)
;	RETURNF 1
ENDIF
RESULTS '= TYPE:1

;差分画像がなく、エフェクトが既に設定されていなければエフェクトを追加
SIF 差分エフェクト:0 == "" && DIFF:1 != ""
	差分エフェクト:0 '= "effect_" + TYPE + DIFF:1
SIF 差分エフェクト:1 == "" && DIFF:2 != ""
	差分エフェクト:1 '= "effect_" + TYPE + DIFF:2
SIF 差分エフェクト:2 == "" && DIFF:3 != ""
	差分エフェクト:2 '= "effect_" + TYPE + DIFF:3

RETURNF 0

;********************************************************************************************
;スプライト合成
;指定した元スプライトにリソース名のスプライトを重ねて合成する関数
;横幅か縦幅に0が入っている場合には自動で元スプライトのサイズを取得してサイズを合わせる
;********************************************************************************************
@スプライト合成(元スプライト, グラフィックID, リソース名, POS_X = 0, POS_Y = 0, 横幅 = 0, 縦幅 = 0)
#FUNCTION
#DIM グラフィックID		;合成先のグラフィックID
#DIMS 元スプライト		;合成先のスプライト名
#DIMS リソース名			;合成元のリソースID
#DIM POS_X					;リソースを配置するX座標
#DIM POS_Y					;リソースを配置するY座標
#DIM 元横幅					;合成する元スプライトの横幅
#DIM 元縦幅					;合成する元スプライトの縦幅
#DIM 横幅					;合成するリソースの横幅
#DIM 縦幅					;合成するリソースの縦幅

SIF !SPRITECREATED(リソース名)
	;リソースが存在しない場合0を返す
	RETURNF 0
SIF !SPRITECREATED(元スプライト)
	;元スプライトが存在しない場合0を返す
	RETURNF 0

元横幅 = SPRITEWIDTH(元スプライト)
元縦幅 = SPRITEHEIGHT(元スプライト)

IF 横幅 == 0 || 縦幅 == 0
	横幅 = SPRITEWIDTH(元スプライト)
	縦幅 = SPRITEHEIGHT(元スプライト)
ENDIF

GDISPOSE TEMP_gID

CBGCLEAR

;元スプライトをグラフィック化
GCREATE TEMP_gID, 元横幅, 元縦幅
GDRAWSPRITE TEMP_gID, 元スプライト, 0, 0, 元横幅, 元縦幅
;CBGSETG TEMP_gID, 0, -1*元縦幅, 1

;グラフィックIDに転写
GDISPOSE グラフィックID
GCREATE グラフィックID, 元横幅, 元縦幅
GDRAWG グラフィックID, TEMP_gID, 0, 0, 元横幅, 元縦幅, 0, 0, 元横幅, 元縦幅
;CBGSETG グラフィックID, 元横幅, -1*元縦幅, 1

GDISPOSE TEMP_gID
SPRITEDISPOSE 元スプライト
GDRAWSPRITE グラフィックID, リソース名, POS_X, POS_Y,  横幅, 縦幅
;CBGSETG グラフィックID, 0, 0, 1
SPRITECREATE 元スプライト, グラフィックID, 0, 0, 元横幅, 元縦幅
;CBGSETSPRITE  元スプライト, 元横幅, 0, 1

RETURNF

@画像フォルダ有効チェック(対象キャラ)
#DIM 対象キャラ
#DIM F_NUM
#DIMS FOLDER

IF CSTR:対象キャラ:画像フォルダ != ""
	;CSTRに入ってるフォルダ名は有効か？
	LOCALS '= "resources\\" + CSTR:対象キャラ:画像フォルダ
	F_NUM = ENUMFILES(LOCALS, "顔_デフォルト.png")
	IF F_NUM
		;デフォルト画像があるなら有効と見なす
		RETURN 1
	ENDIF
ENDIF

;この下はCSTRに文字列が入ってないor入ってるフォルダ名が有効ではないパターン
;仮に一番上のフォルダを見に行く
CSTR:対象キャラ:画像フォルダ = {NO:対象キャラ}%CALLNAME:対象キャラ%

LOCALS '= "resources\\" + CSTR:対象キャラ:画像フォルダ
F_NUM = ENUMFILES(LOCALS, "顔_デフォルト.png")
IF F_NUM
	;デフォルト画像があるなら有効と見なしてこのまま帰る
	RETURN 1
ENDIF

;ないならサブフォルダを見に行く
LOCALS '= "resources\\" + TOSTR(NO:対象キャラ) + CALLNAME:対象キャラ
F_NUM = ENUMFILES(LOCALS, "顔_デフォルト.png", 1)
IF F_NUM < 1
	;そもそも画像がないので帰る、CSTRはそのまま
	RETURN 0
ENDIF
;見つかったら一番最初のフォルダを選択しておく
FOLDER '= SUBSTRING(RESULTS:0, 10, STRLENS(RESULTS:0)-28)
CSTR:対象キャラ:画像フォルダ '= FOLDER
