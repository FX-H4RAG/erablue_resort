@短冊グラ表示用文字列関数(ARG, g_loop = 0)
#FUNCTIONS
#DIM g_loop
;リソース解放
SPRITEDISPOSE @"短冊グラ{g_loop}"
GDISPOSE 3000 + g_loop

SIF CSTR:ARG:画像フォルダ == ""
	CSTR:ARG:画像フォルダ = {NO:ARG}%CALLNAME:ARG%

IF CSTR:ARG:短冊グラ表示切り替え用文字列 == ""
	GCREATEFROMFILE 3000 + g_loop, @"%CSTR:ARG:画像フォルダ%/短冊_デフォルト.png"
ELSE
	GCREATEFROMFILE 3000 + g_loop, @"%CSTR:ARG:画像フォルダ%/%CSTR:ARG:短冊グラ表示切り替え用文字列%.png"
ENDIF
IF RESULT == 0
	;該当画像が無い場合
	IF CSTR:ARG:短冊グラ表示切り替え用文字列 == ""
		;そもそもデフォルト画像が無い場合はNoimage画像で返す
		RETURNF @"<img src='Noimage_短冊' width='180px' height='360px'>"
	ELSEIF GCREATEFROMFILE(3000 + g_loop, @"%CSTR:ARG:画像フォルダ%/%CSTR:ARG:顔グラ表示切り替え用文字列%.png")
		;設定中の顔グラが存在すればそちらを拡縮で表示
		LOCAL = 画像サイズ切り替えOPTION記憶:1
		画像サイズ切り替えOPTION記憶:1 = 1
		SIF スプライト作成(@"短冊グラ{g_loop}", 3000 + g_loop, 1)
			LOCALS '= @"<img src='短冊グラ{g_loop}' width='180px' height='360px'>"
		画像サイズ切り替えOPTION記憶:1 = LOCAL
		RETURNF LOCALS
	ELSEIF GCREATEFROMFILE(3000 + g_loop, @"%CSTR:ARG:画像フォルダ%/短冊_デフォルト.png")
		;変更後画像も設定中の顔グラも無い場合はデフォルト短冊を表示させる
	ELSE
		;どれもない場合はNoimage画像で返す
;		GCREATEFROMFILE 3000 + g_loop, @"{NO:ARG}%CALLNAME:ARG%/短冊_デフォルト.png"
		RETURNF @"<img src='Noimage_短冊' width='180px' height='360px'>"
	ENDIF
ENDIF

SIF スプライト作成(@"短冊グラ{g_loop}", 3000 + g_loop, 1)
	LOCALS '= @"<img src='短冊グラ{g_loop}' width='180px' height='360px'>"

RETURNF LOCALS
