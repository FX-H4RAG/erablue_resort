@顔グラ表示用文字列関数(ARG, g_loop = 0, y_pos = 0, size = 1125, 文字列記録フラグ = 0, デフォルト生成 = 0)
#FUNCTIONS

#DIM 縦幅
#DIM 横幅
#DIM 表示位置調整用
#DIMS グラ表示用一時文字列_短冊
#DIMS グラ表示用一時文字列_顔
#DIM g_loop
#DIM y_pos
#DIM size
#DIM 文字列記録フラグ
#DIM デフォルト生成

;リソース解放
SPRITEDISPOSE @"顔グラ{g_loop}"
GDISPOSE 1000 + g_loop

SIF CSTR:ARG:画像フォルダ == ""
	CSTR:ARG:画像フォルダ = {NO:ARG}%CALLNAME:ARG%

VARSET 差分エフェクト

IF デフォルト生成 == 0
	IF ダンジョン表示モード == ""
		グラ表示用一時文字列_短冊 = %グラ表示用文字列算出(ARG, "短冊_")%
		グラ表示用一時文字列_顔 = %グラ表示用文字列算出(ARG, "顔_")%
	ELSE
		SIF 探索中顔グラキー:(キャラ隊列検索(ARG)) == ""
			探索中顔グラキー:(キャラ隊列検索(ARG)) = 通常時
		グラ表示用一時文字列_顔 = %GETVARS(@"探索中顔グラ保存:(キャラ隊列検索(ARG)):%探索中顔グラキー:(キャラ隊列検索(ARG))%")%
	ENDIF
ENDIF

IF グラ表示用一時文字列_顔 == "" || デフォルト生成
	RESULTS:(2 + g_loop) = 顔_デフォルト.png
	GCREATEFROMFILE 1000 + g_loop, @"%CSTR:ARG:画像フォルダ%/顔_デフォルト.png"
	SIF 文字列記録フラグ
		RSTR:表示グラ記録 = %CSTR:ARG:画像フォルダ%/顔_デフォルト.png
ELSE
	RESULTS:(2 + g_loop) = %グラ表示用一時文字列_顔%.png
	GCREATEFROMFILE 1000 + g_loop, @"%CSTR:ARG:画像フォルダ%/%グラ表示用一時文字列_顔%.png"
	SIF 文字列記録フラグ
		RSTR:表示グラ記録 = %CSTR:ARG:画像フォルダ%/%グラ表示用一時文字列_顔%.png
ENDIF

IF RESULT == 0 && GCREATED(1000 + g_loop) == 0
	;該当画像が無い場合
	IF グラ表示用一時文字列_顔 == ""
		;そもそもデフォルト画像が無い場合はNoimage画像で返す。差分エフェクトも無し
		GCREATEFROMFILE 1000 + g_loop, @"Noimage_顔.png"
		RESULTS:(2 + g_loop) = Noimage_顔.png
		SIF 文字列記録フラグ
			RSTR:表示グラ記録 = 
		RETURNF @"<img src='Noimage_顔' width='{size}' height='{size}' ypos='{y_pos}'>"
	ELSEIF GCREATEFROMFILE(1000 + g_loop, @"%CSTR:ARG:画像フォルダ%/%グラ表示用一時文字列_短冊%.png")
		;設定中の短冊グラが存在すればそちらをトリミングで表示
		LOCAL = 画像サイズ切り替えOPTION記憶:1
		画像サイズ切り替えOPTION記憶:1 = 0
		SIF スプライト作成(@"顔グラ{g_loop}", 1000 + g_loop, 0)
			LOCALS '= @"<img src='顔グラ{g_loop}' width='{size}' height='{size}' ypos='{y_pos}'>"
		画像サイズ切り替えOPTION記憶:1 = LOCAL
		RESULTS:(2 + g_loop) = %グラ表示用一時文字列_短冊%.png
		SIF 文字列記録フラグ
			RSTR:表示グラ記録 = %CSTR:ARG:画像フォルダ%/%グラ表示用一時文字列_短冊%.png
	ELSEIF GCREATEFROMFILE(1000 + g_loop, @"%CSTR:ARG:画像フォルダ%/顔_デフォルト.png")
		;変更後画像も設定中の顔グラも無い場合はデフォルト顔を表示させる
		SIF スプライト作成(@"顔グラ{g_loop}", 1000 + g_loop, 0)
			LOCALS '= @"<img src='顔グラ{g_loop}' width='{size}' height='{size}' ypos='{y_pos}'>"
		RESULTS:(2 + g_loop) = 顔_デフォルト.png
		SIF 文字列記録フラグ
			RSTR:表示グラ記録 = %CSTR:ARG:画像フォルダ%/顔_デフォルト.png
	ELSE
		IF CSTR:ARG:汎用立ち絵登録文字列 == "" || 汎用立ち絵制御OPTION == 0
			GCREATEFROMFILE 1000 + g_loop, @"Noimage_顔.png"
			;どれもない場合はNoimage画像で返す。差分エフェクトも無し
			RESULTS:(2 + g_loop) = Noimage_顔.png
			SIF 文字列記録フラグ
				RSTR:表示グラ記録 = 
			RETURNF @"<img src='Noimage_顔' width='{size}' height='{size}' ypos='{y_pos}'>"
		ELSEIF GCREATEFROMFILE(1000 + g_loop, @"汎用グラフィック/顔_%CSTR:ARG:汎用立ち絵登録文字列%.png")
			;汎用立ち絵登録されてる場合はそれを表示させる
			SIF スプライト作成(@"顔グラ{g_loop}", 1000 + g_loop, 0)
				LOCALS '= @"<img src='顔グラ{g_loop}' width='{size}' height='{size}' ypos='{y_pos}'>"
			RESULTS:(2 + g_loop) = 顔_%CSTR:ARG:汎用立ち絵登録文字列%.png
			SIF 文字列記録フラグ
				RSTR:表示グラ記録 = %CSTR:ARG:画像フォルダ%/顔_%CSTR:ARG:汎用立ち絵登録文字列%.png
		ENDIF
	ENDIF
ELSEIF スプライト作成(@"顔グラ{g_loop}", 1000 + g_loop, 0)
	;該当画像がある場合、そのまま作成
	LOCALS '= @"<img src='顔グラ{g_loop}' width='{size}' height='{size}' ypos='{y_pos}'>"
ELSE
ENDIF

SIF デフォルト生成
	GOTO 文字列返し

SIF 差分エフェクト != ""
	LOCAL = スプライト合成(@"顔グラ{g_loop}", 1000 + g_loop, 差分エフェクト)
;	GDRAWSPRITE 1000 + g_loop, 差分エフェクト, 0, 0, SPRITEWIDTH(@"顔グラ{g_loop}"), SPRITEHEIGHT(@"顔グラ{g_loop}")
SIF  差分エフェクト:1 != ""
	LOCAL = スプライト合成(@"顔グラ{g_loop}", 1000 + g_loop, 差分エフェクト:1)
;	GDRAWSPRITE 1000 + g_loop, 差分エフェクト:1, 0, 0, SPRITEWIDTH(@"顔グラ{g_loop}"), SPRITEHEIGHT(@"顔グラ{g_loop}")
SIF  差分エフェクト:2 != ""
	LOCAL = スプライト合成(@"顔グラ{g_loop}", 1000 + g_loop, 差分エフェクト:2)

IF 文字列記録フラグ
	RSTR:表示グラ記録 += @"+++%差分エフェクト%"
	RSTR:表示グラ記録 += @"+++%差分エフェクト:1%"
	RSTR:表示グラ記録 += @"+++%差分エフェクト:2%"
ENDIF

$文字列返し
立ち絵リソース記録 = 顔グラ{g_loop}
RETURNF LOCALS



@顔グラ単発表示_デフォルト(対象キャラ)
#DIM 対象キャラ

SIF 対象キャラ < 0
	RETURN 0

HTML_PRINT 顔グラ表示用文字列関数(対象キャラ,,,,,1)
描画開始行数 = LINECOUNT
CALL ピクセル自動改行(180)

