@顔グラ表示用文字列関数(ARG)
#FUNCTIONS

#DIM 縦幅
#DIM 横幅
#DIM 表示位置調整用

;リソース解放
SPRITEDISPOSE "顔グラ"
GDISPOSE 1000

IF CSTR:ARG:顔グラ表示切り替え用文字列 == ""
	GCREATEFROMFILE 1000, @"{NO:ARG}%CALLNAME:ARG%/顔_デフォルト.png"
ELSE
	GCREATEFROMFILE 1000, @"{NO:ARG}%CALLNAME:ARG%/%CSTR:ARG:顔グラ表示切り替え用文字列%.png"
ENDIF
IF RESULT == 0
	;該当画像が無い場合
	IF CSTR:ARG:顔グラ表示切り替え用文字列 == ""
		;そもそもデフォルト画像が無い場合はNoimage画像で返す
		RETURNF @"<img src='Noimage_顔' width='180px' height='180px'>"
	ELSE
		;変更後画像が無い場合はデフォルト顔を表示させる
		GCREATEFROMFILE 1000, @"{NO:ARG}%CALLNAME:ARG%/顔_デフォルト.png"
	ENDIF
ENDIF
SIF スプライト作成(@"顔グラ", 1000, 0)
	LOCALS = <img src='顔グラ' width='180px' height='180px'>

RETURNF LOCALS

@CLOTHES_TODAY(ARG)
#DIM CNT
#DIM C_CNT
#DIMS C_CHOICE, 22

;滞在していないキャラは着替えない
SIF CFLAG:ARG:滞在期間 == -1
	RETURN

VARSET LOCALS
LOCALS = デフォルト
SIF MAX(CFLAG:ARG:好感度レベル, CFLAG:ARG:恋慕レベル) > 関係閾値:4
	LOCALS:1 = おしゃれ着

CSTR:ARG:今日の衣装 = %GRAP_RAND(ARG, "顔_", LOCALS, LOCALS:1)%

[SKIPSTART]
VARSET C_CNT
VARSET C_CHOICE
GDISPOSE 1000
;好感度が高い場合はおしゃれ着を優先する
IF MAX(CFLAG:ARG:好感度レベル, CFLAG:ARG:恋慕レベル) > 関係閾値:4
	IF GCREATEFROMFILE(1000, @"{NO:ARG}%CALLNAME:ARG%/顔_おしゃれ着.png")
		GDISPOSE 1000
		C_CHOICE:C_CNT = おしゃれ着
		C_CNT ++
	ENDIF
	FOR CNT, 0, 10
		IF GCREATEFROMFILE(1000, @"{NO:ARG}%CALLNAME:ARG%/顔_おしゃれ着{CNT}.png")
			GDISPOSE 1000
			C_CHOICE:C_CNT = おしゃれ着{CNT}
			C_CNT ++
		ENDIF
	NEXT
	IF C_CNT && RAND:2
		CSTR:ARG:今日の衣装 = {NO:ARG}%CALLNAME:ARG%/顔_%C_CHOICE:(RAND:C_CNT)%.png
		RETURN
	ENDIF
ENDIF
;デフォルトが複数存在する場合はそこからランダム
IF GCREATEFROMFILE(1000, @"{NO:ARG}%CALLNAME:ARG%/顔_デフォルト.png")
	GDISPOSE 1000
	C_CHOICE:C_CNT = デフォルト
	C_CNT ++
ENDIF

FOR CNT, 0, 10
	IF GCREATEFROMFILE(1000, @"{NO:ARG}%CALLNAME:ARG%/顔_デフォルト{CNT}.png")
		GDISPOSE 1000
		C_CHOICE:C_CNT = デフォルト{CNT}
		C_CNT ++
	ENDIF
NEXT

SIF !C_CNT
	RETURN
CSTR:ARG:今日の衣装 = 顔_%C_CHOICE:(RAND:C_CNT)%
[SKIPEND]

@GRAP_RAND(ARG, TYPE, PATRN, PATRN:1)
#FUNCTIONS
#DIMS FOLDER
#DIMS TYPE
#DIMS PATRN, 2
#DIM L_CNT

SIF !INRANGE(ARG, 0, CHARANUM-1)
	RETURNF

VARSET LOCAL
VARSET LOCALS
VARSET RESULTS

FOLDER '= "resources\\" + TOSTR(NO:ARG) + CALLNAME:ARG
LOCAL = ENUMFILES(FOLDER, TYPE + PATRN + "*.png")
DEBUGPRINTFORML %FOLDER%内の%TYPE + PATRN%ファイル合計は{LOCAL}個
FOR L_CNT, 0, LOCAL
	LOCALS:L_CNT '= RESULTS:L_CNT
	DEBUGPRINTFORML %LOCALS:L_CNT%　
NEXT
DEBUGPRINTL
IF PATRN:1 != ""
	LOCAL:1 = ENUMFILES(FOLDER, TYPE + PATRN:1 + "*.png")
	DEBUGPRINTFORML %FOLDER%内の%TYPE + PATRN:1%ファイル合計は{LOCAL:1}個
	FOR L_CNT, LOCAL, LOCAL + LOCAL:1
		LOCALS:L_CNT '= RESULTS:(L_CNT - LOCAL)
		DEBUGPRINTFORML %LOCALS:L_CNT%　
	NEXT
	DEBUGPRINTL
ENDIF
SIF LOCAL + LOCAL:1 < 1
	RETURNF "デフォルト"
RESULTS '= LOCALS:RAND(LOCAL + LOCAL:1)
SUBSTRING RESULTS, 1 +STRLENS(FOLDER) + STRLENS(TYPE), -1
REPLACE RESULTS, "\.png", ""
DEBUGPRINTFORML 選ばれたのは%RESULTS%

RETURNF RESULTS