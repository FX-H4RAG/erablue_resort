@顔グラ表示用文字列関数(ARG, g_loop = 0, y_pos = 0)
#FUNCTIONS

#DIM 縦幅
#DIM 横幅
#DIM 表示位置調整用
#DIMS グラ表示用一時文字列_短冊
#DIMS グラ表示用一時文字列_顔
#DIM g_loop
#DIM y_pos

;リソース解放
SPRITEDISPOSE @"顔グラ{g_loop}"
GDISPOSE 1000 + g_loop
SIF CSTR:ARG:画像フォルダ == ""
	CSTR:ARG:画像フォルダ = {NO:ARG}%CALLNAME:ARG%

グラ表示用一時文字列_短冊 = %グラ表示用文字列算出(ARG, "短冊_")%
グラ表示用一時文字列_顔 = %グラ表示用文字列算出(ARG, "顔_")%
DEBUGPRINTFORM %CALLNAME:ARG%の一時文字列_顔"%グラ表示用一時文字列_顔%"が

IF グラ表示用一時文字列_顔 == ""
	GCREATEFROMFILE 1000 + g_loop, @"%CSTR:ARG:画像フォルダ%/顔_デフォルト.png"
ELSE
	GCREATEFROMFILE 1000 + g_loop, @"%CSTR:ARG:画像フォルダ%/%グラ表示用一時文字列_顔%.png"
ENDIF

IF RESULT == 0
	DEBUGPRINTFORM 無いけど
	;該当画像が無い場合
	IF グラ表示用一時文字列_顔 == ""
		;そもそもデフォルト画像が無い場合はNoimage画像で返す。差分エフェクトも無し
		DEBUGPRINTFORML そもそも空欄なのでNoImageを渡す
		GCREATEFROMFILE 1000 + g_loop, @"Noimage_顔.png"
		RETURNF @"<img src='Noimage_顔' width='180px' height='180px' ypos='{y_pos}px'>"
	ELSEIF GCREATEFROMFILE(1000 + g_loop, @"%CSTR:ARG:画像フォルダ%/%グラ表示用一時文字列_短冊%.png")
		;設定中の短冊グラが存在すればそちらをトリミングで表示
		DEBUGPRINTFORML %グラ表示用一時文字列_短冊%ならあったのでそちらを表示
		LOCAL = 画像サイズ切り替えOPTION記憶:1
		画像サイズ切り替えOPTION記憶:1 = 0
		SIF スプライト作成(@"顔グラ{g_loop}", 1000 + g_loop, 0)
			LOCALS '= @"<img src='顔グラ{g_loop}' width='180px' height='180px' ypos='{y_pos}px'>"
		画像サイズ切り替えOPTION記憶:1 = LOCAL
;		RETURNF LOCALS
	ELSEIF GCREATEFROMFILE(1000 + g_loop, @"%CSTR:ARG:画像フォルダ%/顔_デフォルト.png")
		DEBUGPRINTFORML 顔_デフォルトならあったのでそちらを表示
		;変更後画像も設定中の顔グラも無い場合はデフォルト顔を表示させる
		SIF スプライト作成(@"顔グラ{g_loop}", 1000 + g_loop, 0)
			LOCALS '= @"<img src='顔グラ{g_loop}' width='180px' height='180px' ypos='{y_pos}px'>"
	ELSE
		DEBUGPRINTFORML 代わりが無いのでNoImageを渡す
		GCREATEFROMFILE 1000 + g_loop, @"Noimage_顔.png"
		;どれもない場合はNoimage画像で返す。差分エフェクトも無し
		RETURNF @"<img src='Noimage_顔' width='180px' height='180px' ypos='{y_pos}px'>"
	ENDIF
ELSEIF スプライト作成(@"顔グラ{g_loop}", 1000 + g_loop, 0)
	;該当画像がある場合、そのまま作成
	DEBUGPRINTFORML あったのでそのまま表示
	LOCALS '= @"<img src='顔グラ{g_loop}' width='180px' height='180px' ypos='{y_pos}px'>"
ELSE
	DEBUGPRINTFORML あるにも関わらずスプライト作れないのはなぜ？
ENDIF

SIF 差分エフェクト != ""
	LOCAL = スプライト合成(@"顔グラ{g_loop}", 1000 + g_loop, 差分エフェクト)
;	GDRAWSPRITE 1000 + g_loop, 差分エフェクト, 0, 0, SPRITEWIDTH(@"顔グラ{g_loop}"), SPRITEHEIGHT(@"顔グラ{g_loop}")
SIF  差分エフェクト:1 != ""
	LOCAL = スプライト合成(@"顔グラ{g_loop}", 1000 + g_loop, 差分エフェクト:1)
;	GDRAWSPRITE 1000 + g_loop, 差分エフェクト:1, 0, 0, SPRITEWIDTH(@"顔グラ{g_loop}"), SPRITEHEIGHT(@"顔グラ{g_loop}")

RETURNF LOCALS

