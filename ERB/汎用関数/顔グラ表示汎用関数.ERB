@顔グラ表示用文字列関数(ARG)
#FUNCTIONS

#DIM 縦幅
#DIM 横幅
#DIM 表示位置調整用

;リソース解放
SPRITEDISPOSE "顔グラ"
GDISPOSE 1000

IF CSTR:ARG:顔グラ表示切り替え用文字列 == ""
	GCREATEFROMFILE 1000, @"{NO:ARG}%CALLNAME:ARG%/顔_デフォルト.png"
ELSE
	GCREATEFROMFILE 1000, @"{NO:ARG}%CALLNAME:ARG%/%CSTR:ARG:顔グラ表示切り替え用文字列%.png"
ENDIF
IF RESULT == 0
	;該当画像が無い場合
	IF CSTR:ARG:顔グラ表示切り替え用文字列 == ""
		;そもそもデフォルト画像が無い場合はNoimage画像で返す
		RETURNF @"<img src='Noimage_顔' width='180px' height='180px'>"
	ELSE
		;変更後画像が無い場合はデフォルト顔を表示させる
		GCREATEFROMFILE 1000, @"{NO:ARG}%CALLNAME:ARG%/顔_デフォルト.png"
	ENDIF
ENDIF
SPRITECREATE "顔グラ", 1000

;縦横幅を取得して表示位置を調整
横幅 = SPRITEWIDTH("顔グラ")
縦幅 = SPRITEHEIGHT("顔グラ")
IF 横幅 > 縦幅
	表示位置調整用 = (100 - (縦幅 * 100 / 横幅)) / 2 * 180 / 100
	LOCALS = <img src='顔グラ' width='180px' height='{縦幅 * 180 / 横幅}px' ypos='{表示位置調整用}px'>
ELSEIF 横幅 < 縦幅
	表示位置調整用 = (100 - (横幅 * 100 / 縦幅)) / 2 * 180 / 100
	LOCALS = <shape type='space' param='{表示位置調整用}px'><img src='顔グラ' width='{横幅 * 180 / 縦幅}px' height='180px'>
ELSE
	表示位置調整用 = 0
	LOCALS = <img src='顔グラ' width='180px' height='180px'>
ENDIF

RETURNF LOCALS





