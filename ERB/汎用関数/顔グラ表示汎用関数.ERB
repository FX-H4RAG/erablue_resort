@顔グラ表示用文字列関数(ARG, g_loop = 0)
#FUNCTIONS

#DIM 縦幅
#DIM 横幅
#DIM 表示位置調整用
#DIMS グラ表示用一時文字列_短冊
#DIMS グラ表示用一時文字列_顔
#DIM g_loop

;リソース解放
SPRITEDISPOSE "顔グラ"
GDISPOSE 1000 + g_loop
SIF CSTR:ARG:画像フォルダ == ""
	CSTR:ARG:画像フォルダ = {NO:ARG}%CALLNAME:ARG%

グラ表示用一時文字列_短冊 = %グラ表示用文字列算出_短冊(ARG)%
グラ表示用一時文字列_顔 = %グラ表示用文字列算出_顔(ARG)%
DEBUGPRINTFORML %グラ表示用一時文字列_顔%

IF グラ表示用一時文字列_顔 == ""
	GCREATEFROMFILE 1000 + g_loop, @"%CSTR:ARG:画像フォルダ%/顔_デフォルト.png"
ELSE
	GCREATEFROMFILE 1000 + g_loop, @"%CSTR:ARG:画像フォルダ%/%グラ表示用一時文字列_顔%.png"
ENDIF
IF RESULT == 0
	;該当画像が無い場合
	IF グラ表示用一時文字列_顔 == ""
		;そもそもデフォルト画像が無い場合はNoimage画像で返す
		RETURNF @"<img src='Noimage_顔' width='180px' height='180px'>"
	ELSEIF GCREATEFROMFILE(1000 + g_loop, @"%CSTR:ARG:画像フォルダ%/%グラ表示用一時文字列_短冊%.png")
		;設定中の顔グラが存在すればそちらをトリミングで表示
		LOCAL = 画像サイズ切り替えOPTION記憶:1
		画像サイズ切り替えOPTION記憶:1 = 0
		SIF スプライト作成(@"顔グラ", 1000 + g_loop, 1)
			LOCALS '= @"<img src='顔グラ' width='180px' height='180px'>"
		画像サイズ切り替えOPTION記憶:1 = LOCAL
		RETURNF LOCALS
	ELSEIF GCREATEFROMFILE(1000 + g_loop, @"%CSTR:ARG:画像フォルダ%/顔_デフォルト.png")
		;変更後画像も設定中の顔グラも無い場合はデフォルト顔を表示させる
	ELSE
		;どれもない場合はNoimage画像で返す
		RETURNF @"<img src='Noimage_顔' width='180px' height='180px'>"
;		GCREATEFROMFILE 1000 + g_loop, @"{NO:ARG}%CALLNAME:ARG%/顔_デフォルト.png"
	ENDIF
ENDIF
SIF スプライト作成(@"顔グラ", 1000 + g_loop, 0)
	LOCALS = <img src='顔グラ' width='180px' height='180px'>

RETURNF LOCALS


@グラ表示用文字列算出_顔(ARG)
#FUNCTIONS

IF CSTR:ARG:顔グラ表示切り替え用文字列 == ""
	;デフォルト顔グラ差分を見る
	IF TCVAR:ARG:前ターン絶頂
		;前ターン絶頂時
		IF EXISTFILE(@"resources/%CSTR:ARG:画像フォルダ%/顔_デフォルト_差分_絶頂.png")
			LOCALS = 顔_デフォルト%GRAP_RAND(ARG, "顔_", "_デフォルト_差分_絶頂")%
			RETURNF LOCALS
		ENDIF
	ENDIF
	IF GETPALAMLV(PALAM:ARG:欲情,15) > 3
		;欲情時
		IF EXISTFILE(@"resources/%CSTR:ARG:画像フォルダ%/顔_デフォルト_差分_欲情.png")
			LOCALS = 顔_デフォルト%GRAP_RAND(ARG, "顔_", "_デフォルト_差分_欲情")%
			RETURNF LOCALS
		ENDIF
	ENDIF
	;デフォルトを返す
	RETURNF "顔_デフォルト"
ELSE
	;設定顔グラの差分を見る
	IF TCVAR:ARG:前ターン絶頂
		;前ターン絶頂時
		IF EXISTFILE(@"resources/%CSTR:ARG:画像フォルダ%/%CSTR:ARG:顔グラ表示切り替え用文字列%_差分_絶頂.png")
			;現在表示してる顔グラの差分があるか見る
			LOCALS = %CSTR:ARG:顔グラ表示切り替え用文字列%%GRAP_RAND(ARG, CSTR:ARG:顔グラ表示切り替え用文字列, "_差分_絶頂")%
			RETURNF LOCALS
		ELSEIF EXISTFILE(@"resources/%CSTR:ARG:画像フォルダ%/顔_%CSTR:ARG:表示グラフィックカテゴリ%_差分_絶頂.png")
			;現在表示してる顔グラの、基本画像の差分があるか見る
			LOCALS = %CSTR:ARG:表示グラフィックカテゴリ%%GRAP_RAND(ARG, CSTR:ARG:表示グラフィックカテゴリ, "_差分_絶頂")%
			RETURNF LOCALS
		ENDIF
	ENDIF
	IF GETPALAMLV(PALAM:ARG:欲情,15) > 3
		;欲情時
		IF EXISTFILE(@"resources/%CSTR:ARG:画像フォルダ%/%CSTR:ARG:顔グラ表示切り替え用文字列%_差分_欲情.png")
			;現在表示してる顔グラの差分があるか見る
			LOCALS = %CSTR:ARG:顔グラ表示切り替え用文字列%%GRAP_RAND(ARG, CSTR:ARG:顔グラ表示切り替え用文字列, "_差分_欲情")%
			RETURNF LOCALS
		ELSEIF EXISTFILE(@"resources/%CSTR:ARG:画像フォルダ%/顔_%CSTR:ARG:表示グラフィックカテゴリ%_差分_欲情.png")
			;現在表示してる顔グラの、基本画像の差分があるか見る
			LOCALS = %CSTR:ARG:表示グラフィックカテゴリ%%GRAP_RAND(ARG, CSTR:ARG:表示グラフィックカテゴリ, "_差分_欲情")%
			RETURNF LOCALS
		ENDIF
	ENDIF
	;差分無しを返す
	RETURNF CSTR:ARG:顔グラ表示切り替え用文字列
ENDIF



