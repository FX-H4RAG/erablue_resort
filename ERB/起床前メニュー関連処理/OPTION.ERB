;オプション設定処理
@OPTION
PRINTL 設定項目を選択してください
PRINTL [0] - 口上テキスト設定
PRINTL [1] - コマンドフィルタ
PRINTL [2] - 特定素質の切り替え
PRINTL [3] - 機能切り替え
PRINTL [4] - 起床時刻の設定
PRINTL [5] - キャラクター招待に関する設定
PRINTL 
PRINTL [99] - 特定バグ対応ボタン
PRINTL [100]-戻る


$INPUT_LOOP
INPUT
IF RESULT == 0
	CALL OPTION_TEXT_SETTING
ELSEIF RESULT == 1
	CALL OPTION_COMMAND_FILTER
ELSEIF RESULT == 2
	CALL OPTION_素質切り替え
ELSEIF RESULT == 3
	CALL OPTION_機能切り替え
ELSEIF RESULT == 4
	PRINTL 何時に起床しますか？
	PRINTFORML 現在の起床予定時刻は%CONVERT_HOURS(CFLAG:MASTER:起床予定時刻)%時です

	FOR LOCAL, 0, 24
		PRINTFORM [{LOCAL, 2}] 
		SIF LOCAL % 10 == 9
			PRINTL
	NEXT
	PRINTL
	PRINTBUTTON "[変更しない]", 99

	$INPUT_LOOP2
	INPUT
	SIF RESULT == 99
		RESTART
	SIF RESULT < 0 || RESULT > 23
		GOTO INPUT_LOOP2
	CFLAG:MASTER:起床予定時刻 = RESULT * 60
ELSEIF RESULT == 5
	CALL OPTION_招待切り替え
ELSEIF RESULT == 99
	PRINTL 周回でキャラを引き継いだ時にエラーが発生してしまう現象へ対処するボタンです
	PRINTL 現在存在するキャラの関係性を全てリセットし、初期状態に戻します
	PRINTL ※詰みを回避するための非常用処理です。通常は実行しないでください。
	PRINTL 
	PRINTL 実行してよろしいですか？
	PRINTL [0] はい
	PRINTL [1] いいえ
	BINPUT
	IF RESULT == 0
		FOR LOCAL, 0, CHARANUM
			VARSET RELATION_NO:LOCAL:0
			VARSET RELATION_VAL:LOCAL:0
			CALL SET_RELATION(LOCAL)
		NEXT
		PRINTW 実行しました。
	ENDIF
ELSEIF RESULT == 100
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF
RESTART
;テキスト関連
@OPTION_TEXT_SETTING
PRINTL テキストの設定をしてください
PRINT 現在の設定：
IF FLAG:6 == 1 && FLAG:7 == 2
	PRINTL 【口上テキスト（常に表示）＋情景テキスト】
ELSEIF FLAG:6 == 1 && FLAG:7 == -1
	PRINTL 【情景テキストのみ】
ELSEIF FLAG:6 == 0 && FLAG:7 == -1
	PRINTL 【テキストを表示しない】
ENDIF
PRINTL 　
PRINTL [0] - 口上テキスト＋情景テキスト
PRINTL [1] - 情景テキストのみ
PRINTL [2] - テキストを表示しない
PRINTL [100]-戻る
$INPUT_LOOP
INPUT
IF RESULT == 100
	RETURN 0
ELSEIF RESULT == 0
	PRINTL 【口上テキスト＋情景テキスト】に設定しました
	FLAG:汎用文章表示切り替えオプション = 1
	FLAG:口上表示切り替えオプション = 2
ELSEIF RESULT == 1
	PRINTL 【情景テキストのみ】に設定しました
	FLAG:汎用文章表示切り替えオプション = 1
	FLAG:口上表示切り替えオプション = -1
ELSEIF RESULT == 2
	PRINTL 【テキストを表示しない】に設定しました
	FLAG:汎用文章表示切り替えオプション = 0
	FLAG:口上表示切り替えオプション = -1
ELSE
	GOTO INPUT_LOOP
ENDIF

;文字列変数に保存してみた
@OPTION_COMMAND_FILTER
TOOLTIP_CUSTOM 1
TOOLTIP_SETFONTSIZE 12
TOOLTIP_SETDURATION 100000

VARSET LOCAL
FOR COUNT,0,500
	LOCALS = /{COUNT}/
	SIF STRCOUNT(SAVESTR:0,LOCALS)
		LOCAL:COUNT = 1
NEXT
SIF SAVESTR:0 == ""
	SAVESTR:0 = /
DRAWLINE
TFLAG:ゲームフェイズ管理 = 1
LOCALS:1 = 
LOCAL:2 = 0

FOR COUNT,0,500
	SIF COUNT >= 370 && COUNT <= 390
		CONTINUE
	LOCALS = /{COUNT}/
	; SIF STRCOUNT(SAVESTR:0,LOCALS)
	; 		SETCOLOR 0x666666
	IF STRLENS(TRAIN_COMNAME(COUNT))
		TSTR:ツールチップ受渡 = TRAIN_COMNAME(COUNT)
		IF STRCOUNT(SAVESTR:0,LOCALS)
			LOCALS:1 += @"<font color='#666666'><button value = '{COUNT}'"
		ELSE
			LOCALS:1 += @"<button value = '{COUNT}'"
		ENDIF
		TRYCCALLFORM COM_TOOLTIP_{COUNT}
			IF ツールチップ切り替えOPTION記憶 == 0
				LOCALS:1 += @" title = '%TSTR:ツールチップ受渡%'>"
			ELSE
				LOCALS:1 += @">"
			ENDIF
		CATCH
			LOCALS:1 += @">"
		ENDCATCH
		LOCALS:1 += @"%TRAIN_COMNAME(COUNT), 22%[{COUNT, 3}]</button>"
		SIF STRCOUNT(SAVESTR:0,LOCALS)
			LOCALS:1 += "</font>"
		;PRINTFORMC %TRAIN_COMNAME(COUNT)%[{COUNT,3}]
		LOCAL:2 += 1
	ENDIF
	IF LOCAL:2 >= 4
		LOCAL:2 = 0
		HTML_PRINT LOCALS:1
		LOCALS:1 = 
	ENDIF
NEXT
IF LOCALS:1 != ""
	LOCAL:2 = 0
	HTML_PRINT LOCALS:1
	LOCALS:1 = 
ENDIF

TFLAG:ゲームフェイズ管理 = 0
PRINTL 
DRAWLINE
PRINTL [1000] - 決定　　　　　　　　　 [1001] - 通常コマンドフィルタリセット
PRINTL [1002] - コマンドフィルタの保存 [1003] - コマンドフィルタのロード
SETCOLOR 0x666666
PRINTPLAIN [1010] - SMフィルタ [1011] - Ｖフィルタ（未実装です）
RESETCOLOR
PRINTL
PRINTL [1020] - 固有コマンドフィルタへ
PRINTL [1021] - 派生コマンドフィルタへ
INPUT
IF RESULT == 1000
	RETURN 0
ELSEIF RESULT == 1001
	SAVESTR:0 = 
	RESTART
ELSEIF RESULT == 1002
	$INPUT_LOOP_SAVE
	LOADGLOBAL
	FOR LOCAL,0,3
		PRINTFORM [{LOCAL}] - 
		IF STRLENS(GLOBALS:(LOCAL * 2 + 1))
			PRINTFORML [%GLOBALS:(LOCAL * 2 + 1)%]
		ELSE
			PRINTL ----------
		ENDIF
	NEXT
	PRINTL [100] - 戻る
	INPUT
	LOCAL = RESULT
	IF RESULT == 100
		RESTART
	ELSEIF RESULT < 0 || RESULT > 2
		GOTO INPUT_LOOP_SAVE
	ENDIF
	SIF STRLENS(GLOBALS:(RESULT * 2 + 1))
		PRINTFORML 【%GLOBALS:(RESULT * 2 + 1)%】 このデータに上書きします
	PRINTL 通常コマンドフィルタの名前を登録してください
	INPUTS
	GLOBALS:(LOCAL * 2 + 1) = %RESULTS%
	GLOBALS:(LOCAL * 2) = %SAVESTR:0%
	PRINTFORMW 通常コマンドフィルタ【%GLOBALS:(LOCAL * 2 + 1)%】を登録しました
	SAVEGLOBAL
	RESTART
ELSEIF RESULT == 1003
	$INPUT_LOOP_LOAD
	LOADGLOBAL
	FOR LOCAL,0,3
		PRINTFORM [{LOCAL}] - 
		IF STRLENS(GLOBALS:(LOCAL * 2 + 1))
			PRINTFORML [%GLOBALS:(LOCAL * 2 + 1)%]
		ELSE
			PRINTL ----------
		ENDIF
	NEXT
	PRINTL [100] - 戻る
	INPUT
	IF RESULT == 100
		RESTART
	ELSEIF RESULT < 0 || RESULT > 2
		GOTO INPUT_LOOP_LOAD
	ENDIF
	PRINTFORMW 通常コマンドフィルタ【%GLOBALS:(RESULT * 2 + 1)%】を読み込みました
	SAVESTR:0 = %GLOBALS:(RESULT * 2)%
	RESTART
;各種フィルタ
;ELSEIF RESULT == 1010 || RESULT == 1011
;	FOR COUNT,0,500
;		;SM
;;		IF RESULT == 1010
;			SIF (COUNT >= 100 && COUNT < 120) || (COUNT >= 140 && COUNT < 160)
;				LOCAL:COUNT = 1
;		;V
;		ELSEIF RESULT == 1011
;			SIF COUNT == 3 || COUNT == 44 || COUNT == 60 || COUNT == 61 || COUNT == 65 || COUNT == 67 || COUNT == 68 || COUNT == 71 || COUNT == 122|| COUNT == 141 || COUNT == 143 || COUNT == 185
;				LOCAL:COUNT = 1
;		ENDIF
;	NEXT
ELSEIF RESULT == 1020
	$固有
	CALL OPTION_固有COMMAND_FILTER
	SIF RESULT == 999
		GOTO 派生
ELSEIF RESULT == 1021
	$派生
	CALL OPTION_派生COMMAND_FILTER
	SIF RESULT == 999
		GOTO 固有
ELSEIF STRLENS(TRAIN_COMNAME(RESULT))
	LOCAL:RESULT = !LOCAL:RESULT
ELSE
	RESTART
ENDIF
SAVESTR:0 = /
FOR COUNT,0,500
	SIF !LOCAL:COUNT
		CONTINUE
	LOCALS = /{COUNT}/
	SIF !STRCOUNT(SAVESTR:0,LOCALS)
		SAVESTR:0 = %SAVESTR:0%{COUNT}/
NEXT
RESTART


@OPTION_固有COMMAND_FILTER
#DIM COM番号
#DIM ターゲット保存
#DIM ターゲット保存２
#DIM フィルタ配列, 20, 2000
#DIMS RESULTS分割, 10

VARSET LOCAL
VARSET フィルタ配列
FOR COM番号, 370, 375
	FOR COUNT, 0, CHARANUM
		LOCALS = /{COM番号}_{COUNT}/
		SIF STRCOUNT(SAVESTR:1, LOCALS)
			フィルタ配列:(COM番号 - 370):COUNT = 1
	NEXT
NEXT
FOR COM番号, 380, 385
	FOR COUNT, 0, 2000
		LOCALS = /{COM番号}_{COUNT}/
		SIF STRCOUNT(SAVESTR:1, LOCALS)
			フィルタ配列:(COM番号 - 370):COUNT = 1
	NEXT
NEXT
;お祭りコマンドはフィルタ不可
SIF SAVESTR:1 == ""
	SAVESTR:1 = /
DRAWLINE
TFLAG:ゲームフェイズ管理 = 1

ターゲット保存 = TARGET
FOR COM番号, 370, 375
	FOR COUNT, 1, CHARANUM
		LOCALS = /{COM番号}_{COUNT}/
		TARGET = COUNT
		;固有コマンドのABLEがあれば当然COMNAMEもあるはずなのでTRYCCALLで判定
		TRYCCALLFORM COM_ABLE{COM番号}_{NO:TARGET}
			TSTR:ツールチップ受渡 = TRAIN_COMNAME(COM番号)
			IF STRCOUNT(SAVESTR:1,LOCALS)
				LOCALS:1 += @"<font color='#666666'><button value = '{COM番号}_{COUNT}'"
			ELSE
				LOCALS:1 += @"<button value = '{COM番号}_{COUNT}'"
			ENDIF
			TRYCCALLFORM COM_TOOLTIP_{COM番号}_{COUNT}
				IF ツールチップ切り替えOPTION記憶 == 0
					LOCALS:1 += @" title = '%TSTR:ツールチップ受渡%'>"
				ELSE
					LOCALS:1 += @">"
				ENDIF
			CATCH
				LOCALS:1 += @">"
			ENDCATCH
			LOCALS:1 += @"%CALLNAME:COUNT,20,RIGHT% - %TRAIN_COMNAME(COM番号), 22%[{COM番号}_{COUNT,3}]</button>"
			SIF STRCOUNT(SAVESTR:1,LOCALS)
				LOCALS:1 += "</font>"
			LOCAL:2 += 1
		CATCH
		ENDCATCH
		IF LOCAL:2 >= 2
			LOCAL:2 = 0
			HTML_PRINT LOCALS:1
			LOCALS:1 = 
		ENDIF
	NEXT
NEXT
IF LOCALS:1 != ""
	LOCAL:2 = 0
	HTML_PRINT LOCALS:1
	LOCALS:1 = 
ENDIF
TARGET = ターゲット保存
PRINTL
DRAWLINE
ターゲット保存 = CFLAG:MASTER:現在位置
ターゲット保存２ = CFLAG:MASTER:現在マップ種別
FOR COM番号, 380, 385
	FOR LOCAL:5, 0, 1
		CFLAG:MASTER:現在マップ種別 = LOCAL:5
		FOR COUNT, 1, 2000
			LOCALS = /{COM番号}_{LOCAL:5}_{COUNT}/
			CFLAG:MASTER:現在位置 = COUNT
			;マップ種別とりあえず入れておくだけ
			;固有コマンドのABLEがあれば当然COMNAMEもあるはずなのでTRYCCALLで判定
			TRYCCALLFORM COM_ABLE{COM番号}_{LOCAL:5}_{COUNT}
				IF TRAIN_COMNAME(COM番号) != ""
					TSTR:ツールチップ受渡 = TRAIN_COMNAME(COM番号)
					IF STRCOUNT(SAVESTR:1,LOCALS)
						LOCALS:1 += @"<font color='#666666'><button value = '{COM番号}_{LOCAL:5}_{COUNT}'"
					ELSE
						LOCALS:1 += @"<button value = '{COM番号}_{LOCAL:5}_{COUNT}'"
					ENDIF
					TRYCCALLFORM COM_TOOLTIP_{COM番号}_{LOCAL:5}_{COUNT}
						IF ツールチップ切り替えOPTION記憶 == 0
							LOCALS:1 += @" title = '%TSTR:ツールチップ受渡%'>"
						ELSE
							LOCALS:1 += @">"
						ENDIF
					CATCH
						LOCALS:1 += @">"
					ENDCATCH
					LOCALS:1 += @"%GETPLACENAME(CFLAG:PLAYER:現在マップ種別, CFLAG:MASTER:現在位置),20,RIGHT% - %TRAIN_COMNAME(COM番号),18,RIGHT%[{COM番号}_{LOCAL:5,2}_{COUNT,4}]</button>"
					SIF STRCOUNT(SAVESTR:1,LOCALS)
						LOCALS:1 += "</font>"
					LOCAL:2 += 1
				ENDIF
			CATCH
			ENDCATCH
			IF LOCAL:2 >= 2
				LOCAL:2 = 0
				HTML_PRINT LOCALS:1
				LOCALS:1 = 
			ENDIF
		NEXT
	NEXT
NEXT
IF LOCALS:1 != ""
	LOCAL:2 = 0
	HTML_PRINT LOCALS:1
	LOCALS:1 = 
ENDIF
CFLAG:MASTER:現在位置 = ターゲット保存
CFLAG:MASTER:現在マップ種別 = ターゲット保存２

TFLAG:ゲームフェイズ管理 = 0
PRINTL 
DRAWLINE

PRINTBUTTON "[1000] - 通常コマンドフィルタへ　　　", TOSTR(1000)
PRINTBUTTON "[1001] - 派生コマンドフィルタへ　　　", TOSTR(1001)
PRINTL
PRINTBUTTON "[1002] - 固有コマンドフィルタリセット", TOSTR(1002)
PRINTL
PRINTBUTTON "[1003] - 固有コマンドフィルタの保存　", TOSTR(1003)
PRINTBUTTON "[1004] - 固有コマンドフィルタのロード", TOSTR(1004)
PRINTL

INPUTS

SELECTCASE RESULTS
	CASE "1000"
		RETURN 0
	CASE "1001"
		RETURN 999
	CASE "1002"
		SAVESTR:1 = 
		RESTART
	CASE "1003"
		$INPUT_LOOP_SAVE
		LOADGLOBAL
		FOR LOCAL, 3, 6
			PRINTFORM [{LOCAL}] - 
			IF STRLENS(GLOBALS:(LOCAL * 2 + 1))
				PRINTFORML [%GLOBALS:(LOCAL * 2 + 1)%]
			ELSE
				PRINTL ----------
			ENDIF
		NEXT
		PRINTL [100] - 戻る
		INPUT
		LOCAL = RESULT
		IF LOCAL == 100
			RESTART
		ELSEIF LOCAL < 3 || LOCAL > 5
			GOTO INPUT_LOOP_SAVE
		ENDIF
		SIF STRLENS(GLOBALS:(RESULT * 2 + 1))
			PRINTFORML 【%GLOBALS:(LOCAL * 2 + 1)%】 このデータに上書きします
		PRINTL 固有コマンドフィルタの名前を登録してください
		INPUTS
		GLOBALS:(LOCAL * 2 + 1) = %RESULTS%
		GLOBALS:(LOCAL * 2) = %SAVESTR:1%
		PRINTFORMW 固有コマンドフィルタ【%GLOBALS:(LOCAL * 2 + 1)%】を登録しました
		SAVEGLOBAL
		RESTART
	CASE "1004"
		$INPUT_LOOP_LOAD
		LOADGLOBAL
		FOR LOCAL, 3, 6
			PRINTFORM [{LOCAL}] - 
			IF STRLENS(GLOBALS:(LOCAL * 2 + 1))
				PRINTFORML [%GLOBALS:(LOCAL * 2 + 1)%]
			ELSE
				PRINTL ----------
			ENDIF
		NEXT
		PRINTL [100] - 戻る
		INPUT
		IF RESULT == 100
			RESTART
		ELSEIF RESULT < 3 || RESULT > 5
			GOTO INPUT_LOOP_LOAD
		ENDIF
		PRINTFORMW 固有コマンドフィルタ【%GLOBALS:(RESULT * 2 + 1)%】を読み込みました
		SAVESTR:1 = %GLOBALS:(RESULT * 2)%
		RESTART
	CASEELSE
		SPLIT RESULTS, "_", RESULTS分割
		IF RESULT == 2
			IF TOINT(RESULTS分割:0) >= 370 && TOINT(RESULTS分割:0) <= 374
				フィルタ配列:(TOINT(RESULTS分割:0) - 370):(TOINT(RESULTS分割:1)) = !フィルタ配列:(TOINT(RESULTS分割:0) - 370):(TOINT(RESULTS分割:1))
			ELSEIF TOINT(RESULTS分割:0) >= 380 && TOINT(RESULTS分割:0) <= 384
				フィルタ配列:(TOINT(RESULTS分割:0) - 370):(TOINT(RESULTS分割:1)) = !フィルタ配列:(TOINT(RESULTS分割:0) - 370):(TOINT(RESULTS分割:1))
			ENDIF
		ELSE
			RESTART
		ENDIF
ENDSELECT

SAVESTR:1 = /
FOR COM番号, 370, 375
	FOR COUNT, 1, CHARANUM
		SIF !フィルタ配列:(COM番号 - 370):COUNT
			CONTINUE
		PRINTFORML {フィルタ配列:(COM番号 - 370):COUNT}
		LOCALS = /{COM番号}_{COUNT}/
		SIF !STRCOUNT(SAVESTR:1,LOCALS)
			SAVESTR:1 = %SAVESTR:1%{COM番号}_{COUNT}/
	NEXT
NEXT
FOR COM番号, 380, 385
	FOR COUNT, 1, 2000
		SIF !フィルタ配列:(COM番号 - 370):COUNT
			CONTINUE
		LOCALS = /{COM番号}_{COUNT}/
		SIF !STRCOUNT(SAVESTR:1,LOCALS)
			SAVESTR:1 = %SAVESTR:1%{COM番号}_{COUNT}/
	NEXT
NEXT

RESTART

$処理部分ラベル


@OPTION_派生COMMAND_FILTER
#DIM COM番号
#DIM ターゲット保存
#DIM フィルタ配列, 500, 50
#DIMS RESULTS分割, 10

VARSET LOCAL
VARSET フィルタ配列
FOR COM番号, 0, 500
	FOR COUNT, 1, 50
		LOCALS = /{COM番号}_{COUNT}/
		SIF STRCOUNT(SAVESTR:2, LOCALS)
			フィルタ配列:COM番号:COUNT = 1
	NEXT
NEXT
SIF SAVESTR:2 == ""
	SAVESTR:2 = /
DRAWLINE
TFLAG:ゲームフェイズ管理 = 1

FOR COM番号, 0, 500
	SIF COM番号 >= 370 && COM番号 <=390
		CONTINUE
	FOR COUNT, 1, 50
		LOCALS = /{COM番号}_{COUNT}/
		LOCALS:1 = {COM番号}_{COUNT}
		SIF STRCOUNT(SAVESTR:2,LOCALS)
			SETCOLOR 0x666666
		;派生コマンドのABLEがあれば当然COMNAMEもあるはずなのでTRYCCALLで判定
		TRYCCALLFORM COM_ABLE_S%LOCALS:1%
			IF TRAIN_COMNAME_S(LOCALS:1) != ""
				TSTR:ツールチップ受渡 = TRAIN_COMNAME_S(LOCALS:1)
				IF STRCOUNT(SAVESTR:2,LOCALS)
					LOCALS:2 += @"<font color='#666666'><button value = '%LOCALS:1%'"
				ELSE
					LOCALS:2 += @"<button value = '%LOCALS:1%'"
				ENDIF
				TRYCCALLFORM COM_TOOLTIP_S{COM番号}_{COUNT}
					IF ツールチップ切り替えOPTION記憶 == 0
						LOCALS:2 += @" title = '%TSTR:ツールチップ受渡%'>"
					ELSE
						LOCALS:2 += @">"
					ENDIF
				CATCH
					LOCALS:2 += @">"
				ENDCATCH
				LOCALS:2 += @"%TRAIN_COMNAME(COM番号), 20, RIGHT%派生 - %TRAIN_COMNAME_S(LOCALS:1), 20, LEFT%[{COM番号}_{COUNT,2}]</button>"
				SIF STRCOUNT(SAVESTR:2,LOCALS)
					LOCALS:2 += "</font>"
				LOCAL:2 += 1
				;PRINTBUTTON @"%TRAIN_COMNAME(COM番号), 20, RIGHT%派生 - %TRAIN_COMNAME_S(LOCALS:1), 20, LEFT%[{COM番号}_{COUNT,2}]", @"%LOCALS:1%"
			ENDIF
		CATCH
		ENDCATCH
		IF LOCAL:2 >= 2
			LOCAL:2 = 0
			HTML_PRINT LOCALS:2
			LOCALS:2 = 
		ENDIF
	NEXT
NEXT
IF LOCALS:2 != ""
	LOCAL:2 = 0
	HTML_PRINT LOCALS:2
	LOCALS:2 = 
ENDIF

TFLAG:ゲームフェイズ管理 = 0
PRINTL 
DRAWLINE

PRINTBUTTON "[1000] - 通常コマンドフィルタへ　　　", TOSTR(1000)
PRINTBUTTON "[1001] - 固有コマンドフィルタへ　　　", TOSTR(1001)
PRINTL
PRINTBUTTON "[1002] - 派生コマンドフィルタリセット", TOSTR(1002)
PRINTL
PRINTBUTTON "[1003] - 派生コマンドフィルタの保存　", TOSTR(1003)
PRINTBUTTON "[1004] - 派生コマンドフィルタのロード", TOSTR(1004)
PRINTL

INPUTS

SELECTCASE RESULTS
	CASE "1000"
		RETURN 0
	CASE "1001"
		RETURN 999
	CASE "1002"
		SAVESTR:2 = 
		RESTART
	CASE "1003"
		$INPUT_LOOP_SAVE
		LOADGLOBAL
		FOR LOCAL, 6, 9
			PRINTFORM [{LOCAL}] - 
			IF STRLENS(GLOBALS:(LOCAL * 2 + 1))
				PRINTFORML [%GLOBALS:(LOCAL * 2 + 1)%]
			ELSE
				PRINTL ----------
			ENDIF
		NEXT
		PRINTL [100] - 戻る
		INPUT
		LOCAL = RESULT
		IF LOCAL == 100
			RESTART
		ELSEIF LOCAL < 6 || LOCAL > 8
			GOTO INPUT_LOOP_SAVE
		ENDIF
		SIF STRLENS(GLOBALS:(LOCAL * 2 + 1))
			PRINTFORML 【%GLOBALS:(LOCAL * 2 + 1)%】 このデータに上書きします
		PRINTL 派生コマンドフィルタの名前を登録してください
		INPUTS
		GLOBALS:(LOCAL * 2 + 1) = %RESULTS%
		GLOBALS:(LOCAL * 2) = %SAVESTR:2%
		PRINTFORMW 派生コマンドフィルタ【%GLOBALS:(LOCAL * 2 + 1)%】を登録しました
		SAVEGLOBAL
		RESTART
	CASE "1004"
		$INPUT_LOOP_LOAD
		LOADGLOBAL
		FOR LOCAL, 6, 9
			PRINTFORM [{LOCAL}] - 
			IF STRLENS(GLOBALS:(LOCAL * 2 + 1))
				PRINTFORML [%GLOBALS:(LOCAL * 2 + 1)%]
			ELSE
				PRINTL ----------
			ENDIF
		NEXT
		PRINTL [100] - 戻る
		INPUT
		IF RESULT == 100
			RESTART
		ELSEIF RESULT < 6 || RESULT > 8
			GOTO INPUT_LOOP_LOAD
		ENDIF
		PRINTFORMW 派生コマンドフィルタ【%GLOBALS:(RESULT * 2 + 1)%】を読み込みました
		SAVESTR:2 = %GLOBALS:(RESULT * 2)%
		RESTART
	CASEELSE
		SPLIT RESULTS, "_", RESULTS分割
		IF RESULT == 2
			フィルタ配列:(TOINT(RESULTS分割:0)):(TOINT(RESULTS分割:1)) = !フィルタ配列:(TOINT(RESULTS分割:0)):(TOINT(RESULTS分割:1))
		ELSE
			RESTART
		ENDIF
ENDSELECT

SAVESTR:2 = /
FOR COM番号, 0, 500
	FOR COUNT, 1, 50
		SIF !フィルタ配列:COM番号:COUNT
			CONTINUE
		PRINTFORML {フィルタ配列:COM番号:COUNT}
		LOCALS = /{COM番号}_{COUNT}/
		SIF !STRCOUNT(SAVESTR:2,LOCALS)
			SAVESTR:2 = %SAVESTR:2%{COM番号}_{COUNT}/
	NEXT
NEXT

RESTART

$処理部分ラベル


@OPTION_素質切り替え
DRAWLINE
PRINTL すべてのキャラクターのうち、特定の素質を別の素質で置き換えます。
PRINTL （※一度置き換えた後は戻すことが出来ません）
DRAWLINE
IF 素質切り替えOPTION記憶:ふたなり_女性
	PRINTBUTTON "[0][ふたなり]を[女性]に変更する（適用済み）", 0
ELSE
	PRINTBUTTON "[0][ふたなり]を[女性]に変更する", 0
ENDIF
PRINTL
IF 素質切り替えOPTION記憶:陥没乳首_なし
	PRINTBUTTON "[1][陥没乳首]を[なし]に変更する（適用済み）", 1
ELSE
	PRINTBUTTON "[1][陥没乳首]を[なし]に変更する", 1
ENDIF
PRINTL
IF 素質切り替えOPTION記憶:母乳体質_なし && 素質切り替えOPTION記憶:母乳体質_発情期_なし
	PRINTBUTTON "[2][母乳体質]を[なし]に変更する（適用済み）", 2
ELSE
	PRINTBUTTON "[2][母乳体質]を[なし]に変更する", 2
ENDIF
PRINTL
PRINTBUTTON "[999]戻る", 999
PRINTL
DRAWLINE

$INPUT_LOOP
BINPUT

SELECTCASE RESULT
	CASE 0
		IF 素質切り替えOPTION記憶:ふたなり_女性
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		PRINTL [ふたなり]を[女性]に変更します。変更後は元に戻すことは出来ません。
		PRINTL よろしいですか？
		PRINTBUTTON "[0]今後追加されるキャラも含めて変更する", 0
		PRINTL
		PRINTBUTTON "[1]今存在するキャラのみ変更する", 1
		PRINTL
		PRINTBUTTON "[2]関連する経験（射精経験）を今いるキャラから消す", 2
		PRINTL
		PRINTBUTTON "[10]変更しない", 10
		PRINTL
		$INPUT_LOOP2
		INPUT
		SELECTCASE RESULT
			CASE 0
				FOR LOCAL, 1, CHARANUM
					IF TALENT:LOCAL:性別 == 3
						TALENT:LOCAL:性別 = 1
					ENDIF
				NEXT
				素質切り替えOPTION記憶:ふたなり_女性 = 1
				PRINTW 変更が終了しました。
				RESTART
			CASE 1
				FOR LOCAL, 1, CHARANUM
					IF TALENT:LOCAL:性別 == 3
						TALENT:LOCAL:性別 = 1
					ENDIF
				NEXT
				PRINTW 変更が終了しました。
				RESTART
			CASE 2
				FOR LOCAL, 1, CHARANUM
					IF EXP:LOCAL:射精経験
						EXP:LOCAL:射精経験 = 0
					ENDIF
				NEXT
				PRINTW 消去が終了しました。
				RESTART
			CASE 10
				RESTART
			CASEELSE
				REUSELASTLINE 
				GOTO INPUT_LOOP2
		ENDSELECT
	CASE 1
		IF 素質切り替えOPTION記憶:陥没乳首_なし
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		PRINTL [陥没乳首]を[なし]に変更します。変更後は元に戻すことは出来ません。
		PRINTL よろしいですか？
		PRINTBUTTON "[0]今後追加されるキャラも含めて変更する", 0
		PRINTL
		PRINTBUTTON "[1]今存在するキャラのみ変更する", 1
		PRINTL
		PRINTBUTTON "[10]変更しない", 10
		PRINTL
		$INPUT_LOOP3
		INPUT
		SELECTCASE RESULT
			CASE 0
				FOR LOCAL, 1, CHARANUM
					IF TALENT:LOCAL:陥没乳首
						TALENT:LOCAL:陥没乳首 = 0
					ENDIF
				NEXT
				素質切り替えOPTION記憶:陥没乳首_なし = 1
				PRINTW 変更が終了しました。
				RESTART
			CASE 1
				FOR LOCAL, 1, CHARANUM
					IF TALENT:LOCAL:陥没乳首
						TALENT:LOCAL:陥没乳首 = 0
					ENDIF
				NEXT
				PRINTW 変更が終了しました。
				RESTART
			CASE 10
				RESTART
			CASEELSE
				REUSELASTLINE 
				GOTO INPUT_LOOP3
		ENDSELECT
	CASE 2
		IF 素質切り替えOPTION記憶:母乳体質_なし && 素質切り替えOPTION記憶:母乳体質_発情期_なし
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		PRINTL [母乳体質]を[なし]に変更します。変更後は元に戻すことは出来ません。
		PRINTL よろしいですか？
		IF 素質切り替えOPTION記憶:母乳体質_なし
			SETCOLOR 0x666666
			PRINTPLAIN [0]今後追加されるキャラも含めて変更する（適用済み）
			PRINTL
			PRINTPLAIN [1]今存在するキャラのみ変更する（適用済み）
			PRINTL
			RESETCOLOR
		ELSE
			PRINTBUTTON "[0]今後追加されるキャラも含めて変更する", 0
			PRINTL
			PRINTBUTTON "[1]今存在するキャラのみ変更する", 1
			PRINTL
		ENDIF
		IF 素質切り替えOPTION記憶:母乳体質_発情期_なし
			SETCOLOR 0x666666
			PRINTPLAIN [2]ドラフの[母乳体質(発情期)]を消す（適用済み）
			RESETCOLOR
		ELSE
			PRINTBUTTON "[2]ドラフの[母乳体質(発情期)]を消す", 2
		ENDIF
		PRINTBUTTON "[3]関連する経験（噴乳経験）を今いるキャラから消す", 3
		PRINTL
		PRINTBUTTON "[10]変更しない", 10
		PRINTL
		$INPUT_LOOP4
		INPUT
		SELECTCASE RESULT
			CASE 0
				IF 素質切り替えOPTION記憶:母乳体質_なし
					REUSELASTLINE 
					GOTO INPUT_LOOP4
				ENDIF
				FOR LOCAL, 1, CHARANUM
					IF TALENT:LOCAL:母乳体質 == 1
						TALENT:LOCAL:母乳体質 = 0
					ENDIF
				NEXT
				素質切り替えOPTION記憶:母乳体質_なし = 1
				PRINTW 変更が終了しました。
				RESTART
			CASE 1
				IF 素質切り替えOPTION記憶:母乳体質_なし
					REUSELASTLINE 
					GOTO INPUT_LOOP4
				ENDIF
				FOR LOCAL, 1, CHARANUM
					IF TALENT:LOCAL:母乳体質
						TALENT:LOCAL:母乳体質 = 0
					ENDIF
				NEXT
				PRINTW 変更が終了しました。
				RESTART
			CASE 2
				IF 素質切り替えOPTION記憶:母乳体質_発情期_なし
					REUSELASTLINE 
					GOTO INPUT_LOOP4
				ENDIF
				FOR LOCAL, 1, CHARANUM
					IF TALENT:LOCAL:母乳体質 == 2
						TALENT:LOCAL:母乳体質 = 0
					ENDIF
				NEXT
				素質切り替えOPTION記憶:母乳体質_発情期_なし = 1
				PRINTW 変更が終了しました。
				RESTART
			CASE 3
				FOR LOCAL, 1, CHARANUM
					IF EXP:LOCAL:噴乳経験
						EXP:LOCAL:噴乳経験 = 0
					ENDIF
				NEXT
				PRINTW 消去が終了しました。
				RESTART
			CASE 10
				RESTART
			CASEELSE
				REUSELASTLINE 
				GOTO INPUT_LOOP4
		ENDSELECT
	CASE 999
		RETURN
ENDSELECT

; @OPTION_機能切り替え
; #DIM L_CNT
; L_CNT = LINECOUNT
; DRAWLINE
; PRINTL ・ツールチップ切り替え
; PRINTL 特定の場所にマウスカーソルを合わせた際にポップアップ表示されるツールチップの表示を切り替えます。
; PRINTL （※本来得られるべき情報が得られなくなるため、通常時はONを強く推奨）
; PRINTL 
; PRINTL ・発情期切り替え
; PRINTL エルーンやドラフ、あるいは特定キャラに訪れる発情期のON/OFFを切り替えます。
; PRINTL 
; PRINTL ・妊娠切り替え
; PRINTL セックスによって妊娠する機能のON/OFFを切り替えます。
; PRINTL 
; PRINTL ・画像サイズ切り替え
; PRINTL 能力表示画面、及び通常時に表示されるキャラクターの画像サイズを切り替えます。
; PRINTL また、規定と異なるアスペクト比の画像の処理を切り替えます。
; DRAWLINE
; PRINTL ・ツールチップ切り替え
; IF ツールチップ切り替えOPTION記憶 == 0
; 	PRINTBUTTON "　┗[0]  ツールチップをOFFにする（現在ON）", 0
; ELSEIF ツールチップ切り替えOPTION記憶
; 	PRINTBUTTON "　┗[0]  ツールチップをONにする（現在OFF）", 0
; ENDIF
; PRINTL
; PRINTL ・発情期切り替え
; IF 発情期切り替えOPTION記憶 == 0
; 	PRINTBUTTON "　┗[1]  発情期をOFFにする（現在ON）", 1
; ELSEIF 発情期切り替えOPTION記憶
; 	PRINTBUTTON "　┗[1]  発情期をONにする（現在OFF）", 1
; ENDIF
; PRINTL
; PRINTL ・妊娠切り替え
; IF 妊娠切り替えOPTION記憶 == 0
; 	PRINTBUTTON "　┗[2]  妊娠機能をOFFにする（現在ON）", 2
; ELSEIF 妊娠切り替えOPTION記憶
; 	PRINTBUTTON "　┗[2]  妊娠機能をONにする（現在OFF）", 2
; ENDIF
; PRINTL
; PRINTL
; PRINTL ・画像サイズ切り替え
; PRINTFORML 　画像サイズを表示箇所ごとに正方形サイズか短冊サイズ（縦２横１）のどちらかに設定できます。
; ;PRINTBUTTONLC @"　┣[3]  コマンドリスト（現在：\@ GETBIT(画像サイズ切り替えOPTION記憶, 0)? 短冊　# 正方形\@）　", 3
; LOCALS '= "　┣[3]  コマンドリスト（現在："
; IF GETBIT(画像サイズ切り替えOPTION記憶, 0)
; 	LOCALS += "短冊　）　　　　　　　　　　　　　"
; ELSEIF GETBIT(画像サイズ切り替えOPTION記憶, 4)
; 	LOCALS += "正方形（3P時に縦に並べる））　　　"
; ELSE
; 	LOCALS += "正方形）　　　　　　　　　　　　　"
; ENDIF
; PRINTBUTTONLC LOCALS, 3
; PRINTBUTTONLC @"　[4] 通常能力表示　（現在：\@ GETBIT(画像サイズ切り替えOPTION記憶, 1)? 短冊　# 正方形\@）", 4
; PRINTL
; PRINTBUTTONLC @"　┣[5]  性的能力表示　（現在：\@ GETBIT(画像サイズ切り替えOPTION記憶, 2)? 短冊　# 正方形\@）", 5
; PRINTLC  
; PRINTBUTTONLC @"　[6] 戦闘能力表示　（現在：\@ GETBIT(画像サイズ切り替えOPTION記憶, 3)? 短冊　# 正方形\@）", 6
; PRINTL
; PRINTBUTTONLC @"　┗[7]  一括で変更する", 7
; PRINTL
; PRINTL ・アスペクト比不適の画像の処理の切り替え
; IF 画像サイズ切り替えOPTION記憶:1 == 0
; 	PRINTBUTTON "　┗[8]  画像全体を表示するよう拡大縮小を行う（現在：切り取り式処理）", 8
; ELSE
; 	PRINTBUTTON "　┗[8]  表示領域に合わせて画像を切り取る（現在：拡大縮小処理）", 8
; ENDIF
; PRINTL
; PRINTL
; PRINTFORML ・性行為の挿絵/アニメーション設定
; PRINTFORML 　\@ FLAG_挿絵表示 ? ┣ # ┗ \@[9] 性行為の挿絵を表示する （現在：\@ FLAG_挿絵表示 ? ON # OFF \@）
; IF FLAG_挿絵表示
; 	PRINT 　┣[10]  射精画像の種類 （現在：
; 	SELECTCASE FLAG_射精画像種類
; 		CASE 0
; 			PRINTL [断面＋効果音]）
; 		CASE 1
; 			PRINTL [断面のみ]）
; 		CASE 2
; 			PRINTL [効果音のみ]）
; 	ENDSELECT
; 	PRINTFORML 　\@ FLAG_アニメーション ? ┣ # ┗ \@[11] アニメーションさせる （現在：\@ FLAG_アニメーション > 0 ? 繰り返し回数 {FLAG_アニメーション}回 # OFF \@）
; 	IF FLAG_アニメーション
; 		PRINTFORML 　┗[12] 挿入中は毎ターンアニメ表示 （現在：\@FLAG_挿入アニメフィルタ ? OFF# ON\@）
; 	ELSE
; 		PRINTL 
; 	ENDIF
; ELSE
; 	PRINTL 
; ENDIF
; PRINTL
; PRINTBUTTON "[999]戻る", 999
; PRINTL
; DRAWLINE

; $INPUT_LOOP
; INPUT

; SELECTCASE RESULT
; 	CASE 0
; 		INVERTBIT ツールチップ切り替えOPTION記憶, 0
; 	CASE 1
; 		INVERTBIT 発情期切り替えOPTION記憶, 0
; 	CASE 2
; 		INVERTBIT 妊娠切り替えOPTION記憶, 0
; 	CASE 3
; 		IF GETBIT(画像サイズ切り替えOPTION記憶, 0)
; 			INVERTBIT 画像サイズ切り替えOPTION記憶, 0
; 			SETBIT 画像サイズ切り替えOPTION記憶, 4
; 		ELSEIF GETBIT(画像サイズ切り替えOPTION記憶, 4) 
; 			CLEARBIT 画像サイズ切り替えOPTION記憶, 4
; 		ELSE
; 			INVERTBIT 画像サイズ切り替えOPTION記憶, 0
; 		ENDIF
; 	CASE 4 to 6
; 		;↑ to の数字変えた場合、↓のRESULTから引く数字も同じ値にしてください
; 		INVERTBIT 画像サイズ切り替えOPTION記憶, (RESULT - 3)
; 	CASE 7
; 		INVERTBIT 画像サイズ切り替えOPTION記憶, 5
; 		IF GETBIT(画像サイズ切り替えOPTION記憶, 5)
; 			画像サイズ切り替えOPTION記憶 = 0101111
; 		ELSE
; 			画像サイズ切り替えOPTION記憶 = 0
; 		ENDIF
; 	CASE 8
; 		INVERTBIT 画像サイズ切り替えOPTION記憶:1, 0
; 	CASE 999
; 		RETURN
; 	CASE 9
; 		INVERTBIT FLAG_挿絵表示, 0
; 	CASE 10
; 		FLAG_射精画像種類++
; 		SIF FLAG_射精画像種類 >= 3
; 			FLAG_射精画像種類 = 0
; 	CASE 11
; 		FLAG_アニメーション++
; 		SIF FLAG_アニメーション >= 6
; 			FLAG_アニメーション = 0
; 	CASE 12
; 		INVERTBIT FLAG_挿入アニメフィルタ, 0
; [IF_DEBUG]
; 	CASE 9998
; 		CALL 受精アニメ(0)
; 		WAIT
; 	CASE 9999
; 		CALL 受精アニメ(1)
; 		WAIT
; 	CASEELSE
; 		REUSELASTLINE 
; 		GOTO INPUT_LOOP
; [ENDIF]
; ENDSELECT
; CLEARLINE LINECOUNT - L_CNT
; RESTART




@OPTION_機能切り替え
#DIM 画面表示用変数
#DIM 対象キャラ

DRAWLINE
PRINTL 機能関連のオプション画面です
PRINTL ※開発中のため、正しく挙動しないことがあります。
DRAWLINE
LOCALS = <div rect='81,31,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='2050,31,4000,2812' border='31' bcolor='#C0C0C0'></div>
LOCALS += "<div rect='156,156,1937,2812'>"
LOCALS += "<button value='100'>[100] ツールチップ切り替え</button>"
LOCALS += "<br>"
LOCALS += "<button value='200'>[200] 発情期切り替え</button>"
LOCALS += "<br>"
LOCALS += "<button value='300'>[300] 妊娠切り替え</button>"
LOCALS += "<br>"
LOCALS += "<button value='400'>[400] 画像サイズ切り替え</button>"
LOCALS += "<br>"
LOCALS += "<button value='500'>[500] アスペクト比不適の画像の処理</button>"
LOCALS += "<br>"
LOCALS += "<button value='600'>[600] 性行為の挿絵/アニメーション</button>"
LOCALS += "<br>"
LOCALS += "<button value='700'>[700] 汎用喘ぎ機能切り替え</button>"
LOCALS += "<br>"
LOCALS += "<button value='800'>[800] 身体項目表示切り替え</button>"
LOCALS += "<br>"
LOCALS += "<button value='900'>[900] その他機能切り替え</button>"
LOCALS += "<br>"
LOCALS += "<button value='1000'>[1000] オートコマンド可否切り替え</button>"
LOCALS += "<br>"
LOCALS += "<button value='1100'>[1100] カットイン表示切り替え</button>"
LOCALS += "<br>"
LOCALS += "<br>"
LOCALS += "<button value='999'>[999] 戻る</button>"
LOCALS += "<br>"
LOCALS += "</div>"

LOCALS += "<div rect='2125,156,3875,2812'>"
SELECTCASE 画面表示用変数
	CASE 100
		LOCALS += "・ツールチップ切り替え<br>"
		LOCALS += "特定の場所にマウスカーソルを合わせた際にポップアップ表示されるツールチップの表示を切り替えます。<br>"
		LOCALS += "（※本来得られるべき情報が得られなくなるため、通常時はONを強く推奨）<br>"
		LOCALS += "<br>"
		IF ツールチップ切り替えOPTION記憶 == 0
			LOCALS += "　<button value='101'>[101] ツールチップをOFFにする（現在ON）</button>"
		ELSEIF ツールチップ切り替えOPTION記憶
			LOCALS += "　<button value='101'>[101] ツールチップをONにする（現在OFF）</button>"
		ENDIF
	CASE 200
		LOCALS += "・発情期切り替え<br>"
		LOCALS += "エルーンやドラフ、あるいは特定キャラに訪れる発情期のON/OFFを切り替えます。<br>"
		LOCALS += "<br>"
		;発情期切り替えOPTION記憶 BIT管理 0:エルーン女性 1:エルーン男性 2:ドラフ女性 3:ドラフ男性 4:発情あり女性 5:発情あり男性
		LOCALS += "　<button value='201'>[201] 全体の発情期をOFFにする</button><br>"
		LOCALS += "　<button value='202'>[202] 全体の発情期をONにする</button><br><br>"
		LOCALS += "　<button value='203'>[203] エルーン女性（ふたなり）の発情期を切り替え：現在"
		IF GETBIT(発情期切り替えOPTION記憶, 0) == 0
			LOCALS += "ON</button><br>"
		ELSE
			LOCALS += "OFF</button><br>"
		ENDIF
		LOCALS += "　<button value='204'>[204] エルーン男性の発情期を切り替え：現在"
		IF GETBIT(発情期切り替えOPTION記憶, 1) == 0
			LOCALS += "ON</button><br>"
		ELSE
			LOCALS += "OFF</button><br>"
		ENDIF
		LOCALS += "　<button value='205'>[205] ドラフ女性（ふたなり）の発情期を切り替え：現在"
		IF GETBIT(発情期切り替えOPTION記憶, 2) == 0
			LOCALS += "ON</button><br>"
		ELSE
			LOCALS += "OFF</button><br>"
		ENDIF
		LOCALS += "　<button value='206'>[206] ドラフ男性の発情期を切り替え：現在"
		IF GETBIT(発情期切り替えOPTION記憶, 3) == 0
			LOCALS += "ON</button><br>"
		ELSE
			LOCALS += "OFF</button><br>"
		ENDIF
		LOCALS += "　<button value='207'>[207] 発情あり素質を持つ女性（ふたなり）の発情期を切り替え：現在"
		IF GETBIT(発情期切り替えOPTION記憶, 4) == 0
			LOCALS += "ON</button><br>"
		ELSE
			LOCALS += "OFF</button><br>"
		ENDIF
		LOCALS += "　<button value='208'>[208] 発情あり素質を持つ男性の発情期を切り替え：現在"
		IF GETBIT(発情期切り替えOPTION記憶, 5) == 0
			LOCALS += "ON</button><br>"
		ELSE
			LOCALS += "OFF</button><br>"
		ENDIF
	CASE 300
		LOCALS += "・妊娠切り替え<br>"
		LOCALS += "セックスによって妊娠する機能のON/OFFを切り替えます。<br>"
		LOCALS += "<br>"
		IF 妊娠切り替えOPTION記憶 == 0
			LOCALS += "　<button value='301'>[301] 妊娠機能をOFFにする（現在ON）</button>"
		ELSEIF 妊娠切り替えOPTION記憶
			LOCALS += "　<button value='301'>[301] 妊娠機能をONにする（現在OFF）</button>"
		ENDIF
	CASE 400
		LOCALS += "・画像サイズ切り替え<br>"
		LOCALS += "能力表示画面、及び通常時に表示されるキャラクターの画像サイズを切り替えます。<br>"
		LOCALS += "表示箇所ごとに正方形サイズか短冊サイズ（縦２横１）のどちらかに設定できます。<br>"
		LOCALS += "<br>"
		LOCALS += "　コマンドリスト画面（現在："
		IF GETBIT(画像サイズ切り替えOPTION記憶, 0)
			LOCALS += "短冊）<br>"
			LOCALS += "　　　[短冊にする] / <font color='#666666'><button value='402'>[正方形（3P時縦）にする]</button></font> / <font color='#666666'><button value='403'>[正方形にする]</button></font><br>"
		ELSEIF GETBIT(画像サイズ切り替えOPTION記憶, 4)
			LOCALS += "正方形（3P時に縦に並べる））<br>"
			LOCALS += "　　　<font color='#666666'><button value='401'>[短冊にする]</button></font> / [正方形（3P時縦）にする] / <font color='#666666'><button value='403'>[正方形にする]</button></font><br>"
		ELSE
			LOCALS += "正方形）<br>"
			LOCALS += "　　　<font color='#666666'><button value='401'>[短冊にする]</button></font> / <font color='#666666'><button value='402'>[正方形（3P時縦）にする]</button></font> / [正方形にする]<br>"
		ENDIF
		LOCALS += @"　<button value='404'>[404] 通常能力表示　（現在：\@ GETBIT(画像サイズ切り替えOPTION記憶, 1)? 短冊　# 正方形\@）</button><br>"
		LOCALS += @"　<button value='405'>[405] 性的能力表示　（現在：\@ GETBIT(画像サイズ切り替えOPTION記憶, 2)? 短冊　# 正方形\@）</button><br>"
		LOCALS += @"　<button value='406'>[406] 戦闘能力表示　（現在：\@ GETBIT(画像サイズ切り替えOPTION記憶, 3)? 短冊　# 正方形\@）</button><br>"
		LOCALS += @"　<button value='407'>[407] 一括で変更する</button><br>"
	CASE 500
		LOCALS += "・アスペクト比不適の画像の処理の切り替え<br>"
		LOCALS += "規定と異なるアスペクト比の画像の処理を切り替えます。<br>"
		LOCALS += "規定のアスペクト比は正方形は縦１：横１、短冊は縦２：横１です。<br>"
		LOCALS += "<br>"
		IF 画像サイズ切り替えOPTION記憶:1 == 0
			LOCALS += "　<button value='501'>[501]  画像全体を表示するよう拡大縮小を行う（現在：切り取り式処理）</button><br>"
		ELSE
			LOCALS += "　<button value='501'>[501]  表示領域に合わせて画像を切り取る（現在：拡大縮小処理）</button><br>"
		ENDIF
	CASE 600
		LOCALS += "・性行為の挿絵/アニメーション設定<br>"
		LOCALS += "性行為時に表示される挿絵やアニメーションの設定を変更します。<br>"
		LOCALS += "<br>"
		LOCALS += @"　<button value='601'>[601] 性行為の挿絵を表示する （現在：\@ FLAG_挿絵表示 ? ON # OFF \@）</button><br>"
		IF FLAG_挿絵表示
			LOCALS += "---------------------------------------------------------------------<br>"
			LOCALS += "　射精画像の種類 （現在："
			SELECTCASE FLAG_射精画像種類
				CASE 0
					LOCALS += "[断面＋効果音]）<br>"
					LOCALS += "　　　[断面＋効果音にする] / <font color='#666666'><button value='603'>[断面のみにする]</button></font> / <font color='#666666'><button value='604'>[効果音のみにする]</button></font><br>"
				CASE 1
					LOCALS += "[断面のみ]）<br>"
					LOCALS += "　　　<font color='#666666'><button value='602'>[断面＋効果音にする]</button></font> / [断面のみにする] / <font color='#666666'><button value='604'>[効果音のみにする]</button></font><br>"
				CASE 2
					LOCALS += "[効果音のみ]）<br>"
					LOCALS += "　　　<font color='#666666'><button value='602'>[断面＋効果音にする]</button></font> / <font color='#666666'><button value='603'>[断面のみにする]</button></font> / [効果音のみにする]<br>"
			ENDSELECT
			LOCALS += "<br>"
			LOCALS += @"　アニメーション繰り返し設定<br> （現在：\@ FLAG_アニメーション > 0 ? 繰り返し回数 {FLAG_アニメーション}回 # アニメーションOFF \@）<br>"
			SELECTCASE FLAG_アニメーション
				CASE 0
					LOCALS += "　　　[アニメOFFにする]"
					FOR LOCAL, 1, 6
						LOCALS += @" / <font color='#666666'><button value='{605 + LOCAL}'>[{LOCAL}回]</button></font>"
					NEXT
					LOCALS += "<br>"
				CASE 1 TO 5
					LOCALS += "　　　<font color='#666666'><button value='605'>[アニメOFFにする]</button></font>"
					FOR LOCAL, 1, 6
						IF FLAG_アニメーション == LOCAL
							LOCALS += @" / [{LOCAL}回]"
						ELSE
							LOCALS += @" / <font color='#666666'><button value='{605 + LOCAL}'>[{LOCAL}回]</button></font>"
						ENDIF
					NEXT
					LOCALS += "<br>"
			ENDSELECT
			IF FLAG_アニメーション
				LOCALS += "<br>"
				LOCALS += @"　　　<button value='611'>[611] 挿入中は毎ターンアニメ表示 （現在：\@FLAG_挿入アニメフィルタ ? OFF# ON\@）</button>"
			ENDIF
		ENDIF
	CASE 700
		LOCALS += "・汎用喘ぎ切り替え<br>"
		LOCALS += "快楽を得た時に表示される汎用喘ぎの設定を変更します。<br>"
		LOCALS += "表示されるタイミングはコマンド地の文の後、口上の前です。<br>"
		LOCALS += "<br>"
		LOCALS += "※能力表示画面から設定出来る、キャラ個別の汎用喘ぎ使用可不可設定の方が優先されます。<br>"
		LOCALS += "<br>"
		LOCALS += "・女性（ふたなり・その他）キャラ汎用喘ぎ（現在："
		SELECTCASE 汎用喘ぎ使用OPTION
			CASE 0
				LOCALS += "使用しない）<br>"
				LOCALS += "　　　[使用しない] / <font color='#666666'><button value='702'>[口上無しキャラのみ使用]</button></font> / <font color='#666666'><button value='703'>[全てのキャラに使用]</button></font><br>"
			CASE 1
				LOCALS += "口上が無いキャラのみ使用）<br>"
				LOCALS += "　　　<font color='#666666'><button value='701'>[使用しない]</button></font> / [口上無しキャラのみ使用] / <font color='#666666'><button value='703'>[全てのキャラに使用]</button></font><br>"
			CASE 3
				LOCALS += "全てのキャラに使用）<br>"
				LOCALS += "　　　<font color='#666666'><button value='701'>[使用しない]</button></font> / <font color='#666666'><button value='702'>[口上無しキャラのみ使用]</button></font> / [全てのキャラに使用]<br>"
		ENDSELECT
		LOCALS += "<br>"
		LOCALS += "・男性キャラ汎用喘ぎ（現在："
		SELECTCASE 汎用喘ぎ使用OPTION_男
			CASE 0
				LOCALS += "使用しない）<br>"
				LOCALS += "　　　[使用しない] / <font color='#666666'><button value='705'>[口上無しキャラのみ使用]</button></font> / <font color='#666666'><button value='706'>[全てのキャラに使用]</button></font><br>"
			CASE 1
				LOCALS += "口上が無いキャラのみ使用）<br>"
				LOCALS += "　　　<font color='#666666'><button value='704'>[使用しない]</button></font> / [口上無しキャラのみ使用] / <font color='#666666'><button value='706'>[全てのキャラに使用]</button></font><br>"
			CASE 3
				LOCALS += "全てのキャラに使用）<br>"
				LOCALS += "　　　<font color='#666666'><button value='704'>[使用しない]</button></font> / <font color='#666666'><button value='705'>[口上無しキャラのみ使用]</button></font> / [全てのキャラに使用]<br>"
		ENDSELECT
	CASE 800
		LOCALS += "・身体項目表示切り替え<br>"
		LOCALS += "身長・バストサイズ・体型などの表示を切り替えます。<br>"
		LOCALS += "<br>"
		LOCALS += @"　身長の数値　　（現在：\@ GETBIT(身体項目表示切り替えOPTION, 0)? 表示する　# 表示しない\@ / <font color='#666666'><button value='801'>[801] \@ GETBIT(身体項目表示切り替えOPTION, 0)? 表示しない# 表示する　\@</button></font>）<br>"
		LOCALS += @"　バストの数値　（現在：\@ GETBIT(身体項目表示切り替えOPTION, 1)? 表示する　# 抽象的表現\@ / <font color='#666666'><button value='802'>[802] \@ GETBIT(身体項目表示切り替えOPTION, 1)? 抽象的表現# 表示する　\@</button></font>）<br>"
		LOCALS += @"　体型　　　　　（現在：\@ GETBIT(身体項目表示切り替えOPTION, 2)? 表示する　# 表示しない\@ / <font color='#666666'><button value='803'>[803] \@ GETBIT(身体項目表示切り替えOPTION, 2)? 表示しない# 表示する　\@</button></font>）<br>"
	CASE 900
		LOCALS += "・サイド領域表示切り替え<br>"
		LOCALS += "ゲーム画面右側にあるサイド領域の表示/非表示を切り替えます。<br>"
		LOCALS += "<br>"
		IF サイド領域非表示オプション == 0
			LOCALS += "　<button value='901'>[901] サイド領域を非表示にする（現在：表示）</button>"
		ELSEIF サイド領域非表示オプション
			LOCALS += "　<button value='901'>[901] サイド領域を表示する（現在：非表示）</button>"
		ENDIF
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "・オート射精設定<br>"
		LOCALS += "射精時、自動で射精先を決定するための設定を行います。<br>"
		LOCALS += "<br>"
		LOCALS += "　<button value='902'>[902] オート射精先設定画面を開く</button>"
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "・子宮口挿入設定<br>"
		LOCALS += "子宮口挿入コマンド、及びコマンド解放イベントが出るかどうかを設定します。<br>"
		LOCALS += "<br>"
		IF 子宮口挿入_封印 == 0
			LOCALS += "　<button value='903'>[903] 子宮口挿入関連の機能を禁止する（現在：許可）</button>"
		ELSEIF 子宮口挿入_封印
			LOCALS += "　<button value='903'>[903] 子宮口挿入関連の機能を許可する（現在：禁止）</button>"
		ENDIF
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "・連れ込み禁止設定<br>"
		LOCALS += "キャラが自動で物陰に連れ込んでくるかどうかを設定します。<br>"
		LOCALS += "<br>"
		IF 連れ込み制御OPTION == 0
			LOCALS += "　<button value='904'>[904] キャラの連れ込みを禁止する（現在：許可）</button>"
		ELSEIF 連れ込み制御OPTION
			LOCALS += "　<button value='904'>[904] キャラの連れ込みを許可する（現在：禁止）</button>"
		ENDIF
	CASE 1000
		LOCALS += "・オートコマンド可否切り替え<br>"
		LOCALS += "キャラクターが自動で行動する機能を切り替えます。<br>"
		LOCALS += "<br>"
		IF GETBIT(FLAG:オートコマンド切り替えオプション, 0)
			LOCALS += "<button value='1001'>[1001] オート専用コマンドを許可する（現在：禁止）</button>"
		ELSE
			LOCALS += "<button value='1001'>[1001] オート専用コマンドを禁止する（現在：許可）</button>"
		ENDIF
		LOCALS += "<br>"
		IF GETBIT(FLAG:オートコマンド切り替えオプション, 1)
			LOCALS += "<button value='1002'>[1002] 通常コマンドのオート使用を許可する（現在：禁止）</button>"
		ELSE
			LOCALS += "<button value='1002'>[1002] 通常コマンドのオート使用を禁止する（現在：許可）</button>"
		ENDIF
		LOCALS += "<br>"
		IF GETBIT(FLAG:オートコマンド切り替えオプション, 2)
			LOCALS += "<button value='1003'>[1003] 自慰系コマンドのオート使用を許可する（現在：禁止）</button>"
		ELSE
			LOCALS += "<button value='1003'>[1003] 自慰系コマンドのオート使用を禁止する（現在：許可）</button>"
		ENDIF
		LOCALS += "<br>"
	CASE 1100
		LOCALS += "・カットイン表示切り替え<br>"
		LOCALS += "カットインアニメーション表示機能を切り替えます。<br>"
		LOCALS += "カットイン画像設定については、奥義時カットインの画像優先度を切り替えることが可能です。<br>"
		LOCALS += "<br>"
		LOCALS += "カットイン表示設定："
		SELECTCASE カットイン演出一括表示フラグ
			CASE 0
				LOCALS += "　[表示] / <font color='#666666'><button value='1102'>[非表示]</button></font> / <font color='#666666'><button value='1103'>[一日一回]</button></font><br>"
			CASE 1
				LOCALS += "　<font color='#666666'><button value='1101'>[表示]</button></font> / [非表示] / <font color='#666666'><button value='1103'>[一日一回]</button></font><br>"
			CASE 2
				LOCALS += "　<font color='#666666'><button value='1101'>[表示]</button></font> / <font color='#666666'><button value='1102'>[非表示]</button></font> / [一日一回]<br>"
		ENDSELECT
		LOCALS += "カットイン画像設定："
		SELECTCASE カットイン画像一括表示フラグ
			CASE 0
				LOCALS += "　[縦カットイン優先] / <font color='#666666'><button value='1105'>[横カットイン優先]</button></font><br>　　　　　　　　　　　<font color='#666666'><button value='1106'>[デフォルト顔グラ優先]</button></font><br>"
			CASE 1
				LOCALS += "　<font color='#666666'><button value='1104'>[縦カットイン優先]</button></font> / [横カットイン優先]<br>　　　　　　　　　　　<font color='#666666'><button value='1106'>[デフォルト顔グラ優先]</button></font><br>"
			CASE 2
				LOCALS += "　<font color='#666666'><button value='1104'>[縦カットイン優先]</button></font> / <font color='#666666'><button value='1105'>[横カットイン優先]</button></font><br>　　　　　　　　　　　[デフォルト顔グラ優先]<br>"
		ENDSELECT
		LOCALS += "<br>"
		LOCALS += "・個別奥義カットイン設定<br>"
		LOCALS += "［能力表示］画面からキャラ個別の奥義カットイン設定を行うことが出来ます。<br>"
		LOCALS += "ここでは全てのキャラのカットイン設定を一括して設定することが出来ます。<br>"
		LOCALS += "<br>"
		LOCALS += "・カットインの個別表示設定（全体設定より優先）<br>"
		LOCALS += "　　<button value='1110'>全員［全体設定に従う］にする</button> / <button value='1111'>全員［非表示］にする</button> / <button value='1112'>全員［表示］にする</button><br>　　<button value='1113'>全員［一日一回のみ］にする</button>"
		LOCALS += "<br>"
		LOCALS += "・カットインの画像表示設定（全体設定より優先）<br>"
		LOCALS += "　　<button value='1114'>全員［全体設定に従う］にする</button> / <button value='1115'>全員［縦カットイン優先］にする</button><br>　　<button value='1116'>全員［横カットイン優先］にする</button> / <button value='1117'>全員［デフォルト顔グラ優先］にする</button>"
ENDSELECT
LOCALS += "</div>"



HTML_PRINT LOCALS
FOR LOCAL, 0, 25
	PRINTL
NEXT
DRAWLINE

BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 100, 200, 300, 400, 500, 600, 700, 800, 900, 1000, 1100
		画面表示用変数 = LOCAL
	CASE 101
		INVERTBIT ツールチップ切り替えOPTION記憶, 0
	CASE 201
		発情期切り替えOPTION記憶 = 0
	CASE 202
		SETBIT 発情期切り替えOPTION記憶, 0
		SETBIT 発情期切り替えOPTION記憶, 1
		SETBIT 発情期切り替えOPTION記憶, 2
		SETBIT 発情期切り替えOPTION記憶, 3
		SETBIT 発情期切り替えOPTION記憶, 4
		SETBIT 発情期切り替えOPTION記憶, 5
	CASE 203 TO 208
		INVERTBIT 発情期切り替えOPTION記憶, LOCAL - 203
	CASE 301
		INVERTBIT 妊娠切り替えOPTION記憶, 0
	CASE 401
		SETBIT 画像サイズ切り替えOPTION記憶, 0
		CLEARBIT 画像サイズ切り替えOPTION記憶, 4
	CASE 402
		SETBIT 画像サイズ切り替えOPTION記憶, 4
		CLEARBIT 画像サイズ切り替えOPTION記憶, 0
	CASE 403
		CLEARBIT 画像サイズ切り替えOPTION記憶, 0
		CLEARBIT 画像サイズ切り替えOPTION記憶, 4
	CASE 404 TO 406
		INVERTBIT 画像サイズ切り替えOPTION記憶, (LOCAL - 403)
	CASE 407
		INVERTBIT 画像サイズ切り替えOPTION記憶, 5
		IF GETBIT(画像サイズ切り替えOPTION記憶, 5)
			SETBIT 画像サイズ切り替えOPTION記憶, 0
			SETBIT 画像サイズ切り替えOPTION記憶, 1
			SETBIT 画像サイズ切り替えOPTION記憶, 2
			SETBIT 画像サイズ切り替えOPTION記憶, 3
			CLEARBIT 画像サイズ切り替えOPTION記憶, 4
		ELSE
			画像サイズ切り替えOPTION記憶 = 0
		ENDIF
	CASE 501
		INVERTBIT 画像サイズ切り替えOPTION記憶:1, 0
	CASE 601
		INVERTBIT FLAG_挿絵表示, 0
	CASE 602 TO 604
		FLAG_射精画像種類 = LOCAL - 602
	CASE 605 TO 610
		FLAG_アニメーション = LOCAL - 605
	CASE 611
		INVERTBIT FLAG_挿入アニメフィルタ, 0
	CASE 701
		汎用喘ぎ使用OPTION = 0
	CASE 702
		汎用喘ぎ使用OPTION = 1
	CASE 703
		汎用喘ぎ使用OPTION = 3
	CASE 704
		汎用喘ぎ使用OPTION_男 = 0
	CASE 705
		汎用喘ぎ使用OPTION_男 = 1
	CASE 706
		汎用喘ぎ使用OPTION_男 = 3
	CASE 801 TO 803
		INVERTBIT 身体項目表示切り替えOPTION, (LOCAL - 801)
	CASE 901
		INVERTBIT サイド領域非表示オプション, 0
	CASE 902
		CALL オート射精先設定
	CASE 903
		INVERTBIT 子宮口挿入_封印, 0
	CASE 904
		INVERTBIT 連れ込み制御OPTION, 0
	CASE 1001
		INVERTBIT FLAG:オートコマンド切り替えオプション, 0
	CASE 1002
		INVERTBIT FLAG:オートコマンド切り替えオプション, 1
	CASE 1003
		INVERTBIT FLAG:オートコマンド切り替えオプション, 2
	CASE 1101 TO 1103
		カットイン演出一括表示フラグ = LOCAL - 1101
	CASE 1104 TO 1106
		カットイン画像一括表示フラグ = LOCAL - 1104
	CASE 1110
		FOR 対象キャラ, 0, CHARANUM
			CSTR:対象キャラ:カットイン個別選択オプション = 
		NEXT
		PRINTW キャラ全員の設定を［全体設定に従う］にしました。
	CASE 1111
		FOR 対象キャラ, 0, CHARANUM
			CSTR:対象キャラ:カットイン個別選択オプション = 非表示
		NEXT
		PRINTW キャラ全員の設定を［非表示］にしました。
	CASE 1112
		FOR 対象キャラ, 0, CHARANUM
			CSTR:対象キャラ:カットイン個別選択オプション = 表示
		NEXT
		PRINTW キャラ全員の設定を［表示］にしました。
	CASE 1113
		FOR 対象キャラ, 0, CHARANUM
			CSTR:対象キャラ:カットイン個別選択オプション = 一日一回のみ
		NEXT
		PRINTW キャラ全員の設定を［一日一回のみ］にしました。
	CASE 1114
		FOR 対象キャラ, 0, CHARANUM
			CSTR:対象キャラ:カットイン画像選択オプション = 
		NEXT
		PRINTW キャラ全員の設定を［全体設定に従う］にしました。
	CASE 1115
		FOR 対象キャラ, 0, CHARANUM
			CSTR:対象キャラ:カットイン画像選択オプション = 縦カットイン
		NEXT
		PRINTW キャラ全員の設定を［縦カットイン優先表示］にしました。
	CASE 1116
		FOR 対象キャラ, 0, CHARANUM
			CSTR:対象キャラ:カットイン画像選択オプション = 横カットイン
		NEXT
		PRINTW キャラ全員の設定を［横カットイン優先表示］にしました。
	CASE 1117
		FOR 対象キャラ, 0, CHARANUM
			CSTR:対象キャラ:カットイン画像選択オプション = デフォルト
		NEXT
		PRINTW キャラ全員の設定を［デフォルト顔グラ優先表示］にしました。
	CASE 999
		RETURN 0
ENDSELECT

RESTART


@汎用喘ぎ個別設定(対象キャラ)
#DIM 対象キャラ

DRAWLINE
PRINTL 汎用喘ぎを使用するか否かを個別に設定出来ます。
PRINTL
PRINTL ・現在の全体設定
PRINT ・女性（ふたなり・その他）キャラ：
SELECTCASE 汎用喘ぎ使用OPTION
	CASE 0
		PRINTL 使用しない
	CASE 1
		PRINTL 口上が無いキャラのみ使用
	CASE 3
		PRINTL 全てのキャラに使用
ENDSELECT
PRINT ・男性キャラ：
SELECTCASE 汎用喘ぎ使用OPTION_男
	CASE 0
		PRINTL 使用しない
	CASE 1
		PRINTL 口上が無いキャラのみ使用
	CASE 3
		PRINTL 全てのキャラに使用
ENDSELECT

PRINTFORM 現在の%NAME:対象キャラ%個別設定：
IF CFLAG:対象キャラ:汎用喘ぎ禁止フラグ == 1
	PRINTL 全体設定に関わらず使用しない
ELSEIF CFLAG:対象キャラ:汎用喘ぎ禁止フラグ == 0
	PRINTL 全体設定に従う
ELSEIF CFLAG:対象キャラ:汎用喘ぎ禁止フラグ == -1
	PRINTL 全体設定に関わらず使用する
ENDIF
DRAWLINE

PRINTBUTTON "[0] 全体設定に従う", 0
PRINTL
PRINTBUTTON "[1] 全体設定に関わらず使用しない", 1
PRINTL
PRINTBUTTON "[-1] 全体設定に関わらず使用する", -1
PRINTL
PRINTL
PRINTBUTTON "[999] 戻る", 999
PRINTL

BINPUT
SELECTCASE RESULT
	CASE 999
		RETURN 0
	CASEELSE
		CFLAG:対象キャラ:汎用喘ぎ禁止フラグ = RESULT
		RESTART
ENDSELECT


@OPTION_招待切り替え

DRAWLINE
PRINTL リゾートに訪れるキャラの性別設定を行います。
PRINTL ※広告による招待・常連素質による訪問に影響します
PRINTL 　ZPショップによる解放・イベントなどでの訪問は設定に関わらず実行されます。
DRAWLINE
PRINTL
PRINTL ・現在の設定
PRINT 　　女性：
IF GETBIT(性別招待制御OPTION, 0)
	HTML_PRINT @"<button value='0'>[0] <font color='#%カラーパレット_HTML("グレーアウト")%'>リゾートを訪問する</font> / リゾートを訪問しない</button>"
	PRINTL
ELSE
	HTML_PRINT @"<button value='0'>[0] リゾートを訪問する / <font color='#%カラーパレット_HTML("グレーアウト")%'>リゾートを訪問しない</font></button>"
	PRINTL
ENDIF
PRINT 　　男性：
IF GETBIT(性別招待制御OPTION, 1)
	HTML_PRINT @"<button value='1'>[1] <font color='#%カラーパレット_HTML("グレーアウト")%'>リゾートを訪問する</font> / リゾートを訪問しない</button>"
	PRINTL
ELSE
	HTML_PRINT @"<button value='1'>[1] リゾートを訪問する / <font color='#%カラーパレット_HTML("グレーアウト")%'>リゾートを訪問しない</font></button>"
	PRINTL
ENDIF
PRINT 　　ふたなり：
IF GETBIT(性別招待制御OPTION, 2)
	HTML_PRINT @"<button value='2'>[2] <font color='#%カラーパレット_HTML("グレーアウト")%'>リゾートを訪問する</font> / リゾートを訪問しない</button>"
	PRINTL
ELSE
	HTML_PRINT @"<button value='2'>[2] リゾートを訪問する / <font color='#%カラーパレット_HTML("グレーアウト")%'>リゾートを訪問しない</font></button>"
	PRINTL
ENDIF
PRINT 　　その他：
IF GETBIT(性別招待制御OPTION, 3)
	HTML_PRINT @"<button value='3'>[3] <font color='#%カラーパレット_HTML("グレーアウト")%'>リゾートを訪問する</font> / リゾートを訪問しない</button>"
	PRINTL
ELSE
	HTML_PRINT @"<button value='3'>[3] リゾートを訪問する / <font color='#%カラーパレット_HTML("グレーアウト")%'>リゾートを訪問しない</font></button>"
	PRINTL
ENDIF

PRINTL
PRINTL
PRINTBUTTON "[999] 戻る", 999
PRINTL

BINPUT
SELECTCASE RESULT
	CASE 999
		RETURN 0
	CASEELSE
		INVERTBIT 性別招待制御OPTION, RESULT
		RESTART
ENDSELECT

@OPTION_キャラ制限初期切り替え

DRAWLINE
PRINTL 登場が制限されているキャラクターの初期登場設定を変更します。
PRINTL ※この設定はゲームが始まってからは変更出来ません。
PRINTL ※初期開放時、イベントシナリオと矛盾が発生する場合があります。
DRAWLINE
PRINTL
PRINTL ・現在の設定

PRINT ZPショップ加入キャラ：
IF GETBIT(キャラ制限初期OPTION, 0)
	PRINT 全開放　　　　　
	PRINTBUTTON "[0] 切り替える", 0
	PRINTL
	PRINTL 　（初期からリゾートを訪れる）
	PRINTL
ELSE
	PRINT デフォルト　　　
	PRINTBUTTON "[0] 切り替える", 0
	PRINTL
	PRINTL 　（加入にダンジョン攻略・イベント視聴が必須）
	PRINTL 　（加入にZP消費が必須）
ENDIF
PRINTL
PRINT イベント加入キャラ：
IF GETBIT(キャラ制限初期OPTION, 1)
	PRINT 全開放　　　　　　
	PRINTBUTTON "[1] 切り替える", 1
	PRINTL
	PRINTL 　（初期からリゾートを訪れる）
ELSE
	PRINT デフォルト　　　　
	PRINTBUTTON "[1] 切り替える", 1
	PRINTL
	PRINTL 　（キャラによって異なる特定の条件が必須）
ENDIF
PRINTL
PRINTL
PRINTBUTTON "[999] これで決定する", 999
PRINTL

BINPUT
SELECTCASE RESULT
	CASE 999
		;解放処理
		FOR LOCAL, 1, CHARANUM
			IF (CFLAG:LOCAL:招待不可フラグ == 1 || CFLAG:LOCAL:招待不可フラグ == 3) && GETBIT(キャラ制限初期OPTION, 0)
				CFLAG:LOCAL:招待不可フラグ = 0
			ENDIF
			IF CFLAG:LOCAL:招待不可フラグ == 2 && GETBIT(キャラ制限初期OPTION, 1)
				CFLAG:LOCAL:招待不可フラグ = 0
			ENDIF
		NEXT
		RETURN 0
	CASEELSE
		INVERTBIT キャラ制限初期OPTION, RESULT
		RESTART
ENDSELECT
