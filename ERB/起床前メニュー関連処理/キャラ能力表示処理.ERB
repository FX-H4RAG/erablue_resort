
;ステータス表示
@SHOW_CHARADATA
#DIM ページ数, 2
#DIM 表示候補, キャラクタ数上限
#DIM ソート基準値, キャラクタ数上限
#DIM 候補人数
#DIM 口上ファイル数, キャラクタ数上限
#DIM アビ奥義関数, キャラクタ数上限
#DIM 得意武器番号
#DIM スキップフラグ
#DIM 表示人数 = 50
#DIM L_TIME,2
VARSET ページ数
VARSET 口上ファイル数, -1
VARSET アビ奥義関数, -1

$最初から

;表示候補選定＆ソート処理
VARSET 表示候補
候補人数 = 0
;フィルタ処理・絞り込み処理
FOR LOCAL, 1, CHARANUM
	CALL 装備再計算処理(LOCAL)
	SIF CFLAG:LOCAL:招待不可フラグ
		CONTINUE
	SIF 滞在者のみフィルタ == 1 && CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	表示候補:候補人数 = LOCAL
	候補人数 ++
NEXT
CALL キャラ配列フィルタ処理(表示候補)
候補人数 = RESULT


LOCAL:1 = 1

$表示処理_LOOP
ページ数:1 = MAX(候補人数 / 表示人数 - (候補人数%表示人数 ? 0# 1), 0)
DRAWLINE
PRINTFORML 　 [0] %NAME:MASTER%
DRAWLINE
VARSET LOCAL
VARSET LOCALS
;表示処理
FOR LOCAL, ページ数 * 表示人数, 候補人数
	SIF 表示候補:LOCAL < 1
		BREAK
	IF STRCOUNT(キャラ一覧ソート:0, "恋慕レベル") && CFLAG:(表示候補:LOCAL):ゲージ起動分類 == 1
		LOCALS += "<font color='#FF3399'>"
	ENDIF
	LOCALS += @"<button value = '{表示候補:LOCAL}'>[{表示候補:LOCAL,4}] %NAME表示省略(ADD_STR_SPACE(NAME:(表示候補:LOCAL), "]", 1), 50),51,LEFT%</button>"
	IF STRCOUNT(キャラ一覧ソート:0, "好感度レベル")
		LOCALS += @"好感度Lv：{CFLAG:(表示候補:LOCAL):好感度レベル, 3}　 "
	ELSEIF STRCOUNT(キャラ一覧ソート:0, "恋慕レベル")
		LOCALS += @"　恋慕Lv：{CFLAG:(表示候補:LOCAL):恋慕レベル, 3}　 "
		SIF CFLAG:(表示候補:LOCAL):ゲージ起動分類 == 1
			LOCALS += "</font>"
	ELSEIF STRCOUNT(キャラ一覧ソート:0, "コマンド回数")
		LOCALS += @"　回数：{CFLAG:(表示候補:LOCAL):コマンドタゲ回数記録, 5}　 "
	ELSEIF GETNUM(ABL, キャラ一覧ソート:0) >= 0
		LOCALS += @"%キャラ一覧ソート:0, 8%：{ABL:(表示候補:LOCAL):(キャラ一覧ソート:0), 3}　 "
	ELSEIF GETNUM(装備ステータス補正保存, キャラ一覧ソート:0) >= 0
		LOCALS += @"%キャラ一覧ソート:0, 8%：{基礎BATTLE_STATE:(表示候補:LOCAL):(キャラ一覧ソート:0) + 装備ステータス補正保存:(表示候補:LOCAL):(キャラ一覧ソート:0), 3}　 "
	ELSEIF STRCOUNT(キャラ一覧ソート:0, "Lv")
		LOCALS += @"Lv：{基礎BATTLE_STATE:(表示候補:LOCAL):Lv, 3}　 "
	ELSEIF GETNUM(知識素質, キャラ一覧ソート:0) >= 0
		LOCALS += @"%キャラ一覧ソート:0, 8%：{知識素質:(表示候補:LOCAL):(キャラ一覧ソート:0)}　 "
	ELSEIF GETNUM(性癖素質, キャラ一覧ソート:0) >= 0
		SELECTCASE 性癖素質:(表示候補:LOCAL):(キャラ一覧ソート:0)
			CASE 0
				LOCALS += @"%キャラ一覧ソート:0, 8%：なし　 "
			CASE 1
				LOCALS += @"%キャラ一覧ソート:0, 8%：興味　 "
			CASE 2
				LOCALS += @"%キャラ一覧ソート:0, 8%：愛好　 "
			CASE 3
				LOCALS += @"%キャラ一覧ソート:0, 8%：中毒　 "
		ENDSELECT
	ELSEIF STRCOUNT(キャラ一覧ソート:0, "画像数")
		LOCALS += @"画像数：{MAX(ENUMFILES(@"resources/%CSTR:(表示候補:LOCAL):画像フォルダ%"), 0), 4}　"
	ELSEIF STRCOUNT(キャラ一覧ソート:0, "チョロさ")
		LOCALS += @"チョロさ：{COM353_SUB(表示候補:LOCAL), 4}　"
	ELSE
		IF CFLAG:(表示候補:LOCAL):長期雇用
			LOCALS += "　　長期雇用中　"
		ELSEIF CFLAG:(表示候補:LOCAL):滞在期間 == 1
			LOCALS += @"　　　<font color='#%カラーパレット_HTML("黄")%'>本日帰宅</font>　"
		ELSEIF CFLAG:(表示候補:LOCAL):滞在期間 == 999
			LOCALS += "　　　常時滞在　"
		ELSEIF CFLAG:(表示候補:LOCAL):滞在期間 > 1
			LOCALS += @"　残滞在：{CFLAG:(表示候補:LOCAL):滞在期間 - 1, 2}日　"
		ELSE
			LOCALS += "　　　　　　　　"
		ENDIF
	ENDIF
	LOCAL:1 += 1
	IF LOCAL:1 % 2 == 0
		HTML_PRINT LOCALS
		LOCALS = 
	ENDIF
	SIF LOCAL:1 > 表示人数 - 1
		BREAK
NEXT
IF LOCALS != ""
	HTML_PRINT LOCALS
	LOCALS = 
ENDIF
PRINTL 
LOCALS:2 = 
IF ページ数 > 0
	LOCALS:2 += @"<button value='9000'>[9000] 前のページ </button>"
ELSE
	LOCALS:2 += " " * STRLENS("[9000] 前のページ ")
ENDIF
LOCALS:2 += " " * 24
LOCALS:2 += @"<button value='9006'>{1+ページ数, 4, RIGHT}/{1+ページ数:1, 4, RIGHT}</button>"
LOCALS:2 += " " * 23
IF ページ数 < ページ数:1
	LOCALS:2 += @"<button value='9001'>[9001] 次のページ </button>"
ENDIF
LOCALS:2 += "<br>"
IF ページ数 > 0
	LOCALS:2 += "<button value='9002'>[|≪]</button>"
ELSE
	LOCALS:2 += "     "
ENDIF
IF ページ数 > 4
	LOCALS:2 += "<button value='9003'> [－５]</button>"
ELSE
	LOCALS:2 += " 　　　"
ENDIF
LOCALS:2 += " " * 26
LOCALS:2 += @"<button value='9007'>表示人数：{表示人数, 3}人/page</button>"
LOCALS:2 += " " * (56-20-20)
IF ページ数 <= ページ数:1 - 5
	LOCALS:2 += "<button value='9004'>[＋５] </button>"
ELSE
	LOCALS:2 += " 　　　"
ENDIF
LOCALS:2 += " " * 5
IF ページ数 < ページ数:1
	LOCALS:2 += "<button value='9005'>[≫|]</button>"
ENDIF
HTML_PRINT LOCALS:2
DRAWLINE

PRINTBUTTON "[2000]フィルタ機能", 2000
PRINTL
PRINT 　└
IF 滞在者のみフィルタ
	PRINTBUTTON "[2001]滞在者と非滞在者を表示する", 2001
ELSE
	PRINTBUTTON "[2001]滞在者のみ表示する", 2001
ENDIF
PRINTL
PRINTBUTTON "[3000]ソート機能", 3000
PRINT 　　　　　現在のソート設定：
IF キャラ一覧ソート:0 != ""
	PRINTFORM %キャラ一覧ソート:0%
ENDIF
IF STRCOUNT(キャラ一覧ソート:1, "昇順")
	PRINT (昇順)
	PRINT 　　　　　
	PRINTBUTTON "[降順にする]", 3100
ELSEIF STRCOUNT(キャラ一覧ソート:1, "降順")
	PRINT (降順)
	PRINT 　　　　　
	PRINTBUTTON "[昇順にする]", 3200
ELSE
	PRINT なし(キャラ番号順)
ENDIF
PRINTL
PRINTBUTTON "[4000]素質絞り込み機能", 4000
PRINT 　　現在の絞り込み設定：
IF キャラ一覧素質絞り込み:0 != ""
	IF キャラ一覧素質絞り込み:99 == "AND"
		PRINT (AND条件)：
	ELSE
		PRINT (OR条件)：
	ENDIF
	FOR LOCAL, 0, 99
		SIF キャラ一覧素質絞り込み:LOCAL == ""
			BREAK
		LOCALS:0 = 
		LOCALS:1 = 
		SPLIT キャラ一覧素質絞り込み:LOCAL, "_", LOCALS
		PRINTFORM [%GET_TALENTNAME(TOINT(LOCALS:0), TOINT(LOCALS:1))%]
	NEXT
ELSE
	PRINT なし
ENDIF
PRINTL
PRINTBUTTONLC "[9999]戻る", 9999
PRINTL

$INPUT_LOOP
BINPUT
IF RESULT == 9999
	RETURN 0
ELSEIF RESULT == 2000
	CALL SHOW_CHARADATA_FILTA
	ページ数 = 0
	RESTART
ELSEIF RESULT == 2001
	INVERTBIT 滞在者のみフィルタ, 0
	ページ数 = 0
	RESTART
ELSEIF RESULT == 3000
	CALL SHOW_CHARADATA_SORT
	ページ数 = 0
	RESTART
ELSEIF RESULT == 3100
	キャラ一覧ソート:1 = 降順
	ページ数 = 0
	RESTART
ELSEIF RESULT == 3200
	キャラ一覧ソート:1 = 昇順
	ページ数 = 0
	RESTART
ELSEIF RESULT == 4000
	CALL SHOW_CHARADATA_素質絞り込み
	ページ数 = 0
	RESTART
ELSEIF RESULT == 9000
	ページ数 = MAX(ページ数 - 1, 0)
	GOTO 表示処理_LOOP
ELSEIF RESULT == 9001
	ページ数 = MIN(ページ数 + 1, 候補人数 / 表示人数)
	GOTO 表示処理_LOOP
ELSEIF RESULT == 9002
	ページ数 = 0
	GOTO 表示処理_LOOP
ELSEIF RESULT == 9003
	ページ数 = MAX(ページ数 - 5, 0)
	GOTO 表示処理_LOOP
ELSEIF RESULT == 9004
	ページ数 = MIN(ページ数 + 5, 候補人数 / 表示人数)
	GOTO 表示処理_LOOP
ELSEIF RESULT == 9005
	ページ数 = ページ数:1
	GOTO 表示処理_LOOP
ELSEIF RESULT == 9006
	PRINTFORML 飛びたいページ数を入力してください（1～{1+ページ数:1}）
	INPUT
	SIF INRANGE(RESULT, 1, 1+ページ数:1)
		ページ数 = RESULT - 1
	GOTO 表示処理_LOOP
ELSEIF RESULT == 9007
	PRINTFORML 1ページ辺りに表示したい人数を入力してください（1～100）
	INPUT
	IF INRANGE(RESULT, 1, 100)
		表示人数 = RESULT
		ページ数 = 0
	ENDIF
	GOTO 表示処理_LOOP
ELSEIF RESULT < 0 || RESULT >= CHARANUM
	REUSELASTLINE 
	GOTO INPUT_LOOP
ENDIF
LOCAL = RESULT
$再表示
IF 候補人数 == 1
	CALL PRINT_STATE(LOCAL, 0)
ELSEIF 表示候補:0 == LOCAL
	CALL PRINT_STATE(LOCAL, 2)
ELSEIF 表示候補:(候補人数 - 1) == LOCAL
	CALL PRINT_STATE(LOCAL, 1)
ELSE
	CALL PRINT_STATE(LOCAL, 3)
ENDIF
IF RESULT == 100
	FOR LOCAL:1, 0, 候補人数
		IF 表示候補:(LOCAL:1) == LOCAL
			LOCAL = 表示候補:(LOCAL:1 - 1)
			BREAK
		ENDIF
	NEXT
	GOTO 再表示
ELSEIF RESULT == 101
	FOR LOCAL:1, 0, 候補人数
		IF 表示候補:(LOCAL:1) == LOCAL
			LOCAL = 表示候補:(LOCAL:1 + 1)
			BREAK
		ENDIF
	NEXT
	GOTO 再表示
ENDIF

GOTO 表示処理_LOOP


@SHOW_CHARADATA_FILTA
#DIMS 表示フィルタ種類
#DIMS フィルタマッチ配列, 50
#DIM フィルタマッチ配列番号

DRAWLINE
PRINTL キャラクターの表示に以下の条件でフィルタを掛けます。
PRINTL 暗い色で表示されている条件が非表示設定になっています。
DRAWLINE

IF 表示フィルタ種類 == ""
	PRINTL ■ 性別
	PRINT 　 
	SIF STRCOUNT(キャラ一覧フィルタ:性別, "性別：女性")
		SETCOLOR 0x666666
	PRINTBUTTON "[女性]", "性別：女性"
	RESETCOLOR
	PRINT  / 
	SIF STRCOUNT(キャラ一覧フィルタ:性別, "性別：男性")
		SETCOLOR 0x666666
	PRINTBUTTON "[男性]", "性別：男性"
	RESETCOLOR
	PRINT  / 
	SIF STRCOUNT(キャラ一覧フィルタ:性別, "性別：ふたなり")
		SETCOLOR 0x666666
	PRINTBUTTON "[ふたなり]", "性別：ふたなり"
	RESETCOLOR
	PRINT  / 
	SIF STRCOUNT(キャラ一覧フィルタ:性別, "性別：その他")
		SETCOLOR 0x666666
	PRINTBUTTON "[その他]", "性別：その他"
	RESETCOLOR
	PRINT 　　　　　
	PRINTBUTTON "[全て表示]", "性別：全て表示"
	PRINT /
	PRINTBUTTON "[全て非表示]", "性別：全て非表示"
	PRINTL
	DRAWLINE
	PRINTL ■ 陥落
	PRINT 　 
	SIF STRCOUNT(キャラ一覧フィルタ:陥落, "陥落：恋慕")
		SETCOLOR 0x666666
	PRINTBUTTON "[恋慕]", "陥落：恋慕"
	RESETCOLOR
	PRINT  / 
	SIF STRCOUNT(キャラ一覧フィルタ:陥落, "陥落：セフレ")
		SETCOLOR 0x666666
	PRINTBUTTON "[セフレ]", "陥落：セフレ"
	RESETCOLOR
	PRINT  / 
	SIF STRCOUNT(キャラ一覧フィルタ:陥落, "陥落：なし")
		SETCOLOR 0x666666
	PRINTBUTTON "[なし]", "陥落：なし"
	RESETCOLOR
	PRINT 　　　　　
	PRINTBUTTON "[全て表示]", "陥落：全て表示"
	PRINT /
	PRINTBUTTON "[全て非表示]", "陥落：全て非表示"
	PRINTL
	DRAWLINE
	PRINTL ■ 種族
	PRINT 　 
	FOR LOCAL, 1, 7
		SIF STRCOUNT(キャラ一覧フィルタ:種族, GET_TALENTNAME(200, LOCAL))
			SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(200, LOCAL)%]", @"種族：%GET_TALENTNAME(200, LOCAL)%"
		RESETCOLOR
		IF LOCAL == 6
			PRINT 　　　　　
			PRINTBUTTON "[全て表示]", "種族：全て表示"
			PRINT /
			PRINTBUTTON "[全て非表示]", "種族：全て非表示"
			PRINTL
		ELSE
			PRINT  / 
		ENDIF
	NEXT
	DRAWLINE
	PRINTL ■ バストサイズ
	PRINT 　 
	FOR LOCAL, -2, 4
		SIF STRCOUNT(キャラ一覧フィルタ:バストサイズ, GET_TALENTNAME(205, LOCAL))
			SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(205, LOCAL)%]", @"バストサイズ：%GET_TALENTNAME(205, LOCAL)%"
		RESETCOLOR
		IF LOCAL == 3
			PRINT / 
			SIF STRCOUNT(キャラ一覧フィルタ:バストサイズ, "バストサイズ：なし")
				SETCOLOR 0x666666
			PRINTBUTTON "[バストなし]", "バストサイズ：なし"
			RESETCOLOR
			PRINT 　　　　　　　　　　
			PRINTBUTTON "[全て表示]", "バストサイズ：全て表示"
			PRINT /
			PRINTBUTTON "[全て非表示]", "バストサイズ：全て非表示"
			PRINTL
		ELSEIF LOCAL == 1
			PRINTL 
			PRINT 　 
		ELSE
			PRINT  / 
		ENDIF
	NEXT
	DRAWLINE
	PRINTL ■ 男性器サイズ
	PRINT 　 
	FOR LOCAL, -2, 3
		SIF STRCOUNT(キャラ一覧フィルタ:男性器サイズ, GET_TALENTNAME(162, LOCAL))
			SETCOLOR 0x666666
		PRINTBUTTON @"[%GET_TALENTNAME(162, LOCAL)%]", @"男性器サイズ：%GET_TALENTNAME(162, LOCAL)%"
		RESETCOLOR
		IF LOCAL == 2
			PRINT / 
			SIF STRCOUNT(キャラ一覧フィルタ:男性器サイズ, "男性器サイズ：なし")
				SETCOLOR 0x666666
			PRINTBUTTON "[男性器なし]", "男性器サイズ：なし"
			RESETCOLOR
			PRINT 　　　　　
			PRINTBUTTON "[全て表示]", "男性器サイズ：全て表示"
			PRINT /
			PRINTBUTTON "[全て非表示]", "男性器サイズ：全て非表示"
			PRINTL
		ELSE
			PRINT  / 
		ENDIF
	NEXT
	DRAWLINE
	PRINTL ■ 性知識Lv
	PRINT 　 
	FOR LOCAL, -2, 4
		SIF STRCOUNT(キャラ一覧フィルタ:性知識Lv, 性知識段階表示(LOCAL, 0))
			SETCOLOR 0x666666
		PRINTBUTTON @"[%性知識段階表示(LOCAL, 0)%]", @"性知識Lv：%性知識段階表示(LOCAL, 0)%"
		RESETCOLOR
		IF LOCAL == 0
			PRINT  / 
			SIF STRCOUNT(キャラ一覧フィルタ:性知識Lv, 性知識段階表示(0, 3))
				SETCOLOR 0x666666
			PRINTBUTTON @"[%性知識段階表示(0, 3)%]", @"性知識Lv：%性知識段階表示(0, 3)%"
			RESETCOLOR
		ENDIF
		IF LOCAL == 3
			PRINT 　　　　　
			PRINTBUTTON "[全て表示]", "性知識Lv：全て表示"
			PRINT /
			PRINTBUTTON "[全て非表示]", "性知識Lv：全て非表示"
			PRINTL
		ELSE
			PRINT  / 
		ENDIF
	NEXT
	DRAWLINE
	PRINTL ■ 発情期
	PRINT 　 
	SIF STRCOUNT(キャラ一覧フィルタ:発情期, @"発情期フラグ：発情期なし")
		SETCOLOR 0x666666
	PRINTBUTTON @"[発情期なし]", @"発情期フラグ：発情期なし"
	RESETCOLOR
	PRINT  / 
	SIF STRCOUNT(キャラ一覧フィルタ:発情期, @"発情期フラグ：発情中")
		SETCOLOR 0x666666
	PRINTBUTTON @"[発情中]", @"発情期フラグ：発情中"
	RESETCOLOR
	PRINT  / 
	SIF STRCOUNT(キャラ一覧フィルタ:発情期, @"発情期フラグ：発情期ではない")
		SETCOLOR 0x666666
	PRINTBUTTON @"[発情期ではない]", @"発情期フラグ：発情期ではない"
	RESETCOLOR
	PRINT 　　　　　
	PRINTBUTTON "[全て表示]", "発情期フラグ：全て表示"
	PRINT /
	PRINTBUTTON "[全て非表示]", "発情期フラグ：全て非表示"
	PRINTL
	DRAWLINE
	PRINTL ■ 弱点コマンド
	PRINT 　
	PRINTBUTTON "[弱点コマンドフィルタを起動]", "弱点コマンドフィルタ"
	PRINT  / 
	IF 弱点コマンド検索タイプ == 0
		HTML_PRINT @"<button value='弱点コマンド検索タイプ'>[OR検索 / <font color='#%カラーパレット_HTML("グレーアウト")%'>AND検索</font>]</button>", 1
	ELSEIF 弱点コマンド検索タイプ == 1
		HTML_PRINT @"<button value='弱点コマンド検索タイプ'>[<font color='#%カラーパレット_HTML("グレーアウト")%'>OR検索</font> / AND検索]</button>", 1
	ENDIF
	PRINTL
	IF キャラ一覧フィルタ:弱点コマンド != ""
		PRINT 　 
		VARSET フィルタマッチ配列
		SPLIT キャラ一覧フィルタ:弱点コマンド, "+++", フィルタマッチ配列
		FOR LOCAL, 0, RESULT
			SIF LOCAL > 0
				PRINT ・
			PRINTFORM %弱点の欠片名前表示(フィルタマッチ配列:LOCAL)%
		NEXT
		PRINTL
	ENDIF
	DRAWLINE
	PRINTL ■ 妊娠
	PRINT 　 
	SIF STRCOUNT(キャラ一覧フィルタ:妊娠, @"妊娠フラグ：妊娠中")
		SETCOLOR 0x666666
	PRINTBUTTON @"[妊娠中]", @"妊娠フラグ：妊娠中"
	RESETCOLOR
	PRINT  / 
	SIF STRCOUNT(キャラ一覧フィルタ:妊娠, @"妊娠フラグ：妊娠していない")
		SETCOLOR 0x666666
	PRINTBUTTON @"[妊娠していない]", @"妊娠フラグ：妊娠していない"
	RESETCOLOR

	PRINT 　　　　　
	PRINTBUTTON "[全て表示]", "妊娠フラグ：全て表示"
	PRINT /
	PRINTBUTTON "[全て非表示]", "妊娠フラグ：全て非表示"
	PRINTL
	DRAWLINE
	PRINTL ■ 口上有無
	PRINT 　 
	SIF STRCOUNT(キャラ一覧フィルタ:口上, @"口上：口上あり")
		SETCOLOR 0x666666
	PRINTBUTTON @"[口上あり]", "口上：口上あり"
	RESETCOLOR
	PRINT  / 
	SIF STRCOUNT(キャラ一覧フィルタ:口上, @"口上：口上なし")
		SETCOLOR 0x666666
	PRINTBUTTON @"[口上なし]", "口上：口上なし"
	RESETCOLOR
	PRINT 　　　　　
	PRINTBUTTON "[全て表示]", "口上：全て表示"
	PRINT /
	PRINTBUTTON "[全て非表示]", "口上：全て非表示"
	PRINTL
	DRAWLINE
	PRINTL ■ 固有アビ奥義有無
	PRINT 　 
	SIF STRCOUNT(キャラ一覧フィルタ:固有アビ奥義, @"固有アビ奥義：あり")
		SETCOLOR 0x666666
	PRINTBUTTON @"[固有アビ奥義あり]", "固有アビ奥義：あり"
	RESETCOLOR
	PRINT  / 
	SIF STRCOUNT(キャラ一覧フィルタ:固有アビ奥義, @"固有アビ奥義：なし")
		SETCOLOR 0x666666
	PRINTBUTTON @"[固有アビ奥義なし]", "固有アビ奥義：なし"
	RESETCOLOR
	PRINT 　　　　　
	PRINTBUTTON "[全て表示]", "固有アビ奥義：全て表示"
	PRINT /
	PRINTBUTTON "[全て非表示]", "固有アビ奥義：全て非表示"
	PRINTL
	DRAWLINE
	PRINTBUTTONLC "[999]戻る", 999
	PRINTBUTTONLC "[1000]戦闘系フィルタへ", 1000
	PRINTL
ELSEIF 表示フィルタ種類 == "戦闘系フィルタ"
	PRINTL ■ 属性
	PRINT 　 
	FOR LOCAL, 0, 6
		SIF STRCOUNT(キャラ一覧フィルタ:属性, 属性数値文字列変換(LOCAL))
			SETCOLOR 0x666666
		PRINTBUTTON @"[%属性数値文字列変換(LOCAL)%]", @"属性：%属性数値文字列変換(LOCAL)%"
		RESETCOLOR
		IF LOCAL == 5
			PRINT 　　　　　
			PRINTBUTTON "[全て表示]", "属性：全て表示"
			PRINT /
			PRINTBUTTON "[全て非表示]", "属性：全て非表示"
			PRINTL
		ELSE
			PRINT  / 
		ENDIF
	NEXT
	DRAWLINE
	PRINTL ■ 得意武器
	PRINT 　 
	FOR LOCAL, 0, 10
		SIF STRCOUNT(キャラ一覧フィルタ:得意武器, @"：%得意武器数値文字列変換(LOCAL)%")
			SETCOLOR 0x666666
		PRINTBUTTON @"[%得意武器数値文字列変換(LOCAL)%]", @"得意武器：%得意武器数値文字列変換(LOCAL)%"
		RESETCOLOR
		IF LOCAL == 9
			PRINT 　　　　　
			PRINTBUTTON "[全て表示]", "得意武器：全て表示"
			PRINT /
			PRINTBUTTON "[全て非表示]", "得意武器：全て非表示"
			PRINTL
		ELSE
			PRINT  / 
		ENDIF
	NEXT
	DRAWLINE
	PRINTL ■ タイプ
	PRINT 　 
	FOR LOCAL, 0, 8
		SIF STRCOUNT(キャラ一覧フィルタ:タイプ, タイプ数値文字列変換(LOCAL))
			SETCOLOR 0x666666
		PRINTBUTTON @"[%タイプ数値文字列変換(LOCAL)%]", @"タイプ：%タイプ数値文字列変換(LOCAL)%"
		RESETCOLOR
		IF LOCAL == 7
			PRINT 　　　　　
			PRINTBUTTON "[全て表示]", "タイプ：全て表示"
			PRINT /
			PRINTBUTTON "[全て非表示]", "タイプ：全て非表示"
			PRINTL
		ELSE
			PRINT  / 
		ENDIF
	NEXT
	DRAWLINE
	PRINTBUTTONLC "[999]戻る", 999
	PRINTBUTTONLC "[1001]通常フィルタへ", 1001
	PRINTL
ENDIF

$INPUT_LOOP_ANY
RESULT = 0
RESULTS = 
INPUTANY
IF RESULT == 999
	RETURN 0
ELSEIF RESULT == 1000
	表示フィルタ種類 = 戦闘系フィルタ
	RESTART
ELSEIF RESULT == 1001
	表示フィルタ種類 = 
	RESTART
ENDIF
IF RESULTS != ""
	LOCALS = %RESULTS%
	IF LOCALS == "弱点コマンドフィルタ"
		CALL 弱点コマンド_FILTA()
	ELSEIF LOCALS == "弱点コマンド検索タイプ"
		INVERTBIT 弱点コマンド検索タイプ, 0
	ELSEIF STRCOUNT(LOCALS, "種族")
		LOCAL = 0
	ELSEIF STRCOUNT(LOCALS, "バストサイズ")
		LOCAL = 1
	ELSEIF STRCOUNT(LOCALS, "性知識Lv")
		LOCAL = 2
	ELSEIF STRCOUNT(LOCALS, "発情期フラグ")
		LOCAL = 3
	ELSEIF STRCOUNT(LOCALS, "妊娠フラグ")
		LOCAL = 4
	ELSEIF STRCOUNT(LOCALS, "男性器サイズ")
		LOCAL = 5
	ELSEIF STRCOUNT(LOCALS, "陥落")
		LOCAL = 6
	ELSEIF STRCOUNT(LOCALS, "性別")
		LOCAL = 7
	ELSEIF STRCOUNT(LOCALS, "属性")
		LOCAL = 10
	ELSEIF STRCOUNT(LOCALS, "得意武器")
		LOCAL = 11
	ELSEIF STRCOUNT(LOCALS, "タイプ")
		LOCAL = 12
	ELSEIF STRCOUNT(LOCALS, "固有アビ奥義")
		LOCAL = 18
	ELSEIF STRCOUNT(LOCALS, "口上")
		LOCAL = 19
	ELSE
		GOTO INPUT_LOOP_ANY
	ENDIF
	IF STRCOUNT(LOCALS, "全て表示")
		キャラ一覧フィルタ:LOCAL =
	ELSEIF STRCOUNT(LOCALS, "全て非表示")
		SELECTCASE LOCAL
			CASE 0
				キャラ一覧フィルタ:種族 = "種族：ヒューマン_種族：エルーン_種族：ドラフ_種族：ハーヴィン_種族：星晶獣_種族：その他_"
			CASE 1
				キャラ一覧フィルタ:バストサイズ = "バストサイズ：絶壁（AAA～AA）_バストサイズ：貧乳（A～B）_バストサイズ：並乳（C～D）_バストサイズ：巨乳（E～F）_バストサイズ：爆乳（G～I）_バストサイズ：魔乳（J↑）_バストサイズ：なし_"
			CASE 2
				キャラ一覧フィルタ:性知識Lv = "性知識Lv：無知_性知識Lv：獣並み_性知識Lv：疎い_性知識Lv：耳年増_性知識Lv：必要十分_性知識Lv：有識者_性知識Lv：百戦錬磨_"
			CASE 3
				キャラ一覧フィルタ:発情期 = "発情期フラグ：発情期なし_発情期フラグ：発情中_発情期フラグ：発情期ではない_"
			CASE 4
				キャラ一覧フィルタ:妊娠 = "妊娠フラグ：妊娠中_妊娠フラグ：妊娠していない_"
			CASE 5
				キャラ一覧フィルタ:男性器サイズ = "男性器サイズ：こどもちんこ_男性器サイズ：短小_男性器サイズ：平凡_男性器サイズ：巨根_男性器サイズ：規格外_男性器サイズ：なし_"
			CASE 6
				キャラ一覧フィルタ:陥落 = "陥落：恋慕_陥落：セフレ_陥落：なし_"
			CASE 7
				キャラ一覧フィルタ:性別 = "性別：女性_性別：男性_性別：ふたなり_性別：その他_"
			CASE 10
				キャラ一覧フィルタ:属性 = "属性：火_属性：水_属性：風_属性：土_属性：光_属性：闇_"
			CASE 11
				キャラ一覧フィルタ:得意武器 = "得意武器：剣_得意武器：短剣_得意武器：斧_得意武器：槍_得意武器：弓_得意武器：銃_得意武器：楽器_得意武器：格闘_得意武器：刀_得意武器：杖_"
			CASE 12
				キャラ一覧フィルタ:タイプ = "タイプ：バランス_タイプ：攻撃_タイプ：防御_タイプ：回復_タイプ：サポート_タイプ：連撃_タイプ：回避_タイプ：奥義_"
			CASE 18
				キャラ一覧フィルタ:固有アビ奥義 = "固有アビ奥義：あり_固有アビ奥義：なし_"
			CASE 19
				キャラ一覧フィルタ:口上 = "口上：口上あり_口上：口上なし"
		ENDSELECT
	ELSEIF STRCOUNT(キャラ一覧フィルタ:LOCAL, LOCALS)
		REPLACE キャラ一覧フィルタ:LOCAL, @"%LOCALS%_", ""
		キャラ一覧フィルタ:LOCAL = %RESULTS%
	ELSE
		キャラ一覧フィルタ:LOCAL += @"%LOCALS%_"
	ENDIF
ENDIF
RESTART

@SHOW_CHARADATA_SORT
DRAWLINE
PRINTL キャラクターの表示を任意の条件でソートします。
PRINT 現在のソート条件：
IF キャラ一覧ソート:0 != ""
	PRINTFORM %キャラ一覧ソート:0%(%キャラ一覧ソート:1%)
ELSE
	PRINT なし(キャラ番号順)
ENDIF
PRINT 　　　
IF STRCOUNT(キャラ一覧ソート:1, "昇順")
	PRINTBUTTON "[降順にする]", "降順"
ELSEIF STRCOUNT(キャラ一覧ソート:1, "降順")
	PRINTBUTTON "[昇順にする]", "昇順"
ENDIF
PRINTL
DRAWLINE
PRINTL ■ ソート条件一覧
PRINTBUTTONLC "[なし(キャラ番号順)]", "なし"
PRINTBUTTONLC "[好感度レベル]", "好感度レベル"
PRINTBUTTONLC "[恋慕レベル]", "恋慕レベル"
PRINTBUTTONLC "[コマンド回数]", "コマンド回数"
PRINTBUTTONLC "[画像数]", "画像数"
;#;PRINTBUTTONLC "[チョロさ]", "チョロさ"
PRINTL
DRAWLINE
PRINTBUTTONLC "[Ｃ感覚]", "Ｃ感覚"
PRINTBUTTONLC "[Ｖ感覚]", "Ｖ感覚"
PRINTBUTTONLC "[Ａ感覚]", "Ａ感覚"
PRINTBUTTONLC "[Ｂ感覚]", "Ｂ感覚"
PRINTBUTTONLC "[Ｓ感覚]", "Ｓ感覚"
PRINTBUTTONLC "[欲望]", "欲望"
PRINTBUTTONLC "[露出癖]", "露出癖"
PRINTBUTTONLC "[奉仕精神]", "奉仕精神"
PRINTBUTTONLC "[マゾ性感]", "マゾ性感"
PRINTBUTTONLC "[サド性感]", "サド性感"
PRINTBUTTONLC "[同性愛]", "同性愛"
PRINTBUTTONLC "[玩具嗜好]", "玩具嗜好"
PRINTBUTTONLC "[技巧]", "技巧"
PRINTBUTTONLC "[道具扱い]", "道具扱い"
PRINTL
DRAWLINE
PRINTBUTTONLC "[Lv]", "Lv"
PRINTBUTTONLC "[最大ＨＰ]", "最大ＨＰ"
PRINTBUTTONLC "[最大ＭＰ]", "最大ＭＰ"
PRINTBUTTONLC "[攻撃力]", "攻撃力"
PRINTBUTTONLC "[防御力]", "防御力"
PRINTBUTTONLC "[回復力]", "回復力"
PRINTBUTTONLC "[行動速度]", "行動速度"
PRINTBUTTONLC "[DA率]", "DA率"
PRINTBUTTONLC "[TA率]", "TA率"
PRINTBUTTONLC "[命中率]", "命中率"
PRINTBUTTONLC "[回避率]", "回避率"
PRINTL
DRAWLINE
FOR LOCAL, 1, 100
	SIF ERDNAME(知識素質, LOCAL) == ""
		BREAK
	PRINTBUTTONLC @"[%ERDNAME(知識素質, LOCAL)%]", @"%ERDNAME(知識素質, LOCAL)%"
NEXT
PRINTL
DRAWLINE
FOR LOCAL, 1, 100
	SIF ERDNAME(性癖素質, LOCAL) == ""
		BREAK
	PRINTBUTTONLC @"[%ERDNAME(性癖素質, LOCAL)%]", @"%ERDNAME(性癖素質, LOCAL)%"
NEXT
PRINTL
DRAWLINE

PRINTBUTTON "[999]戻る", 999
PRINTL
$INPUT_LOOP_ANY
RESULT = 0
RESULTS = 
INPUTANY
IF RESULT == 999
	RETURN 0
ENDIF
LOCALS = %RESULTS%
SELECTCASE LOCALS
	CASE "なし"
		キャラ一覧ソート:0 = 
		キャラ一覧ソート:1 = 
		RETURN 0
	CASE "昇順"
		IF STRCOUNT(キャラ一覧ソート:1, "降順")
			キャラ一覧ソート:1 = 昇順
		ENDIF
		RETURN 0
	CASE "降順"
		IF STRCOUNT(キャラ一覧ソート:1, "昇順")
			キャラ一覧ソート:1 = 降順
		ENDIF
		RETURN 0
	CASEELSE
		IF LOCALS == "好感度レベル" || LOCALS == "恋慕レベル" || GETNUM(ABL, LOCALS) >= 0 || GETNUM(装備ステータス補正保存, LOCALS) >= 0 || LOCALS == "Lv" || GETNUM(知識素質, LOCALS) >= 0 || LOCALS == "コマンド回数" || GETNUM(性癖素質, LOCALS) > 0 || LOCALS == "画像数"
			キャラ一覧ソート:0 = %LOCALS%
			IF STRCOUNT(キャラ一覧ソート:1, "昇順")
				キャラ一覧ソート:1 = 昇順
			ELSE
				キャラ一覧ソート:1 = 降順
			ENDIF
			RETURN 0
		ENDIF
		[IF_DEBUG]
		IF LOCALS == "チョロさ"
			キャラ一覧ソート:0 = %LOCALS%
			IF STRCOUNT(キャラ一覧ソート:1, "昇順")
				キャラ一覧ソート:1 = 昇順
			ELSE
				キャラ一覧ソート:1 = 降順
			ENDIF
			RETURN 0
		ENDIF
		[ENDIF]
ENDSELECT
REUSELASTLINE 
GOTO INPUT_LOOP_ANY


@SHOW_CHARADATA_素質絞り込み
DRAWLINE
PRINTL キャラクターの表示を素質の有無で絞り込みします。
PRINTL 複数選択時はAND条件とOR条件を切り替えることができます。
DRAWLINE
VARSET LOCAL
IF キャラ一覧素質絞り込み:0 != ""
	IF キャラ一覧素質絞り込み:99 == "AND"
		PRINT 現在の複数選択時設定：
		PRINTBUTTON "[AND条件]", 201
		PRINTL 
	ELSE
		PRINT 現在の複数選択時設定：
		PRINTBUTTON "[OR条件]", 201
		PRINTL 
	ENDIF
	PRINT 現在の絞り込み素質：
	FOR LOCAL, 0, 99
		LOCALS:0 = 
		LOCALS:1 = 
		SIF キャラ一覧素質絞り込み:LOCAL == ""
			CONTINUE
		SPLIT キャラ一覧素質絞り込み:LOCAL, "_", LOCALS
		PRINTBUTTON @"[%GET_TALENTNAME(TOINT(LOCALS:0), TOINT(LOCALS:1))%]", LOCAL
		LOCAL:10 += STRLENS(GET_TALENTNAME(TOINT(LOCALS:0), TOINT(LOCALS:1)))
		IF LOCAL:10 > 50
			PRINTL
		ENDIF
	NEXT
	PRINTL
	PRINTBUTTON "[全消去]", 200
ELSE
	PRINT 現在の絞り込み素質：なし
ENDIF
PRINTL
DRAWLINE
FOR LOCAL, 0, 1000
	SIF TALENTNAME:LOCAL == ""
		CONTINUE
	SIF LOCAL >= 200 && LOCAL <= 219
		CONTINUE
	SIF GROUPMATCH(LOCAL, 162, 164, 165)
		CONTINUE
	SIF LOCAL < 5
		CONTINUE
	FOR LOCAL:1 , -10, 11
		IF GET_TALENTNAME(LOCAL, LOCAL:1) != ""
			SIF MATCH(キャラ一覧素質絞り込み, @"{LOCAL}_{LOCAL:1}")
				SETCOLOR カラーパレット("黄")
			LOCALS = [%GET_TALENTNAME(LOCAL, LOCAL:1)%]
			PRINTBUTTON @"%LOCALS, 20, LEFT%", @"{LOCAL}_{LOCAL:1}"
			RESETCOLOR
		ENDIF
	NEXT
NEXT
PRINTL
DRAWLINE
PRINTBUTTON "[999]戻る", 999

$INPUT_LOOP_ANY
RESULT = -1
RESULTS = 
VARSET LOCALS
INPUTANY
IF RESULT == 999
	RETURN 0
ELSEIF RESULT == 201
	IF キャラ一覧素質絞り込み:99 == "AND"
		キャラ一覧素質絞り込み:99 = 
		RESTART
	ELSE
		キャラ一覧素質絞り込み:99 = AND
		RESTART
	ENDIF
ELSEIF RESULT == 200
	VARSET キャラ一覧素質絞り込み
	RESTART
ELSEIF RESULT >= 0 && RESULT < 99
	LOCAL:1 = RESULT
	IF キャラ一覧素質絞り込み:(LOCAL:1) != ""
		FOR LOCAL, (LOCAL:1), 99
			キャラ一覧素質絞り込み:LOCAL = %キャラ一覧素質絞り込み:(LOCAL + 1)%
			SIF キャラ一覧素質絞り込み:LOCAL == ""
				BREAK
		NEXT
		RESTART
	ENDIF
ENDIF
LOCALS = %RESULTS%
IF STRCOUNT(LOCALS, "_")
	IF MATCH(キャラ一覧素質絞り込み, LOCALS)
		キャラ一覧素質絞り込み:(FINDELEMENT(キャラ一覧素質絞り込み, LOCALS)) = 
		RESTART
	ELSE
		SPLIT LOCALS, "_", LOCALS
		IF GET_TALENTNAME(TOINT(LOCALS:0), TOINT(LOCALS:1)) != ""
			キャラ一覧素質絞り込み:(FINDELEMENT(キャラ一覧素質絞り込み, "", , , 1)) = %LOCALS:0%_%LOCALS:1%
			RESTART
		ENDIF
	ENDIF
ENDIF
REUSELASTLINE 
GOTO INPUT_LOOP_ANY


@弱点コマンド_FILTA()
#DIMS 設定フィルタ配列, 50
#DIM フィルタマッチ配列番号
#DIM 現在ページ
#DIMS 表示カテゴリ
#DIM 行カウント
#DIM モード数
#DIMS 弱点候補, 10000
#DIM 弱点候補数
#DIM CONST 表示行数 = 40

;既存の設定を配列に突っ込む
SPLIT キャラ一覧フィルタ:弱点コマンド, "+++", 設定フィルタ配列

VARSET 弱点候補
弱点候補数 = 0
;現在のページに該当するコマンドを取得
SELECTCASE 表示カテゴリ
	CASE "モード"
		VARSET RESULTS
		モード数 = ENUMFUNCBEGINSWITH("MODETYPE_")
		FOR LOCAL, 0, モード数
			;モード
			弱点候補:弱点候補数 '= REPLACE(RESULTS:LOCAL, "MODETYPE_", "MODE_")
			弱点候補数 += 1
		NEXT
	CASE "シチュエーション"
		;自動で取る手段がないので力技で全部直接書く
		;キャラ固有のシチュなどは取らない
		弱点候補:弱点候補数 = SITUATION_マッサージモード
		弱点候補数 += 1
		弱点候補:弱点候補数 = SITUATION_むりやりうふふモード
		弱点候補数 += 1
		弱点候補:弱点候補数 = SITUATION_キャラからお誘い
		弱点候補数 += 1
		弱点候補:弱点候補数 = SITUATION_物陰連れ込み
		弱点候補数 += 1
		弱点候補:弱点候補数 = SITUATION_キャラからお誘い
		弱点候補数 += 1
		弱点候補:弱点候補数 = SITUATION_あなたの自慰目撃
		弱点候補数 += 1
		弱点候補:弱点候補数 = SITUATION_呼び出す
		弱点候補数 += 1
		弱点候補:弱点候補数 = SITUATION_押し倒す
		弱点候補数 += 1
		弱点候補:弱点候補数 = SITUATION_性交渉お願い
		弱点候補数 += 1
		弱点候補:弱点候補数 = SITUATION_あなたの挑発に乗る
		弱点候補数 += 1
		弱点候補:弱点候補数 = SITUATION_睡姦からなし崩し
		弱点候補数 += 1
		弱点候補:弱点候補数 = SITUATION_キャラ同士うふふ乱入
		弱点候補数 += 1
		弱点候補:弱点候補数 = SITUATION_キャラの野外オナニー目撃
		弱点候補数 += 1
		弱点候補:弱点候補数 = SITUATION_寝取らせ
		弱点候補数 += 1
	CASE "マッサージ"
		FOR LOCAL, 0, 500
			;通常コマンド
			IF GETBIT(マッサージコマンド存在フラグ:LOCAL, 0)
				弱点候補:弱点候補数 = MASSAGE_{LOCAL}
				弱点候補数 += 1
				;派生コマンド
				FOR LOCAL:1, 0, 20
					SIF !GETBIT(マッサージコマンド存在フラグ:LOCAL, LOCAL:1 + 1)
						CONTINUE
					弱点候補:弱点候補数 = MASSAGE_{LOCAL}_{LOCAL:1}
					弱点候補数 += 1
				NEXT
			ENDIF
		NEXT
	CASE "むりやりうふふ"
		FOR LOCAL, 0, 500
			;通常コマンド
			IF GETBIT(むりやりうふふコマンド存在フラグ:LOCAL, 0)
				弱点候補:弱点候補数 = RAPE_{LOCAL}
				弱点候補数 += 1
				;派生コマンド
				FOR LOCAL:1, 0, 20
					SIF !GETBIT(むりやりうふふコマンド存在フラグ:LOCAL, LOCAL:1 + 1)
						CONTINUE
					弱点候補:弱点候補数 = RAPE_{LOCAL}_{LOCAL:1}
					弱点候補数 += 1
				NEXT
			ENDIF
		NEXT
	CASEELSE
		FOR LOCAL, 0, 1000
			;お祭りコマンドは弱点候補にしない
			SIF INRANGE(LOCAL, 385, 389)
				CONTINUE
			;専用コマンドはフィルタリングに不適なので飛ばす
			IF INRANGE(LOCAL, 370, 374)
				CONTINUE
			;場所固有コマンドはフィルタリングに不適なので飛ばす
			ELSEIF INRANGE(LOCAL, 380, 384)
				CONTINUE
				; FOR LOCAL:1, 0, 20
				; 	FOR LOCAL:2, 0, 20
				; 		SIF !GETBIT(場所固有コマンド存在フラグ:(LOCAL - 380):(LOCAL:5):(LOCAL:1), LOCAL:2)
				; 			CONTINUE
				; 		LOCAL:3 = LOCAL:1 * 100 + LOCAL : 2 + 1
				; 		弱点候補:弱点候補数 = {LOCAL}_{LOCAL:5}_{LOCAL:3}
				; 		弱点候補数 += 1
				; 	NEXT
				; NEXT
			ENDIF
			;通常コマンド
			IF GETBIT(通常コマンド存在フラグ:LOCAL, 0)
				弱点候補:弱点候補数 = {LOCAL}
				弱点候補数 += 1
				;派生コマンド
				FOR LOCAL:1, 0, 20
					SIF !GETBIT(通常コマンド存在フラグ:LOCAL, LOCAL:1 + 1)
						CONTINUE
					弱点候補:弱点候補数 = {LOCAL}_{LOCAL:1}
					弱点候補数 += 1
				NEXT
			ENDIF
		NEXT
ENDSELECT

$表示処理
DRAWLINE
PRINTL 表示したい弱点コマンドを選択してください。
DRAWLINE
PRINTL

FOR 行カウント, 現在ページ * 表示行数, 現在ページ * 表示行数 + 表示行数
	SIF 行カウント >= 弱点候補数
		BREAK
	SIF 行カウント != 現在ページ * 表示行数 && 行カウント % 4 == 0
		PRINTL
	SIF MATCH(設定フィルタ配列, 弱点候補:行カウント)
		SETCOLOR カラーパレット("黄")
	PRINTBUTTON @"%@"[%弱点の欠片名前表示(弱点候補:行カウント)%]", 32, LEFT%", 行カウント
	RESETCOLOR
NEXT
PRINTL
DRAWLINE

IF 現在ページ > 0
	PRINTBUTTON "[900] 前ページへ", 900
ELSE
	PRINT 　　　　　　　　
ENDIF
PRINT 　　　
IF 弱点候補数 > 現在ページ * 表示行数 + 表示行数
	PRINTBUTTON "[901] 次ページへ", 901
ELSE
	PRINT 　　　　　　　　
ENDIF
PRINT 　　　
PRINTBUTTON "[902] 選択全解除", 902
PRINTL 
PRINTL 

IF 表示カテゴリ == ""
	SETCOLOR カラーパレット("黄")
	PRINTPLAIN [1014] 通常コマンド　
	RESETCOLOR
ELSE
	PRINTBUTTON "[1014] 通常コマンド　", 1014
ENDIF
PRINT  / 
IF 表示カテゴリ == "モード"
	SETCOLOR カラーパレット("黄")
	PRINTPLAIN [1015] モード弱点　　
	RESETCOLOR
ELSE
	PRINTBUTTON "[1015] モード弱点　　", 1015
ENDIF
PRINT  / 
IF 表示カテゴリ == "シチュエーション"
	SETCOLOR カラーパレット("黄")
	PRINTPLAIN [1016] シチュ弱点　　
	RESETCOLOR
ELSE
	PRINTBUTTON "[1016] シチュ弱点　　", 1016
ENDIF
PRINT  / 
IF 表示カテゴリ == "マッサージ"
	SETCOLOR カラーパレット("黄")
	PRINTPLAIN [1017] マッサージ弱点
	RESETCOLOR
ELSE
	PRINTBUTTON "[1017] マッサージ弱点", 1017
ENDIF
PRINT  / 
IF 表示カテゴリ == "むりやりうふふ"
	SETCOLOR カラーパレット("黄")
	PRINTPLAIN [1018] むりやり弱点　
	RESETCOLOR
ELSE
	PRINTBUTTON "[1018] むりやり弱点　", 1018
ENDIF
PRINTL
PRINTL
PRINTBUTTON "[999] 戻る", 999
PRINTL
DRAWLINE

BINPUT
SELECTCASE RESULT
	CASE 999
		現在ページ = 0
		表示カテゴリ = 
		;フィルタ配列を全部セット
		キャラ一覧フィルタ:弱点コマンド = 
		FOR 行カウント, 0, 50
			SIF 設定フィルタ配列:行カウント == ""
				CONTINUE
			SIF 行カウント > 0
				キャラ一覧フィルタ:弱点コマンド += "+++"
			キャラ一覧フィルタ:弱点コマンド += @"%設定フィルタ配列:行カウント%"
		NEXT
		RETURN 0
	CASE 1014
		表示カテゴリ = 
	CASE 1015
		表示カテゴリ = モード
	CASE 1016
		表示カテゴリ = シチュエーション
	CASE 1017
		表示カテゴリ = マッサージ
	CASE 1018
		表示カテゴリ = むりやりうふふ
	CASE 900
		現在ページ --
		GOTO 表示処理
	CASE 901
		現在ページ ++
		GOTO 表示処理
	CASE 902
		VARSET 設定フィルタ配列
		GOTO 表示処理
	CASEELSE
		設定フィルタ配列:(FINDELEMENT(設定フィルタ配列, "", , , 1)) '= 弱点候補:RESULT
		GOTO 表示処理
ENDSELECT

RESTART
