;ARGS 文字列の初期値　ARG キャラ登録番号　MODE 表示分制御用
@KSTR構文入力(ARGS, ARG, MODE="")
#DIMS DYNAMIC コメント
#DIMS MODE
#DIMS CONST 修飾_K = "_B_", "_I_", "_LINE_", "_-_"
#DIMS CONST 修飾_H = "<b>太字</b>", "<i>斜体</i>", "<u>下線</u>", "<s>打消</s>"
#DIMS CONST L_COLOR = "真っ赤", "青", "黄", "緑", "紫", "ピンク", "えっちな色"
#DIMS L_COLOR_K, 7
#DIMS CONST 置換_K = "_主人_", "_呼び名_", "_1st_", "_2nd_", "_H_", "_A_"
#DIMS CONST 置換_H = "「あなた」の呼び名", "キャラの呼び名", "一人称", "二人称", "<img src='えっちハート' height='112' width='112'>", "<img src='怒りマーク' height='112' width='112'>"
#DIMS Column_M
#DIMS Column_S
#DIM L_CNT
#DIM L_CNT2
#DIM 入力補助
#DIM L_COUNT
#DIMS DYNAMIC マウスオーバー

VARSET LOCAL
VARSET LOCALS
コメント '= ARGS

FOR LOCAL, 0, VARSIZE("L_COLOR")
	L_COLOR_K:LOCAL '= @"_COLOR_%カラーパレット_HTML(L_COLOR:LOCAL)%_"
NEXT

TOOLTIP_CUSTOM 1
TOOLTIP_SETFONTSIZE 12
TOOLTIP_SETDURATION 100000
TOOLTIP_SETFONT "Ariel"

$入力_LOOP
SETTEXTBOX コメント
L_COUNT = LINECOUNT
PRINTFORML \@ MODE == "コメント_解説"? 解説文# コメント\@を入力してください（半角の「,」「/」「|」「(」「)」は使用しないでください。
PRINT 半角の「
FONTBOLD
PRINT ＿
FONTREGULAR
PRINTL 」(アンダーバー)は下に示すような特殊構文としてのみ使用してください）

SIF MODE == "コメント_解説"
	PRINTL （解説文中に空白で改行を入れたい場合、その行には全角スペースを入力してください）

IF 入力補助 == 0
	Column_M '= "<clearbutton>"
ELSE
	Column_M '= @"<font color='#%カラーパレット_HTML("黄")%'>　＊＊＊ボタンをクリックする前にはEnterキーを押して入力を確定してください。入力内容が消える場合があります。＊＊＊</font><br>"
ENDIF

Column_M += "　　文字を修飾：　"
FOR LOCAL, 0, VARSIZE("修飾_K")
	Column_M += @"<button title='<br>　%修飾_K:LOCAL%　<br>' value='%修飾_K:LOCAL%'>[%修飾_H:LOCAL%]</button>　"
NEXT

FOR LOCAL, 0, VARSIZE("L_COLOR")
	Column_M += @"<button title='<br>　%L_COLOR_K:LOCAL%　<br>' value='%L_COLOR_K:LOCAL%'>"
	Column_M += @"<font color='#%カラーパレット_HTML(L_COLOR:LOCAL)%'>"
	Column_M += @"[%L_COLOR:LOCAL%\@ STRLENS(L_COLOR:LOCAL) < 4 ? 色# \@]</font></button>　"
NEXT
Column_M += "<button title='<br>　_　<br>' value='_'>修飾終了</button><br>"

Column_M += "　　特殊文字　：　"
FOR LOCAL, 0, VARSIZE("置換_K")
	Column_M += @"<button title='<br>　%置換_K:LOCAL%　<br>' value='%置換_K:LOCAL%'>[%置換_H:LOCAL%]</button>　"
NEXT

IF 入力補助 == 0
	;INPUTS関数を用いる。ボタンは使用せず、ロールオーバーで構文を表示する
	Column_M += "</clearbutton>"
	Column_M += "<br>" + "　" * 30 + "<button title='各種特殊構文をボタン入力可能にする<br>ボタン入力した場合、入力前の内容の最後に特殊構文が追記される' value='100'>[100] 入力補助切り替え（OFF→ON） </button>"
	Column_M += "<button title='入力をキャンセルし、入力前の内容に戻す' value='999'>[999] キャンセル </button>"
	HTML_PRINT Column_M
	INPUTS
	IF RESULTS == "999"
		RESULTS '= ARGS
		RETURN 0
	ELSEIF RESULTS == "100"
		入力補助 = 1
		CLEARLINE LINECOUNT - L_COUNT
		GOTO 入力_LOOP
	ELSE
		RETURN 1
	ENDIF
ELSE
	;INPUTS関数を用いる。ボタンを使用し、ボタン押下時は入力前の内容の最後尾に追記する
	Column_M += "<br>" + "　" * 30 + "<button title='各種特殊構文をボタン入力を無効にする<br>特殊構文を使いたい場合、ツールチップに表示される文字列を手打ちしてください' value='100'>[100] 入力補助切り替え（ ON → OFF ） </button>"
	Column_M += "<button title='入力を確定させ、選択に戻る' value='900'>[900] 確定　</button>"
	Column_M += "<button title='入力をキャンセルし、入力前の内容に戻す' value='999'>[999] キャンセル</button>"
	HTML_PRINT Column_M
	INPUTS
	IF MATCH(修飾_K, RESULTS) || MATCH(L_COLOR_K, RESULTS) || MATCH(置換_K, RESULTS) || RESULTS == "_"
		コメント += RESULTS
	ELSEIF GROUPMATCH(RESULTS, "100", "900", "999")
		IF RESULTS == "999"
			RESULTS '= ARGS
			RETURN 0
		ELSEIF RESULTS == "900"
			RESULTS '= コメント
			RETURN 1
		ELSEIF RESULTS == "100"
			入力補助 = 0
		ELSE
			RETURN 1
		ENDIF
	ELSE
		コメント = %RESULTS%
	ENDIF
	CLEARLINE LINECOUNT - L_COUNT
	GOTO 入力_LOOP
ENDIF

RETURN 0
