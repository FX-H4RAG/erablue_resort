@従業員仕事割り振り
#DIM 従業員一覧, 500
#DIM ページ数
VARSET 従業員一覧, -1
VARSET 仕事人数表示用

DRAWLINE
PRINTL 従業員が行う仕事の設定を行います。
PRINTL ※開発中のため、仕事が正しく挙動しないことがあります。
DRAWLINE
LOCALS = <div rect='81,31,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='2050,31,3875,2812' border='31' bcolor='#C0C0C0'></div>

;現在いる従業員を一覧表示
従業員一覧:0 = 0
LOCAL:1 = 1
仕事人数表示用:(現在仕事:0:0) += 1
FOR LOCAL, 1, CHARANUM
	SIF TALENT:LOCAL:従業員 == 0
		CONTINUE
	従業員一覧:(LOCAL:1) = LOCAL
	仕事人数表示用:(現在仕事:(従業員一覧:(LOCAL:1)):0) += 1
	LOCAL:1 += 1
NEXT
LOCALS += "<div rect='156,156,1737,2812'><nobr>"
FOR LOCAL, ページ数 * 17, ページ数 * 17 + 17
	IF 従業員一覧:LOCAL < 0
		LOCALS += "<br>"
		CONTINUE
	ENDIF
	LOCALS += @"<button value='{LOCAL}'>%CALLNAME:(従業員一覧:LOCAL), 16, LEFT%[%現在仕事数字文字列変換(現在仕事:(従業員一覧:LOCAL):0)%"
	IF 現在仕事:(従業員一覧:LOCAL):0 == 2
		LOCALS = %SUBSTRING(LOCALS, 0, STRLENS(LOCALS) - 4)%
		LOCALS += @"：%MAP_GET("常駐業務名", GETPLACENAME(現在仕事:(従業員一覧:LOCAL):1 / 10000, 現在仕事:(従業員一覧:LOCAL):1 % 10000))%"
	ENDIF
	LOCALS += "]</button>"
	LOCALS += "<br>"
NEXT
IF ページ数 > 0
	LOCALS += "<button value='1000'>　＜ 前ページ</button>"
ELSE
	LOCALS += "　　　　　　　 "
ENDIF
LOCALS += "　　　　"
IF 従業員一覧:(ページ数 * 17 + 17) > -1
	LOCALS += "<button value='1001'>次ページ ＞</button>"
ENDIF
LOCALS += "<br>"
LOCALS += "<br>"
LOCALS += "<button value='999'>[999] 戻る</button>"
LOCALS += "<br>"
LOCALS += "</nobr></div>"

;仕事の種類と現在そこに従事している人数を表示
LOCALS += "<div rect='2125,156,3675,2812'>"
LOCALS += "仕事内容と従事人数一覧<br><br>"
FOR LOCAL, 0, 10
	IF 現在仕事数字文字列変換(LOCAL) != ""
		LOCALS += @"■%現在仕事数字文字列変換(LOCAL), 16, LEFT%：従事人数{仕事人数表示用:LOCAL,3}人<br>"
	ENDIF
NEXT
LOCALS += "</div>"


HTML_PRINT LOCALS
FOR LOCAL, 0, 25
	PRINTL
NEXT
DRAWLINE

BINPUT

LOCAL = RESULT
SELECTCASE LOCAL
	CASE 1000
		ページ数 -= 1
		RESTART
	CASE 1001
		ページ数 += 1
		RESTART
	CASE 999
		ページ数 = 0
		RETURN 0
	CASE 0 TO 499
		CALL 従業員仕事割り振り_切り替え(従業員一覧:LOCAL)
		RESTART
ENDSELECT

@従業員仕事割り振り_切り替え(ARG)

DRAWLINE
PRINTL 従業員が行う仕事の設定を行います。
PRINTL ※開発中のため、仕事が正しく挙動しないことがあります。
DRAWLINE
LOCALS = <div rect='81,31,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='2050,31,3875,2812' border='31' bcolor='#C0C0C0'></div>
LOCALS += "<div rect='156,156,1937,2812'>"
LOCALS += @"■%CALLNAME:ARG%の業務を<br>"
FOR LOCAL, 0, 10
	IF 現在仕事数字文字列変換(LOCAL) != ""
		SIF 仕事条件判定関数(ARG, LOCAL) == 0
			CONTINUE
		IF 現在仕事:ARG:0 == LOCAL
			LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[{LOCAL}] %現在仕事数字文字列変換(LOCAL), 14, LEFT%</font><br>"
		ELSE
			LOCALS += @"<button value='{LOCAL}'>[{LOCAL}] %現在仕事数字文字列変換(LOCAL), 14, LEFT%に変更</button><br>"
		ENDIF
	ENDIF
NEXT
LOCALS += "<br>"
LOCALS += "<button value='999'>[999] 戻る</button>"
LOCALS += "<br>"
LOCALS += "</div>"

;仕事の説明を表示
LOCALS:1 = <div rect='2125,156,3675,2812'>
LOCALS:1 += "現在従事している業務<br>"
LOCALS:1 += @"■%現在仕事数字文字列変換(現在仕事:ARG:0), 16, LEFT%"
LOCALS:1 += "</div>"
LOCALS:1 += "<div rect='2312,437,3675,2812'>"
LOCALS:1 += @"%現在仕事数字文字列変換(現在仕事:ARG:0, 1, ARG)%"
LOCALS:1 += "<br><br>"
;仕事レベルが存在するなら表示
IF 仕事レベル存在チェック(現在仕事:ARG:0)
	LOCALS:1 += @"現在の%現在仕事数字文字列変換(現在仕事:ARG:0)%レベル：{仕事レベル:ARG:(現在仕事:ARG:0)}"
ELSE
	LOCALS:1 += @"仕事レベルなし"
ENDIF
LOCALS:1 += "</div>"


HTML_PRINT LOCALS, 1
HTML_PRINT LOCALS:1, 1
FOR LOCAL, 0, 25
	PRINTL
NEXT
DRAWLINE

$INPUT_LOOP
BINPUT

LOCAL = RESULT
SELECTCASE LOCAL
	CASE 999
		IF 現在仕事:ARG:0 == 2 && 現在仕事:ARG:1 == 0
			現在仕事:ARG:0 = 0
		ENDIF
		IF 現在仕事:ARG:0 == 7
			SIF 現在仕事:ARG:1 == 0
				現在仕事:ARG:0 = 0
			IF 知識素質:ARG:(現在仕事:ARG:1) > 1
				現在仕事:ARG:0 = 0
				現在仕事:ARG:1 = 0
			ENDIF
		ENDIF
		IF 現在仕事:ARG:0 == 8
			SIF 現在仕事:ARG:1 == 0
				現在仕事:ARG:0 = 0
			IF 知識素質:ARG:(現在仕事:ARG:1) < 2
				現在仕事:ARG:0 = 0
				現在仕事:ARG:1 = 0
			ENDIF
		ENDIF
		RETURN 0
	CASE 0 TO 8
		IF 仕事条件判定関数(ARG, LOCAL) == 0
			PRINTFORMW %CALLNAME:ARG%は%現在仕事数字文字列変換(LOCAL)%の仕事に付くことは出来ません。
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		現在仕事:ARG:0 = LOCAL
		現在仕事:ARG:1 = 0
		RESTART
	CASEELSE
		CALL 仕事追加処理関数(ARG, LOCAL)
		RESTART
ENDSELECT

@現在仕事数字文字列変換(ARG, ARG:1 = 0, キャラ番号 = 0)
#FUNCTIONS
#DIM キャラ番号

SELECTCASE ARG
	CASE 0
		IF ARG:1
			RETURNF "特に仕事を申し付けず、待機している状態。<br>効果：特になし<br>　　　通常の客と同じように行動する。"
		ENDIF
		RETURNF "なし"
	CASE 1
		IF ARG:1
			RETURNF "リゾート内の細々とした雑務を処理する業務。<br>効果：一日の最後に、全ての場所の雑務ゲージが<br>　　　［（仕事レベル＋１）＾２×１００］減少する。<br>　　　リゾート内をランダムに移動する。"
		ENDIF
		RETURNF "雑務"
	CASE 2
		IF ARG:1
			LOCALS = 特定の場所で専門的な仕事を行う業務。<br>効果：一日の最後に、ルピが<br>　　　［滞在者数×（仕事レベル＋１）＾３］増加する。
			LOCALS += "<br><br>"
			IF 現在仕事:キャラ番号:1 == 2
				LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[102] ロビー ：現在常駐場所</font><br>"
			ELSE
				LOCALS += @"<button value='102'>[102] ロビー でロビー受付として働く</button><br>"
			ENDIF
			IF 施設改造度:5:0
				IF 現在仕事:キャラ番号:1 == 503
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[503] バーエリア ：現在常駐場所</font><br>"
				ELSE
					LOCALS += @"<button value='503'>[503] バーエリア でバーテンダーとして働く</button><br>"
				ENDIF
			ENDIF
			IF 施設改造度:5:6
				IF 現在仕事:キャラ番号:1 == 506
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[506] 図書室 ：現在常駐場所</font><br>"
				ELSE
					LOCALS += @"<button value='506'>[506] 図書室 で司書として働く</button><br>"
				ENDIF
			ENDIF
			IF 施設改造度:15:0
				IF 現在仕事:キャラ番号:1 == 1503
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[1503] プールサイド ：現在常駐場所</font><br>"
				ELSE
					LOCALS += @"<button value='1503'>[1503] プールサイド で監視員として働く</button><br>"
				ENDIF
			ELSE
				IF 現在仕事:キャラ番号:1 == 15
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[15] プール ：現在常駐場所</font><br>"
				ELSE
					LOCALS += @"<button value='15'>[15] プール で監視員として働く</button><br>"
				ENDIF
			ENDIF
			IF 空艇港初来訪イベントフラグ
				FOR LOCAL, 3, 9
					IF 空艇港店舗状況:LOCAL:0 > 0
						CALLFORMF 空艇港店舗名表示_{空艇港店舗状況:LOCAL:0}
						IF 現在仕事:キャラ番号:1 == LOCAL + 10000
							LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[{10000 + LOCAL}] 店舗{LOCAL}:%TSTR:場所名受渡% ：現在常駐場所</font><br>"
						ELSE
							LOCALS += @"<button value='{10000 + LOCAL}'>[{10000 + LOCAL}] 店舗{LOCAL}:%TSTR:場所名受渡% で店員として働く</button><br>"
						ENDIF
					ENDIF
				NEXT
			ENDIF
			RETURNF LOCALS
		ENDIF
		RETURNF "常駐業務"
	CASE 3
		IF ARG:1
			RETURNF @"%CALLNAME:PLAYER%の補佐を行う業務。<br>効果：一日の最後に、好感度要素：信頼度が<br>　　　［（仕事レベル＋１）＾２×２００］増加する。<br>　　　リゾート内をランダムに移動する。"
		ENDIF
		RETURNF "秘書"
	CASE 4
		IF ARG:1
			RETURNF @"手紙や広告などでリゾートを宣伝する業務。<br>効果：一日の最後に、リゾートの人気度が<br>　　　［（仕事レベル＋１）＾２×１］増加する。<br>　　　従業員部屋の自室でチラシ作りなどを行う。"
		ENDIF
		RETURNF "宣伝"
	CASE 5
		IF 施設改造度:3:8 == 0
			RETURNF ""
		ENDIF
		IF ARG:1
			RETURNF @"訓練場で戦闘能力を鍛える業務。<br>効果：一日の最後にEXPを獲得する。<br>　　　獲得量は全キャラ中最も高いレベルに応じて変動する。<br>　　　（全キャラ中最も高いレベルより上には成長しない）<br>　　　訓練場に常駐する。"
		ENDIF
		RETURNF "訓練"
	CASE 6
		IF あなた特殊能力:ダンジョン探索PRO < 1
			RETURNF ""
		ENDIF
		IF ARG:1
			RETURNF @"クリア済みのダンジョンに赴き、素材を集めてくる業務。<br>効果：一日の最後に素材やEXPを獲得する。<br>　　　獲得量は赴いたダンジョンによって変動する。<br>　　　（赴くダンジョンの設定は「探索へ出かける」から行う）<br>　　　%CALLNAME:MASTER%が探索をしていない日は効果が増加する。<br>　　　日中はリゾートにおらず、夕方以降は自室にいる。<br>　　　この業務に業務レベルは存在しない。"
		ENDIF
		RETURNF "自動探索"
	CASE 7
		IF 居住区改造度:0:0 == 0
			RETURNF ""
		ENDIF
		IF ARG:1
			LOCALS = 学校に赴き、指定された知識について学習する業務。<br>効果：午前と午後の学習終了時に知識経験値を得る。<br>　　　学校にいない時間帯は自室にいる。<br>　　　Lv2に到達した知識は選択出来ない。
			LOCALS += "<br><br>"
			LOCAL:1 = 0
			FOR LOCAL, 1, 100
				SIF ERDNAME(知識素質, LOCAL) == ""
					BREAK
				IF 知識素質:キャラ番号:LOCAL > 1
					LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[{LOCAL + 100}] %ERDNAME(知識素質, LOCAL), 9, LEFT%を学ぶ(現在Lv{知識素質:キャラ番号:LOCAL})</font>　　　"
				ELSEIF 現在仕事:キャラ番号:1 == LOCAL
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[{LOCAL + 100}] %ERDNAME(知識素質, LOCAL), 9, LEFT%を学ぶ(現在Lv{知識素質:キャラ番号:LOCAL})選択</font>　"
				ELSE
					LOCALS += @"<button value='{LOCAL + 100}'>[{LOCAL + 100}] %ERDNAME(知識素質, LOCAL), 9, LEFT%を学ぶ(現在Lv{知識素質:キャラ番号:LOCAL})</button>　　　"
				ENDIF
				LOCAL:1 ++
				SIF LOCAL:1 % 2 == 0
					LOCALS += "<br>"
			NEXT
			RETURNF LOCALS
		ENDIF
		RETURNF "知識習得"
	CASE 8
		IF 居住区改造度:0:0 == 0
			RETURNF ""
		ENDIF
		IF ARG:1
			LOCALS = 学校に赴き、指定された知識について講義を行う業務。<br>効果：学校で学ぶキャラの学習効率が上昇する。<br>　　　学校にいない時間帯は自室にいる。<br>　　　Lv2以上の知識のみ選択可能。
			LOCALS += "<br><br>"
			LOCAL:1 = 0
			FOR LOCAL, 1, 100
				SIF ERDNAME(知識素質, LOCAL) == ""
					BREAK
				IF 知識素質:キャラ番号:LOCAL < 2
					LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'>[{LOCAL + 100}] %ERDNAME(知識素質, LOCAL), 9, LEFT%教師(現在Lv{知識素質:キャラ番号:LOCAL})</font>　　　　"
				ELSEIF 現在仕事:キャラ番号:1 == LOCAL
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[{LOCAL + 100}] %ERDNAME(知識素質, LOCAL), 9, LEFT%教師(現在Lv{知識素質:キャラ番号:LOCAL})選択</font>　　"
				ELSE
					LOCALS += @"<button value='{LOCAL + 100}'>[{LOCAL + 100}] %ERDNAME(知識素質, LOCAL), 9, LEFT%教師(現在Lv{知識素質:キャラ番号:LOCAL})</button>　　　　"
				ENDIF
				LOCAL:1 ++
				SIF LOCAL:1 % 2 == 0
					LOCALS += "<br>"
			NEXT
			RETURNF LOCALS
		ENDIF
		RETURNF "教師"
ENDSELECT

@仕事条件判定関数(キャラ番号, 仕事番号)
#FUNCTION
#DIM キャラ番号
#DIM 仕事番号

SELECTCASE 仕事番号
	CASE 2,3
		SIF キャラ番号 == PLAYER
			RETURNF 0
	CASE 5,6
		SIF キャラ番号 == PLAYER
			RETURNF 0
		SIF TALENT:キャラ番号:非戦闘キャラ
			RETURNF 0
ENDSELECT

RETURNF 1


@仕事追加処理関数(キャラ番号, 選択番号)
#DIM キャラ番号
#DIM 選択番号

SELECTCASE 現在仕事:キャラ番号:0
	CASE 2
		;常駐する店舗を選べる
		IF 選択番号 == 102
			現在仕事:キャラ番号:1 = 2
		ELSE
			現在仕事:キャラ番号:1 = 選択番号
		ENDIF
	CASE 7
		;知識を選べる
		現在仕事:キャラ番号:1 = 選択番号 - 100
	CASE 8
		;知識を選べる
		現在仕事:キャラ番号:1 = 選択番号 - 100
ENDSELECT

@仕事レベル存在チェック(チェック番号)
#FUNCTION
#DIM チェック番号

IF GROUPMATCH(チェック番号, 1, 2, 3, 4)
	RETURNF 1
ELSE
	RETURNF 0
ENDIF

@従業員休暇判定(対象キャラ)
#FUNCTION
#DIM 対象キャラ

;ゲーム中に変動しない数値として、人物番号を使う
IF DAY % 7 == CFLAG:対象キャラ:人物番号 % 7
	RETURNF 1
ENDIF

RETURNF 0