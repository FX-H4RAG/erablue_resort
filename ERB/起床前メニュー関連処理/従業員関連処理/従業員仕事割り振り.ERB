@従業員仕事割り振り
#DIM 従業員一覧, 50
VARSET 従業員一覧, -1
VARSET 仕事人数表示用

DRAWLINE
PRINTL 従業員が行う仕事の設定を行います。
PRINTL ※開発中のため、仕事が正しく挙動しないことがあります。
DRAWLINE
LOCALS = <div rect='81,31,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='2050,31,3875,2812' border='31' bcolor='#C0C0C0'></div>

;現在いる従業員を一覧表示
従業員一覧:0 = 0
LOCAL:1 = 1
FOR LOCAL, 1, CHARANUM
	SIF TALENT:LOCAL:従業員 == 0
		CONTINUE
	従業員一覧:(LOCAL:1) = LOCAL
	LOCAL:1 += 1
NEXT
LOCALS += "<div rect='156,156,1937,2812'>"
FOR LOCAL, 0, 50
	SIF 従業員一覧:LOCAL < 0
		BREAK
	仕事人数表示用:(現在仕事:(従業員一覧:LOCAL):0) += 1
	LOCALS += @"<button value='{LOCAL}'>%CALLNAME:(従業員一覧:LOCAL), 16, LEFT%[%現在仕事数字文字列変換(現在仕事:(従業員一覧:LOCAL):0)%]</button>"
	LOCALS += "<br>"
NEXT
LOCALS += "<br>"
LOCALS += "<button value='999'>[999] 戻る</button>"
LOCALS += "<br>"
LOCALS += "</div>"

;仕事の種類と現在そこに従事している人数を表示
LOCALS += "<div rect='2125,156,3675,2812'>"
LOCALS += "仕事内容と従事人数一覧<br><br>"
FOR LOCAL, 0, 10
	IF 現在仕事数字文字列変換(LOCAL) != ""
		LOCALS += @"■%現在仕事数字文字列変換(LOCAL), 16, LEFT%：従事人数{仕事人数表示用:LOCAL,3}人<br>"
	ENDIF
NEXT
LOCALS += "</div>"


HTML_PRINT LOCALS
FOR LOCAL, 0, 25
	PRINTL
NEXT
DRAWLINE

$INPUT_LOOP
INPUT

LOCAL = RESULT
SELECTCASE LOCAL
	CASE 999
		RETURN 0
	CASE 0 TO 49
		IF 従業員一覧:LOCAL < 0
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		CALL 従業員仕事割り振り_切り替え(従業員一覧:LOCAL)
		RESTART
	CASEELSE
		REUSELASTLINE 
		GOTO INPUT_LOOP
ENDSELECT

@従業員仕事割り振り_切り替え(ARG)

DRAWLINE
PRINTL 従業員が行う仕事の設定を行います。
PRINTL ※開発中のため、仕事が正しく挙動しないことがあります。
DRAWLINE
LOCALS = <div rect='81,31,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='2050,31,3875,2812' border='31' bcolor='#C0C0C0'></div>
LOCALS += "<div rect='156,156,1937,2812'>"
FOR LOCAL, 0, 10
	IF 現在仕事数字文字列変換(LOCAL) != ""
		SIF ARG == PLAYER && LOCAL == 3
			CONTINUE
		SIF ARG == PLAYER && LOCAL == 2
			CONTINUE
		IF 現在仕事:ARG:0 == LOCAL
			LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[{LOCAL}] %現在仕事数字文字列変換(LOCAL), 14, LEFT%</font><br>"
		ELSE
			LOCALS += @"<button value='{LOCAL}'>[{LOCAL}] %現在仕事数字文字列変換(LOCAL), 14, LEFT%に変更</button><br>"
		ENDIF
	ENDIF
NEXT
LOCALS += "<br>"
LOCALS += "<button value='999'>[999] 戻る</button>"
LOCALS += "<br>"
LOCALS += "</div>"

;仕事の説明を表示
LOCALS:1 = <div rect='2125,156,3675,2812'>
LOCALS:1 += "現在従事している業務<br>"
LOCALS:1 += @"■%現在仕事数字文字列変換(現在仕事:ARG:0), 16, LEFT%"
LOCALS:1 += "</div>"
LOCALS:1 += "<div rect='2312,437,3675,2812'>"
LOCALS:1 += @"%現在仕事数字文字列変換(現在仕事:ARG:0, 1, ARG)%"
LOCALS:1 += "<br><br>"
LOCALS:1 += @"現在の%現在仕事数字文字列変換(現在仕事:ARG:0)%レベル：{仕事レベル:ARG:(現在仕事:ARG:0)}"
LOCALS:1 += "</div>"


HTML_PRINT LOCALS, 1
HTML_PRINT LOCALS:1, 1
FOR LOCAL, 0, 25
	PRINTL
NEXT
DRAWLINE

$INPUT_LOOP
BINPUT

LOCAL = RESULT
SELECTCASE LOCAL
	CASE 999
		IF 現在仕事:ARG:0 == 2 && 現在仕事:ARG:1 == 0
			現在仕事:ARG:0 = 0
		ENDIF
		RETURN 0
	CASE 0 TO 5
		IF 仕事条件判定関数(ARG, LOCAL) == 0
			PRINTFORMW %CALLNAME:ARG%は%現在仕事数字文字列変換(LOCAL)%の仕事に付くことは出来ません。
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		現在仕事:ARG:0 = LOCAL
		現在仕事:ARG:1 = 0
		RESTART
	CASEELSE
		CALL 仕事追加処理関数(ARG, LOCAL)
		RESTART
ENDSELECT

@現在仕事数字文字列変換(ARG, ARG:1 = 0, キャラ番号 = 0)
#FUNCTIONS
#DIM キャラ番号

SELECTCASE ARG
	CASE 0
		IF ARG:1
			RETURNF "特に仕事を申し付けず、待機している状態。<br>効果：特になし<br>　　　通常の客と同じように行動する。"
		ENDIF
		RETURNF "なし"
	CASE 1
		IF ARG:1
			RETURNF "リゾート内の細々とした雑務を処理する業務。<br>効果：一日の最後に、全ての場所の雑務ゲージが<br>　　　［（仕事レベル＋１）＾２×１００］減少する。<br>　　　リゾート内をランダムに移動する。"
		ENDIF
		RETURNF "雑務"
	CASE 2
		IF ARG:1
			LOCALS = 特定の場所で専門的な仕事を行う業務。<br>効果：一日の最後に、ルピが<br>　　　［滞在者数×（仕事レベル＋１）＾３］増加する。
			LOCALS += "<br><br>"
			IF 現在仕事:キャラ番号:1 == 2
				LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[102] ロビー ：現在常駐場所</font><br>"
			ELSE
				LOCALS += @"<button value='102'>[102] ロビー でロビー受付として働く</button><br>"
			ENDIF
			IF 施設改造度:5:0
				IF 現在仕事:キャラ番号:1 == 503
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[503] バーエリア ：現在常駐場所</font><br>"
				ELSE
					LOCALS += @"<button value='503'>[503] バーエリア でバーテンダーとして働く</button><br>"
				ENDIF
			ENDIF
			IF 施設改造度:5:6
				IF 現在仕事:キャラ番号:1 == 506
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[506] 図書室 ：現在常駐場所</font><br>"
				ELSE
					LOCALS += @"<button value='506'>[506] 図書室 で司書として働く</button><br>"
				ENDIF
			ENDIF
			IF 施設改造度:15:0
				IF 現在仕事:キャラ番号:1 == 1503
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[1503] プールサイド ：現在常駐場所</font><br>"
				ELSE
					LOCALS += @"<button value='1503'>[1503] プールサイド で監視員として働く</button><br>"
				ENDIF
			ELSE
				IF 現在仕事:キャラ番号:1 == 15
					LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[15] プール ：現在常駐場所</font><br>"
				ELSE
					LOCALS += @"<button value='15'>[15] プール で監視員として働く</button><br>"
				ENDIF
			ENDIF
			IF 空艇港初来訪イベントフラグ
				FOR LOCAL, 3, 9
					IF 空艇港店舗状況:LOCAL:0 > 0
						CALLFORMF 空艇港店舗名表示_{空艇港店舗状況:LOCAL:0}
						IF 現在仕事:キャラ番号:1 == LOCAL
							LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'>[{10000 + LOCAL}] 店舗{LOCAL}:%TSTR:場所名受渡% ：現在常駐場所</font><br>"
						ELSE
							LOCALS += @"<button value='{10000 + LOCAL}'>[{10000 + LOCAL}] 店舗{LOCAL}:%TSTR:場所名受渡% で店員として働く</button><br>"
						ENDIF
					ENDIF
				NEXT
			ENDIF
			RETURNF LOCALS
		ENDIF
		RETURNF "常駐業務"
	CASE 3
		IF ARG:1
			RETURNF @"%CALLNAME:PLAYER%の補佐を行う業務。<br>効果：一日の最後に、好感度要素：信頼度が<br>　　　［（仕事レベル＋１）＾２×２００］増加する。<br>　　　リゾート内をランダムに移動する。"
		ENDIF
		RETURNF "秘書"
	CASE 4
		IF ARG:1
			RETURNF @"手紙や広告などでリゾートを宣伝する業務。<br>効果：一日の最後に、リゾートの人気度が<br>　　　［（仕事レベル＋１）＾２×１］増加する。<br>　　　従業員部屋の自室でチラシ作りなどを行う。"
		ENDIF
		RETURNF "宣伝"
	CASE 5
		IF 施設改造度:3:8 == 0
			RETURNF ""
		ENDIF
		IF ARG:1
			RETURNF @"訓練場で戦闘能力を鍛える業務。<br>効果：一日の最後にEXPを獲得する。<br>　　　獲得量は全キャラ中最も高いレベルに応じて変動する。<br>　　　訓練場に常駐する。"
		ENDIF
		RETURNF "訓練"
ENDSELECT

@仕事条件判定関数(キャラ番号, 仕事番号)
#FUNCTION
#DIM キャラ番号
#DIM 仕事番号

SELECTCASE 仕事番号
	CASE 2,3
		IF キャラ番号 == PLAYER
			RETURNF 0
		ENDIF
ENDSELECT

RETURNF 1


@仕事追加処理関数(キャラ番号, 選択番号)
#DIM キャラ番号
#DIM 選択番号

;今のところ常駐業務のみ
;常駐する店舗を選べる

SELECTCASE 現在仕事:キャラ番号:0
	CASE 2
		IF 選択番号 == 102
			現在仕事:キャラ番号:1 = 2
		ELSE
			現在仕事:キャラ番号:1 = 選択番号
		ENDIF
ENDSELECT
