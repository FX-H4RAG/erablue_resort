@従業員制服設定
#DIM 従業員一覧, 50
VARSET 従業員一覧, -1

DRAWLINE
PRINTL 従業員が着用する制服の設定を行います。
PRINTL ※開発中のため、仕事が正しく挙動しないことがあります。
DRAWLINE
LOCALS = <div rect='81,31,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='2050,31,3875,2812' border='31' bcolor='#C0C0C0'></div>

;現在いる従業員を一覧表示
従業員一覧:0 = 0
LOCAL:1 = 1
FOR LOCAL, 1, CHARANUM
	SIF TALENT:LOCAL:従業員 == 0
		CONTINUE
	従業員一覧:(LOCAL:1) = LOCAL
	LOCAL:1 += 1
NEXT
LOCALS += "<div rect='156,156,1937,2812'>"
FOR LOCAL, 0, 50
	SIF 従業員一覧:LOCAL < 0
		BREAK
	LOCALS += @"<button value='{LOCAL}'>%CALLNAME:(従業員一覧:LOCAL), 16, LEFT%["
	IF 現在制服:(従業員一覧:LOCAL):0 == ""
		LOCALS += "設定なし"
	ELSE
		LOCALS += @"%現在制服:(従業員一覧:LOCAL):0%"
	ENDIF
	LOCALS += "]</button><br>"
NEXT
LOCALS += "<br>"
LOCALS += "<button value='999'>[999] 戻る</button>"
LOCALS += "<br>"
LOCALS += "</div>"

LOCALS += "<div rect='2125,156,3675,2812'>"
LOCALS += "</div>"

HTML_PRINT LOCALS
FOR LOCAL, 0, 25
	PRINTL
NEXT
DRAWLINE

DO
BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 999
		RETURN 0
	CASE 0 TO 49
		CALL 従業員制服設定_切り替え(従業員一覧:LOCAL)
		RESTART
ENDSELECT
LOOP 1


@従業員制服設定_切り替え(ARG)
#DIM ページ数
#DIMS 制服一時配列, 100
#DIMS 分割文字列配列, 2
ページ数 = 0

DRAWLINE
PRINTL 従業員が着用する制服の設定を行います。
PRINTL ※開発中のため、正しく挙動しないことがあります。
DRAWLINE
LOCALS = <div rect='81,31,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='2050,31,3875,2812' border='31' bcolor='#C0C0C0'></div>
LOCALS += "<div rect='156,156,1937,2812'>"
	LOCALS += @"%CALLNAME:ARG, 16, LEFT%["
	IF 現在制服:ARG:0 == ""
		LOCALS += "設定なし]"
	ELSE
		LOCALS += @"%現在制服:ARG:0%]"
	ENDIF
LOCALS += "<br>"
LOCALS += "<br>"
LOCALS += "<button value='999'>[999] 戻る</button>"
LOCALS += "<br>"
LOCALS += "</div>"

;仕事の説明を表示
LOCALS:1 = <div rect='2125,156,3675,2812'>
LOCALS:1 += "<button value='100'>[100] なし　　　　　に変更</button><br>"
SPLIT 所持制服文字列, "_", 制服一時配列
FOR LOCAL, 0, 100
	IF 制服一時配列:LOCAL == ""
		BREAK
	ENDIF
	SPLIT 制服一時配列:LOCAL, "/", 分割文字列配列
	LOCALS:1 += @"<button value='{LOCAL}'>[{LOCAL}] %分割文字列配列:1, 14, LEFT%に変更</button><br>"
NEXT
LOCALS:1 += "</div>"


HTML_PRINT LOCALS, 1
HTML_PRINT LOCALS:1, 1
FOR LOCAL, 0, 25
	PRINTL
NEXT
DRAWLINE

DO
BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE 999
		RETURN 0
	CASE 100
		現在制服:ARG:0 = 
		CALL CLOTHES_RESET_TRAIN(ARG)
		RETURN 0
	CASE 0 TO 99
		IF STRCOUNT(制服一時配列:LOCAL, "キャラ専用")
			SPLIT 制服一時配列:LOCAL, "/", 分割文字列配列
			現在制服:ARG:0 = %分割文字列配列:0%
		ELSE
			SPLIT 制服一時配列:LOCAL, "/", 分割文字列配列
			現在制服:ARG:0 = %分割文字列配列:1%
		ENDIF
		CALL CLOTHES_RESET_TRAIN(ARG)
		RETURN 0
ENDSELECT
LOOP 1

@制服登録処理(ARGS)

;汎用服にあるか？
IF EXISTFUNCTION(@"CLOTHES_CHANGE_%ARGS%")
	;被ってない？
	IF STRCOUNT(所持制服文字列, @"汎用/%ARGS%_") == 0 && STRCOUNT(所持エロ衣装文字列, @"エロ/%ARGS%_") == 0
		所持制服文字列 += @"汎用/%ARGS%_"
	ENDIF
	RETURN 1
ELSE
	;キャラ服にあるか？
	FOR LOCAL, 1, CHARANUM
		IF CSTR:LOCAL:服名称 == ARGS
			;被ってない？
			IF STRCOUNT(所持制服文字列, @"キャラ専用{NO:LOCAL}/%ARGS%_") == 0
				所持制服文字列 += @"キャラ専用{NO:LOCAL}/%ARGS%_"
			ENDIF
			RETURN 1
		ENDIF
	NEXT
ENDIF

;どっちにもないならエラー返しておしまい
PRINTW 該当する服が見つかりません。
RETURN 0

@エロ衣装登録処理(ARGS)

;汎用服にあるか？
IF EXISTFUNCTION(@"CLOTHES_CHANGE_%ARGS%")
	;被ってない？
	IF STRCOUNT(所持制服文字列, @"汎用/%ARGS%_") == 0 && STRCOUNT(所持エロ衣装文字列, @"エロ/%ARGS%_") == 0
		所持エロ衣装文字列 += @"エロ/%ARGS%_"
	ENDIF
	RETURN 1
ENDIF

;ないならエラー返しておしまい
PRINTW 該当する服が見つかりません。
RETURN 0
