@従業員素質指定雇用
#DIM ランダムキャラ素質, 70
#DIM TALENT一時保存, 300
#DIM TALENT数値保存, 300
#DIM フレーバー素質数値保存, 10
#DIM ABL一時保存, 50
#DIM ABL数値保存, 50
#DIM EXP一時保存, 100
#DIM EXP数値保存, 100
#DIM ランダムキャラ経験, 80
#DIM ランダムキャラ戦闘タイプ, 10
#DIMS ランダムキャラネーム
#DIM 重複チェック用, 10
#DIM 表示モード
#DIM 表示数カウント
#DIM ページ数
#DIM 必要ルピ

表示モード = 0
ページ数 = 0
表示数カウント = 0
ランダムキャラネーム = 
VARSET ランダムキャラ素質:0
VARSET ランダムキャラ経験:0
VARSET TALENT一時保存:0
VARSET フレーバー素質数値保存
VARSET TALENT数値保存:0, 5
VARSET ABL一時保存:0, -1
VARSET ABL数値保存:0
VARSET EXP一時保存:0, -1
VARSET EXP数値保存:0
VARSET ランダムキャラ戦闘タイプ:0
VARSET LOCAL
ランダムキャラ素質:0 = 1
ランダムキャラ素質:1 = 1

CALL 従業員人数チェック()

FOR LOCAL, 10, 71
	IF TALENTNAME:LOCAL != ""
		TALENT一時保存:(LOCAL:10) = LOCAL
		LOCAL:10 += 1
	ENDIF
NEXT
FOR LOCAL, 77, 132
	IF TALENTNAME:LOCAL != ""
		TALENT一時保存:(LOCAL:10) = LOCAL
		LOCAL:10 += 1
	ENDIF
NEXT
FOR LOCAL, 140, 150
	IF TALENTNAME:LOCAL != ""
		TALENT一時保存:(LOCAL:10) = LOCAL
		LOCAL:10 += 1
	ENDIF
NEXT
FOR LOCAL, 220, 300
	IF TALENTNAME:LOCAL != ""
		TALENT一時保存:(LOCAL:10) = LOCAL
		LOCAL:10 += 1
	ENDIF
NEXT

FOR LOCAL, 0, 21
	IF ABLNAME:LOCAL != ""
		ABL一時保存:(LOCAL:11) = LOCAL
		LOCAL:11 += 1
	ENDIF
NEXT
FOR LOCAL, 30, 50
	IF ABLNAME:LOCAL != ""
		ABL一時保存:(LOCAL:11) = LOCAL
		LOCAL:11 += 1
	ENDIF
NEXT

FOR LOCAL, 0, 89
	IF EXPNAME:LOCAL != ""
		EXP一時保存:(LOCAL:12) = LOCAL
		LOCAL:12 += 1
	ENDIF
NEXT
FOR LOCAL, 90, 100
	IF EXPNAME:LOCAL != ""
		EXP一時保存:(LOCAL:12) = LOCAL
		LOCAL:12 += 1
	ENDIF
NEXT

$従業員_LOOP
DRAWLINE
PRINTL 従業員の素質を完全指定して雇います。
PRINTPLAIN 指定した素質の数に応じて必要ルピが上昇するので注意してください。
PRINTL
DRAWLINE
LOCALS = <div rect='13px,0px,310px,55px' border='5px' bcolor='#C0C0C0'></div>
LOCALS += @"<div rect='25px,10px,306px,45px'>所持ルピ：{MONEY}<br>所持ZP　：{FLAG:ZP所持量}.{(ZP小数点以下所持量 % 1000) / 100}{(ZP小数点以下所持量 % 100) / 10}{(ZP小数点以下所持量 % 10)}</div>"

LOCALS += "<div rect='328px,0px,620px,55px' border='5px' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='340px,10px,610px,45px'>"
必要ルピ = 1000
;名前・戦闘タイプではルピ増加無し
;肉体情報
;バストサイズ・容姿は普通から外れるほどルピが嵩む
IF ランダムキャラ素質:0 == 3
	必要ルピ += ABS(ランダムキャラ素質:3 - 3) * 500
	必要ルピ += ABS(ランダムキャラ素質:31 + 1) * 500
ELSEIF ランダムキャラ素質:0 == 4
	必要ルピ += ABS(ランダムキャラ素質:3 + 2) * 500
	必要ルピ += ABS(ランダムキャラ素質:31 + 2) * 500
ELSE
	必要ルピ += ABS(ランダムキャラ素質:3) * 200
	必要ルピ += ABS(ランダムキャラ素質:31) * 200
ENDIF
必要ルピ += ABS(ランダムキャラ素質:6) * 300
必要ルピ += ABS(ランダムキャラ素質:30) * 500
必要ルピ += ABS(ランダムキャラ素質:32) * 200
LOCAL:1 = 0
FOR LOCAL, 0, 300
	SIF TALENT数値保存:LOCAL == 0
		CONTINUE
	SIF TALENT数値保存:LOCAL == 5
		CONTINUE
	LOCAL:1 += 1
NEXT
必要ルピ += LOCAL:1 * LOCAL:1 * LOCAL:1 * 50 * (LOCAL:1 / 5 + 1)
LOCAL:1 = 0
FOR LOCAL, 0, 50
	SIF ABL数値保存:LOCAL < 1
		CONTINUE
	LOCAL:1 += ABL数値保存:LOCAL
NEXT
必要ルピ += LOCAL:1 * LOCAL:1 * LOCAL:1 * 50 * (LOCAL:1 / 10 + 1)
LOCAL:1 = 0
FOR LOCAL, 0, 100
	SIF EXP数値保存:LOCAL < 1
		CONTINUE
	LOCAL:1 += EXP数値保存:LOCAL
NEXT
必要ルピ += LOCAL:1 *  5 * (LOCAL:1 / 100 + 1)

必要ルピ += ランダムキャラ素質:30 * ランダムキャラ素質:30 * 100


LOCALS += @"現在の必要ルピ：{必要ルピ, 10}ルピ<br>"
LOCALS += @"現在の従業員数 / 上限：{従業員人数} / {従業員キャラ上限}ルピ"
LOCALS += "</div>"

LOCALS += "<div rect='13px,60px,310px,450px' border='5px' bcolor='#C0C0C0'></div><div rect='328px,60px,620px,450px' border='5px' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='25px,80px,310px,450px'>"
LOCALS += "<button value='100'>[100] 名前を設定する</button>"
LOCALS += "<br>"
LOCALS += "<button value='110'>[110] 戦闘タイプを設定する</button>"
LOCALS += "<br>"
LOCALS += "<button value='120'>[120] 肉体情報を設定する</button>"
LOCALS += "<br>"
LOCALS += "<button value='130'>[130] 素質を設定する</button>"
LOCALS += "<br>"
IF フレーバー素質封印設定 == 0
	LOCALS += "<button value='131'>[131] フレーバー素質を設定する</button>"
	LOCALS += "<br>"
ENDIF
LOCALS += "<button value='140'>[140] 能力を設定する</button>"
LOCALS += "<br>"
LOCALS += "<button value='150'>[150] 経験を設定する</button>"
LOCALS += "<br>"
LOCALS += "<br>"
LOCALS += "<button value='200'>[200] これで決定する</button>"
LOCALS += "<br>"
LOCALS += "<button value='999'>[999] 戻る</button>"
LOCALS += "</div>"

LOCALS += "<div rect='340px,80px,588px,450px'>"


SELECTCASE 表示モード
	CASE 0
		;現状の設定表示
		IF ランダムキャラネーム != ""
			LOCALS += @"■[従業員]%ランダムキャラネーム, 18, LEFT%"
		ELSE
			LOCALS += @"■[従業員]（名称未定）　　　"
		ENDIF
		LOCALS:1 = %カラーパレット_HTML(@"%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%属性")%
		LOCALS += @"<font color='#%LOCALS:1%'>属性：%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%</font>"
		LOCALS += @"　得意武器：%得意武器数値文字列変換(ランダムキャラ戦闘タイプ:1)%　タイプ：%成長タイプ:(ランダムキャラ戦闘タイプ:2)%"
		LOCALS += "<br><br>"
		LOCALS += "□ 情報 □---------------------------------------------------------------<br>"

		LOCALS += "　種族："
		SELECTCASE ランダムキャラ素質:0
			CASE 1
				LOCALS += "ヒューマン"
			CASE 2
				LOCALS += "エルーン"
				ランダムキャラ素質:4 = 208
				LOCALS += "（エルーン耳）"
				IF ランダムキャラ素質:5 == 207
					LOCALS += "（尻尾）"
				ENDIF
			CASE 3
				LOCALS += "ドラフ"
				ランダムキャラ素質:4 = 206
				LOCALS += "（角あり）"
			CASE 4
				LOCALS += "ハーヴィン"
				ランダムキャラ素質:4 = 209
				LOCALS += "（ハーヴィン耳）"
		ENDSELECT
		LOCALS += "<br>"
		LOCALS += "　性別："
		SELECTCASE ランダムキャラ素質:1
			CASE 1
				LOCALS += "女性"
				IF GETBIT(ランダムキャラ素質:2, 0) == 0
					LOCALS += "（処女）　　　　　"
				ELSE
					LOCALS += "　　　　　　　　　"
				ENDIF
			CASE 3
				LOCALS += "ふたなり"
				IF GETBIT(ランダムキャラ素質:2, 0) == 0
					LOCALS += "（処女）"
				ELSE
					LOCALS += "　　　　"
				ENDIF
				IF GETBIT(ランダムキャラ素質:2, 1) == 0
					LOCALS += "（童貞）　"
				ELSE
					LOCALS += "　　　　　"
				ENDIF
		ENDSELECT
		LOCALS += @"バストサイズ：%GET_TALENTNAME(205,ランダムキャラ素質:3)%"
		LOCALS += "<br>"
		LOCALS += @"　性知識レベル："
		SELECTCASE ランダムキャラ素質:6
			CASE -2
				LOCALS += "無知"
			CASE -1
				LOCALS += "獣並み"
			CASE 0
				LOCALS += "疎い"
			CASE 1
				LOCALS += "必要十分"
			CASE 2
				LOCALS += "有識者"
			CASE 3
				LOCALS += "百戦錬磨"
		ENDSELECT
		LOCALS += "<br>　容姿："
		IF ランダムキャラ素質:30 == 0
			LOCALS += "モブ顔　　　"
		ELSE
			LOCALS += @"%GET_TALENTNAME(160,ランダムキャラ素質:30), 12, LEFT%"
		ENDIF
		LOCALS += "体格："
		IF ランダムキャラ素質:31 == 0
			LOCALS += "普通"
		ELSE
			LOCALS += @"%GET_TALENTNAME(161,ランダムキャラ素質:31)%"
		ENDIF
		IF ランダムキャラ素質:1 == 3
			LOCALS += "<br>　男性器サイズ："
			IF ランダムキャラ素質:32 == 0
				LOCALS += "平凡"
			ELSE
				LOCALS += @"%GET_TALENTNAME(162,ランダムキャラ素質:32)%"
			ENDIF
		ENDIF
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "□ 素質 □---------------------------------------------------------------<br>"
		LOCALS:1 = 
		FOR LOCAL, 0, 300
			SIF TALENT数値保存:LOCAL == 0
				CONTINUE
			SIF TALENT数値保存:LOCAL == 5
				CONTINUE
			LOCALS += @"[%GET_TALENTNAME(TALENT一時保存:LOCAL, TALENT数値保存:LOCAL - 5)%]"
		NEXT
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "□ フレーバー素質 □-----------------------------------------------------<br>"
		LOCALS:1 = 
		LOCAL = 0
		FOR COUNT, 0, 10
			IF COUNT == 6
				PRINTL
				LOCAL = 0
			ENDIF
			SIF フレーバー素質数値保存:COUNT == 0
				CONTINUE
			LOCALS += @"%ERDNAME(フレーバー素質, COUNT), 10, LEFT%：%GET_フレーバー素質NAME(COUNT, フレーバー素質数値保存:COUNT), 12, LEFT%"
			LOCAL += 1
			IF LOCAL == 3
				LOCAL = 0
				PRINTL
			ENDIF
		NEXT
	CASE 1
		;現状の設定表示（能力素質）
		IF ランダムキャラネーム != ""
			LOCALS += @"■[従業員]%ランダムキャラネーム, 18, LEFT%"
		ELSE
			LOCALS += @"■[従業員]（名称未定）　　　"
		ENDIF
		LOCALS:1 = %カラーパレット_HTML(@"%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%属性")%
		LOCALS += @"<font color='#%LOCALS:1%'>属性：%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%</font>"
		LOCALS += @"　得意武器：%得意武器数値文字列変換(ランダムキャラ戦闘タイプ:1)%　タイプ：%成長タイプ:(ランダムキャラ戦闘タイプ:2)%"
		LOCALS += "<br><br>"
		LOCALS += "□ 能力 □---------------------------------------------------------------<br>"
		LOCAL:2 = 0
		FOR LOCAL, 0, 50
			SIF ABL数値保存:LOCAL < 1
				CONTINUE
			IF ABL一時保存:LOCAL > -1
				LOCALS += @"%ABLNAME:(ABL一時保存:LOCAL), 16%:{ABL数値保存:LOCAL, 2}   "
				LOCAL:2 += 1
				IF LOCAL:2 == 3
					LOCALS += "<br>"
					LOCAL:2 = 0
				ENDIF
			ENDIF
		NEXT
		SIF LOCAL:2 != 0
			LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "□ 経験 □---------------------------------------------------------------<br>"
		LOCAL:2 = 0
		FOR LOCAL, 0, 100
			SIF EXP数値保存:LOCAL < 1
				CONTINUE
			IF EXP一時保存:LOCAL > -1
				LOCALS += @"%EXPNAME:(EXP一時保存:LOCAL), 16%:{EXP数値保存:LOCAL, 3}   "
				LOCAL:2 += 1
				IF LOCAL:2 == 3
					LOCALS += "<br>"
					LOCAL:2 = 0
				ENDIF
			ENDIF
		NEXT
	CASE 110
		LOCALS += "属性："
		FOR LOCAL, 0, 6
			IF ランダムキャラ戦闘タイプ:0 == LOCAL
				LOCALS:1 = %カラーパレット_HTML(@"%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%属性")%
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{11000 + LOCAL}'>[%属性数値文字列変換(LOCAL)%]</button></font>"
		NEXT
		LOCALS += "<br>"
		LOCALS += "得意武器："
		FOR LOCAL, 0, 10
			IF ランダムキャラ戦闘タイプ:1 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{11010 + LOCAL}'>[%得意武器数値文字列変換(LOCAL)%]</button></font>"
		NEXT
		LOCALS += "<br>"
		LOCALS += "タイプ："
		FOR LOCAL, 0, 8
			IF ランダムキャラ戦闘タイプ:2 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{11020 + LOCAL}'>[%成長タイプ:LOCAL%]</button></font>"
		NEXT
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "<button value='11100'>[ステータス画面に戻る]</button>"
	CASE 120
		LOCALS += "種族："
		FOR LOCAL, 1, 5
			IF ランダムキャラ素質:0 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12000 + LOCAL}'>[%GET_TALENTNAME(200,LOCAL)%]</button></font>"
		NEXT
		LOCALS += "<br>"
		IF ランダムキャラ素質:0 == 2
			LOCALS += "尻尾："
			IF ランダムキャラ素質:5
				LOCALS:1 = C0C0C0
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12005}'>[あり]</button></font>"
				LOCALS:1 = 333333
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12006}'>[なし]</button></font>"
			ELSE
				LOCALS:1 = 333333
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12005}'>[あり]</button></font>"
				LOCALS:1 = C0C0C0
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12006}'>[なし]</button></font>"
			ENDIF
			LOCALS += "<br>"
		ENDIF
		LOCALS += "性別："
		IF ランダムキャラ素質:1 == 1
			LOCALS:1 = C0C0C0
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12007}'>[女性]</button></font>"
			LOCALS:1 = 333333
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12008}'>[ふたなり]</button></font>"
		ELSE
			LOCALS:1 = 333333
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12007}'>[女性]</button></font>"
			LOCALS:1 = C0C0C0
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12008}'>[ふたなり]</button></font>"
		ENDIF
		LOCALS += "　　　純潔："
		IF GETBIT(ランダムキャラ素質:2, 0) == 0
			LOCALS:1 = C0C0C0
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12010}'>[処女]</button></font>"
			LOCALS:1 = 333333
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12011}'>[非処女]</button></font>"
		ELSE
			LOCALS:1 = 333333
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12010}'>[処女]</button></font>"
			LOCALS:1 = C0C0C0
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12011}'>[非処女]</button></font>"
		ENDIF
		IF ランダムキャラ素質:1 == 3
			IF GETBIT(ランダムキャラ素質:2, 1) == 0
				LOCALS:1 = C0C0C0
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12012}'>[童貞]</button></font>"
				LOCALS:1 = 333333
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12013}'>[非童貞]</button></font>"
			ELSE
				LOCALS:1 = 333333
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12012}'>[童貞]</button></font>"
				LOCALS:1 = C0C0C0
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12013}'>[非童貞]</button></font>"
			ENDIF
		ENDIF
		LOCALS += "<br>"
		LOCALS += "バストサイズ："
		FOR LOCAL, -2, 4
			IF ランダムキャラ素質:3 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12020 + LOCAL}'>[%GET_TALENTNAME(205,LOCAL)%]</button></font>"
		NEXT
		LOCALS += "<br>"
		LOCALS += "性知識レベル："
		FOR LOCAL, -2, 4
			IF ランダムキャラ素質:6 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12030 + LOCAL}'>[%性知識段階表示(LOCAL, 0)%]</button></font>"
		NEXT
		LOCALS += "<br>"
		LOCALS += "容姿："
		FOR LOCAL, -1, 3
			IF ランダムキャラ素質:30 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			IF LOCAL == 0
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12040 + LOCAL}'>[モブ顔]</button></font>"
			ELSE
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12040 + LOCAL}'>[%GET_TALENTNAME(160, LOCAL)%]</button></font>"
			ENDIF
		NEXT
		LOCALS += "<br>"
		LOCALS += "体格："
		FOR LOCAL, -2, 3
			IF ランダムキャラ素質:31 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			IF LOCAL == 0
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12050 + LOCAL}'>[普通]</button></font>"
			ELSE
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12050 + LOCAL}'>[%GET_TALENTNAME(161, LOCAL)%]</button></font>"
			ENDIF
		NEXT
		LOCALS += "<br>"
		IF ランダムキャラ素質:1 == 3
			LOCALS += "男性器サイズ："
			FOR LOCAL, -2, 3
				IF ランダムキャラ素質:32 == LOCAL
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				IF LOCAL == 0
					LOCALS += @"<font color='#%LOCALS:1%'><button value='{12060 + LOCAL}'>[平凡]</button></font>"
				ELSE
					LOCALS += @"<font color='#%LOCALS:1%'><button value='{12060 + LOCAL}'>[%GET_TALENTNAME(162, LOCAL)%]</button></font>"
				ENDIF
			NEXT
			LOCALS += "<br>"
		ENDIF
		LOCALS += "<br>"
		LOCALS += "<button value='11100'>[ステータス画面に戻る]</button>"
	CASE 130
		FOR LOCAL, ページ数 * 20, ページ数 * 20 + 20
			SIF TALENT一時保存:LOCAL == 0
				BREAK
			LOCALS += @"%TALENTNAME:(TALENT一時保存:LOCAL), 18, LEFT%："
			FOR LOCAL:1, -2, 4
				LOCALS:2 = %GET_TALENTNAME(TALENT一時保存:LOCAL,LOCAL:1)%
				IF LOCAL:1 == 0
					LOCALS:2 = なし
				ELSEIF LOCALS:2 == ""
					CONTINUE
				ENDIF
				IF TALENT数値保存:LOCAL - 5 == LOCAL:1
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{20000 + LOCAL * 10 + LOCAL:1 + 5}'>[%LOCALS:2%]</button></font>"
			NEXT
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='340px,481px,588px,450px'>"
		IF ページ数 > 0
			LOCALS += "<button value='29998'>[前のページ]</button>"
		ELSE
			LOCALS += "　　　　　　"
		ENDIF
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>　　　　"
		IF ページ数 < LOCAL:10 / 20
			LOCALS += "<button value='29999'>[次のページ]</button>"
		ENDIF
	CASE 131
		FOR COUNT, 1, 6
			SIF ERDNAME(フレーバー素質, COUNT) == ""
				CONTINUE
			LOCALS += @"%ERDNAME(フレーバー素質, COUNT), 12, LEFT%："
			FOR LOCAL:1, 0, 6
				LOCALS:2 = %GET_フレーバー素質NAME(COUNT,LOCAL:1)%
				IF LOCAL:1 == 0
					LOCALS:2 = 非表示
				ENDIF
				IF COUNT == 5 && LOCAL:1 == 4
					CONTINUE
				ELSEIF LOCALS:2 == ""
					CONTINUE
				ENDIF
				IF フレーバー素質数値保存:COUNT == LOCAL:1
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{50000 + COUNT * 10 + LOCAL:1}'>[%LOCALS:2%]</button></font>"
			NEXT
			LOCALS += "<br>"
		NEXT
		LOCALS += @"%ERDNAME(フレーバー素質, 6), 12, LEFT%：　　　　"
		FOR LOCAL:1, 0, 4
			LOCALS:2 = %GET_フレーバー素質NAME(6, POWER(2, LOCAL:1))%
			IF LOCALS:2 == ""
				CONTINUE
			ELSEIF LOCAL:1 == 3 && GETBIT(ランダムキャラ素質:1, 1) == 0
				CONTINUE
			ENDIF
			IF GETBIT(フレーバー素質数値保存:6,LOCAL:1)
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{50000 + 6 * 10 + LOCAL:1}'>[%LOCALS:2%]</button></font>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='340px,481px,588px,450px'>"
		LOCALS += "　　　　　　"
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>"
	CASE 140
		FOR LOCAL, ページ数 * 20, ページ数 * 20 + 20
			SIF ABL一時保存:LOCAL == -1
				BREAK
			LOCALS += @"%ABLNAME:(ABL一時保存:LOCAL), 18, LEFT%："
			FOR LOCAL:1, 0, 6
				IF ABL数値保存:LOCAL == LOCAL:1
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{30000 + LOCAL * 10 + LOCAL:1}'>[%TOFULL(TOSTR(LOCAL:1))%]</button></font>"
			NEXT
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='340px,481px,588px,450px'>"
		IF ページ数 > 0
			LOCALS += "<button value='29998'>[前のページ]</button>"
		ELSE
			LOCALS += "　　　　　　"
		ENDIF
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>　　　　"
		IF ページ数 < LOCAL:11 / 20
			LOCALS += "<button value='29999'>[次のページ]</button>"
		ENDIF
	CASE 150
		FOR LOCAL, ページ数 * 20, ページ数 * 20 + 20
			SIF EXP一時保存:LOCAL == -1
				BREAK
			LOCALS += @"<button value='{40000 + LOCAL * 10}'>[%EXPNAME:(EXP一時保存:LOCAL), 18, LEFT%]：{EXP数値保存:LOCAL, 8}</button>"
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='340px,481px,588px,450px'>"
		IF ページ数 > 0
			LOCALS += "<button value='29998'>[前のページ]</button>"
		ELSE
			LOCALS += "　　　　　　"
		ENDIF
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>　　　　"
		IF ページ数 < LOCAL:12 / 20
			LOCALS += "<button value='29999'>[次のページ]</button>"
		ENDIF
ENDSELECT
LOCALS += "</div>"
IF 表示モード == 0
	LOCALS += "<div rect='770px,480px,200px,50px'><button value='1'>[1] 能力・経験表示へ</button></div>"
ELSEIF 表示モード == 1
	LOCALS += "<div rect='770px,480px,200px,50px'><button value='1'>[1] 情報・素質表示へ</button></div>"
ENDIF

HTML_PRINT LOCALS
FOR LOCAL, 0, 28
	PRINTL
NEXT
DRAWLINE

BINPUT

LOCAL = RESULT
SELECTCASE LOCAL
;戦闘タイプ--------------------------------------------------------
	CASE 11000 TO 11005
		ランダムキャラ戦闘タイプ:0 = LOCAL - 11000
	CASE 11010 TO 11019
		ランダムキャラ戦闘タイプ:1 = LOCAL - 11010
	CASE 11020 TO 11027
		ランダムキャラ戦闘タイプ:2 = LOCAL - 11020
	CASE 11100
		表示モード = 0
;肉体情報--------------------------------------------------------
	CASE 12001
		ランダムキャラ素質:0 = 1
		ランダムキャラ素質:5 = 0
	CASE 12002
		ランダムキャラ素質:0 = 2
		ランダムキャラ素質:4 = 208
	CASE 12003
		ランダムキャラ素質:0 = 3
		ランダムキャラ素質:3 = 3
		ランダムキャラ素質:4 = 206
		ランダムキャラ素質:5 = 0
		ランダムキャラ素質:31 = -1
	CASE 12004
		ランダムキャラ素質:0 = 4
		ランダムキャラ素質:3 = -2
		ランダムキャラ素質:4 = 209
		ランダムキャラ素質:5 = 0
		ランダムキャラ素質:31 = -2
	CASE 12005
		ランダムキャラ素質:5 = 207
	CASE 12006
		ランダムキャラ素質:5 = 0
	CASE 12007
		ランダムキャラ素質:1 = 1
		ランダムキャラ素質:32 = 0
	CASE 12008
		ランダムキャラ素質:1 = 3
	CASE 12010
		CLEARBIT ランダムキャラ素質:2, 0
	CASE 12011
		SETBIT ランダムキャラ素質:2, 0
	CASE 12012
		CLEARBIT ランダムキャラ素質:2, 1
	CASE 12013
		SETBIT ランダムキャラ素質:2, 1
	CASE 12018 TO 12023
		IF ランダムキャラ素質:0 == 3
			ランダムキャラ素質:3 = MAX(LOCAL - 12020, 2)
		ELSEIF ランダムキャラ素質:0 == 4
			ランダムキャラ素質:3 = MIN(LOCAL - 12020, -1)
		ELSE
			ランダムキャラ素質:3 = LOCAL - 12020
		ENDIF
	CASE 12028 TO 12033
		ランダムキャラ素質:6 = LOCAL - 12030
	CASE 12039 TO 12042
		ランダムキャラ素質:30 = LOCAL - 12040
	CASE 12048 TO 12052
		IF ランダムキャラ素質:0 == 3
			ランダムキャラ素質:31 = MIN(LOCAL - 12050, 0)
		ELSEIF ランダムキャラ素質:0 == 4
			ランダムキャラ素質:31 = MIN(LOCAL - 12050, -1)
		ELSE
			ランダムキャラ素質:31 = LOCAL - 12050
		ENDIF
	CASE 12058 TO 12062
		ランダムキャラ素質:32 = LOCAL - 12060
;タレント--------------------------------------------------------
	CASE 20000 TO 29997
		TALENT数値保存:((LOCAL - 20000) / 10) = LOCAL % 10
	CASE 29998
		ページ数 -= 1
	CASE 29999
		ページ数 += 1
;ABL--------------------------------------------------------
	CASE 30000 TO 39997
		ABL数値保存:((LOCAL - 30000) / 10) = LOCAL % 10
;EXP--------------------------------------------------------
	CASE 40000 TO 49997
		RESULT = 0
		PRINTL 経験数を入力してください。（３桁まで）
		INPUT
		EXP数値保存:((LOCAL - 40000) / 10) = MIN(RESULT, 999)
;フレーバー素質----------------------------------------------------
	CASE 50000 TO 50059
		フレーバー素質数値保存:((LOCAL - 50000) / 10) = LOCAL % 10
	CASE 50060 TO 50069
		INVERTBIT フレーバー素質数値保存:((LOCAL - 50000) / 10), LOCAL % 10
;メイン画面--------------------------------------------------------
	CASE 100
		PRINTL 名前を入力してください。（９文字まで）
		INPUTS
		ランダムキャラネーム = %SUBSTRING(RESULTS, 0, 18)%
	CASE 110
		表示モード = 110
	CASE 120
		表示モード = 120
	CASE 130
		ページ数 = 0
		表示モード = 130
	CASE 131
		ページ数 = 0
		表示モード = 131
	CASE 140
		ページ数 = 0
		表示モード = 140
	CASE 150
		ページ数 = 0
		表示モード = 150
	CASE 200
		IF 従業員部屋割り配列:(従業員キャラ上限 - 1) > 0
			PRINTW 従業員部屋が一杯です。
			RETURN 0
		ENDIF
		IF 必要ルピ > MONEY
			PRINTW ルピが足りません。
			RETURN 0
		ENDIF
		MONEY -= 必要ルピ
		PRINTFORMW [従業員]%ランダムキャラネーム%を雇用しました。
		FLAG:従業員数 += 1
		LOCAL:1 = CHARANUM
		ADDCHARA 999
		TALENT:(LOCAL:1):従業員 = 1
		NAME:(LOCAL:1) = [従業員]%ランダムキャラネーム%
		CALLNAME:(LOCAL:1) = %ランダムキャラネーム%
		TALENT:(LOCAL:1):200 = ランダムキャラ素質:0
		TALENT:(LOCAL:1):2 = ランダムキャラ素質:1
		IF GETBIT(ランダムキャラ素質:2, 0)
			TALENT:(LOCAL:1):0 = 0
		ELSE
			TALENT:(LOCAL:1):0 = 1
		ENDIF
		IF GETBIT(ランダムキャラ素質:2, 1)
			TALENT:(LOCAL:1):1 = 1
		ELSE
			TALENT:(LOCAL:1):1 = 0
		ENDIF
		TALENT:(LOCAL:1):205 = ランダムキャラ素質:3
		SIF ランダムキャラ素質:4
			TALENT:(LOCAL:1):(ランダムキャラ素質:4) = 1
		SIF ランダムキャラ素質:5
			TALENT:(LOCAL:1):(ランダムキャラ素質:5) = 1
		FOR LOCAL, 0, 300
			IF TALENT数値保存:LOCAL == 5
				CONTINUE
			ENDIF
			TALENT:(LOCAL:1):(TALENT一時保存:LOCAL) = TALENT数値保存:LOCAL - 5
		NEXT
		TALENT:(LOCAL:1):容姿 = ランダムキャラ素質:30
		TALENT:(LOCAL:1):体格 = ランダムキャラ素質:31
		TALENT:(LOCAL:1):男性器サイズ = ランダムキャラ素質:32
		FOR LOCAL, 0, 300
			SIF TALENT数値保存:LOCAL == 0
				CONTINUE
			SIF TALENT数値保存:LOCAL == 5
				CONTINUE
			TALENT:(LOCAL:1):(TALENT一時保存:LOCAL) = TALENT数値保存:LOCAL - 5
		NEXT
		LOCAL = 0
		FOR COUNT, 1, 10
			SIF フレーバー素質数値保存:COUNT == 0
				CONTINUE
			フレーバー素質:(LOCAL:1):COUNT = フレーバー素質数値保存:COUNT
			LOCAL = 1
		NEXT
		IF LOCAL
			フレーバー素質:(LOCAL:1):素質表示設定 = 1
		ENDIF
		FOR LOCAL, 0, 50
			IF ABL数値保存:LOCAL > 0
				ABL:(LOCAL:1):(ABL一時保存:LOCAL) = ABL数値保存:LOCAL
			ENDIF
		NEXT
		FOR LOCAL, 0, 100
			IF EXP数値保存:LOCAL > 0
				EXP:(LOCAL:1):(EXP一時保存:LOCAL) = EXP数値保存:LOCAL
			ENDIF
		NEXT

		;戦闘関連設定
		CSTR:(LOCAL:1):戦闘基礎ステータス設定 =  属性_%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)% 得意武器_%得意武器数値文字列変換(ランダムキャラ戦闘タイプ:1)% 初期ステータスタイプ_%成長タイプ:(ランダムキャラ戦闘タイプ:2)%

		CALL コマンド存在チェック
		CALL キャラクター初期化(LOCAL:1)
		
		;性知識は初期化の後
		知識素質:(LOCAL:1):性知識 = ランダムキャラ素質:6
		
		CFLAG:(LOCAL:1):滞在期間 = 999

		FOR LOCAL:2, 0, 従業員キャラ上限
			IF 従業員部屋割り配列:(LOCAL:2) < 1
				従業員部屋割り配列:(LOCAL:2) = LOCAL:1
				BREAK
			ENDIF
		NEXT
		CALL 従業員人数チェック()
		RETURN 0
	CASE 1
		INVERTBIT 表示モード, 0
	CASE 999
		RETURN 0
ENDSELECT

GOTO 従業員_LOOP
