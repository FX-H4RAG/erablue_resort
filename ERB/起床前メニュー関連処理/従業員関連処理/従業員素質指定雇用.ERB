@従業員素質指定雇用
#DIM ランダムキャラ素質, 70
#DIM TALENT一時保存, 300
#DIM TALENT数値保存, 300
#DIM 肉体特徴数値保存, 10
#DIM 身長保存
#DIM 体型保存
#DIM バスト保存
#DIM ウエスト保存
#DIM ヒップ保存
#DIM 下半身保存
#DIM Ａ処女保存
#DIM 身長数値設定フラグ
#DIM バスト数値設定フラグ
#DIM ウエスト数値設定フラグ
#DIM ヒップ数値設定フラグ
#DIM フレーバー素質数値保存, 10
#DIM 知識素質数値保存, 100
#DIM 性癖素質数値保存, 20
#DIM 服装数値保存, 31
#DIMS CONST 水着一覧 = "ビキニ型","ワンピース型","ボーイレッグ・ビキニ（ボトムが短パン型）","プランジングネック（首元から胸下まで切り込みの入ったワンピース型）"
#DIMS CONST 男水着一覧 = "通常","全身タイプ"
#DIM ABL一時保存, 50
#DIM ABL数値保存, 50
#DIM EXP一時保存, 100
#DIM EXP数値保存, 100
#DIMS CONST 好物一覧 = "食べ物","酒","宝石","アクセサリ","実用品","本","花","その他"
#DIMS 好物一時保存
#DIM 同行者一時保存, 3000
#DIM 同行確率一時保存, 3000
#DIMS CONST 同行確率一覧 = "-25","-5","+5","+25"
#DIM ランダムキャラ経験, 80
#DIM ランダムキャラ戦闘タイプ, 10
#DIMS 固有素質一時保存, 10
#DIMS ランダムキャラネーム
#DIMS ランダムキャラCALLネーム
#DIMS ランダムキャラ二つ名
#DIMS ランダムキャラ出演作品
#DIM ランダムキャラ招待不可フラグ
#DIMS ランダムキャラ服装名
#DIM 重複チェック用, 10
#DIM 表示モード
#DIM 表示数カウント
#DIM ページ数
#DIM 必要ルピ
#DIM 登録番号
#DIMS 汎用画像キー
#DIMS CONST 表示切替 = "情報・素質","能力・経験","服装","同行者"
#DIM 部屋使用数
#DIM CSV番号
#DIMS CSTR切り分け文字列格納, 2
#DIMS ランダムキャラ奥義名, 6

表示モード = 0
ページ数 = 0
表示数カウント = 0
ランダムキャラネーム = 
ランダムキャラCALLネーム = 
ランダムキャラ二つ名 = （未定）
ランダムキャラ服装名 = （未定）
ランダムキャラ出演作品 = 
汎用画像キー = 
SIF CSVエディタフラグ
	汎用立ち絵制御OPTION = 0
ウエスト保存 = 0
ヒップ保存 = 0
下半身保存 = 0
VARSET ランダムキャラ素質:0
VARSET ランダムキャラ経験:0
VARSET TALENT一時保存:0
VARSET フレーバー素質数値保存
VARSET 知識素質数値保存
VARSET 肉体特徴数値保存
VARSET 服装数値保存
VARSET TALENT数値保存:0, 5
VARSET ABL一時保存:0, -1
VARSET ABL数値保存:0
VARSET EXP一時保存:0, -1
VARSET EXP数値保存:0
VARSET ランダムキャラ戦闘タイプ:0
VARSET ランダムキャラ奥義名
VARSET LOCAL
ランダムキャラ素質:0 = 1
ランダムキャラ素質:1 = 1
好物一時保存 = %好物一覧:(RAND(VARSIZE("好物一覧")))%_
服装数値保存:30 = !RAND:3 + !RAND:3 + !RAND:3
;ビキニ　8/27 ワンピース 12/27 ボーイレッグ 6/27 プランジングネック 1/27
ランダムキャラ招待不可フラグ = 3


CALL 従業員人数チェック()

; FOR LOCAL, 10, 71
; 	IF TALENTNAME:LOCAL != ""
; 		TALENT一時保存:(LOCAL:10) = LOCAL
; 		LOCAL:10 += 1
; 	ENDIF
; NEXT
; FOR LOCAL, 77, 132
; 	IF TALENTNAME:LOCAL != ""
; 		TALENT一時保存:(LOCAL:10) = LOCAL
; 		LOCAL:10 += 1
; 	ENDIF
; NEXT
; FOR LOCAL, 140, 150
; 	IF TALENTNAME:LOCAL != ""
; 		TALENT一時保存:(LOCAL:10) = LOCAL
; 		LOCAL:10 += 1
; 	ENDIF
; NEXT
; FOR LOCAL, 220, 300
; 	IF TALENTNAME:LOCAL != ""
; 		TALENT一時保存:(LOCAL:10) = LOCAL
; 		LOCAL:10 += 1
; 	ENDIF
; NEXT

FOR LOCAL, 0, 300
	IF TALENTNAME:LOCAL != "" && 素質カテゴリ(LOCAL) == "優先表示"
		TALENT一時保存:(LOCAL:10) = LOCAL
		LOCAL:10 += 1
	ENDIF
NEXT
FOR LOCAL, 0, 300
	IF TALENTNAME:LOCAL != "" && 素質カテゴリ(LOCAL) == "性格"
		TALENT一時保存:(LOCAL:10) = LOCAL
		LOCAL:10 += 1
	ENDIF
NEXT
FOR LOCAL, 0, 300
	IF TALENTNAME:LOCAL != "" && 素質カテゴリ(LOCAL) == "肉体"
		TALENT一時保存:(LOCAL:10) = LOCAL
		LOCAL:10 += 1
	ENDIF
NEXT
FOR LOCAL, 0, 300
	IF TALENTNAME:LOCAL != "" && 素質カテゴリ(LOCAL) == "その他"
		TALENT一時保存:(LOCAL:10) = LOCAL
		LOCAL:10 += 1
	ENDIF
NEXT

FOR LOCAL, 0, 21
	IF ABLNAME:LOCAL != ""
		ABL一時保存:(LOCAL:11) = LOCAL
		LOCAL:11 += 1
	ENDIF
NEXT
FOR LOCAL, 30, 50
	IF ABLNAME:LOCAL != ""
		ABL一時保存:(LOCAL:11) = LOCAL
		LOCAL:11 += 1
	ENDIF
NEXT

FOR LOCAL, 0, 100
	SIF GROUPMATCH(LOCAL:0, 89, 33)
		CONTINUE
	IF EXPNAME:LOCAL != ""
		EXP一時保存:(LOCAL:12) = LOCAL
		LOCAL:12 += 1
	ENDIF
NEXT

身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))

$従業員_LOOP
DRAWLINE
IF CSVエディタフラグ
	PRINTL 素質などを指定し、CSVファイルを作成します。
	PRINTL 作成したCSVファイルは「CSV_EDITFILE」フォルダの中にあるので、使用時にはCSVフォルダへ移動させてください。
	DRAWLINE
	LOCALS = 
ELSE
	PRINTL 従業員の素質を完全指定して雇います。
	PRINTPLAIN 指定した素質の数に応じて必要ルピが上昇するので注意してください。
	PRINTL
	DRAWLINE
	LOCALS = <div rect='81,0,1937,344' border='31' bcolor='#C0C0C0'></div>
	LOCALS += @"<div rect='156,62,1912,281'>所持ルピ：%NUM_FORMAT(MONEY)%<br>所持ZP　：%ZP所持量全文字列()%</div>"


	LOCALS += "<div rect='2050,0,3875,344' border='31' bcolor='#C0C0C0'></div>"
	LOCALS += "<div rect='2125,62,3812,281'>"
	必要ルピ = 1000
	;名前・戦闘タイプではルピ増加無し
	;肉体情報
	;バストサイズ・容姿は普通から外れるほどルピが嵩む
	IF ランダムキャラ素質:0 == 3
		必要ルピ += ABS(ランダムキャラ素質:3 - 3) * 500
		必要ルピ += ABS(ランダムキャラ素質:31 + 1) * 500
	ELSEIF ランダムキャラ素質:0 == 4
		必要ルピ += ABS(ランダムキャラ素質:3 + 2) * 500
		必要ルピ += ABS(ランダムキャラ素質:31 + 2) * 500
	ELSE
		必要ルピ += ABS(ランダムキャラ素質:3) * 200
		必要ルピ += ABS(ランダムキャラ素質:31) * 200
	ENDIF
	必要ルピ += ABS(ランダムキャラ素質:6) * 300
	必要ルピ += ABS(ランダムキャラ素質:30) * 500
	必要ルピ += ABS(ランダムキャラ素質:32) * 200
	LOCAL:1 = 0
	FOR LOCAL, 0, 300
		SIF TALENT数値保存:LOCAL == 0
			CONTINUE
		SIF TALENT数値保存:LOCAL == 5
			CONTINUE
		LOCAL:1 += 1
	NEXT
	必要ルピ += LOCAL:1 * LOCAL:1 * LOCAL:1 * 50 * (LOCAL:1 / 5 + 1)
	LOCAL:1 = 0
	FOR LOCAL, 0, 50
		SIF ABL数値保存:LOCAL < 1
			CONTINUE
		LOCAL:1 += ABL数値保存:LOCAL
	NEXT
	必要ルピ += LOCAL:1 * LOCAL:1 * LOCAL:1 * 50 * (LOCAL:1 / 10 + 1)
	LOCAL:1 = 0
	FOR LOCAL, 0, 100
		SIF EXP数値保存:LOCAL < 1
			CONTINUE
		LOCAL:1 += EXP数値保存:LOCAL
	NEXT
	必要ルピ += LOCAL:1 *  5 * (LOCAL:1 / 100 + 1)

	必要ルピ += ランダムキャラ素質:30 * ランダムキャラ素質:30 * 100


	LOCALS += @"現在の必要ルピ：%NUM_FORMAT(必要ルピ), 12%ルピ<br>"
	部屋使用数 = 0
	FOR LOCAL, 0, 従業員部屋数
		IF 従業員部屋割り配列:LOCAL != 0
			部屋使用数 ++
		ENDIF
	NEXT
	LOCALS += @"現在の従業員部屋使用数 / 部屋数：{部屋使用数}室 / {従業員部屋数}室"
	LOCALS += "</div>"
ENDIF

LOCALS += "<div rect='81,375,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='2050,375,3875,2812' border='31' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='156,500,1937,2812'>"
LOCALS += "<button value='100'>[100] 名前を設定する</button>"
LOCALS += "<br>"
IF CSVエディタフラグ
	LOCALS += "<button value='101'>[101] 二つ名を設定する</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='102'>[102] 呼び名を設定する</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='103'>[103] 出演作品を設定する</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='104'>[104] プレゼントの好みを設定する</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='105'>[105] 同行者候補を設定する</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='106'>[106] 招待可能・不可能を設定する</button>"
	LOCALS += "<br>"
ENDIF
LOCALS += "<button value='110'>[110] 戦闘タイプを設定する</button>"
LOCALS += "<br>"
LOCALS += "<button value='120'>[120] 肉体情報を設定する</button>"
LOCALS += "<br>"
LOCALS += "<button value='130'>[130] 素質を設定する</button>"
LOCALS += "<br>"
IF フレーバー素質設定 != 1 && CSVエディタフラグ == 0
	LOCALS += "<button value='131'>[131] フレーバー素質を設定する</button>"
	LOCALS += "<br>"
ENDIF
IF CSVエディタフラグ
	LOCALS += "<button value='132'>[132] 知識素質を設定する</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='133'>[133] 性癖素質を設定する</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='134'>[134] 固有素質を設定する</button>"
	LOCALS += "<br>"
ENDIF
LOCALS += "<button value='140'>[140] 能力を設定する</button>"
LOCALS += "<br>"
LOCALS += "<button value='150'>[150] 経験を設定する</button>"
LOCALS += "<br>"
IF CSVエディタフラグ
	LOCALS += "<button value='160'>[160] 服装を設定する</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='161'>[161] 水着を設定する</button>"
	LOCALS += "<br>"
ELSE
	IF 汎用立ち絵制御OPTION
		LOCALS += "<button value='170'>[170] 汎用立ち絵を選択する</button>"
		LOCALS += "<button value='171'>[171] 汎用立ち絵をOFFにする</button>"
		LOCALS += "<br>"
	ELSE
		LOCALS += "<button value='171'>[171] 汎用立ち絵をONにする</button>"
		LOCALS += "<br>"
	ENDIF
ENDIF
LOCALS += "<br>"
LOCALS += "<button value='200'>[200] これで決定する</button>"
LOCALS += "<br>"
IF CSVエディタフラグ
	LOCALS += "<button value='998'>[998] 既存キャラをコピーする</button>"
	LOCALS += "<br>"
ENDIF
LOCALS += "<button value='999'>[999] 戻る</button>"
LOCALS += "</div>"

LOCALS += "<div rect='2125,500,3675,2812'>"


SELECTCASE 表示モード
	CASE 0
		;現状の設定表示
		IF ランダムキャラネーム != ""
			IF CSVエディタフラグ
				LOCALS += @"■[%ランダムキャラ二つ名%]%ランダムキャラネーム, 18, LEFT%"
			ELSE
				LOCALS += @"■[従業員]%ランダムキャラネーム, 18, LEFT%"
			ENDIF
		ELSE
			IF CSVエディタフラグ
				LOCALS += @"■[%ランダムキャラ二つ名%]（ランダム決定）　　　"
			ELSE
				LOCALS += @"■[従業員]（ランダム決定）　　　"
			ENDIF
		ENDIF
		LOCALS:1 = %属性数値文字色変換_HTML(ランダムキャラ戦闘タイプ:0)%
		LOCALS += @"<font color='#%LOCALS:1%'>属性：%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%</font>"
		LOCALS += @"　得意武器：%得意武器数値文字列変換(ランダムキャラ戦闘タイプ:1)%　タイプ：%成長タイプ:(ランダムキャラ戦闘タイプ:2)%"
		IF CSVエディタフラグ
			IF ランダムキャラCALLネーム != ""
				LOCALS += @"<br>呼び名：%ランダムキャラCALLネーム%"
			ELSE
				LOCALS += @"<br>呼び名：（名前と共通）"
			ENDIF
		ENDIF
		IF ランダムキャラ出演作品 != ""
			LOCALS += @"<br>出演作品：%ランダムキャラ出演作品%"
		ENDIF
		IF CSVエディタフラグ
			LOCALS += @"<br>招待不可フラグ：{ランダムキャラ招待不可フラグ}"
		ENDIF
		LOCALS += "<br><br>"
		LOCALS += "□ 情報 □---------------------------------------------------------------<br>"

		LOCALS += "　種族："
		SELECTCASE ランダムキャラ素質:0
			CASE 1
				LOCALS += "ヒューマン"
			CASE 2
				LOCALS += "エルーン"
			CASE 3
				LOCALS += "ドラフ"
			CASE 4
				LOCALS += "ハーヴィン"
			CASE 5
				LOCALS += "星晶獣"
			CASE 6
				LOCALS += "その他"
		ENDSELECT
		IF 肉体特徴数値保存:0
			LOCALS += "（角あり）"
		ENDIF
		IF 肉体特徴数値保存:1
			LOCALS += "（尻尾あり）"
		ENDIF
		IF 肉体特徴数値保存:2
			LOCALS += "（エルーン耳）"
		ENDIF
		IF 肉体特徴数値保存:3
			LOCALS += "（ハーヴィン耳）"
		ENDIF
		LOCALS += "<br>"
		LOCALS += @"　身長：%DECIMAL_STRING(身長保存, 1)%cm"
		LOCALS += "　性別："
		SELECTCASE ランダムキャラ素質:1
			CASE 1
				LOCALS += "女性"
				IF GETBIT(ランダムキャラ素質:2, 0) == 0
					LOCALS += "（処女）"
				ENDIF
				IF Ａ処女保存 == 0
					LOCALS += "（Ａ処女）"
				ENDIF
			CASE 2
				LOCALS += "男性"
				IF GETBIT(ランダムキャラ素質:2, 1) == 0
					LOCALS += "（童貞）"
				ENDIF
				IF Ａ処女保存 == 0
					LOCALS += "（Ａ処女）"
				ENDIF
			CASE 3
				LOCALS += "ふたなり"
				IF GETBIT(ランダムキャラ素質:2, 0) == 0
					LOCALS += "（処女）"
				ENDIF
				IF GETBIT(ランダムキャラ素質:2, 1) == 0
					LOCALS += "（童貞）"
				ENDIF
				IF Ａ処女保存 == 0
					LOCALS += "（Ａ処女）"
				ENDIF
		ENDSELECT
		IF GETBIT(ランダムキャラ素質:1, 0)
			LOCALS += "<br>"
			LOCALS += @"　バストサイズ："
			IF バスト数値設定フラグ
				LOCALS += @"%DECIMAL_STRING(バスト保存, 1)%cm"
				LOCALS += @"%EDIT用バストサイズ表示(バスト保存, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))%"
			ELSE
				LOCALS += @"%GET_TALENTNAME(205,ランダムキャラ素質:3)%"
			ENDIF
			IF ウエスト数値設定フラグ
				LOCALS += @"　ウエスト："
				LOCALS += @"%DECIMAL_STRING(ウエスト保存, 1)%cm"
			ENDIF
			IF ヒップ数値設定フラグ
				LOCALS += @"　ヒップ："
				LOCALS += @"%DECIMAL_STRING(ヒップ保存, 1)%cm"
			ENDIF
		ENDIF
		LOCALS += "<br>"
		LOCALS += @"　性知識レベル："
		SELECTCASE ランダムキャラ素質:6
			CASE -2
				LOCALS += "無知"
			CASE -1
				LOCALS += "獣並み"
			CASE 0
				LOCALS += "疎い"
			CASE 1
				LOCALS += "必要十分"
			CASE 2
				LOCALS += "有識者"
			CASE 3
				LOCALS += "百戦錬磨"
		ENDSELECT
		LOCALS += "<br>"
		LOCALS += "　体格："
		IF ランダムキャラ素質:31 == 0
			LOCALS += "普通"
		ELSE
			LOCALS += @"%GET_TALENTNAME(161,ランダムキャラ素質:31)%"
		ENDIF
		LOCALS += "　　　体型："
		IF 体型保存 == 0
			LOCALS += "普通"
		ELSE
			LOCALS += @"%GET_TALENTNAME(163,体型保存)%"
		ENDIF
		LOCALS += "　　下半身："
		IF 下半身保存 == 0
			LOCALS += "普通"
		ELSE
			LOCALS += @"%GET_TALENTNAME(210,下半身保存)%"
		ENDIF
		IF CSVエディタフラグ == 0
			LOCALS += "　　　容姿："
			IF ランダムキャラ素質:30 == 0
				LOCALS += "モブ顔　　　"
			ELSE
				LOCALS += @"%GET_TALENTNAME(160,ランダムキャラ素質:30), 12, LEFT%"
			ENDIF
		ENDIF
		IF GETBIT(ランダムキャラ素質:1, 1)
			LOCALS += "<br>　男性器サイズ："
			LOCALS += @"%GET_TALENTNAME(162,ランダムキャラ素質:32)%"
		ENDIF
		LOCALS += "<br>"
		REPLACE 好物一時保存, "_", "、"
		LOCALS += @"　プレゼントの好み：%SUBSTRINGU(RESULTS, 0, STRLENSU(RESULTS) - 1)%"
		LOCALS += "<br>"
		LOCALS += "□ 素質 □---------------------------------------------------------------<br>"
		LOCALS:1 = 
		FOR LOCAL, 0, 300
			SIF TALENT数値保存:LOCAL == 0
				CONTINUE
			SIF TALENT数値保存:LOCAL == 5
				CONTINUE
			LOCALS += @"[%GET_TALENTNAME(TALENT一時保存:LOCAL, TALENT数値保存:LOCAL - 5)%]"
		NEXT
		FOR LOCAL, 1, 100
			SIF 知識素質数値保存:LOCAL == 0
				CONTINUE
			LOCALS += @"[%ERDNAME(知識素質, LOCAL)%Lv{知識素質数値保存:LOCAL}]"
		NEXT
		FOR LOCAL, 0, 10
			SIF 固有素質一時保存:LOCAL == ""
				CONTINUE
			LOCALS += @"[%固有素質一時保存:LOCAL%]"
		NEXT
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "□ 性癖 □---------------------------------------------------------------<br>"
		LOCALS:1 = 
		FOR LOCAL, 1, 20
			SIF 性癖素質数値保存:LOCAL == 0
				CONTINUE
			LOCALS += @"[%ERDNAME(性癖素質, LOCAL)%"
			SELECTCASE 性癖素質数値保存:LOCAL
				CASE 1
					LOCALS += "(興味)]"
				CASE 2
					LOCALS += "(愛好)]"
				CASE 3
					LOCALS += "(中毒)]"
			ENDSELECT
		NEXT
		LOCALS += "<br>"
		LOCALS += "<br>"
		IF CSVエディタフラグ == 0
			LOCALS += "□ フレーバー素質 □-----------------------------------------------------<br>"
			LOCALS:1 = 
			LOCAL = 0
			FOR COUNT, 0, 10
				IF COUNT == 6
					PRINTL
					LOCAL = 0
				ENDIF
				SIF フレーバー素質数値保存:COUNT == 0
					CONTINUE
				LOCALS += @"%ERDNAME(フレーバー素質, COUNT), 10, LEFT%：%GET_フレーバー素質NAME(COUNT, フレーバー素質数値保存:COUNT), 12, LEFT%"
				LOCAL += 1
				IF LOCAL == 3
					LOCAL = 0
					PRINTL
				ENDIF
			NEXT
		ENDIF
	CASE 1
		;現状の設定表示（能力素質）
		IF ランダムキャラネーム != ""
			IF CSVエディタフラグ
				LOCALS += @"■[%ランダムキャラ二つ名%]%ランダムキャラネーム, 18, LEFT%"
			ELSE
				LOCALS += @"■[従業員]%ランダムキャラネーム, 18, LEFT%"
			ENDIF
		ELSE
			IF CSVエディタフラグ
				LOCALS += @"■[%ランダムキャラ二つ名%]（ランダム決定）　　　"
			ELSE
				LOCALS += @"■[従業員]（ランダム決定）　　　"
			ENDIF
		ENDIF
		LOCALS:1 = %属性数値文字色変換_HTML(ランダムキャラ戦闘タイプ:0)%
		LOCALS += @"<font color='#%LOCALS:1%'>属性：%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%</font>"
		LOCALS += @"　得意武器：%得意武器数値文字列変換(ランダムキャラ戦闘タイプ:1)%　タイプ：%成長タイプ:(ランダムキャラ戦闘タイプ:2)%"
		LOCALS += @"<br>　通常時奥義名："
		IF ランダムキャラ奥義名:0 != ""
			LOCALS += @"%ランダムキャラ奥義名:0% / \@ ランダムキャラ奥義名:1 != ""? %ランダムキャラ奥義名:1%# %ランダムキャラ奥義名:0%＋ \@ / \@ ランダムキャラ奥義名:2 != ""? %ランダムキャラ奥義名:2%# %ランダムキャラ奥義名:0%＋＋ \@"
		ELSE
			LOCALS += "(汎用奥義)"
		ENDIF
		LOCALS += @"<br>　陥落時奥義名："
		IF ランダムキャラ奥義名:3 != ""
			LOCALS += @"%ランダムキャラ奥義名:3% / \@ ランダムキャラ奥義名:4 != ""? %ランダムキャラ奥義名:4%# %ランダムキャラ奥義名:3%＋ \@ / \@ ランダムキャラ奥義名:5 != ""? %ランダムキャラ奥義名:5%# %ランダムキャラ奥義名:3%＋＋ \@"
		ELSE
			LOCALS += "(陥落時汎用奥義)"
		ENDIF
		IF ランダムキャラ出演作品 != ""
			LOCALS += @"<br>出演作品：%ランダムキャラ出演作品%"
		ENDIF
		IF CSVエディタフラグ
			LOCALS += @"<br>招待不可フラグ：{ランダムキャラ招待不可フラグ}"
		ENDIF
		LOCALS += "<br><br>"
		LOCALS += "□ 能力 □---------------------------------------------------------------<br>"
		LOCAL:2 = 0
		FOR LOCAL, 0, 50
			SIF ABL数値保存:LOCAL < 1
				CONTINUE
			IF ABL一時保存:LOCAL > -1
				LOCALS += @"%ABLNAME:(ABL一時保存:LOCAL), 16%:{ABL数値保存:LOCAL, 2}   "
				LOCAL:2 += 1
				IF LOCAL:2 == 3
					LOCALS += "<br>"
					LOCAL:2 = 0
				ENDIF
			ENDIF
		NEXT
		SIF LOCAL:2 != 0
			LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "□ 経験 □---------------------------------------------------------------<br>"
		LOCAL:2 = 0
		FOR LOCAL, 0, 100
			SIF EXP数値保存:LOCAL < 1
				CONTINUE
			IF EXP一時保存:LOCAL > -1
				LOCALS += @"%EXPNAME:(EXP一時保存:LOCAL), 16%:{EXP数値保存:LOCAL, 3}   "
				LOCAL:2 += 1
				IF LOCAL:2 == 3
					LOCALS += "<br>"
					LOCAL:2 = 0
				ENDIF
			ENDIF
		NEXT
	CASE 2
		IF ランダムキャラネーム != ""
			IF CSVエディタフラグ
				LOCALS += @"■[%ランダムキャラ二つ名%]%ランダムキャラネーム, 18, LEFT%"
			ELSE
				LOCALS += @"■[従業員]%ランダムキャラネーム, 18, LEFT%"
			ENDIF
		ELSE
			IF CSVエディタフラグ
				LOCALS += @"■[%ランダムキャラ二つ名%]（ランダム決定）　　　"
			ELSE
				LOCALS += @"■[従業員]（ランダム決定）　　　"
			ENDIF
		ENDIF
		LOCALS:1 = %属性数値文字色変換_HTML(ランダムキャラ戦闘タイプ:0)%
		LOCALS += @"<font color='#%LOCALS:1%'>属性：%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%</font>"
		LOCALS += @"　得意武器：%得意武器数値文字列変換(ランダムキャラ戦闘タイプ:1)%　タイプ：%成長タイプ:(ランダムキャラ戦闘タイプ:2)%"
		IF ランダムキャラ出演作品 != ""
			LOCALS += @"<br>出演作品：%ランダムキャラ出演作品%"
		ENDIF
		IF CSVエディタフラグ
			LOCALS += @"<br>招待不可フラグ：{ランダムキャラ招待不可フラグ}"
		ENDIF
		LOCALS += "<br><br>"
		LOCALS += "□ 服装 □---------------------------------------------------------------<br>"
		LOCALS += @"　名称：%ランダムキャラ服装名%<br>"
		LOCAL = 0
		;鎧
		IF 服装数値保存:1
			LOCALS += "　　[上半身鎧]"
			LOCAL = 1
		ENDIF
		;鎧
		IF 服装数値保存:11 && 服装数値保存:12
			LOCALS += "[下半身鎧]"
			LOCAL = 1
		ELSEIF 服装数値保存:11
			LOCALS += "[下半身後方鎧]"
			LOCAL = 1
		ELSEIF 服装数値保存:12
			LOCALS += "[下半身前方鎧]"
			LOCAL = 1
		ENDIF
		SIF LOCAL
			LOCALS += "<br>"

		LOCAL = 0
		;上半身服
		IF 服装数値保存:2
			LOCALS += "　　[上半身服"
			IF 服装数値保存:3
				LOCALS += "（脇露出）"
			ENDIF
			IF 服装数値保存:4
				LOCALS += "（へそ露出）"
			ENDIF
			IF 服装数値保存:5
				LOCALS += "（はだけ可）"
			ENDIF
			IF 服装数値保存:6
				LOCALS += "（差し込み可）"
			ENDIF
			IF 服装数値保存:7
				LOCALS += "（着衣パイズリ可）"
			ENDIF
			IF 服装数値保存:8 && 服装数値保存:9
				LOCALS += "（ブラ抜き取り可）"
			ENDIF
			LOCALS += "]"
			LOCALS += "<br>"
		ENDIF

		;下半身服
		IF 服装数値保存:13
			LOCALS += "　　[下半身服"
			SIF 服装数値保存:14
				LOCALS += "（めくり可）"
			SIF 服装数値保存:15
				LOCALS += "（ずらし可）"
			SIF 服装数値保存:18 && 服装数値保存:17
				LOCALS += "（下着抜き取り可）"
			LOCALS += "]"
			LOCALS += "<br>"
		ENDIF

		IF 服装数値保存:20
			LOCALS += "　　[上下タイツ"
			SIF 服装数値保存:21 || 服装数値保存:22
				LOCALS += "（破れ）"
			LOCALS += "]"
			LOCALS += "<br>"
		ELSEIF 服装数値保存:8
			LOCALS += "　　[インナー"
			SIF 服装数値保存:21
				LOCALS += "（破れ）"
			LOCALS += "]"
			LOCALS += "<br>"
		ELSEIF 服装数値保存:16
			LOCALS += "　　[タイツ"
			SIF 服装数値保存:22
				LOCALS += "（破れ）"
			LOCALS += "]"
			LOCALS += "<br>"
		ENDIF

		LOCALS += "　　"
		SIF 服装数値保存:9 == 0
			LOCALS += "[ノーブラ]"
		SIF 服装数値保存:9 == 1
			LOCALS += "[ブラ着用]"
		SIF 服装数値保存:9 == 2
			LOCALS += "[ニプレス]"
		SIF 服装数値保存:17 == 0
			LOCALS += "[ノーパン]"
		SIF 服装数値保存:17 == 1
			LOCALS += "[下着着用]"
		SIF 服装数値保存:17 == 2
			LOCALS += "[前張り]"
		SIF 服装数値保存:17 == 3
			LOCALS += "[ふんどし]"

		LOCALS += "<br>"

		LOCALS += "<br>"
		LOCALS += @"　水着：%水着一覧:(服装数値保存:30)%"
		LOCALS += "<br>"
	CASE 3
		LOCALS += "■同行候補キャラの上限は100人です"
		IF DT_EXIST("DT_CHARA")
			VARSET 同行者一時保存
			VARSET 同行確率一時保存
			LOCAL:1 = 0
			FOR LOCAL, 0, DT_ROW_LENGTH("DT_CHARA")
				SIF !DT_CELL_GET("DT_CHARA", LOCAL, "VALUE")
					CONTINUE
				同行者一時保存:(LOCAL:1) = LOCAL
				同行確率一時保存:(LOCAL:1) = DT_CELL_GET("DT_CHARA", LOCAL, "VALUE")
				LOCAL:1 ++
				SIF LOCAL:1 >= VARSIZE("同行者一時保存")
					BREAK
			NEXT
			LOCALS += @"(現在{LOCAL:1, 3, RIGHT}人)■<br>"
			FOR LOCAL, ページ数 * 20, ページ数 * 20 + 20
				SIF LOCAL >= LOCAL:1
					BREAK
				LOCAL:2 = 同行者一時保存:LOCAL
				LOCALS += @"<nonbutton>[{DT_CELL_GET("DT_CHARA", LOCAL:2, "NO"), LOG10(キャラクタ数上限)+1, RIGHT}] %ADD_STR_SPACE(DT_CELL_GETS("DT_CHARA", LOCAL:2, "NAME"), "]"), 41, LEFT%：{DT_CELL_GET("DT_CHARA", LOCAL:2, "VALUE"), 3}％</nonbutton>"
				LOCALS += "<br>"
			NEXT
		ELSE
			LOCALS += "(現在  0人)■<br>"
		ENDIF
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,2906,3675,2962'>"
		IF ページ数 > 0
			LOCALS += "<button value='29994'>[|≪]</button>"
		ELSE
			LOCALS += "     "
		ENDIF
		IF ページ数 > 5
			LOCALS += "<button value='29996'> [－５]</button>"
		ELSE
			LOCALS += " 　　　"
		ENDIF
		LOCALS += @"　　　　　　　{ページ数, 4, RIGHT}/{LOCAL:1 / 20, 4, RIGHT}　　　　　 　　"
		IF ページ数 < LOCAL:1 / 20 - 5
			LOCALS += "<button value='29997'>[＋５] </button>"
		ELSE
			LOCALS += " 　　　"
		ENDIF
		IF ページ数 < LOCAL:1 / 20
			LOCALS += "<button value='29995'>[≫|]</button>"
		ENDIF
		LOCALS += "<br>"
		IF ページ数 > 0
			LOCALS += "<button value='29998'>[前のページ]</button>"
		ELSE
			LOCALS += "　　　　　　"
		ENDIF
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>　　　　"
		IF ページ数 < LOCAL:1 / 20
			LOCALS += "<button value='29999'>[次のページ]</button>"
		ENDIF

	CASE 110
		LOCALS += "属性："
		FOR LOCAL, 0, 6
			IF ランダムキャラ戦闘タイプ:0 == LOCAL
				LOCALS:1 = %属性数値文字色変換_HTML(ランダムキャラ戦闘タイプ:0)%
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{11000 + LOCAL}'>[%属性数値文字列変換(LOCAL)%]</button></font>"
		NEXT
		LOCALS += "<br>"
		LOCALS += "得意武器："
		FOR LOCAL, 0, 10
			IF ランダムキャラ戦闘タイプ:1 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{11010 + LOCAL}'>[%得意武器数値文字列変換(LOCAL)%]</button></font>"
		NEXT
		LOCALS += "<br>"
		LOCALS += "タイプ："
		FOR LOCAL, 0, 8
			IF ランダムキャラ戦闘タイプ:2 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{11020 + LOCAL}'>[%成長タイプ:LOCAL%]</button></font>"
		NEXT
		LOCALS += "<br>"
		LOCAL = 0
		DO
			IF LOCAL % 3 == 0
				LOCALS += @"<br>\@ LOCAL == 0? 通常# 陥落\@時奥義名："
				IF ランダムキャラ奥義名:(LOCAL/3) == ""
					LOCALS += @"（汎用\@ LOCAL == 0? # 陥落時\@奥義）"
					LOCAL += 3
					CONTINUE
				ENDIF
			ELSE
				LOCALS += " / "
			ENDIF
			LOCALS += @"%ランダムキャラ奥義名:LOCAL%"
			LOCAL ++
		LOOP LOCAL < 6
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "<button value='11100'>[ステータス画面に戻る]</button>"
	CASE 120
		LOCALS += "種族："
		FOR LOCAL, 1, 7
			IF ランダムキャラ素質:0 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12000 + LOCAL}'>[%GET_TALENTNAME(200,LOCAL)%]</button></font>"
		NEXT
		LOCALS += "<br>"
		LOCALS += "角："
		IF 肉体特徴数値保存:0
			LOCALS += @"<font color='#C0C0C0'><button value='{12070}'>[あり]</button></font>"
		ELSE
			LOCALS += @"<font color='#333333'><button value='{12070}'>[なし]</button></font>"
		ENDIF
		LOCALS += "　尻尾："
		IF 肉体特徴数値保存:1
			LOCALS += @"<font color='#C0C0C0'><button value='{12071}'>[あり]</button></font>"
		ELSE
			LOCALS += @"<font color='#333333'><button value='{12071}'>[なし]</button></font>"
		ENDIF
		LOCALS += "　エルーン耳："
		IF 肉体特徴数値保存:2
			LOCALS += @"<font color='#C0C0C0'><button value='{12072}'>[あり]</button></font>"
		ELSE
			LOCALS += @"<font color='#333333'><button value='{12072}'>[なし]</button></font>"
		ENDIF
		LOCALS += "　ハーヴィン耳："
		IF 肉体特徴数値保存:3
			LOCALS += @"<font color='#C0C0C0'><button value='{12073}'>[あり]</button></font>"
		ELSE
			LOCALS += @"<font color='#333333'><button value='{12073}'>[なし]</button></font>"
		ENDIF
		LOCALS += "<br>"
		LOCALS += "性別："
		IF ランダムキャラ素質:1 == 1
			LOCALS += @"<button value='{12007}'>[女性]</button>"
			LOCALS += @"<font color='#333333'><button value='{12008}'>[男性]</button></font>"
			LOCALS += @"<font color='#333333'><button value='{12009}'>[ふたなり]</button></font>"
		ELSEIF ランダムキャラ素質:1 == 2
			LOCALS += @"<font color='#333333'><button value='{12007}'>[女性]</button></font>"
			LOCALS += @"<button value='{12008}'>[男性]</button>"
			LOCALS += @"<font color='#333333'><button value='{12009}'>[ふたなり]</button></font>"
		ELSE
			LOCALS += @"<font color='#333333'><button value='{12007}'>[女性]</button></font>"
			LOCALS += @"<font color='#333333'><button value='{12008}'>[男性]</button></font>"
			LOCALS += @"<button value='{12009}'>[ふたなり]</button>"
		ENDIF
		LOCALS += "　　　純潔："
		IF GETBIT(ランダムキャラ素質:1, 0)
			IF GETBIT(ランダムキャラ素質:2, 0) == 0
				LOCALS:1 = C0C0C0
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12010}'>[処女]</button></font>"
				LOCALS:1 = 333333
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12011}'>[非処女]</button></font>"
			ELSE
				LOCALS:1 = 333333
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12010}'>[処女]</button></font>"
				LOCALS:1 = C0C0C0
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12011}'>[非処女]</button></font>"
			ENDIF
		ENDIF
		IF ランダムキャラ素質:1 != 1
			IF GETBIT(ランダムキャラ素質:2, 1) == 0
				LOCALS:1 = C0C0C0
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12012}'>[童貞]</button></font>"
				LOCALS:1 = 333333
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12013}'>[非童貞]</button></font>"
			ELSE
				LOCALS:1 = 333333
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12012}'>[童貞]</button></font>"
				LOCALS:1 = C0C0C0
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12013}'>[非童貞]</button></font>"
			ENDIF
		ENDIF
		IF Ａ処女保存 == 0
			LOCALS:1 = C0C0C0
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12014}'>[Ａ処女]</button></font>"
			LOCALS:1 = 333333
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12015}'>[非Ａ処女]</button></font>"
		ELSE
			LOCALS:1 = 333333
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12014}'>[Ａ処女]</button></font>"
			LOCALS:1 = C0C0C0
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12015}'>[非Ａ処女]</button></font>"
		ENDIF
		LOCALS += "<br>"
		LOCALS += "性知識レベル："
		FOR LOCAL, -2, 4
			IF ランダムキャラ素質:6 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12030 + LOCAL}'>[%性知識段階表示(LOCAL, -1)%]</button></font>"
		NEXT
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "身長："
		IF 身長数値設定フラグ
			IF 身長保存
				LOCALS:1 = %DECIMAL_STRING(身長保存, 1)%cm
			ELSE
				LOCALS:1 = 未設定
			ENDIF
			LOCALS += @"<button value='{12025}'>[%LOCALS:1%]</button>"
			LOCALS += @"　　　<button value='{12998}'>[自動で設定する]</button>"
		ELSE
			LOCALS += @"%DECIMAL_STRING(身長保存, 1)%cm"
			LOCALS += @"　　　<button value='{12998}'>[手動で設定する]</button>"
			LOCALS += @"　<button value='{12996}'>[リロール]</button>"
		ENDIF
		LOCALS += "<br>"
		IF GETBIT(ランダムキャラ素質:1, 0)
			LOCALS += "バストサイズ："
			IF バスト数値設定フラグ
				IF バスト保存
					LOCALS:1 = %DECIMAL_STRING(バスト保存, 1)%cm
					LOCALS:1 += @"%EDIT用バストサイズ表示(バスト保存, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))%"
				ELSE
					LOCALS:1 = 未設定
				ENDIF
				LOCALS += @"<button value='{12026}'>[%LOCALS:1%]</button>"
				LOCALS += @"　　　<button value='{12999}'>[カップ数で指定する]</button>"
				LOCALS += @"　<button value='{12997}'>[リロール]</button>"
			ELSE
				FOR LOCAL, -2, 4
					IF ランダムキャラ素質:3 == LOCAL
						LOCALS:1 = C0C0C0
					ELSE
						LOCALS:1 = 333333
					ENDIF
					LOCALS += @"<font color='#%LOCALS:1%'><button value='{12020 + LOCAL}'>[%GET_TALENTNAME(205,LOCAL)%]</button></font>"
				NEXT
				LOCALS += @"　　　<button value='{12999}'>[数値で指定する]</button>"
			ENDIF
			LOCALS += "<br>"
			LOCALS += "ウエストサイズ："
			IF ウエスト数値設定フラグ
				IF ウエスト保存
					LOCALS:1 = %DECIMAL_STRING(ウエスト保存, 1)%cm
				ELSE
					LOCALS:1 = 未設定
				ENDIF
				LOCALS += @"<button value='{12990}'>[%LOCALS:1%]</button>"
				LOCALS += @"　　　<button value='{12992}'>[指定しない]</button>"
				LOCALS += @"　<button value='{12994}'>[リロール]</button>"
			ELSE
				LOCALS += @"体格・体型により自動算出"
				LOCALS += @"　　　<button value='{12992}'>[数値で指定する]</button>"
			ENDIF
			LOCALS += "<br>"
			LOCALS += "ヒップサイズ："
			IF ヒップ数値設定フラグ
				IF ヒップ保存
					LOCALS:1 = %DECIMAL_STRING(ヒップ保存, 1)%cm
				ELSE
					LOCALS:1 = 未設定
				ENDIF
				LOCALS += @"<button value='{12991}'>[%LOCALS:1%]</button>"
				LOCALS += @"　　　<button value='{12993}'>[指定しない]</button>"
				LOCALS += @"　<button value='{12995}'>[リロール]</button>"
			ELSE
				LOCALS += @"体格・体型により自動算出"
				LOCALS += @"　　　<button value='{12993}'>[数値で指定する]</button>"
			ENDIF
		ENDIF
		LOCALS += "<br>"
		IF CSVエディタフラグ == 0
			LOCALS += "容姿："
			FOR LOCAL, -1, 3
				IF ランダムキャラ素質:30 == LOCAL
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				IF LOCAL == 0
					LOCALS += @"<font color='#%LOCALS:1%'><button value='{12040 + LOCAL}'>[モブ顔]</button></font>"
				ELSE
					LOCALS += @"<font color='#%LOCALS:1%'><button value='{12040 + LOCAL}'>[%GET_TALENTNAME(160, LOCAL)%]</button></font>"
				ENDIF
			NEXT
		ENDIF
		LOCALS += "<br>"
		IF !身長数値設定フラグ
			LOCALS += "体格："
			FOR LOCAL, -3, 3
				IF ランダムキャラ素質:31 == LOCAL
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				IF LOCAL == 0
					LOCALS += @"<font color='#%LOCALS:1%'><button value='{12050 + LOCAL}'>[普通]</button></font>"
				ELSE
					LOCALS += @"<font color='#%LOCALS:1%'><button value='{12050 + LOCAL}'>[%GET_TALENTNAME(161, LOCAL)%]</button></font>"
				ENDIF
			NEXT
			LOCALS += "<br>"
		ENDIF
		LOCALS += "体型："
		FOR LOCAL, -3, 4
			IF 体型保存 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			IF LOCAL == 0
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12080 + LOCAL}'>[普通]</button></font>"
			ELSE
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12080 + LOCAL}'>[%GET_TALENTNAME(163, LOCAL)%]</button></font>"
			ENDIF
		NEXT
		LOCALS += "<br>"
		LOCALS += "下半身："
		FOR LOCAL, -1, 2
			IF 下半身保存 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			IF LOCAL == 0
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12090 + LOCAL}'>[普通]</button></font>"
			ELSE
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12090 + LOCAL}'>[%GET_TALENTNAME(210, LOCAL)%]</button></font>"
			ENDIF
		NEXT
		LOCALS += "<br>"
		IF GETBIT(ランダムキャラ素質:1, 1)
			LOCALS += "男性器サイズ："
			FOR LOCAL, -2, 3
				IF ランダムキャラ素質:32 == LOCAL
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12060 + LOCAL}'>[%GET_TALENTNAME(162, LOCAL)%]</button></font>"
			NEXT
			LOCALS += "<br>"
		ENDIF
		LOCALS += "<br>"
		LOCALS += "<button value='11100'>[ステータス画面に戻る]</button>"
	CASE 130
		FOR LOCAL, ページ数 * 20, ページ数 * 20 + 20
			SIF TALENT一時保存:LOCAL == 0
				BREAK
			LOCALS += @"%TALENTNAME:(TALENT一時保存:LOCAL), 18, LEFT%："
			FOR LOCAL:1, -2, 4
				;母乳体質だけ例外
				IF TALENT一時保存:LOCAL == 149 && LOCAL:1 > 1
					CONTINUE
				ENDIF
				LOCALS:2 = %GET_TALENTNAME(TALENT一時保存:LOCAL,LOCAL:1)%
				IF LOCAL:1 == 0
					LOCALS:2 = なし
				ELSEIF LOCALS:2 == ""
					CONTINUE
				ENDIF
				IF TALENT数値保存:LOCAL - 5 == LOCAL:1
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{20000 + LOCAL * 10 + LOCAL:1 + 5}'>[%LOCALS:2%]</button></font>"
			NEXT
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,3006,3675,2812'>"
		IF ページ数 > 0
			LOCALS += "<button value='29998'>[前のページ]</button>"
		ELSE
			LOCALS += "　　　　　　"
		ENDIF
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>　　　　"
		IF ページ数 < LOCAL:10 / 20
			LOCALS += "<button value='29999'>[次のページ]</button>"
		ENDIF
	CASE 131
		FOR COUNT, 1, 6
			SIF ERDNAME(フレーバー素質, COUNT) == ""
				CONTINUE
			LOCALS += @"%ERDNAME(フレーバー素質, COUNT), 12, LEFT%："
			FOR LOCAL:1, 0, 6
				LOCALS:2 = %GET_フレーバー素質NAME(COUNT,LOCAL:1)%
				IF LOCAL:1 == 0
					LOCALS:2 = 非表示
				ENDIF
				IF COUNT == 5 && LOCAL:1 == 4
					CONTINUE
				ELSEIF LOCALS:2 == ""
					CONTINUE
				ENDIF
				IF フレーバー素質数値保存:COUNT == LOCAL:1
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{50000 + COUNT * 10 + LOCAL:1}'>[%LOCALS:2%]</button></font>"
			NEXT
			LOCALS += "<br>"
		NEXT
		LOCALS += @"%ERDNAME(フレーバー素質, 6), 12, LEFT%：　　　　"
		FOR LOCAL:1, 0, 4
			LOCALS:2 = %GET_フレーバー素質NAME(6, POWER(2, LOCAL:1))%
			IF LOCALS:2 == ""
				CONTINUE
			ELSEIF LOCAL:1 == 3 && GETBIT(ランダムキャラ素質:1, 1) == 0
				CONTINUE
			ENDIF
			IF GETBIT(フレーバー素質数値保存:6,LOCAL:1)
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{50000 + 6 * 10 + LOCAL:1}'>[%LOCALS:2%]</button></font>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,3006,3675,2812'>"
		LOCALS += "　　　　　　"
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>"
	CASE 132
		LOCALS += "趣味レベルは1、プロ級・職業に出来るレベルで2<br>全空でも最強レベルの場合は3が目安<br><br>"
		FOR COUNT, 1, 100
			SIF ERDNAME(知識素質, COUNT) == ""
				CONTINUE
			LOCALS += @"%ERDNAME(知識素質, COUNT), 12, LEFT%："
			FOR LOCAL:1, 0, 4
				IF LOCAL:1 == 0
					LOCALS:2 = なし
				ELSE
					LOCALS:2 = Lv{LOCAL:1}
				ENDIF
				IF 知識素質数値保存:COUNT == LOCAL:1
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{60000 + COUNT * 10 + LOCAL:1}'>[%LOCALS:2%]</button></font>"
			NEXT
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,3006,3675,2812'>"
		LOCALS += "　　　　　　"
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>"
	CASE 133
		FOR COUNT, 1, 20
			SIF ERDNAME(性癖素質, COUNT) == ""
				CONTINUE
			LOCALS += @"%ERDNAME(性癖素質, COUNT), 12, LEFT%："
			FOR LOCAL:1, 0, 4
				SELECTCASE LOCAL:1
					CASE 0
						LOCALS:2 = なし
					CASE 1
						LOCALS:2 = 興味
					CASE 2
						LOCALS:2 = 愛好
					CASE 3
						LOCALS:2 = 中毒
				ENDSELECT
				IF 性癖素質数値保存:COUNT == LOCAL:1
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{80000 + COUNT * 10 + LOCAL:1}'>[%LOCALS:2%]</button></font>"
			NEXT
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,3006,3675,2812'>"
		LOCALS += "　　　　　　"
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>"
	CASE 134
		FOR COUNT, 0, 10
			LOCALS += @"<button value='{85000 + COUNT}'>固有素質{COUNT}：[%固有素質一時保存:COUNT%]</button><br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,3006,3675,2812'>"
		LOCALS += "　　　　　　"
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>"
	CASE 140
		FOR LOCAL, ページ数 * 20, ページ数 * 20 + 20
			SIF ABL一時保存:LOCAL == -1
				BREAK
			LOCALS += @"%ABLNAME:(ABL一時保存:LOCAL), 18, LEFT%："
			FOR LOCAL:1, 0, 6
				IF ABL数値保存:LOCAL == LOCAL:1
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{30000 + LOCAL * 10 + LOCAL:1}'>[%TOFULL(TOSTR(LOCAL:1))%]</button></font>"
			NEXT
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,3006,3675,2812'>"
		IF ページ数 > 0
			LOCALS += "<button value='29998'>[前のページ]</button>"
		ELSE
			LOCALS += "　　　　　　"
		ENDIF
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>　　　　"
		IF ページ数 < LOCAL:11 / 20
			LOCALS += "<button value='29999'>[次のページ]</button>"
		ENDIF
	CASE 150
		FOR LOCAL, ページ数 * 20, ページ数 * 20 + 20
			SIF EXP一時保存:LOCAL == -1
				BREAK
			LOCALS += @"<button value='{40000 + LOCAL * 10}'>[%EXPNAME:(EXP一時保存:LOCAL), 18, LEFT%]：{EXP数値保存:LOCAL, 8}</button>"
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,3006,3675,2812'>"
		IF ページ数 > 0
			LOCALS += "<button value='29998'>[前のページ]</button>"
		ELSE
			LOCALS += "　　　　　　"
		ENDIF
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>　　　　"
		IF ページ数 < LOCAL:12 / 20
			LOCALS += "<button value='29999'>[次のページ]</button>"
		ENDIF
	CASE 160
		FOR LOCAL, 1, 23
			IF EQUIPNAME_TIPS(LOCAL) != ""
				LOCALS += @"<nonbutton title='%EQUIPNAME_TIPS(LOCAL)%'>%EQUIPNAME:LOCAL%"
				LOCALS += @"<font color='#808080'>%"(?)", 24 - STRLENS(EQUIPNAME:LOCAL), LEFT%</font>"
				LOCALS += "：</nonbutton>"
			ELSE
				LOCALS += @"%EQUIPNAME:LOCAL, 24, LEFT%："
			ENDIF
			LOCALS:1 = 333333
			LOCALS:2 = 333333
			LOCALS:3 = 333333
			LOCALS:4 = 333333
			LOCALS:(服装数値保存:LOCAL + 1) = C0C0C0
			IF LOCAL == 9
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{70000 + LOCAL * 10}'>[ノーブラ]</button></font>"
				LOCALS += @"<font color='#%LOCALS:2%'><button value='{70000 + LOCAL * 10 + 1}'>[ブラ着用]</button></font>"
				LOCALS += @"<font color='#%LOCALS:3%'><button value='{70000 + LOCAL * 10 + 2}'>[ニプレス]</button></font>"
			ELSEIF LOCAL == 17
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{70000 + LOCAL * 10}'>[ノーパン]</button></font>"
				LOCALS += @"<font color='#%LOCALS:2%'><button value='{70000 + LOCAL * 10 + 1}'>[下着着用]</button></font>"
				LOCALS += @"<font color='#%LOCALS:3%'><button value='{70000 + LOCAL * 10 + 2}'>[前張り]</button></font>"
				LOCALS += @"<font color='#%LOCALS:4%'><button value='{70000 + LOCAL * 10 + 3}'>[ふんどし]</button></font>"
			ELSE
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{70000 + LOCAL * 10}'>[なし]</button></font>"
				LOCALS += @"<font color='#%LOCALS:2%'><button value='{70000 + LOCAL * 10 + 1}'>[あり]</button></font>"
			ENDIF
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,3006,3675,2812'>"
		LOCALS += @"服装名：<button value='71000'>%ランダムキャラ服装名, 30, LEFT%</button>"
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>　　　　"
	CASE 105
		IF !DT_EXIST("DT_CHARA")
			DT_CREATE "DT_CHARA"
			DT_COLUMN_ADD "DT_CHARA", "NO", 2
			DT_COLUMN_ADD "DT_CHARA", "NAME"
			DT_COLUMN_ADD "DT_CHARA", "VALUE", 1
			FOR LOCAL, 1, キャラクタ数上限
				SIF !EXISTCSV(LOCAL)
					CONTINUE
				;ランダムキャラ排除
				SIF LOCAL == 999
					CONTINUE
				DT_ROW_ADD "DT_CHARA", "NO", LOCAL, "NAME", CSVNAME(LOCAL), "VALUE", 0
			NEXT
		ENDIF
		LOCALS += "■同行候補キャラは最大100人まで設定できます■<br>"
		LOCAL:1 = DT_ROW_LENGTH("DT_CHARA")
		FOR LOCAL, ページ数 * 20, ページ数 * 20 + 20
			SIF LOCAL >= LOCAL:1
				BREAK
			LOCALS += @"<button value='{100000 + LOCAL * 10}'>[{DT_CELL_GET("DT_CHARA", LOCAL, "NO"), LOG10(キャラクタ数上限)+1, RIGHT}] %ADD_STR_SPACE(DT_CELL_GETS("DT_CHARA", LOCAL, "NAME"), "]"), 41, LEFT%：{DT_CELL_GET("DT_CHARA", LOCAL, "VALUE"), 3}％</button>"
			LOCALS += "<font color='#606060'>"
			FOR LOCAL:2, 0, VARSIZE("同行確率一覧")
				LOCALS += @"<button value='{100000 + LOCAL * 10 + LOCAL:2 + 1}'>[%同行確率一覧:(LOCAL:2)%]</button>"
			NEXT
			LOCALS += "</font><br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='2125,2906,3675,2962'>"
		IF ページ数 > 0
			LOCALS += "<button value='29994'>[|≪]</button>"
		ELSE
			LOCALS += "     "
		ENDIF
		IF ページ数 > 5
			LOCALS += "<button value='29996'> [－５]</button>"
		ELSE
			LOCALS += " 　　　"
		ENDIF
		LOCALS += @"　　　　　　　{ページ数, 4, RIGHT}/{LOCAL:1 / 20, 4, RIGHT}　　　　　 　　"
		IF ページ数 < LOCAL:1 / 20 - 5
			LOCALS += "<button value='29997'>[＋５] </button>"
		ELSE
			LOCALS += " 　　　"
		ENDIF
		IF ページ数 < LOCAL:1 / 20
			LOCALS += "<button value='29995'>[≫|]</button>"
		ENDIF
		LOCALS += "<br>"
		IF ページ数 > 0
			LOCALS += "<button value='29998'>[前のページ]</button>"
		ELSE
			LOCALS += "　　　　　　"
		ENDIF
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>　　　　"
		IF ページ数 < LOCAL:1 / 20
			LOCALS += "<button value='29999'>[次のページ]</button>"
		ENDIF
ENDSELECT
LOCALS += "</div>"
IF CSVエディタフラグ && 表示モード < VARSIZE("表示切替") && 表示モード != 3
	LOCALS += "<div rect='2125,2850,3675,312'>"
	FOR LOCAL, 0, VARSIZE("表示切替")
		IF 表示モード == LOCAL
			LOCALS += @"<font color='#606060'>[{LOCAL}] %表示切替:LOCAL%表示へ</font>　"
		ELSE
			LOCALS += @"<button value='{LOCAL}'>[{LOCAL}] %表示切替:LOCAL%表示へ</button>　"
		ENDIF
		SIF LOCAL % 3 == 2
			LOCALS += "<br>"
	NEXT
	LOCALS += "</div>"
;	IF 表示モード == 0
;		LOCALS += "<div rect='3750,3000,2500,312'><button value='1'>[1] 能力・経験表示へ</button>　<button value='2'>[2] 服装表示へ</button></div>"
;	ELSEIF 表示モード == 1
;		LOCALS += "<div rect='3750,3000,2500,312'><button value='0'>[0] 情報・素質表示へ</button>　<button value='2'>[2] 服装表示へ</button></div>"
;	ELSEIF 表示モード == 2
;		LOCALS += "<div rect='3750,3000,2500,312'><button value='0'>[0] 情報・素質表示へ</button>　<button value='1'>[1] 能力・経験表示へ</button></div>"
;	ENDIF
ELSE
	IF 表示モード == 0
		LOCALS += "<div rect='4812,3000,2500,312'><button value='1'>[1] 能力・経験表示へ</button></div>"
	ELSEIF 表示モード == 1
		LOCALS += "<div rect='4812,3000,2500,312'><button value='0'>[0] 情報・素質表示へ</button></div>"
	ENDIF
ENDIF


IF 汎用立ち絵制御OPTION
	LOCALS:1 = <div rect='2050,31,1200,1200' padding='6' border='31' bcolor='#C0C0C0'>
	IF 汎用画像キー == ""
		LOCALS:1 += "<button value='170'><img src='Noimage_顔' height='1125'></button>"
	ELSE
		SPRITEDISPOSE @"短冊グラ3000"
		GDISPOSE 3000
		GCREATEFROMFILE 3000, @"汎用グラフィック/顔_%汎用画像キー%.png"
		;設定中の顔グラが存在すればそちらを拡縮で表示
		LOCAL = 画像サイズ切り替えOPTION記憶:1
		画像サイズ切り替えOPTION記憶:1 = 1
		SIF スプライト作成(@"短冊グラ3000", 3000, 0)
			LOCALS:1 += @"<button value='170'><img src='短冊グラ3000'height='1125'></button>"
		画像サイズ切り替えOPTION記憶:1 = LOCAL
	ENDIF
	LOCALS:1 += "</div>"
	HTML_PRINT LOCALS:1
	FOR LOCAL, 0, 10
		PRINTL
	NEXT
ENDIF

HTML_PRINT LOCALS
FOR LOCAL, 0, 28
	PRINTL
NEXT
DRAWLINE

BINPUT

LOCAL = RESULT
SELECTCASE LOCAL
;戦闘タイプ--------------------------------------------------------
	CASE 11000 TO 11005
		ランダムキャラ戦闘タイプ:0 = LOCAL - 11000
	CASE 11010 TO 11019
		ランダムキャラ戦闘タイプ:1 = LOCAL - 11010
	CASE 11020 TO 11027
		ランダムキャラ戦闘タイプ:2 = LOCAL - 11020
	CASE 11100
		表示モード = 0
;肉体情報--------------------------------------------------------
	CASE 12001
		ランダムキャラ素質:0 = 1
		肉体特徴数値保存:0 = 0
		肉体特徴数値保存:1 = 0
		肉体特徴数値保存:2 = 0
		肉体特徴数値保存:3 = 0
		IF !身長数値設定フラグ
			身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
			SIF !バスト数値設定フラグ
				バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
		ENDIF
	CASE 12002
		ランダムキャラ素質:0 = 2
		肉体特徴数値保存:0 = 0
		肉体特徴数値保存:1 = 0
		肉体特徴数値保存:2 = 1
		肉体特徴数値保存:3 = 0
		IF !身長数値設定フラグ
			身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
			SIF !バスト数値設定フラグ
				バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
		ENDIF
	CASE 12003
		ランダムキャラ素質:0 = 3
		ランダムキャラ素質:3 = 3
		肉体特徴数値保存:0 = 1
		肉体特徴数値保存:1 = 0
		肉体特徴数値保存:2 = 0
		肉体特徴数値保存:3 = 0
		IF ランダムキャラ素質:1 != 2
			ランダムキャラ素質:31 = -1
		ELSEIF ランダムキャラ素質:1 == 2
			ランダムキャラ素質:31 = 2
		ENDIF
		IF !身長数値設定フラグ
			身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
			SIF !バスト数値設定フラグ
				バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
		ENDIF
	CASE 12004
		ランダムキャラ素質:0 = 4
		ランダムキャラ素質:3 = -2
		肉体特徴数値保存:0 = 0
		肉体特徴数値保存:1 = 0
		肉体特徴数値保存:2 = 0
		肉体特徴数値保存:3 = 1
		ランダムキャラ素質:31 = -2
		IF !身長数値設定フラグ
			身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
			SIF !バスト数値設定フラグ
				バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
		ENDIF
	CASE 12005
		ランダムキャラ素質:0 = 5
		IF !身長数値設定フラグ
			身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
			SIF !バスト数値設定フラグ
				バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
		ENDIF
	CASE 12006
		ランダムキャラ素質:0 = 6
		IF !身長数値設定フラグ
			身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
			SIF !バスト数値設定フラグ
				バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
		ENDIF
	CASE 12007
		ランダムキャラ素質:1 = 1
		ランダムキャラ素質:32 = 0
		ランダムキャラ素質:2 = 0
		服装数値保存:30 = 0
		IF ランダムキャラ素質:0 == 3
			ランダムキャラ素質:31 = MIN(ランダムキャラ素質:31, 0)
		ENDIF
	CASE 12008
		ランダムキャラ素質:1 = 2
		ランダムキャラ素質:3 = 0
		ランダムキャラ素質:2 = 0
		服装数値保存:30 = 0
		汎用画像キー = 
		IF ランダムキャラ素質:0 == 3
			ランダムキャラ素質:31 = MAX(ランダムキャラ素質:31, 1)
		ENDIF
	CASE 12009
		ランダムキャラ素質:1 = 3
		服装数値保存:30 = 0
		IF ランダムキャラ素質:0 == 3
			ランダムキャラ素質:31 = MIN(ランダムキャラ素質:31, 0)
		ENDIF
	CASE 12010
		CLEARBIT ランダムキャラ素質:2, 0
	CASE 12011
		SETBIT ランダムキャラ素質:2, 0
	CASE 12012
		CLEARBIT ランダムキャラ素質:2, 1
	CASE 12013
		SETBIT ランダムキャラ素質:2, 1
	CASE 12014
		CLEARBIT Ａ処女保存, 1
	CASE 12015
		SETBIT Ａ処女保存, 1
	CASE 12018 TO 12023
		IF ランダムキャラ素質:0 == 3
			ランダムキャラ素質:3 = MAX(LOCAL - 12020, 2)
		ELSEIF ランダムキャラ素質:0 == 4
			ランダムキャラ素質:3 = MIN(LOCAL - 12020, -1)
		ELSE
			ランダムキャラ素質:3 = LOCAL - 12020
		ENDIF
		バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
	CASE 12025
		DRAWLINE
		PRINTL 身長をmm単位で入力してください。
		INPUT
		身長保存 = RESULT
		ランダムキャラ素質:31 = EDIT用体格算出(ランダムキャラ素質:1, 身長保存)
		SIF !バスト数値設定フラグ
			バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
	CASE 12026
		DRAWLINE
		PRINTL バストサイズをmm単位で入力してください。
		INPUT
		バスト保存 = RESULT
		ランダムキャラ素質:3 = EDIT用バストTALENT算出(バスト保存, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
	CASE 12028 TO 12033
		ランダムキャラ素質:6 = LOCAL - 12030
	CASE 12039 TO 12042
		ランダムキャラ素質:30 = LOCAL - 12040
	CASE 12047 TO 12052
		IF ランダムキャラ素質:0 == 3 && ランダムキャラ素質:1 != 2
			ランダムキャラ素質:31 = MIN(LOCAL - 12050, 0)
		ELSEIF ランダムキャラ素質:0 == 3 && ランダムキャラ素質:1 == 2
			ランダムキャラ素質:31 = MAX(LOCAL - 12050, 1)
		ELSEIF ランダムキャラ素質:0 == 4
			ランダムキャラ素質:31 = MIN(LOCAL - 12050, -1)
		ELSE
			ランダムキャラ素質:31 = LOCAL - 12050
		ENDIF
		身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
		SIF !バスト数値設定フラグ
			バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
	CASE 12058 TO 12062
		ランダムキャラ素質:32 = LOCAL - 12060
	CASE 12070 TO 12073
		INVERTBIT 肉体特徴数値保存:(LOCAL - 12070), 0
	CASE 12077 TO 12083
		体型保存 = LOCAL - 12080
	CASE 12089 TO 12091
		下半身保存 = LOCAL - 12090
	CASE 12990
		DRAWLINE
		PRINTL ウエストサイズをmm単位で入力してください。
		INPUT
		ウエスト保存 = RESULT
	CASE 12991
		DRAWLINE
		PRINTL ヒップサイズをmm単位で入力してください。
		INPUT
		ヒップ保存 = RESULT
	CASE 12992
		INVERTBIT ウエスト数値設定フラグ, 0
	CASE 12993
		INVERTBIT ヒップ数値設定フラグ, 0
	CASE 12994
		ウエスト保存 = EDIT用ウエスト算出(ランダムキャラ素質:1, 身長保存, ランダムキャラ素質:31, 体型保存)
	CASE 12995
		ヒップ保存 = EDIT用ヒップ算出(ランダムキャラ素質:1, 身長保存, ランダムキャラ素質:31, 体型保存, )
	CASE 12996
		身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
		ランダムキャラ素質:3 = EDIT用バストTALENT算出(バスト保存, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
	CASE 12997
		バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
	CASE 12998
		INVERTBIT 身長数値設定フラグ, 0
	CASE 12999
		INVERTBIT バスト数値設定フラグ, 0
;タレント--------------------------------------------------------
	CASE 20000 TO 29993
		TALENT数値保存:((LOCAL - 20000) / 10) = LOCAL % 10
	CASE 29994
		ページ数 = 0
	CASE 29995
		ページ数 = LOCAL:1 / 20
	CASE 29996
		ページ数 -= 5
	CASE 29997
		ページ数 += 5
	CASE 29998
		ページ数 -= 1
	CASE 29999
		ページ数 += 1
;ABL--------------------------------------------------------
	CASE 30000 TO 39997
		ABL数値保存:((LOCAL - 30000) / 10) = LOCAL % 10
;EXP--------------------------------------------------------
	CASE 40000 TO 49997
		RESULT = 0
		PRINTL 経験数を入力してください。（３桁まで）
		INPUT
		EXP数値保存:((LOCAL - 40000) / 10) = MIN(RESULT, 999)
;フレーバー素質----------------------------------------------------
	CASE 50000 TO 50059
		フレーバー素質数値保存:((LOCAL - 50000) / 10) = LOCAL % 10
	CASE 50060 TO 50069
		INVERTBIT フレーバー素質数値保存:((LOCAL - 50000) / 10), LOCAL % 10
;知識素質----------------------------------------------------
	CASE 60000 TO 60209
		知識素質数値保存:((LOCAL - 60000) / 10) = LOCAL % 10
;服装----------------------------------------------------
	CASE 70010 TO 70221
		服装数値保存:((LOCAL - 70000) / 10) = LOCAL % 10
	CASE 71000
		PRINTL 服装名を入力してください。（１５文字まで）
		INPUTS
		ランダムキャラ服装名 = %SUBSTRING(RESULTS, 0, 30)%
;性癖素質----------------------------------------------------
	CASE 80000 TO 80209
		性癖素質数値保存:((LOCAL - 80000) / 10) = LOCAL % 10
;固有素質----------------------------------------------------
	CASE 85000 TO 85010
		PRINTL 固有素質名を入力してください。
		INPUTS
		固有素質一時保存:(LOCAL - 85000) = %RESULTS%
;同行者候補----------------------------------------------------
	CASE 100000 TO 100000 + キャラクタ数上限 * 10
		IF LOCAL % 10 == 0
			PRINTL 同行確率を％で入力してください。（０～１００の間）
			INPUT
			DT_CELL_SET "DT_CHARA", (LOCAL - 100000) / 10, "VALUE", LIMIT(RESULT, 0, 100)
		ELSE
			LOCAL:2 = DT_CELL_GET("DT_CHARA", (LOCAL - 100000) / 10, "VALUE")
			LOCAL:2 += TOINT(同行確率一覧:(LOCAL % 10 - 1))
			DT_CELL_SET "DT_CHARA", (LOCAL - 100000) / 10, "VALUE", LIMIT(LOCAL:2, 0, 100)
		ENDIF
;メイン画面--------------------------------------------------------
	CASE 100
		$INPUT_NAME
		PRINTFORML 名前を入力してください。（９文字まで）　（現在：%ランダムキャラネーム%）
		PRINTL ┗[1] 命名テーブルからランダムに決定する　[2] 完全にランダムな名前を生成する　[0] 現在のものを使用する
		INPUTS (%ランダムキャラネーム%)
		IF RESULTS == "1" || RESULTS == "2"
			ランダムキャラネーム = %ランダムキャラ名前生成(ランダムキャラ素質:0, ランダムキャラ素質:1)%
			SIF RESULTS == "2"
				ランダムキャラネーム = %ランダムキャラ名ジェネレータ(ランダムキャラ素質:0, ランダムキャラ素質:1)%
			CLEARLINE 3
			GOTO INPUT_NAME
		ELSEIF RESULTS == "0"
		ELSE
			ランダムキャラネーム = %SUBSTRING(RESULTS, 0, 18)%
		ENDIF
	CASE 101
		PRINTL 二つ名を入力してください。（１２文字まで）
		INPUTS
		ランダムキャラ二つ名 = %SUBSTRING(RESULTS, 0, 24)%
	CASE 102
		PRINTL 呼び名を入力してください。（１２文字まで）
		INPUTS
		ランダムキャラCALLネーム = %SUBSTRING(RESULTS, 0, 24)%
	CASE 103
		PRINTL 出演作品を入力してください。（１２文字まで）
		INPUTS
		ランダムキャラ出演作品 = %SUBSTRING(RESULTS, 0, 24)%
	CASE 104
		$好物_LOOP
		DRAWLINE
		PRINTL プレゼントの好みを設定してください。
		FOR LOCAL:1, 0, VARSIZE("好物一覧")
			SIF STRCOUNT(好物一時保存, 好物一覧:(LOCAL:1))
				SETCOLOR カラーパレット("黄")
			PRINTFORM [{LOCAL:1}] %好物一覧:(LOCAL:1), 10, LEFT%　
			RESETCOLOR
			SIF (LOCAL:1 + 1) % 5 == 0
				PRINTL 
		NEXT
		SIF LOCAL:1 + 1 % 8
			PRINTL
		PRINTL 
		PRINTBUTTON "[999] 戻る", 999
		PRINTL
		BINPUT
		SIF RESULT == 999
			GOTO 従業員_LOOP
		IF STRCOUNT(好物一時保存, 好物一覧:RESULT)
			REPLACE 好物一時保存, @"%好物一覧:RESULT%_", ""
			好物一時保存 = %RESULTS%
		ELSE
			好物一時保存 += @"%好物一覧:RESULT%_"
		ENDIF
		GOTO 好物_LOOP
	CASE 106
		PRINTL ゲームを始めた直後の状態で、招待対象にならないキャラを設定します
		PRINTL 
		PRINTL 招待不可フラグ=0　初期から招待対象となる
		PRINTL 
		PRINTL 招待不可フラグ=1  ZPショップの「特別なキャラを招待候補に加える」の一覧に搭載される。
		PRINTL その際、「ZPショップ値段」の数値が値段になる。０(設定していない)場合、50ZPになる
		PRINTL フラグ３とはフレーバーによって使い分けること
		PRINTL 
		PRINTL 招待不可フラグ=2  ZPショップに搭載されず、また招待対象にもならない
		PRINTL 特別なイベントで招待可能になるキャラ用
		PRINTL 
		PRINTL 招待不可フラグ=3  ZPショップの「特別なキャラを招待候補に加える」の一覧に搭載される。
		PRINTL ただしイベント「「平行世界」という場所」をクリアしていなければ一覧に表示されない。
		PRINTL （基本的にグラブル以外のキャラに対して設定する）
		PRINTL その際、「ZPショップ値段」の数値が値段になる。０(設定していない)場合、50ZPになる
		PRINTL フラグ１とはフレーバーによって使い分けること
		DRAWLINE
		PRINTFORML 現在の招待不可フラグ＝{ランダムキャラ招待不可フラグ}
		FOR LOCAL, 0, 4
			SIF LOCAL == ランダムキャラ招待不可フラグ
				CONTINUE
			PRINTBUTTON @"[{LOCAL}] 招待不可フラグを{LOCAL}にする", LOCAL
			PRINTL
		NEXT
		PRINTL
		PRINTBUTTON "[999] 戻る", 999
		PRINTL
		BINPUT
		SELECTCASE RESULT
			CASE 999
				GOTO 従業員_LOOP
			CASEELSE
				ランダムキャラ招待不可フラグ = RESULT
		ENDSELECT
	CASE 110
		表示モード = 110
	CASE 120
		表示モード = 120
	CASE 130
		ページ数 = 0
		表示モード = 130
	CASE 131
		ページ数 = 0
		表示モード = 131
	CASE 132
		ページ数 = 0
		表示モード = 132
	CASE 133
		ページ数 = 0
		表示モード = 133
	CASE 134
		ページ数 = 0
		表示モード = 134
	CASE 140
		ページ数 = 0
		表示モード = 140
	CASE 150
		ページ数 = 0
		表示モード = 150
	CASE 160
		ページ数 = 0
		表示モード = 160
	CASE 105
		ページ数 = 0
		表示モード = 105
	CASE 161
		IF ランダムキャラ素質:1 == 2
			PRINTFORML [0] %男水着一覧:0%  [1] %男水着一覧:1%
		ELSE
			PRINTFORML [0] %水着一覧:0%  [1] %水着一覧:1%  [2] %水着一覧:2%  [3] %水着一覧:3%  
		ENDIF
		BINPUT
		服装数値保存:30 = RESULT
	CASE 170
		CALL 汎用立ち絵選択処理(汎用画像キー)
	CASE 171
		INVERTBIT 汎用立ち絵制御OPTION, 0
	CASE 200
		IF ランダムキャラネーム == ""
			ランダムキャラネーム = %ランダムキャラ名前生成(ランダムキャラ素質:0, ランダムキャラ素質:1)%
		ENDIF
		IF CSVエディタフラグ
			CLEARLINE LINECOUNT
			;空き番号取得
			FOR LOCAL:1, 2000, キャラクタ数上限
				IF EXISTCSV(LOCAL:1) == 0
					登録番号 = LOCAL:1
					BREAK
				ENDIF
			NEXT
			PRINTFORML 番号,{登録番号}
			PRINTFORML 名前,[%ランダムキャラ二つ名%]%ランダムキャラネーム%
			IF ランダムキャラCALLネーム != ""
				PRINTFORML 呼び名,%ランダムキャラCALLネーム%
			ELSE
				PRINTFORML 呼び名,%ランダムキャラネーム%
			ENDIF
			PRINTL 基礎,体力,1200
			PRINTL 基礎,勃起,1500
			PRINTL 基礎,ムード,1500
			PRINTL 基礎,理性,1000
			PRINTL 基礎,怒り,1000
			PRINTL 
			IF ランダムキャラ出演作品 != ""
				PRINTL ;-------------------------------------------------------------------------------------------
				PRINTL ;初期招待不可キャラ設定
				PRINTL ;ゲームを始めた直後の状態で、招待対象にならないキャラを設定する
				PRINTL ;招待不可フラグ=1  ZPショップの「特別なキャラを招待候補に加える」の一覧に搭載される。
				PRINTL 		;その際、「ZPショップ値段」の数値が値段になる。０(設定していない)場合、50ZPになる
				PRINTL 		;フラグ３とはフレーバーによって使い分けること
				PRINTL 
				PRINTL ;招待不可フラグ=2  ZPショップに搭載されず、また招待対象にもならない
				PRINTL 		;特別なイベントで招待可能になるキャラ用
				PRINTL 
				PRINTL ;招待不可フラグ=3  ZPショップの「特別なキャラを招待候補に加える」の一覧に搭載される。
				PRINTL 		;ただしイベント「「平行世界」という場所」をクリアしていなければ一覧に表示されない。
				PRINTL 		;その際、「ZPショップ値段」の数値が値段になる。０(設定していない)場合、50ZPになる
				PRINTL 		;フラグ１とはフレーバーによって使い分けること
				PRINTL ;-------------------------------------------------------------------------------------------
				PRINTFORML CSTR,出演作品,%ランダムキャラ出演作品%
				PRINTFORML フラグ,招待不可フラグ,{ランダムキャラ招待不可フラグ}
			ENDIF
			PRINTL 
			PRINTFORML 基礎,身長,{身長保存}
			SIF バスト数値設定フラグ && ランダムキャラ素質:1 != 2
				PRINTFORML 基礎,バスト,{バスト保存}
			SIF ウエスト数値設定フラグ && ウエスト保存 && ランダムキャラ素質:1 != 2
				PRINTFORML 基礎,ウエスト,{ウエスト保存}
			SIF ヒップ数値設定フラグ && ヒップ保存 && ランダムキャラ素質:1 != 2
				PRINTFORML 基礎,ヒップ,{ヒップ保存}
			PRINTL 
			PRINTL {
			PRINTL CSTR,知識素質設定,
			PRINTFORML 性知識Lv{ランダムキャラ素質:6}
			FOR LOCAL, 1, 20
				IF ERDNAME(知識素質, LOCAL) != ""
					PRINTFORML %ERDNAME(知識素質, LOCAL)%Lv{知識素質数値保存:LOCAL}
				ENDIF
			NEXT
			PRINTL }
			PRINTL 
			PRINTL {
			PRINTL CSTR,性癖素質設定,
			FOR LOCAL, 1, 20
				IF ERDNAME(性癖素質, LOCAL) != ""
					PRINTFORML %ERDNAME(性癖素質, LOCAL)%Lv{性癖素質数値保存:LOCAL}
				ENDIF
			NEXT
			PRINTL }
			PRINTL
			PRINTFORML 素質,種族,{ランダムキャラ素質:0}
			PRINTFORML 素質,性別,{ランダムキャラ素質:1}
			IF GETBIT(ランダムキャラ素質:2, 0)
				PRINTL 素質,処女,0
			ELSE
				PRINTL 素質,処女,1
			ENDIF
			IF GETBIT(ランダムキャラ素質:2, 1)
				PRINTL 素質,非童貞,1
			ELSE
				PRINTL 素質,非童貞,0
			ENDIF
			IF Ａ処女保存
				PRINTL 素質,Ａ処女,0
			ELSE
				PRINTL 素質,Ａ処女,1
			ENDIF
			PRINTFORML 素質,バストサイズ,{ランダムキャラ素質:3}
			SIF 肉体特徴数値保存:0
				PRINTFORML 素質,角あり,1
			SIF 肉体特徴数値保存:1
				PRINTFORML 素質,尻尾あり,1
			SIF 肉体特徴数値保存:2
				PRINTFORML 素質,エルーン耳,1
			SIF 肉体特徴数値保存:3
				PRINTFORML 素質,ハーヴィン耳,1
			FOR LOCAL, 0, 300
				SIF TALENT数値保存:LOCAL == 0
					CONTINUE
				SIF TALENT数値保存:LOCAL == 5
					CONTINUE
				PRINTFORML 素質,%TALENTNAME:(TALENT一時保存:LOCAL)%,{TALENT数値保存:LOCAL - 5}
			NEXT
			FOR LOCAL, 0, 10
				SIF 固有素質一時保存:LOCAL == ""
					CONTINUE
				PRINTFORML CSTR,固有素質{LOCAL},%固有素質一時保存:LOCAL%
			NEXT
			PRINTFORML 素質,体格,{ランダムキャラ素質:31}
			PRINTFORML 素質,体型,{体型保存}
			PRINTFORML 素質,男性器サイズ,{ランダムキャラ素質:32}
			PRINTFORML 素質,下半身,{下半身保存}
			PRINTL 

			FOR LOCAL, 0, 50
				IF ABL数値保存:LOCAL > 0
					PRINTFORML 能力,%ABLNAME:(ABL一時保存:LOCAL)%,{ABL数値保存:LOCAL}
				ENDIF
			NEXT
			PRINTL 
			FOR LOCAL, 0, 100
				IF EXP数値保存:LOCAL > 0
					PRINTFORML 経験,%EXPNAME:(EXP一時保存:LOCAL)%,{EXP数値保存:LOCAL}
				ENDIF
			NEXT
			PRINTL 

			PRINTL ;-------------------------------------------------------------------------------------------
			PRINTL ;プレゼント好み
			PRINTL ;・カテゴリ一覧
			PRINTL ;食べ物,酒,宝石,アクセサリ,実用品,本,花,その他
			PRINTL ;複数好みにする場合は「食べ物_酒」のように記述すること
			PRINTL ;-------------------------------------------------------------------------------------------
			PRINTFORML CSTR,プレゼント好み,%SUBSTRINGU(好物一時保存, 0, STRLENSU(好物一時保存) - 1)%
			PRINTL 
			PRINTL ;-------------------------------------------------------------------------------------------
			PRINTL ;同行キャラ設定
			PRINTL ;「キャラのフルネーム_同行確率%」で記述すること。フルネームは二つ名込みで記述。（被り防止のため）
			PRINTL ;-------------------------------------------------------------------------------------------
			PRINTL {
			PRINTL CSTR,同行キャラ,
			FOR LOCAL, 0, キャラクタ数上限
				IF DT_CELL_GET("DT_CHARA", LOCAL, "VALUE")
					PRINTFORM %DT_CELL_GETS("DT_CHARA", LOCAL, "NAME")%_
					PRINTFORM {DT_CELL_GET("DT_CHARA", LOCAL, "VALUE")}
					PRINTL %
				ENDIF
			NEXT
			PRINTL }
			PRINTL 
			
			PRINTFORML CSTR,服名称,%ランダムキャラ服装名%
			FOR LOCAL, 1, 23
				PRINTFORML 装着物,%EQUIPNAME:LOCAL%,{服装数値保存:LOCAL}
				SIF EQUIPNAME_TIPS(LOCAL) != ""
					PRINTFORML ;%EQUIPNAME_TIPS(LOCAL)%
			NEXT
			
			IF 服装数値保存:30
				PRINTFORML ;水着は%水着一覧:(服装数値保存:30)%
				PRINTFORML フラグ,水着,{服装数値保存:30}
			ENDIF
			PRINTL 

;-------------------------------------------------------------------------------------------
			PRINTL ;戦闘ステータス設定
			PRINTL ;初期値や成長タイプは基本的に用意したテンプレートを選択する仕様。
			PRINTL ;細かく設定したい場合は「キャラ名_パラメータ設定」の「@口上側初期パラメータ変更_XXXX」で行うこと
			PRINTL ;-------------------------------------------------------------------------------------------
			PRINTL {
			PRINTFORML CSTR,戦闘基礎ステータス設定,
			PRINTFORML 属性_%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%
			PRINTFORML 得意武器_%得意武器数値文字列変換(ランダムキャラ戦闘タイプ:1)%
			PRINTFORML 初期ステータスタイプ_%成長タイプ:(ランダムキャラ戦闘タイプ:2)%
			PRINTL }
			PRINTL 
			PRINTL ;-------------------------------------------------------------------------------------------
			PRINTL ;表示奥義設定
			PRINTL ;設定すると固有奥義を口上で設定しなくても、汎用奥義の表示名のみを変更する
			PRINTL ;陥落奥義設定に記述しておくと、陥落時に表示名を変更できる
			PRINTL ; _で区切ることで＋/＋＋とランクアップするときに表示名を変更できる。
			PRINTL ;ただし、ランクアップ後のみを設定することはできない。
			PRINTL ;-------------------------------------------------------------------------------------------
			PRINT CSTR,表示奥義設定,
			IF ランダムキャラ奥義名:0 != ""
				PRINTFORM %ランダムキャラ奥義名:0%
				SIF ランダムキャラ奥義名:1 != "" || ランダムキャラ奥義名:2 != ""
					PRINTFORM %ランダムキャラ奥義名:1%_%ランダムキャラ奥義名:2%
				PRINTL
			ELSE
				PRINTL
			ENDIF
			PRINT CSTR,陥落奥義設定,
			IF ランダムキャラ奥義名:3 != ""
				PRINTFORM %ランダムキャラ奥義名:3%
				SIF ランダムキャラ奥義名:4 != "" || ランダムキャラ奥義名:5 != ""
					PRINTFORM %ランダムキャラ奥義名:4%_%ランダムキャラ奥義名:5%
				PRINTL
			ELSE
				PRINTL
			ENDIF
			PRINTL 
			OUTPUTLOG @"CSV_EDITFILE/Chara{登録番号} %ランダムキャラネーム%.csv"
			PRINTW CSV_EDITFILEフォルダに出力しました。CSV/キャラデータフォルダに移動して再起動してください
			DT_RELEASE "DT_CHARA"
			RETURN 0
		ENDIF
		LOCAL:5 = 0
		FOR LOCAL, 0, 従業員部屋数
			IF 従業員部屋割り配列:LOCAL < 1
				LOCAL:5 = 1
				BREAK
			ENDIF
		NEXT
		IF LOCAL:5 == 0
			PRINTW 従業員部屋が一杯です。
			RETURN 0
		ENDIF
		IF 必要ルピ > MONEY
			PRINTW ルピが足りません。
			RETURN 0
		ENDIF
		MONEY -= 必要ルピ
		PRINTFORMW [従業員]%ランダムキャラネーム%を雇用しました。
		FLAG:従業員数 += 1
		LOCAL:1 = CHARANUM
		ADDCHARA 999
		TALENT:(LOCAL:1):従業員 = 1
		NAME:(LOCAL:1) = [従業員]%ランダムキャラネーム%
		CALLNAME:(LOCAL:1) = %ランダムキャラネーム%
		TALENT:(LOCAL:1):200 = ランダムキャラ素質:0
		TALENT:(LOCAL:1):2 = ランダムキャラ素質:1
		
		IF Ａ処女保存
			TALENT:(LOCAL:1):Ａ処女 = 0
		ELSE
			TALENT:(LOCAL:1):Ａ処女 = 1
		ENDIF
		IF GETBIT(ランダムキャラ素質:2, 0)
			TALENT:(LOCAL:1):0 = 0
		ELSE
			TALENT:(LOCAL:1):0 = 1
		ENDIF
		IF GETBIT(ランダムキャラ素質:2, 1)
			TALENT:(LOCAL:1):1 = 1
		ELSE
			TALENT:(LOCAL:1):1 = 0
		ENDIF
		TALENT:(LOCAL:1):205 = ランダムキャラ素質:3
		
		SIF 汎用画像キー == ""
			汎用画像キー '= 汎用立ち絵取得処理(ランダムキャラネーム, ランダムキャラ素質:0, ランダムキャラ素質:3, ランダムキャラ素質:31)
		CSTR:(LOCAL:1):汎用立ち絵登録文字列 = %汎用画像キー%
		
		BASE:(LOCAL:1):身長 = 身長保存
		MAXBASE:(LOCAL:1):身長 = 身長保存
		BASE:(LOCAL:1):バスト = バスト保存
		MAXBASE:(LOCAL:1):バスト = バスト保存

		SIF 肉体特徴数値保存:0
			TALENT:(LOCAL:1):206 = 1
		SIF 肉体特徴数値保存:1
			TALENT:(LOCAL:1):207 = 1
		SIF 肉体特徴数値保存:2
			TALENT:(LOCAL:1):208 = 1
		SIF 肉体特徴数値保存:3
			TALENT:(LOCAL:1):209 = 1
		FOR LOCAL, 0, 300
			IF TALENT数値保存:LOCAL == 5
				CONTINUE
			ENDIF
			TALENT:(LOCAL:1):(TALENT一時保存:LOCAL) = TALENT数値保存:LOCAL - 5
		NEXT
		TALENT:(LOCAL:1):容姿 = ランダムキャラ素質:30
		TALENT:(LOCAL:1):体格 = ランダムキャラ素質:31
		TALENT:(LOCAL:1):体型 = 体型保存
		TALENT:(LOCAL:1):男性器サイズ = ランダムキャラ素質:32
		FOR LOCAL, 0, 300
			SIF TALENT数値保存:LOCAL == 0
				CONTINUE
			SIF TALENT数値保存:LOCAL == 5
				CONTINUE
			TALENT:(LOCAL:1):(TALENT一時保存:LOCAL) = TALENT数値保存:LOCAL - 5
		NEXT
		LOCAL = 0
		FOR COUNT, 1, 10
			SIF フレーバー素質数値保存:COUNT == 0
				CONTINUE
			フレーバー素質:(LOCAL:1):COUNT = フレーバー素質数値保存:COUNT
			LOCAL = 1
		NEXT
		IF LOCAL
			フレーバー素質:(LOCAL:1):素質表示設定 = 1
		ENDIF
		FOR LOCAL, 0, 50
			IF ABL数値保存:LOCAL > 0
				ABL:(LOCAL:1):(ABL一時保存:LOCAL) = ABL数値保存:LOCAL
			ENDIF
		NEXT
		FOR LOCAL, 0, 100
			IF EXP数値保存:LOCAL > 0
				EXP:(LOCAL:1):(EXP一時保存:LOCAL) = EXP数値保存:LOCAL
			ENDIF
		NEXT

		CSTR:(LOCAL:1):プレゼント好み = %SUBSTRING(好物一時保存, 0, STRLENS(好物一時保存) - 1)%
		CFLAG:(LOCAL:1):水着 = 服装数値保存:30

		;戦闘関連設定
		CSTR:(LOCAL:1):戦闘基礎ステータス設定 =  属性_%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)% 得意武器_%得意武器数値文字列変換(ランダムキャラ戦闘タイプ:1)% 初期ステータスタイプ_%成長タイプ:(ランダムキャラ戦闘タイプ:2)%

		CALL ADD_PERSON_FROM_CHARA(LOCAL:1)
		CALL コマンド存在チェック
		従業員追加中フラグ = 1
		CALL キャラクター初期化(LOCAL:1)
		
		;性知識は初期化の後
		知識素質:(LOCAL:1):性知識 = ランダムキャラ素質:6
		
		CFLAG:(LOCAL:1):滞在期間 = 999
		CFLAG:(LOCAL:1):招待不可フラグ = 0
		CFLAG:(LOCAL:1):探索参加許可フラグ = 1
		CALL ステータス記録(LOCAL:1)

		FOR LOCAL:2, 0, 従業員部屋数
			IF 従業員部屋割り配列:(LOCAL:2) < 1
				従業員部屋割り配列:(LOCAL:2) = LOCAL:1
				BREAK
			ENDIF
		NEXT
		CALL 従業員人数チェック()
		RETURN 0
	CASE 0
		表示モード = 0
	CASE 1
		表示モード = 1
	CASE 2
		表示モード = 2
	CASE 3
		ページ数 = 0
		表示モード = 3
	CASE 998
		CALL CSVコピー処理
		CSV番号 = RESULT
		IF EXISTCSV(CSV番号)
			;コピー処理
			PRINTFORMW %CSVNAME(CSV番号)%のデータをコピーしました。
			VARSET RESULTS
			SPLIT CSVNAME(CSV番号), "]", RESULTS
			ランダムキャラ二つ名 = %SUBSTRING(RESULTS:0, 1, -1)%
			ランダムキャラネーム = %REPLACE(CSVNAME(CSV番号), ".*\]", "")%
			ランダムキャラCALLネーム = %CSVCALLNAME(CSV番号)%

			LOCALS = %CSVCSTR(CSV番号, GETNUM(CSTR, "戦闘基礎ステータス設定"))%
			VARSET RESULTS

			SUBSTRING LOCALS, 1, -1
			SPLIT RESULTS, " ", LOCALS
			;属性
			SPLIT LOCALS:0, "_", CSTR切り分け文字列格納
			ランダムキャラ戦闘タイプ:0 = 属性文字列数値変換(CSTR切り分け文字列格納:1)
			;得意武器
			SPLIT LOCALS:1, "_", CSTR切り分け文字列格納
			ランダムキャラ戦闘タイプ:1 = 得意武器文字列数値変換(CSTR切り分け文字列格納:1)
			;初期ステータス
			SPLIT LOCALS:2, "_", CSTR切り分け文字列格納
			ランダムキャラ戦闘タイプ:2 = タイプ文字列数値変換(CSTR切り分け文字列格納:1)

			ランダムキャラ出演作品 = %CSVCSTR(CSV番号, GETNUM(CSTR, "出演作品"))%
			ランダムキャラ招待不可フラグ = CSVCFLAG(CSV番号, GETNUM(CFLAG, "招待不可フラグ"))

			ランダムキャラ素質:0 = CSVTALENT(CSV番号, GETNUM(TALENT, "種族"))
			ランダムキャラ素質:1 = CSVTALENT(CSV番号, GETNUM(TALENT, "性別"))
			ランダムキャラ素質:2 = 0
			SIF CSVTALENT(CSV番号, GETNUM(TALENT, "処女")) == 0
				SETBIT ランダムキャラ素質:2, 0
			SIF CSVTALENT(CSV番号, GETNUM(TALENT, "非童貞"))
				SETBIT ランダムキャラ素質:2, 1
			IF CSVTALENT(CSV番号, GETNUM(TALENT, "Ａ処女"))
				Ａ処女保存 = 0
			ELSE
				Ａ処女保存 = 1
			ENDIF
			肉体特徴数値保存:0 = CSVTALENT(CSV番号, GETNUM(TALENT, "角あり"))
			肉体特徴数値保存:1 = CSVTALENT(CSV番号, GETNUM(TALENT, "尻尾あり"))
			肉体特徴数値保存:2 = CSVTALENT(CSV番号, GETNUM(TALENT, "エルーン耳"))
			肉体特徴数値保存:3 = CSVTALENT(CSV番号, GETNUM(TALENT, "ハーヴィン耳"))

			身長保存 = CSVBASE(CSV番号, GETNUM(BASE, "身長"))
			IF GETBIT(ランダムキャラ素質:1, 0)
				バスト保存 = CSVBASE(CSV番号, GETNUM(BASE, "バスト"))
				ランダムキャラ素質:3 = CSVTALENT(CSV番号, GETNUM(TALENT, "バストサイズ"))
				ウエスト保存 = CSVBASE(CSV番号, GETNUM(BASE, "ウエスト"))
				ヒップ保存 = CSVBASE(CSV番号, GETNUM(BASE, "ヒップ"))
			ENDIF
			ランダムキャラ素質:31 = CSVTALENT(CSV番号, GETNUM(TALENT, "体格"))
			体型保存 = CSVTALENT(CSV番号, GETNUM(TALENT, "体型"))
			下半身保存 = CSVTALENT(CSV番号, GETNUM(TALENT, "下半身"))
			IF GETBIT(ランダムキャラ素質:1, 1)
				ランダムキャラ素質:32 = CSVTALENT(CSV番号, GETNUM(TALENT, "男性器サイズ"))
			ENDIF
			好物一時保存 = %CSVCSTR(CSV番号, GETNUM(CSTR, "プレゼント好み"))%_

			;素質
			FOR LOCAL, 0, 300
				SIF TALENT一時保存:LOCAL < 1
					CONTINUE
				TALENT数値保存:LOCAL = CSVTALENT(CSV番号, TALENT一時保存:LOCAL) + 5
			NEXT

			;知識素質
			VARSET LOCALS
			SUBSTRING CSVCSTR(CSV番号, GETNUM(CSTR, "知識素質設定")), 1, -1
			SPLIT RESULTS, " ", LOCALS
			LOCAL:2 = RESULT - 1
			;性知識
			SPLIT LOCALS:0, "Lv", CSTR切り分け文字列格納
			ランダムキャラ素質:6 = TOINT(CSTR切り分け文字列格納:1)
			FOR LOCAL, 1, LOCAL:2
				SPLIT LOCALS:LOCAL, "Lv", CSTR切り分け文字列格納
				;CSTR切り分け文字列格納:0に素質名、CSTR切り分け文字列格納:1に数値が入っているはず
				知識素質数値保存:(GETNUM(知識素質, CSTR切り分け文字列格納:0)) = TOINT(CSTR切り分け文字列格納:1)
			NEXT

			FOR LOCAL, 0, 10
				固有素質一時保存:LOCAL = %CSVCSTR(CSV番号, LOCAL + 80)%
			NEXT

			;性癖素質
			VARSET LOCALS
			SUBSTRING CSVCSTR(CSV番号, GETNUM(CSTR, "性癖素質設定")), 1, -1
			SPLIT RESULTS, " ", LOCALS
			LOCAL:2 = RESULT - 1
			FOR LOCAL, 0, LOCAL:2
				SPLIT LOCALS:LOCAL, "Lv", CSTR切り分け文字列格納
				;CSTR切り分け文字列格納:0に素質名、CSTR切り分け文字列格納:1に数値が入っているはず
				性癖素質数値保存:(GETNUM(性癖素質, CSTR切り分け文字列格納:0)) = TOINT(CSTR切り分け文字列格納:1)
			NEXT

			;能力
			FOR LOCAL, 0, 50
				SIF ABL一時保存:LOCAL < 0
					BREAK
				ABL数値保存:LOCAL = CSVABL(CSV番号, ABL一時保存:LOCAL)
			NEXT

			;経験
			FOR LOCAL, 0, 100
				SIF EXP一時保存:LOCAL < 0
					BREAK
				EXP数値保存:LOCAL = CSVEXP(CSV番号, EXP一時保存:LOCAL)
			NEXT

			;服装
			ランダムキャラ服装名 = %CSVCSTR(CSV番号, GETNUM(CSTR, "服名称"))%
			FOR LOCAL, 1, 23
				服装数値保存:LOCAL = CSVEQUIP(CSV番号, LOCAL)
			NEXT
			服装数値保存:30 = CSVCFLAG(CSV番号, GETNUM(CFLAG, "水着"))

			;同行者
			SIF DT_EXIST("DT_CHARA")
				DT_RELEASE "DT_CHARA"

			DT_CREATE "DT_CHARA"
			DT_COLUMN_ADD "DT_CHARA", "NO", 2
			DT_COLUMN_ADD "DT_CHARA", "NAME"
			DT_COLUMN_ADD "DT_CHARA", "VALUE", 1

			VARSET LOCALS
			REPLACE CSVCSTR(CSV番号, GETNUM(CSTR, "同行キャラ")), " ", ""
			SPLIT RESULTS, "%", LOCALS
			LOCAL:2 = RESULT - 1
			FOR LOCAL, 1, キャラクタ数上限
				SIF !EXISTCSV(LOCAL)
					CONTINUE
				;ランダムキャラ排除
				SIF LOCAL == 999
					CONTINUE
				DT_ROW_ADD "DT_CHARA", "NO", LOCAL, "NAME", CSVNAME(LOCAL), "VALUE", 0
			NEXT
			FOR LOCAL, 0, LOCAL:2
				SPLIT LOCALS:LOCAL, "_", CSTR切り分け文字列格納
				;同行キャラ格納格納:0に名前、同行キャラ格納:1に確率が入っているはず
				DT_SELECT "DT_CHARA", @"NAME = '%CSTR切り分け文字列格納:0%'"
				DT_CELL_SET "DT_CHARA", RESULT:1, "VALUE", TOINT(CSTR切り分け文字列格納:1), 1
			NEXT
		ENDIF
	CASE 999
		IF CSVエディタフラグ
			CALL CHOICE("CSVエディタを終了しますか？（セーブは自動では行われません）", "はい", "いいえ")
			SIF RESULT
				GOTO 従業員_LOOP
		ENDIF
		RETURN 0
ENDSELECT

GOTO 従業員_LOOP


@ステータス記録(キャラ番号)
;引き継ぎ処理時に使う初期値の記録用
#DIM キャラ番号
#DIM 記録位置

LOCAL:1 = 0
FOR LOCAL, 0, 従業員初期値最大記録数
	IF 従業員初期値記録:LOCAL == ""
		記録位置 = LOCAL
		LOCAL:1 = 1
		BREAK
	ENDIF
NEXT
IF LOCAL:1 == 0
	PRINTW ※記録枠がいっぱいのため、このキャラは周回時に引き継がれません
	RETURN 0
ENDIF

従業員初期値記録:記録位置 = 

従業員初期値記録:記録位置 += @"キャラ番号_{キャラ番号}_"
従業員初期値記録:記録位置 += "^^^"
従業員初期値記録:記録位置 += @"名前_%CALLNAME:キャラ番号%_"
従業員初期値記録:記録位置 += "^^^"
FOR LOCAL, 0, 300
	IF TALENT:キャラ番号:LOCAL
		従業員初期値記録:記録位置 += @"素質_{LOCAL}_{TALENT:キャラ番号:LOCAL}_"
	ENDIF
NEXT
従業員初期値記録:記録位置 += "^^^"
FOR LOCAL, 0, 10
	IF フレーバー素質:キャラ番号:LOCAL
		従業員初期値記録:記録位置 += @"フレーバー素質_{LOCAL}_{フレーバー素質:キャラ番号:LOCAL}_"
	ENDIF
NEXT
従業員初期値記録:記録位置 += "^^^"
FOR LOCAL, 0, 50
	IF ABL:キャラ番号:LOCAL
		従業員初期値記録:記録位置 += @"ABL_{LOCAL}_{ABL:キャラ番号:LOCAL}_"
	ENDIF
NEXT
従業員初期値記録:記録位置 += "^^^"
FOR LOCAL, 0, 100
	IF EXP:キャラ番号:LOCAL
		従業員初期値記録:記録位置 += @"EXP_{LOCAL}_{EXP:キャラ番号:LOCAL}_"
	ENDIF
NEXT
従業員初期値記録:記録位置 += "^^^"
従業員初期値記録:記録位置 += @"属性_{基礎BATTLE_STATE:キャラ番号:属性}_"
従業員初期値記録:記録位置 += @"得意武器_{基礎BATTLE_STATE:キャラ番号:得意武器}_"
従業員初期値記録:記録位置 += @"タイプ_{基礎BATTLE_STATE:キャラ番号:ステータスタイプ保存}_"

従業員初期値記録:記録位置 += @"性知識_{知識素質:キャラ番号:性知識}_"



@EDIT用バストサイズ表示(バスト保存, アンダーバスト)
#FUNCTIONS
#DIM バスト保存
#DIM アンダーバスト
#DIMS CONST 表記 = "(-)","(AAA)","(AA)","(A)","(B)","(C)","(D)","(E)","(F)","(G)","(H)","(I)","(J)","(K)","(L)","(M)","(N)","(O)","(P)","(Q)","(R)","(S)","(T)","(U)","(V)","(W)","(X)","(Y)","(Z)","(Z+) "

LOCAL = (バスト保存 - アンダーバスト + 10) / 25 - 1

;文字列変数が若干不自由なので無理やり作成
LOCAL = MAX(LOCAL, 1)
LOCAL = MIN(LOCAL, 29)

LOCALS:0 = %表記:(LOCAL)%

; IF !HAS_VAGINA(対象)
; 	RETURNF "(--)"
; ELSE
	RETURNF LOCALS:0
; ENDIF

@EDIT用バストTALENT算出(バスト保存, アンダーバスト)
#FUNCTION
#DIM バスト保存
#DIM アンダーバスト

LOCAL = バスト保存 - アンダーバスト

SELECTCASE LOCAL
	CASE IS < 89
		RETURNF -2
	CASE IS < 139
		RETURNF -1
	CASE IS < 184
		RETURNF 0
	CASE IS < 239
		RETURNF 1
	CASE IS < 314
		RETURNF 2
	CASEELSE
		RETURNF 3
ENDSELECT


@EDIT用バストcm算出(バストサイズ, アンダーバスト)
#FUNCTION
#DIM バストサイズ
#DIM アンダーバスト

;最低サイズ算出
LOCAL = アンダーバスト + 40 + ((バストサイズ + 2) * 50)
SIF バストサイズ == 3
	LOCAL += 25


IF バストサイズ < 2
	;-2~1の時、+4.9cmまで許容
	LOCAL = LOCAL + RAND:50
ELSE
	;それ以上の時、+7.4cmまで許容
	LOCAL = LOCAL + RAND:75
ENDIF

IF バストサイズ == 3 && RAND:10 == 0
	;魔乳時、たまーーに限界突破する
	LOCAL = LOCAL + RAND:75
ENDIF

; IF !HAS_VAGINA(対象)
; 	RETURNF "(--)"
; ELSE
	RETURNF LOCAL
; ENDIF

@EDIT用アンダーバスト算出(性別, 種族, 身長, 体格, 体型)
#FUNCTION
#DIM 性別
#DIM 体格
#DIM 体型
#DIM 身長
#DIM 種族
#DIM 基礎値
基礎値 = 身長 * (!GETBIT(性別, 0) ? 50 # 43) / 100

SELECTCASE 体格
	CASE 体格_ハーヴィン並
		TIMES 基礎値, 1.15
	CASE 体格_子供
		TIMES 基礎値, 1.05
	CASE 体格_小柄
		SIF 種族 != 種族_ドラフ
			TIMES 基礎値, 0.95
	CASE 体格_普通
	CASE 体格_長身
		TIMES 基礎値, 1.05
	CASE 体格_男ドラフ並
		TIMES 基礎値, 1.10
ENDSELECT

SELECTCASE 体型
	CASE 体型_痩せすぎ
		TIMES 基礎値, 0.90
	CASE 体型_細身
		TIMES 基礎値, 0.94
	CASE 体型_スレンダー
		TIMES 基礎値, 0.97
	CASE 体型_グラマー
		TIMES 基礎値, 1.01
	CASE 体型_豊満
		TIMES 基礎値, 1.05
	CASE 体型_肥満
		TIMES 基礎値, 1.10
ENDSELECT

;夢詰め込んだらドラフカップが大きくなりすぎるので補正
SIF 種族 == 種族_ドラフ && 体格 >= 体格_小柄 && GETBIT(性別, 0)
	基礎値 = 基礎値 * 110 / 100

RETURNF 基礎値

@EDIT用身長算出(性別, 種族, 体格)
#FUNCTION
#DIM 性別
#DIM 体格
#DIM 種族
#DIM 基礎値

;今のところ子供は未実装なのでコメントアウト
;IF CFLAG:対象:行動不能状態 == 行動不能_子供
;	基礎値 = MIN(500 + CFLAG:対象:子の成長度 * 20, 1300)
;ELSEIF TALENT:対象:幼児
;	基礎値 = MIN(800 + (CFLAG:対象:子の成長度 - 6) * 50, 1300)
;ELSE
	SELECTCASE 体格
		CASE 体格_ハーヴィン並
			基礎値 = !GETBIT(性別, 0) ? RAND(800, 1000) # RAND(700, 900)
		CASE 体格_子供
			IF 種族 == 種族_ハーヴィン
				基礎値 = !GETBIT(性別, 0) ? RAND(1000, 1400) # RAND(900, 1300)
			ELSE
				基礎値 = !GETBIT(性別, 0) ? RAND(1200, 1400) # RAND(1100, 1300)
			ENDIF
		CASE 体格_小柄
			IF 種族 == 種族_ドラフ && GETBIT(性別, 0) && RAND:4
				基礎値 = RAND(1300, 1400)
			ELSE
				基礎値 = !GETBIT(性別, 0) ? RAND(1400, 1600) # RAND(1300, 1500)
			ENDIF
		CASE 体格_普通
			基礎値 = !GETBIT(性別, 0) ? RAND(1600, 1800) # RAND(1500, 1700)
		CASE 体格_長身
			基礎値 = !GETBIT(性別, 0) ? RAND(1800, 2150) # RAND(1700, 2000)
		CASE 体格_男ドラフ並
			基礎値 = !GETBIT(性別, 0) ? RAND(2150, 2450) # RAND(2000, 2200)
	ENDSELECT
;ENDIF

RETURNF 基礎値

@EDIT用ウエスト算出(性別, 身長, 体格, 体型)
#FUNCTION
#DIM 性別
#DIM 体格
#DIM 体型
#DIM 身長
#DIM 基礎値

基礎値 = 身長 * (性別 == 2 ? 43 # 38) / 100

SELECTCASE 体格
	CASE 体格_ハーヴィン並
		TIMES 基礎値, 1.22
	CASE 体格_子供
		TIMES 基礎値, 1.07
	CASE 体格_小柄
		TIMES 基礎値, 1.03
	CASE 体格_普通
	CASE 体格_長身
		TIMES 基礎値, 0.98
	CASE 体格_男ドラフ並
		TIMES 基礎値, 0.97
ENDSELECT

SELECTCASE 体型
	CASE 体型_痩せすぎ
		TIMES 基礎値, 0.85
	CASE 体型_細身
		TIMES 基礎値, 0.9
	CASE 体型_スレンダー
		TIMES 基礎値, 0.95
	CASE 体型_グラマー
		TIMES 基礎値, 1.02
	CASE 体型_豊満
		TIMES 基礎値, 1.06
	CASE 体型_肥満
		TIMES 基礎値, 1.10
ENDSELECT

基礎値 = 基礎値 * RAND(9700, 10301) / 10000

RETURNF 基礎値

@EDIT用ヒップ算出(性別, 身長, 体格, 体型, 下半身)
#FUNCTION
#DIM 性別
#DIM 体格
#DIM 体型
#DIM 身長
#DIM 下半身
#DIM 基礎値
#DIM 補正値

基礎値 = 身長 * (性別 == 2 ? 47 # 54) / 100

SELECTCASE 体格
	CASE 体格_ハーヴィン並
		TIMES 基礎値, 1.2
	CASE 体格_子供
		TIMES 基礎値, 1.05
	CASE 体格_小柄
		TIMES 基礎値, 1.02
	CASE 体格_普通
	CASE 体格_長身
		TIMES 基礎値, 0.99
	CASE 体格_男ドラフ並
		TIMES 基礎値, 0.98
ENDSELECT

SELECTCASE 体型
	CASE 体型_痩せすぎ
		TIMES 基礎値, 0.92
	CASE 体型_細身
		TIMES 基礎値, 0.95
	CASE 体型_スレンダー
		TIMES 基礎値, 0.98
	CASE 体型_グラマー
		TIMES 基礎値, 1.04
	CASE 体型_豊満
		TIMES 基礎値, 1.08
	CASE 体型_肥満
		TIMES 基礎値, 1.12
ENDSELECT

IF 性別 == 2
	補正値 = 100
ELSE
	SELECTCASE 下半身
		CASE -1
			補正値 = 93
		CASE 0
			補正値 = 100
		CASE 1
			補正値 = 108
	ENDSELECT
ENDIF

基礎値 = 基礎値 * 補正値 / 100

基礎値 = 基礎値 * RAND(9600, 10401) / 10000

RETURNF 基礎値


@EDIT用体格算出(性別, 身長)
#FUNCTION
#DIM 性別
#DIM 身長
#DIM 基礎値

;身長と性別をもとに算出する
SELECTCASE 身長
	CASE IS < 900 + !GETBIT(性別, 0)*100
		RETURNF 体格_ハーヴィン並
	CASE IS < 1300 + !GETBIT(性別, 0)*100
		RETURNF 体格_子供
	CASE IS < 1500 + !GETBIT(性別, 0)*100
		RETURNF 体格_小柄
	CASE IS < 1700 + !GETBIT(性別, 0)*100
		RETURNF 体格_普通
	CASE IS < 2000 + !GETBIT(性別, 0)*150
		RETURNF 体格_長身
	CASEELSE
		RETURNF 体格_男ドラフ並
ENDSELECT



@EQUIPNAME_TIPS(ARG)
#FUNCTIONS

SELECTCASE ARG
	CASE 5
		LOCALS = ボタンなどを外さなくても乳が露出出来るか？
	CASE 6
		LOCALS = 脱がさなくてもノーブラ時乳首がいじれるか？
	CASE 7
		LOCALS = 脱がさなくてもパイズリ出来るか？
	CASE 10
		LOCALS = 脱がさなくてもブラを取れるか？
	CASE 14
		LOCALS = スカートと書いてはあるけど前垂れとかも含む
	CASE 15
		LOCALS = レオタードやバニーガールなどのハイレグ形状を想定
	CASE 18
		LOCALS = 脱がさなくてもパンツを取れるか？
	CASEELSE
		LOCALS '= ""
ENDSELECT

RETURNF LOCALS


@汎用立ち絵選択処理(汎用画像キー)
#DIMS REF 汎用画像キー
#DIM F_NUM
#DIM ページ数
#DIM 改行フラグ
#DIMS G_NAME

;汎用立ち絵を全部ずらっと並べてそこから任意のものを選ぶ
;種族や体型・バストなどによる制限はない
VARSET RESULTS
VARSET LOCAL
VARSET TEMP_HTML
SPRITEDISPOSEALL 0
ページ数 = 0

DRAWLINE
LOCALS '= "resources\\汎用グラフィック"
F_NUM = ENUMFILES(LOCALS, "顔_*.png", 1)

$表示_LOOP
改行フラグ = 0
TEMP_HTML = 
FOR LOCAL, ページ数 * 40, ページ数 * 40 + 40
	SIF LOCAL >= F_NUM
		BREAK
	G_NAME '= SUBSTRING(RESULTS:LOCAL, 10, -1)
	GDISPOSE LOCAL
	GCREATEFROMFILE LOCAL, G_NAME
	SIF スプライト作成(TOSTR(LOCAL), LOCAL, 0) == 0
		CONTINUE
	LOCALS = {(LOCAL % 8) * 750}, 0, 750, 750
	TEMP_HTML += @"<div rect='%LOCALS%'><button value='{LOCAL}' title='%ESCAPE(G_NAME)%'><img src='{LOCAL}' width='750' height='750'></button></div>"
	IF (LOCAL + 1) % 8 == 0
		改行フラグ = 0
		TEMP_HTML += "<br><br><br><br><br><br><br>"
	ELSE
		改行フラグ = 1
	ENDIF
NEXT
SIF 改行フラグ
	TEMP_HTML += "<br><br><br><br><br><br><br>"
HTML_PRINT TEMP_HTML

TEMP_HTML = 
DRAWLINE
IF ページ数 > 0
	TEMP_HTML += "<button value='9997'>[9997] 前のページへ</button>"
ELSE
	TEMP_HTML += "　　　 　　　　　　"
ENDIF
TEMP_HTML += "　　　　　　<button value='9999'>[9999] 戻る</button>　　　　　　"
IF F_NUM >= ページ数 * 40 + 40
	TEMP_HTML += "<button value='9998'>[9998] 次のページへ</button>"
ELSE
	TEMP_HTML += "　　　 　　　　　　"
ENDIF
HTML_PRINT TEMP_HTML
BINPUT
SELECTCASE RESULT
	CASE 9997
		ページ数 -= 1
		GOTO 表示_LOOP
	CASE 9998
		ページ数 += 1
		GOTO 表示_LOOP
	CASE 9999
		RETURN 0
	CASEELSE
		DRAWLINE
		汎用画像キー = %REPLACE(RESULTS:RESULT, ESCAPE("resources\\汎用グラフィック\\顔_"), "")%
		汎用画像キー = %REPLACE(汎用画像キー, ".png", "")%
		HTML_PRINT @"<div rect='0, 0, 750, 750'><img src='{RESULT}' width='750' height='750'></div><br><br><br><br><br><br><br>"
		PRINTFORMW %汎用画像キー%を選択しました。
		RETURN 0
ENDSELECT

@CSVコピー処理()
#DIM CSV番号
#DIM 配列番号
#DIM 表示数
#DIM CSVページ数
#DIM 次ページフラグ
#DIM 表示配列, 3000

VARSET 表示配列
表示数 = 0
FOR CSV番号, 1, 3000
	SIF EXISTCSV(CSV番号) == 0
		CONTINUE
	SIF CSV番号 == 999
		CONTINUE
	表示配列:表示数 = CSV番号
	表示数 ++
NEXT

$処理部分

DRAWLINE
PRINTL 既存のCSVから設定をコピーします。コピー対象を選択してください
DRAWLINE

表示数 = 0
次ページフラグ = 0
FOR 配列番号, CSVページ数 * 30, CSVページ数 * 30 + 30
	SIF 表示配列:配列番号 < 1
		CONTINUE
	PRINTBUTTON @"[{表示配列:配列番号, 4}] %CSVNAME(表示配列:配列番号),41,LEFT%", 表示配列:配列番号
	表示数 ++
	SIF 表示数 % 2 == 0
		PRINTL
NEXT
SIF 表示数 % 2
	PRINTL
DRAWLINE
IF CSVページ数 > 0
	PRINTBUTTONLC "[9997] 前ページへ", 9997
ELSE
	PRINTLC 　
ENDIF
IF CSVページ数 * 30 + 30 < 3000 && 表示配列:(CSVページ数 * 30 + 30) > 0
	PRINTBUTTONLC "[9998] 次ページへ", 9998
ENDIF
PRINTL
PRINTBUTTONLC "[9999] 戻る", 9999
PRINTL

BINPUT
SELECTCASE RESULT
	CASE 9997
		CSVページ数 --
		GOTO 処理部分
	CASE 9998
		CSVページ数 ++
		GOTO 処理部分
	CASE 9999
		RETURN -1
	CASEELSE
		CSV番号 = RESULT
		PRINTFORML %CSVNAME(CSV番号)%のデータをコピーします。よろしいですか？
		PRINTBUTTONLC "[0] はい", 0
		PRINTBUTTONLC "[1] いいえ", 1
		PRINTL
		BINPUT
		IF RESULT == 0
			RETURN CSV番号
		ELSE
			RESTART
		ENDIF
ENDSELECT

