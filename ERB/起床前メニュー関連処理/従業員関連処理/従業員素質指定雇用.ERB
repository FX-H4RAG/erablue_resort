@従業員素質指定雇用
#DIM ランダムキャラ素質, 70
#DIM TALENT一時保存, 300
#DIM TALENT数値保存, 300
#DIM 肉体特徴数値保存, 10
#DIM 身長保存
#DIM 体型保存
#DIM バスト保存
#DIM 身長数値設定フラグ
#DIM バスト数値設定フラグ
#DIM フレーバー素質数値保存, 10
#DIM 知識素質数値保存, 20
#DIM 性癖素質数値保存, 20
#DIM 服装数値保存, 30
#DIM ABL一時保存, 50
#DIM ABL数値保存, 50
#DIM EXP一時保存, 100
#DIM EXP数値保存, 100
#DIM ランダムキャラ経験, 80
#DIM ランダムキャラ戦闘タイプ, 10
#DIMS ランダムキャラネーム
#DIMS ランダムキャラ二つ名
#DIMS ランダムキャラ出演作品
#DIMS ランダムキャラ服装名
#DIM 重複チェック用, 10
#DIM 表示モード
#DIM 表示数カウント
#DIM ページ数
#DIM 必要ルピ
#DIM 登録番号

表示モード = 0
ページ数 = 0
表示数カウント = 0
ランダムキャラネーム = 
ランダムキャラ二つ名 = （未定）
ランダムキャラ服装名 = （未定）
ランダムキャラ出演作品 = 
VARSET ランダムキャラ素質:0
VARSET ランダムキャラ経験:0
VARSET TALENT一時保存:0
VARSET フレーバー素質数値保存
VARSET 知識素質数値保存
VARSET 肉体特徴数値保存
VARSET 服装数値保存
VARSET TALENT数値保存:0, 5
VARSET ABL一時保存:0, -1
VARSET ABL数値保存:0
VARSET EXP一時保存:0, -1
VARSET EXP数値保存:0
VARSET ランダムキャラ戦闘タイプ:0
VARSET LOCAL
ランダムキャラ素質:0 = 1
ランダムキャラ素質:1 = 1

CALL 従業員人数チェック()

; FOR LOCAL, 10, 71
; 	IF TALENTNAME:LOCAL != ""
; 		TALENT一時保存:(LOCAL:10) = LOCAL
; 		LOCAL:10 += 1
; 	ENDIF
; NEXT
; FOR LOCAL, 77, 132
; 	IF TALENTNAME:LOCAL != ""
; 		TALENT一時保存:(LOCAL:10) = LOCAL
; 		LOCAL:10 += 1
; 	ENDIF
; NEXT
; FOR LOCAL, 140, 150
; 	IF TALENTNAME:LOCAL != ""
; 		TALENT一時保存:(LOCAL:10) = LOCAL
; 		LOCAL:10 += 1
; 	ENDIF
; NEXT
; FOR LOCAL, 220, 300
; 	IF TALENTNAME:LOCAL != ""
; 		TALENT一時保存:(LOCAL:10) = LOCAL
; 		LOCAL:10 += 1
; 	ENDIF
; NEXT

FOR LOCAL, 0, 300
	IF TALENTNAME:LOCAL != "" && 素質カテゴリ(LOCAL) == "優先表示"
		TALENT一時保存:(LOCAL:10) = LOCAL
		LOCAL:10 += 1
	ENDIF
NEXT
FOR LOCAL, 0, 300
	IF TALENTNAME:LOCAL != "" && 素質カテゴリ(LOCAL) == "性格"
		TALENT一時保存:(LOCAL:10) = LOCAL
		LOCAL:10 += 1
	ENDIF
NEXT
FOR LOCAL, 0, 300
	IF TALENTNAME:LOCAL != "" && 素質カテゴリ(LOCAL) == "肉体"
		TALENT一時保存:(LOCAL:10) = LOCAL
		LOCAL:10 += 1
	ENDIF
NEXT
FOR LOCAL, 0, 300
	IF TALENTNAME:LOCAL != "" && 素質カテゴリ(LOCAL) == "その他"
		TALENT一時保存:(LOCAL:10) = LOCAL
		LOCAL:10 += 1
	ENDIF
NEXT

FOR LOCAL, 0, 21
	IF ABLNAME:LOCAL != ""
		ABL一時保存:(LOCAL:11) = LOCAL
		LOCAL:11 += 1
	ENDIF
NEXT
FOR LOCAL, 30, 50
	IF ABLNAME:LOCAL != ""
		ABL一時保存:(LOCAL:11) = LOCAL
		LOCAL:11 += 1
	ENDIF
NEXT

FOR LOCAL, 0, 100
	SIF GROUPMATCH(LOCAL:0, 89, 33)
		CONTINUE
	IF EXPNAME:LOCAL != ""
		EXP一時保存:(LOCAL:12) = LOCAL
		LOCAL:12 += 1
	ENDIF
NEXT

身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))

$従業員_LOOP
DRAWLINE
IF CSVエディタフラグ
	PRINTL 素質などを指定し、CSVファイルを作成します。
	PRINTL 作成したCSVファイルは「CSV_EDITFILE」フォルダの中にあるので、使用時にはCSVフォルダへ移動させてください。
	DRAWLINE
	LOCALS = 
ELSE
	PRINTL 従業員の素質を完全指定して雇います。
	PRINTPLAIN 指定した素質の数に応じて必要ルピが上昇するので注意してください。
	PRINTL
	DRAWLINE
	LOCALS = <div rect='13px,0px,310px,55px' border='5px' bcolor='#C0C0C0'></div>
	LOCALS += @"<div rect='25px,10px,306px,45px'>所持ルピ：%NUM_FORMAT(MONEY)%<br>所持ZP　：%ZP所持量全文字列()%</div>"


	LOCALS += "<div rect='328px,0px,620px,55px' border='5px' bcolor='#C0C0C0'></div>"
	LOCALS += "<div rect='340px,10px,610px,45px'>"
	必要ルピ = 1000
	;名前・戦闘タイプではルピ増加無し
	;肉体情報
	;バストサイズ・容姿は普通から外れるほどルピが嵩む
	IF ランダムキャラ素質:0 == 3
		必要ルピ += ABS(ランダムキャラ素質:3 - 3) * 500
		必要ルピ += ABS(ランダムキャラ素質:31 + 1) * 500
	ELSEIF ランダムキャラ素質:0 == 4
		必要ルピ += ABS(ランダムキャラ素質:3 + 2) * 500
		必要ルピ += ABS(ランダムキャラ素質:31 + 2) * 500
	ELSE
		必要ルピ += ABS(ランダムキャラ素質:3) * 200
		必要ルピ += ABS(ランダムキャラ素質:31) * 200
	ENDIF
	必要ルピ += ABS(ランダムキャラ素質:6) * 300
	必要ルピ += ABS(ランダムキャラ素質:30) * 500
	必要ルピ += ABS(ランダムキャラ素質:32) * 200
	LOCAL:1 = 0
	FOR LOCAL, 0, 300
		SIF TALENT数値保存:LOCAL == 0
			CONTINUE
		SIF TALENT数値保存:LOCAL == 5
			CONTINUE
		LOCAL:1 += 1
	NEXT
	必要ルピ += LOCAL:1 * LOCAL:1 * LOCAL:1 * 50 * (LOCAL:1 / 5 + 1)
	LOCAL:1 = 0
	FOR LOCAL, 0, 50
		SIF ABL数値保存:LOCAL < 1
			CONTINUE
		LOCAL:1 += ABL数値保存:LOCAL
	NEXT
	必要ルピ += LOCAL:1 * LOCAL:1 * LOCAL:1 * 50 * (LOCAL:1 / 10 + 1)
	LOCAL:1 = 0
	FOR LOCAL, 0, 100
		SIF EXP数値保存:LOCAL < 1
			CONTINUE
		LOCAL:1 += EXP数値保存:LOCAL
	NEXT
	必要ルピ += LOCAL:1 *  5 * (LOCAL:1 / 100 + 1)

	必要ルピ += ランダムキャラ素質:30 * ランダムキャラ素質:30 * 100


	LOCALS += @"現在の必要ルピ：%NUM_FORMAT(必要ルピ), 12%ルピ<br>"
	LOCALS += @"現在の従業員数 / 上限：{従業員人数}人 / {従業員キャラ上限}人"
	LOCALS += "</div>"
ENDIF

LOCALS += "<div rect='13px,60px,310px,450px' border='5px' bcolor='#C0C0C0'></div><div rect='328px,60px,620px,450px' border='5px' bcolor='#C0C0C0'></div>"
LOCALS += "<div rect='25px,80px,310px,450px'>"
LOCALS += "<button value='100'>[100] 名前を設定する</button>"
LOCALS += "<br>"
IF CSVエディタフラグ
	LOCALS += "<button value='101'>[101] 二つ名を設定する</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='102'>[102] 出演作品を設定する</button>"
	LOCALS += "<br>"
ENDIF
LOCALS += "<button value='110'>[110] 戦闘タイプを設定する</button>"
LOCALS += "<br>"
LOCALS += "<button value='120'>[120] 肉体情報を設定する</button>"
LOCALS += "<br>"
LOCALS += "<button value='130'>[130] 素質を設定する</button>"
LOCALS += "<br>"
IF フレーバー素質設定 != 1 && CSVエディタフラグ == 0
	LOCALS += "<button value='131'>[131] フレーバー素質を設定する</button>"
	LOCALS += "<br>"
ENDIF
IF CSVエディタフラグ
	LOCALS += "<button value='132'>[132] 知識素質を設定する</button>"
	LOCALS += "<br>"
	LOCALS += "<button value='133'>[133] 性癖素質を設定する</button>"
	LOCALS += "<br>"
ENDIF
LOCALS += "<button value='140'>[140] 能力を設定する</button>"
LOCALS += "<br>"
LOCALS += "<button value='150'>[150] 経験を設定する</button>"
LOCALS += "<br>"
IF CSVエディタフラグ
	LOCALS += "<button value='160'>[160] 服装を設定する</button>"
	LOCALS += "<br>"
ENDIF
LOCALS += "<br>"
LOCALS += "<button value='200'>[200] これで決定する</button>"
LOCALS += "<br>"
LOCALS += "<button value='999'>[999] 戻る</button>"
LOCALS += "</div>"

LOCALS += "<div rect='340px,80px,588px,450px'>"


SELECTCASE 表示モード
	CASE 0
		;現状の設定表示
		IF ランダムキャラネーム != ""
			IF CSVエディタフラグ
				LOCALS += @"■[%ランダムキャラ二つ名%]%ランダムキャラネーム, 18, LEFT%"
			ELSE
				LOCALS += @"■[従業員]%ランダムキャラネーム, 18, LEFT%"
			ENDIF
		ELSE
			IF CSVエディタフラグ
				LOCALS += @"■[%ランダムキャラ二つ名%]（名称未定）　　　"
			ELSE
				LOCALS += @"■[従業員]（名称未定）　　　"
			ENDIF
		ENDIF
		LOCALS:1 = %カラーパレット_HTML(@"%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%属性")%
		LOCALS += @"<font color='#%LOCALS:1%'>属性：%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%</font>"
		LOCALS += @"　得意武器：%得意武器数値文字列変換(ランダムキャラ戦闘タイプ:1)%　タイプ：%成長タイプ:(ランダムキャラ戦闘タイプ:2)%"
		IF ランダムキャラ出演作品 != ""
			LOCALS += @"<br>出演作品：%ランダムキャラ出演作品%"
		ENDIF
		LOCALS += "<br><br>"
		LOCALS += "□ 情報 □---------------------------------------------------------------<br>"

		LOCALS += "　種族："
		SELECTCASE ランダムキャラ素質:0
			CASE 1
				LOCALS += "ヒューマン"
			CASE 2
				LOCALS += "エルーン"
			CASE 3
				LOCALS += "ドラフ"
			CASE 4
				LOCALS += "ハーヴィン"
			CASE 5
				LOCALS += "星晶獣"
			CASE 6
				LOCALS += "その他"
		ENDSELECT
		IF 肉体特徴数値保存:0
			LOCALS += "（角あり）"
		ENDIF
		IF 肉体特徴数値保存:1
			LOCALS += "（尻尾あり）"
		ENDIF
		IF 肉体特徴数値保存:2
			LOCALS += "（エルーン耳）"
		ENDIF
		IF 肉体特徴数値保存:3
			LOCALS += "（ハーヴィン耳）"
		ENDIF
		LOCALS += "<br>"
		LOCALS += @"　身長：%DECIMAL_STRING(身長保存, 1)%cm"
		LOCALS += "<br>"
		LOCALS += "　性別："
		SELECTCASE ランダムキャラ素質:1
			CASE 1
				LOCALS += "女性"
				IF GETBIT(ランダムキャラ素質:2, 0) == 0
					LOCALS += "（処女）　　　　　"
				ELSE
					LOCALS += "　　　　　　　　　"
				ENDIF
			CASE 3
				LOCALS += "ふたなり"
				IF GETBIT(ランダムキャラ素質:2, 0) == 0
					LOCALS += "（処女）"
				ELSE
					LOCALS += "　　　　"
				ENDIF
				IF GETBIT(ランダムキャラ素質:2, 1) == 0
					LOCALS += "（童貞）　"
				ELSE
					LOCALS += "　　　　　"
				ENDIF
		ENDSELECT
		LOCALS += @"バストサイズ："
		IF バスト数値設定フラグ
			LOCALS += @"%DECIMAL_STRING(バスト保存, 1)%cm"
			LOCALS += @"%EDIT用バストサイズ表示(バスト保存, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))%"
		ELSE
			LOCALS += @"%GET_TALENTNAME(205,ランダムキャラ素質:3)%"
		ENDIF
		LOCALS += "<br>"
		LOCALS += @"　性知識レベル："
		SELECTCASE ランダムキャラ素質:6
			CASE -2
				LOCALS += "無知"
			CASE -1
				LOCALS += "獣並み"
			CASE 0
				LOCALS += "疎い"
			CASE 1
				LOCALS += "必要十分"
			CASE 2
				LOCALS += "有識者"
			CASE 3
				LOCALS += "百戦錬磨"
		ENDSELECT
		LOCALS += "<br>"
		LOCALS += "　体格："
		IF ランダムキャラ素質:31 == 0
			LOCALS += "普通"
		ELSE
			LOCALS += @"%GET_TALENTNAME(161,ランダムキャラ素質:31)%"
		ENDIF
		LOCALS += "　　　体型："
		IF 体型保存 == 0
			LOCALS += "普通"
		ELSE
			LOCALS += @"%GET_TALENTNAME(163,体型保存)%"
		ENDIF
		IF CSVエディタフラグ == 0
			LOCALS += "　　　容姿："
			IF ランダムキャラ素質:30 == 0
				LOCALS += "モブ顔　　　"
			ELSE
				LOCALS += @"%GET_TALENTNAME(160,ランダムキャラ素質:30), 12, LEFT%"
			ENDIF
		ENDIF
		IF ランダムキャラ素質:1 == 3
			LOCALS += "<br>　男性器サイズ："
			IF ランダムキャラ素質:32 == 0
				LOCALS += "平凡"
			ELSE
				LOCALS += @"%GET_TALENTNAME(162,ランダムキャラ素質:32)%"
			ENDIF
		ENDIF
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "□ 素質 □---------------------------------------------------------------<br>"
		LOCALS:1 = 
		FOR LOCAL, 0, 300
			SIF TALENT数値保存:LOCAL == 0
				CONTINUE
			SIF TALENT数値保存:LOCAL == 5
				CONTINUE
			LOCALS += @"[%GET_TALENTNAME(TALENT一時保存:LOCAL, TALENT数値保存:LOCAL - 5)%]"
		NEXT
		FOR LOCAL, 1, 20
			SIF 知識素質数値保存:LOCAL == 0
				CONTINUE
			LOCALS += @"[%ERDNAME(知識素質, LOCAL)%Lv{知識素質数値保存:LOCAL}]"
		NEXT
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "□ 性癖 □---------------------------------------------------------------<br>"
		LOCALS:1 = 
		FOR LOCAL, 1, 20
			SIF 性癖素質数値保存:LOCAL == 0
				CONTINUE
			LOCALS += @"[%ERDNAME(性癖素質, LOCAL)%"
			SELECTCASE 性癖素質数値保存:LOCAL
				CASE 1
					LOCALS += "(興味)]"
				CASE 2
					LOCALS += "(愛好)]"
				CASE 3
					LOCALS += "(中毒)]"
			ENDSELECT
		NEXT
		LOCALS += "<br>"
		LOCALS += "<br>"
		IF CSVエディタフラグ == 0
			LOCALS += "□ フレーバー素質 □-----------------------------------------------------<br>"
			LOCALS:1 = 
			LOCAL = 0
			FOR COUNT, 0, 10
				IF COUNT == 6
					PRINTL
					LOCAL = 0
				ENDIF
				SIF フレーバー素質数値保存:COUNT == 0
					CONTINUE
				LOCALS += @"%ERDNAME(フレーバー素質, COUNT), 10, LEFT%：%GET_フレーバー素質NAME(COUNT, フレーバー素質数値保存:COUNT), 12, LEFT%"
				LOCAL += 1
				IF LOCAL == 3
					LOCAL = 0
					PRINTL
				ENDIF
			NEXT
		ENDIF
	CASE 1
		;現状の設定表示（能力素質）
		IF ランダムキャラネーム != ""
			IF CSVエディタフラグ
				LOCALS += @"■[%ランダムキャラ二つ名%]%ランダムキャラネーム, 18, LEFT%"
			ELSE
				LOCALS += @"■[従業員]%ランダムキャラネーム, 18, LEFT%"
			ENDIF
		ELSE
			IF CSVエディタフラグ
				LOCALS += @"■[%ランダムキャラ二つ名%]（名称未定）　　　"
			ELSE
				LOCALS += @"■[従業員]（名称未定）　　　"
			ENDIF
		ENDIF
		LOCALS:1 = %カラーパレット_HTML(@"%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%属性")%
		LOCALS += @"<font color='#%LOCALS:1%'>属性：%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%</font>"
		LOCALS += @"　得意武器：%得意武器数値文字列変換(ランダムキャラ戦闘タイプ:1)%　タイプ：%成長タイプ:(ランダムキャラ戦闘タイプ:2)%"
		IF ランダムキャラ出演作品 != ""
			LOCALS += @"<br>出演作品：%ランダムキャラ出演作品%"
		ENDIF
		LOCALS += "<br><br>"
		LOCALS += "□ 能力 □---------------------------------------------------------------<br>"
		LOCAL:2 = 0
		FOR LOCAL, 0, 50
			SIF ABL数値保存:LOCAL < 1
				CONTINUE
			IF ABL一時保存:LOCAL > -1
				LOCALS += @"%ABLNAME:(ABL一時保存:LOCAL), 16%:{ABL数値保存:LOCAL, 2}   "
				LOCAL:2 += 1
				IF LOCAL:2 == 3
					LOCALS += "<br>"
					LOCAL:2 = 0
				ENDIF
			ENDIF
		NEXT
		SIF LOCAL:2 != 0
			LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "□ 経験 □---------------------------------------------------------------<br>"
		LOCAL:2 = 0
		FOR LOCAL, 0, 100
			SIF EXP数値保存:LOCAL < 1
				CONTINUE
			IF EXP一時保存:LOCAL > -1
				LOCALS += @"%EXPNAME:(EXP一時保存:LOCAL), 16%:{EXP数値保存:LOCAL, 3}   "
				LOCAL:2 += 1
				IF LOCAL:2 == 3
					LOCALS += "<br>"
					LOCAL:2 = 0
				ENDIF
			ENDIF
		NEXT
	CASE 2
		IF ランダムキャラネーム != ""
			IF CSVエディタフラグ
				LOCALS += @"■[%ランダムキャラ二つ名%]%ランダムキャラネーム, 18, LEFT%"
			ELSE
				LOCALS += @"■[従業員]%ランダムキャラネーム, 18, LEFT%"
			ENDIF
		ELSE
			IF CSVエディタフラグ
				LOCALS += @"■[%ランダムキャラ二つ名%]（名称未定）　　　"
			ELSE
				LOCALS += @"■[従業員]（名称未定）　　　"
			ENDIF
		ENDIF
		LOCALS:1 = %カラーパレット_HTML(@"%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%属性")%
		LOCALS += @"<font color='#%LOCALS:1%'>属性：%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%</font>"
		LOCALS += @"　得意武器：%得意武器数値文字列変換(ランダムキャラ戦闘タイプ:1)%　タイプ：%成長タイプ:(ランダムキャラ戦闘タイプ:2)%"
		IF ランダムキャラ出演作品 != ""
			LOCALS += @"<br>出演作品：%ランダムキャラ出演作品%"
		ENDIF
		LOCALS += "<br><br>"
		LOCALS += "□ 服装 □---------------------------------------------------------------<br>"
		LOCALS += @"　名称：%ランダムキャラ服装名%<br>"
		LOCAL = 0
		;鎧
		IF 服装数値保存:1
			LOCALS += "　　[上半身鎧]"
			LOCAL = 1
		ENDIF
		;鎧
		IF 服装数値保存:11 && 服装数値保存:12
			LOCALS += "[下半身鎧]"
			LOCAL = 1
		ELSEIF 服装数値保存:11
			LOCALS += "[下半身後方鎧]"
			LOCAL = 1
		ELSEIF 服装数値保存:12
			LOCALS += "[下半身前方鎧]"
			LOCAL = 1
		ENDIF
		SIF LOCAL
			LOCALS += "<br>"

		LOCAL = 0
		;上半身服
		IF 服装数値保存:2
			LOCALS += "　　[上半身服"
			IF 服装数値保存:3
				LOCALS += "（脇露出）"
			ENDIF
			IF 服装数値保存:4
				LOCALS += "（へそ露出）"
			ENDIF
			IF 服装数値保存:5
				LOCALS += "（はだけ可）"
			ENDIF
			IF 服装数値保存:6
				LOCALS += "（差し込み可）"
			ENDIF
			IF 服装数値保存:7
				LOCALS += "（着衣パイズリ可）"
			ENDIF
			IF 服装数値保存:8 && 服装数値保存:9
				LOCALS += "（ブラ抜き取り可）"
			ENDIF
			LOCALS += "]"
			LOCALS += "<br>"
		ENDIF

		;下半身服
		IF 服装数値保存:13
			LOCALS += "　　[下半身服"
			SIF 服装数値保存:14
				LOCALS += "（めくり可）"
			SIF 服装数値保存:15
				LOCALS += "（ずらし可）"
			SIF 服装数値保存:18 && 服装数値保存:17
				LOCALS += "（下着抜き取り可）"
			LOCALS += "]"
			LOCALS += "<br>"
		ENDIF

		IF 服装数値保存:20
			LOCALS += "　　[上下タイツ"
			SIF 服装数値保存:21 || 服装数値保存:22
				LOCALS += "（破れ）"
			LOCALS += "]"
			LOCALS += "<br>"
		ELSEIF 服装数値保存:8
			LOCALS += "　　[インナー"
			SIF 服装数値保存:21
				LOCALS += "（破れ）"
			LOCALS += "]"
			LOCALS += "<br>"
		ELSEIF 服装数値保存:16
			LOCALS += "　　[タイツ"
			SIF 服装数値保存:22
				LOCALS += "（破れ）"
			LOCALS += "]"
			LOCALS += "<br>"
		ENDIF


		SIF 服装数値保存:9 == 0
			LOCALS += "[ノーブラ]"
		SIF 服装数値保存:9 == 1
			LOCALS += "[ブラ着用]"
		SIF 服装数値保存:9 == 2
			LOCALS += "[ニプレス]"
		SIF 服装数値保存:17 == 0
			LOCALS += "[ノーパン]"
		SIF 服装数値保存:17 == 1
			LOCALS += "[下着着用]"
		SIF 服装数値保存:17 == 2
			LOCALS += "[前張り]"
		SIF 服装数値保存:17 == 3
			LOCALS += "[ふんどし]"

		LOCALS += "<br>"
	CASE 110
		LOCALS += "属性："
		FOR LOCAL, 0, 6
			IF ランダムキャラ戦闘タイプ:0 == LOCAL
				LOCALS:1 = %カラーパレット_HTML(@"%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%属性")%
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{11000 + LOCAL}'>[%属性数値文字列変換(LOCAL)%]</button></font>"
		NEXT
		LOCALS += "<br>"
		LOCALS += "得意武器："
		FOR LOCAL, 0, 10
			IF ランダムキャラ戦闘タイプ:1 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{11010 + LOCAL}'>[%得意武器数値文字列変換(LOCAL)%]</button></font>"
		NEXT
		LOCALS += "<br>"
		LOCALS += "タイプ："
		FOR LOCAL, 0, 8
			IF ランダムキャラ戦闘タイプ:2 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{11020 + LOCAL}'>[%成長タイプ:LOCAL%]</button></font>"
		NEXT
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "<button value='11100'>[ステータス画面に戻る]</button>"
	CASE 120
		LOCALS += "種族："
		FOR LOCAL, 1, 7
			IF ランダムキャラ素質:0 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12000 + LOCAL}'>[%GET_TALENTNAME(200,LOCAL)%]</button></font>"
		NEXT
		LOCALS += "<br>"
		LOCALS += "角："
		IF 肉体特徴数値保存:0
			LOCALS += @"<font color='#C0C0C0'><button value='{12070}'>[あり]</button></font>"
		ELSE
			LOCALS += @"<font color='#333333'><button value='{12070}'>[なし]</button></font>"
		ENDIF
		LOCALS += "　尻尾："
		IF 肉体特徴数値保存:1
			LOCALS += @"<font color='#C0C0C0'><button value='{12071}'>[あり]</button></font>"
		ELSE
			LOCALS += @"<font color='#333333'><button value='{12071}'>[なし]</button></font>"
		ENDIF
		LOCALS += "　エルーン耳："
		IF 肉体特徴数値保存:2
			LOCALS += @"<font color='#C0C0C0'><button value='{12072}'>[あり]</button></font>"
		ELSE
			LOCALS += @"<font color='#333333'><button value='{12072}'>[なし]</button></font>"
		ENDIF
		LOCALS += "　ハーヴィン耳："
		IF 肉体特徴数値保存:3
			LOCALS += @"<font color='#C0C0C0'><button value='{12073}'>[あり]</button></font>"
		ELSE
			LOCALS += @"<font color='#333333'><button value='{12073}'>[なし]</button></font>"
		ENDIF
		LOCALS += "<br>"
		LOCALS += "性別："
		IF ランダムキャラ素質:1 == 1
			LOCALS:1 = C0C0C0
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12007}'>[女性]</button></font>"
			LOCALS:1 = 333333
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12008}'>[ふたなり]</button></font>"
		ELSE
			LOCALS:1 = 333333
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12007}'>[女性]</button></font>"
			LOCALS:1 = C0C0C0
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12008}'>[ふたなり]</button></font>"
		ENDIF
		LOCALS += "　　　純潔："
		IF GETBIT(ランダムキャラ素質:2, 0) == 0
			LOCALS:1 = C0C0C0
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12010}'>[処女]</button></font>"
			LOCALS:1 = 333333
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12011}'>[非処女]</button></font>"
		ELSE
			LOCALS:1 = 333333
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12010}'>[処女]</button></font>"
			LOCALS:1 = C0C0C0
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12011}'>[非処女]</button></font>"
		ENDIF
		IF ランダムキャラ素質:1 == 3
			IF GETBIT(ランダムキャラ素質:2, 1) == 0
				LOCALS:1 = C0C0C0
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12012}'>[童貞]</button></font>"
				LOCALS:1 = 333333
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12013}'>[非童貞]</button></font>"
			ELSE
				LOCALS:1 = 333333
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12012}'>[童貞]</button></font>"
				LOCALS:1 = C0C0C0
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12013}'>[非童貞]</button></font>"
			ENDIF
		ENDIF
		LOCALS += "<br>"
		LOCALS += "性知識レベル："
		FOR LOCAL, -2, 4
			IF ランダムキャラ素質:6 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{12030 + LOCAL}'>[%性知識段階表示(LOCAL, -1)%]</button></font>"
		NEXT
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "身長："
		IF 身長数値設定フラグ
			IF 身長保存
				LOCALS:1 = %DECIMAL_STRING(身長保存, 1)%cm
			ELSE
				LOCALS:1 = 未設定
			ENDIF
			LOCALS += @"<button value='{12025}'>[%LOCALS:1%]</button>"
			LOCALS += @"　　　<button value='{12998}'>[自動で設定する]</button>"
		ELSE
			LOCALS += @"%DECIMAL_STRING(身長保存, 1)%cm"
			LOCALS += @"　　　<button value='{12998}'>[手動で設定する]</button>"
		ENDIF
		LOCALS += "<br>"
		LOCALS += "バストサイズ："
		IF バスト数値設定フラグ
			IF バスト保存
				LOCALS:1 = %DECIMAL_STRING(バスト保存, 1)%cm
				LOCALS:1 += @"%EDIT用バストサイズ表示(バスト保存, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))%"
			ELSE
				LOCALS:1 = 未設定
			ENDIF
			LOCALS += @"<button value='{12026}'>[%LOCALS:1%]</button>"
			LOCALS += @"　　　<button value='{12999}'>[カップ数で指定する]</button>"
		ELSE
			FOR LOCAL, -2, 4
				IF ランダムキャラ素質:3 == LOCAL
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12020 + LOCAL}'>[%GET_TALENTNAME(205,LOCAL)%]</button></font>"
			NEXT
			LOCALS += @"　　　<button value='{12999}'>[数値で指定する]</button>"
		ENDIF
		LOCALS += "<br>"
		IF CSVエディタフラグ == 0
			LOCALS += "容姿："
			FOR LOCAL, -1, 3
				IF ランダムキャラ素質:30 == LOCAL
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				IF LOCAL == 0
					LOCALS += @"<font color='#%LOCALS:1%'><button value='{12040 + LOCAL}'>[モブ顔]</button></font>"
				ELSE
					LOCALS += @"<font color='#%LOCALS:1%'><button value='{12040 + LOCAL}'>[%GET_TALENTNAME(160, LOCAL)%]</button></font>"
				ENDIF
			NEXT
		ENDIF
		LOCALS += "<br>"
		IF !身長数値設定フラグ
			LOCALS += "体格："
			FOR LOCAL, -2, 3
				IF ランダムキャラ素質:31 == LOCAL
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				IF LOCAL == 0
					LOCALS += @"<font color='#%LOCALS:1%'><button value='{12050 + LOCAL}'>[普通]</button></font>"
				ELSE
					LOCALS += @"<font color='#%LOCALS:1%'><button value='{12050 + LOCAL}'>[%GET_TALENTNAME(161, LOCAL)%]</button></font>"
				ENDIF
			NEXT
			LOCALS += "<br>"
		ENDIF
		LOCALS += "体型："
		FOR LOCAL, -3, 4
			IF 体型保存 == LOCAL
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			IF LOCAL == 0
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12080 + LOCAL}'>[普通]</button></font>"
			ELSE
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{12080 + LOCAL}'>[%GET_TALENTNAME(163, LOCAL)%]</button></font>"
			ENDIF
		NEXT
		LOCALS += "<br>"
		IF ランダムキャラ素質:1 == 3
			LOCALS += "男性器サイズ："
			FOR LOCAL, -2, 3
				IF ランダムキャラ素質:32 == LOCAL
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				IF LOCAL == 0
					LOCALS += @"<font color='#%LOCALS:1%'><button value='{12060 + LOCAL}'>[平凡]</button></font>"
				ELSE
					LOCALS += @"<font color='#%LOCALS:1%'><button value='{12060 + LOCAL}'>[%GET_TALENTNAME(162, LOCAL)%]</button></font>"
				ENDIF
			NEXT
			LOCALS += "<br>"
		ENDIF
		LOCALS += "<br>"
		LOCALS += "<button value='11100'>[ステータス画面に戻る]</button>"
	CASE 130
		FOR LOCAL, ページ数 * 20, ページ数 * 20 + 20
			SIF TALENT一時保存:LOCAL == 0
				BREAK
			LOCALS += @"%TALENTNAME:(TALENT一時保存:LOCAL), 18, LEFT%："
			FOR LOCAL:1, -2, 4
				;母乳体質だけ例外
				IF TALENT一時保存:LOCAL == 149 && LOCAL:1 > 1
					CONTINUE
				ENDIF
				LOCALS:2 = %GET_TALENTNAME(TALENT一時保存:LOCAL,LOCAL:1)%
				IF LOCAL:1 == 0
					LOCALS:2 = なし
				ELSEIF LOCALS:2 == ""
					CONTINUE
				ENDIF
				IF TALENT数値保存:LOCAL - 5 == LOCAL:1
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{20000 + LOCAL * 10 + LOCAL:1 + 5}'>[%LOCALS:2%]</button></font>"
			NEXT
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='340px,481px,588px,450px'>"
		IF ページ数 > 0
			LOCALS += "<button value='29998'>[前のページ]</button>"
		ELSE
			LOCALS += "　　　　　　"
		ENDIF
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>　　　　"
		IF ページ数 < LOCAL:10 / 20
			LOCALS += "<button value='29999'>[次のページ]</button>"
		ENDIF
	CASE 131
		FOR COUNT, 1, 6
			SIF ERDNAME(フレーバー素質, COUNT) == ""
				CONTINUE
			LOCALS += @"%ERDNAME(フレーバー素質, COUNT), 12, LEFT%："
			FOR LOCAL:1, 0, 6
				LOCALS:2 = %GET_フレーバー素質NAME(COUNT,LOCAL:1)%
				IF LOCAL:1 == 0
					LOCALS:2 = 非表示
				ENDIF
				IF COUNT == 5 && LOCAL:1 == 4
					CONTINUE
				ELSEIF LOCALS:2 == ""
					CONTINUE
				ENDIF
				IF フレーバー素質数値保存:COUNT == LOCAL:1
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{50000 + COUNT * 10 + LOCAL:1}'>[%LOCALS:2%]</button></font>"
			NEXT
			LOCALS += "<br>"
		NEXT
		LOCALS += @"%ERDNAME(フレーバー素質, 6), 12, LEFT%：　　　　"
		FOR LOCAL:1, 0, 4
			LOCALS:2 = %GET_フレーバー素質NAME(6, POWER(2, LOCAL:1))%
			IF LOCALS:2 == ""
				CONTINUE
			ELSEIF LOCAL:1 == 3 && GETBIT(ランダムキャラ素質:1, 1) == 0
				CONTINUE
			ENDIF
			IF GETBIT(フレーバー素質数値保存:6,LOCAL:1)
				LOCALS:1 = C0C0C0
			ELSE
				LOCALS:1 = 333333
			ENDIF
			LOCALS += @"<font color='#%LOCALS:1%'><button value='{50000 + 6 * 10 + LOCAL:1}'>[%LOCALS:2%]</button></font>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='340px,481px,588px,450px'>"
		LOCALS += "　　　　　　"
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>"
	CASE 132
		FOR COUNT, 1, 20
			SIF ERDNAME(知識素質, COUNT) == ""
				CONTINUE
			LOCALS += @"%ERDNAME(知識素質, COUNT), 12, LEFT%："
			FOR LOCAL:1, 0, 4
				IF LOCAL:1 == 0
					LOCALS:2 = なし
				ELSE
					LOCALS:2 = Lv{LOCAL:1}
				ENDIF
				IF 知識素質数値保存:COUNT == LOCAL:1
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{60000 + COUNT * 10 + LOCAL:1}'>[%LOCALS:2%]</button></font>"
			NEXT
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='340px,481px,588px,450px'>"
		LOCALS += "　　　　　　"
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>"
	CASE 133
		FOR COUNT, 1, 20
			SIF ERDNAME(性癖素質, COUNT) == ""
				CONTINUE
			LOCALS += @"%ERDNAME(性癖素質, COUNT), 12, LEFT%："
			FOR LOCAL:1, 0, 4
				SELECTCASE LOCAL:1
					CASE 0
						LOCALS:2 = なし
					CASE 1
						LOCALS:2 = 興味
					CASE 2
						LOCALS:2 = 愛好
					CASE 3
						LOCALS:2 = 中毒
				ENDSELECT
				IF 性癖素質数値保存:COUNT == LOCAL:1
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{80000 + COUNT * 10 + LOCAL:1}'>[%LOCALS:2%]</button></font>"
			NEXT
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='340px,481px,588px,450px'>"
		LOCALS += "　　　　　　"
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>"
	CASE 140
		FOR LOCAL, ページ数 * 20, ページ数 * 20 + 20
			SIF ABL一時保存:LOCAL == -1
				BREAK
			LOCALS += @"%ABLNAME:(ABL一時保存:LOCAL), 18, LEFT%："
			FOR LOCAL:1, 0, 6
				IF ABL数値保存:LOCAL == LOCAL:1
					LOCALS:1 = C0C0C0
				ELSE
					LOCALS:1 = 333333
				ENDIF
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{30000 + LOCAL * 10 + LOCAL:1}'>[%TOFULL(TOSTR(LOCAL:1))%]</button></font>"
			NEXT
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='340px,481px,588px,450px'>"
		IF ページ数 > 0
			LOCALS += "<button value='29998'>[前のページ]</button>"
		ELSE
			LOCALS += "　　　　　　"
		ENDIF
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>　　　　"
		IF ページ数 < LOCAL:11 / 20
			LOCALS += "<button value='29999'>[次のページ]</button>"
		ENDIF
	CASE 150
		FOR LOCAL, ページ数 * 20, ページ数 * 20 + 20
			SIF EXP一時保存:LOCAL == -1
				BREAK
			LOCALS += @"<button value='{40000 + LOCAL * 10}'>[%EXPNAME:(EXP一時保存:LOCAL), 18, LEFT%]：{EXP数値保存:LOCAL, 8}</button>"
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='340px,481px,588px,450px'>"
		IF ページ数 > 0
			LOCALS += "<button value='29998'>[前のページ]</button>"
		ELSE
			LOCALS += "　　　　　　"
		ENDIF
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>　　　　"
		IF ページ数 < LOCAL:12 / 20
			LOCALS += "<button value='29999'>[次のページ]</button>"
		ENDIF
	CASE 160
		FOR LOCAL, 1, 23
			LOCALS += @"%EQUIPNAME:LOCAL, 22, LEFT%："
			LOCALS:1 = 333333
			LOCALS:2 = 333333
			LOCALS:3 = 333333
			LOCALS:4 = 333333
			LOCALS:(服装数値保存:LOCAL + 1) = C0C0C0
			IF LOCAL == 9
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{70000 + LOCAL * 10}'>[ノーブラ]</button></font>"
				LOCALS += @"<font color='#%LOCALS:2%'><button value='{70000 + LOCAL * 10 + 1}'>[ブラ着用]</button></font>"
				LOCALS += @"<font color='#%LOCALS:3%'><button value='{70000 + LOCAL * 10 + 2}'>[ニプレス]</button></font>"
			ELSEIF LOCAL == 17
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{70000 + LOCAL * 10}'>[ノーパン]</button></font>"
				LOCALS += @"<font color='#%LOCALS:2%'><button value='{70000 + LOCAL * 10 + 1}'>[下着着用]</button></font>"
				LOCALS += @"<font color='#%LOCALS:3%'><button value='{70000 + LOCAL * 10 + 2}'>[前張り]</button></font>"
				LOCALS += @"<font color='#%LOCALS:4%'><button value='{70000 + LOCAL * 10 + 3}'>[ふんどし]</button></font>"
			ELSE
				LOCALS += @"<font color='#%LOCALS:1%'><button value='{70000 + LOCAL * 10}'>[なし]</button></font>"
				LOCALS += @"<font color='#%LOCALS:2%'><button value='{70000 + LOCAL * 10 + 1}'>[あり]</button></font>"
			ENDIF
			LOCALS += "<br>"
		NEXT
		LOCALS += "</div>"
		LOCALS += "<div rect='340px,481px,588px,450px'>"
		LOCALS += @"服装名：<button value='71000'>%ランダムキャラ服装名, 30, LEFT%</button>"
		LOCALS += "　　　　<button value='11100'>[ステータス画面に戻る]</button>　　　　"
ENDSELECT
LOCALS += "</div>"
IF CSVエディタフラグ
	IF 表示モード == 0
		LOCALS += "<div rect='600px,480px,400px,50px'><button value='1'>[1] 能力・経験表示へ</button>　<button value='2'>[2] 服装表示へ</button></div>"
	ELSEIF 表示モード == 1
		LOCALS += "<div rect='600px,480px,400px,50px'><button value='0'>[0] 情報・素質表示へ</button>　<button value='2'>[2] 服装表示へ</button></div>"
	ELSEIF 表示モード == 2
		LOCALS += "<div rect='600px,480px,400px,50px'><button value='0'>[0] 情報・素質表示へ</button>　<button value='1'>[1] 能力・経験表示へ</button></div>"
	ENDIF
ELSE
	IF 表示モード == 0
		LOCALS += "<div rect='770px,480px,400px,50px'><button value='1'>[1] 能力・経験表示へ</button></div>"
	ELSEIF 表示モード == 1
		LOCALS += "<div rect='770px,480px,400px,50px'><button value='0'>[0] 情報・素質表示へ</button></div>"
	ENDIF
ENDIF

HTML_PRINT LOCALS
FOR LOCAL, 0, 28
	PRINTL
NEXT
DRAWLINE

BINPUT

LOCAL = RESULT
SELECTCASE LOCAL
;戦闘タイプ--------------------------------------------------------
	CASE 11000 TO 11005
		ランダムキャラ戦闘タイプ:0 = LOCAL - 11000
	CASE 11010 TO 11019
		ランダムキャラ戦闘タイプ:1 = LOCAL - 11010
	CASE 11020 TO 11027
		ランダムキャラ戦闘タイプ:2 = LOCAL - 11020
	CASE 11100
		表示モード = 0
;肉体情報--------------------------------------------------------
	CASE 12001
		ランダムキャラ素質:0 = 1
		肉体特徴数値保存:0 = 0
		肉体特徴数値保存:1 = 0
		肉体特徴数値保存:2 = 0
		肉体特徴数値保存:3 = 0
		IF !身長数値設定フラグ
			身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
			SIF !バスト数値設定フラグ
				バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
		ENDIF
	CASE 12002
		ランダムキャラ素質:0 = 2
		肉体特徴数値保存:0 = 0
		肉体特徴数値保存:1 = 0
		肉体特徴数値保存:2 = 1
		肉体特徴数値保存:3 = 0
		IF !身長数値設定フラグ
			身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
			SIF !バスト数値設定フラグ
				バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
		ENDIF
	CASE 12003
		ランダムキャラ素質:0 = 3
		ランダムキャラ素質:3 = 3
		肉体特徴数値保存:0 = 1
		肉体特徴数値保存:1 = 0
		肉体特徴数値保存:2 = 0
		肉体特徴数値保存:3 = 0
		ランダムキャラ素質:31 = -1
		IF !身長数値設定フラグ
			身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
			SIF !バスト数値設定フラグ
				バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
		ENDIF
	CASE 12004
		ランダムキャラ素質:0 = 4
		ランダムキャラ素質:3 = -2
		肉体特徴数値保存:0 = 0
		肉体特徴数値保存:1 = 0
		肉体特徴数値保存:2 = 0
		肉体特徴数値保存:3 = 1
		ランダムキャラ素質:31 = -2
		IF !身長数値設定フラグ
			身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
			SIF !バスト数値設定フラグ
				バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
		ENDIF
	CASE 12005
		ランダムキャラ素質:0 = 5
		IF !身長数値設定フラグ
			身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
			SIF !バスト数値設定フラグ
				バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
		ENDIF
	CASE 12006
		ランダムキャラ素質:0 = 6
		IF !身長数値設定フラグ
			身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
			SIF !バスト数値設定フラグ
				バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
		ENDIF
	CASE 12007
		ランダムキャラ素質:1 = 1
		ランダムキャラ素質:32 = 0
	CASE 12008
		ランダムキャラ素質:1 = 3
	CASE 12010
		CLEARBIT ランダムキャラ素質:2, 0
	CASE 12011
		SETBIT ランダムキャラ素質:2, 0
	CASE 12012
		CLEARBIT ランダムキャラ素質:2, 1
	CASE 12013
		SETBIT ランダムキャラ素質:2, 1
	CASE 12018 TO 12023
		IF ランダムキャラ素質:0 == 3
			ランダムキャラ素質:3 = MAX(LOCAL - 12020, 2)
		ELSEIF ランダムキャラ素質:0 == 4
			ランダムキャラ素質:3 = MIN(LOCAL - 12020, -1)
		ELSE
			ランダムキャラ素質:3 = LOCAL - 12020
		ENDIF
		バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
	CASE 12025
		DRAWLINE
		PRINTL 身長をmm単位で入力してください。
		INPUT
		身長保存 = RESULT
		ランダムキャラ素質:31 = EDIT用体格算出(ランダムキャラ素質:1, 身長保存)
		SIF !バスト数値設定フラグ
			バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
	CASE 12026
		DRAWLINE
		PRINTL バストサイズをmm単位で入力してください。
		INPUT
		バスト保存 = RESULT
		ランダムキャラ素質:3 = EDIT用バストTALENT算出(バスト保存, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
	CASE 12028 TO 12033
		ランダムキャラ素質:6 = LOCAL - 12030
	CASE 12039 TO 12042
		ランダムキャラ素質:30 = LOCAL - 12040
	CASE 12048 TO 12052
		IF ランダムキャラ素質:0 == 3
			ランダムキャラ素質:31 = MIN(LOCAL - 12050, 0)
		ELSEIF ランダムキャラ素質:0 == 4
			ランダムキャラ素質:31 = MIN(LOCAL - 12050, -1)
		ELSE
			ランダムキャラ素質:31 = LOCAL - 12050
		ENDIF
		身長保存 = EDIT用身長算出(ランダムキャラ素質:1, ランダムキャラ素質:0, ランダムキャラ素質:31)
		SIF !バスト数値設定フラグ
			バスト保存 = EDIT用バストcm算出(ランダムキャラ素質:3, EDIT用アンダーバスト算出(ランダムキャラ素質:1, ランダムキャラ素質:0, 身長保存, ランダムキャラ素質:31, 体型保存))
	CASE 12058 TO 12062
		ランダムキャラ素質:32 = LOCAL - 12060
	CASE 12070 TO 12073
		INVERTBIT 肉体特徴数値保存:(LOCAL - 12070), 0
	CASE 12077 TO 12083
		体型保存 = LOCAL - 12080
	CASE 12998
		INVERTBIT 身長数値設定フラグ, 0
	CASE 12999
		INVERTBIT バスト数値設定フラグ, 0
;タレント--------------------------------------------------------
	CASE 20000 TO 29997
		TALENT数値保存:((LOCAL - 20000) / 10) = LOCAL % 10
	CASE 29998
		ページ数 -= 1
	CASE 29999
		ページ数 += 1
;ABL--------------------------------------------------------
	CASE 30000 TO 39997
		ABL数値保存:((LOCAL - 30000) / 10) = LOCAL % 10
;EXP--------------------------------------------------------
	CASE 40000 TO 49997
		RESULT = 0
		PRINTL 経験数を入力してください。（３桁まで）
		INPUT
		EXP数値保存:((LOCAL - 40000) / 10) = MIN(RESULT, 999)
;フレーバー素質----------------------------------------------------
	CASE 50000 TO 50059
		フレーバー素質数値保存:((LOCAL - 50000) / 10) = LOCAL % 10
	CASE 50060 TO 50069
		INVERTBIT フレーバー素質数値保存:((LOCAL - 50000) / 10), LOCAL % 10
;知識素質----------------------------------------------------
	CASE 60000 TO 60209
		知識素質数値保存:((LOCAL - 60000) / 10) = LOCAL % 10
;服装----------------------------------------------------
	CASE 70010 TO 70221
		服装数値保存:((LOCAL - 70000) / 10) = LOCAL % 10
	CASE 71000
		PRINTL 服装名を入力してください。（１５文字まで）
		INPUTS
		ランダムキャラ服装名 = %SUBSTRING(RESULTS, 0, 30)%
;性癖素質----------------------------------------------------
	CASE 80000 TO 80209
		性癖素質数値保存:((LOCAL - 80000) / 10) = LOCAL % 10
;メイン画面--------------------------------------------------------
	CASE 100
		PRINTL 名前を入力してください。（９文字まで）
		INPUTS
		ランダムキャラネーム = %SUBSTRING(RESULTS, 0, 18)%
	CASE 101
		PRINTL 二つ名を入力してください。（１２文字まで）
		INPUTS
		ランダムキャラ二つ名 = %SUBSTRING(RESULTS, 0, 24)%
	CASE 102
		PRINTL 出演作品を入力してください。（１２文字まで）
		INPUTS
		ランダムキャラ出演作品 = %SUBSTRING(RESULTS, 0, 24)%
	CASE 110
		表示モード = 110
	CASE 120
		表示モード = 120
	CASE 130
		ページ数 = 0
		表示モード = 130
	CASE 131
		ページ数 = 0
		表示モード = 131
	CASE 132
		ページ数 = 0
		表示モード = 132
	CASE 133
		ページ数 = 0
		表示モード = 133
	CASE 140
		ページ数 = 0
		表示モード = 140
	CASE 150
		ページ数 = 0
		表示モード = 150
	CASE 160
		ページ数 = 0
		表示モード = 160
	CASE 200
		IF CSVエディタフラグ
			CLEARLINE LINECOUNT
			;空き番号取得
			FOR LOCAL:1, 2000, キャラクタ数上限
				IF EXISTCSV(LOCAL:1) == 0
					登録番号 = LOCAL:1
					BREAK
				ENDIF
			NEXT
			PRINTFORML 番号,{登録番号}
			PRINTFORML 名前,[%ランダムキャラ二つ名%]%ランダムキャラネーム%
			PRINTFORML 呼び名,%ランダムキャラネーム%
			IF ランダムキャラ出演作品 != ""
				PRINTFORML CSTR,出演作品,%ランダムキャラ出演作品%
				PRINTL フラグ,招待不可フラグ,3
			ENDIF
			PRINTL 基礎,体力,1200
			PRINTL 基礎,勃起,1500
			PRINTL 基礎,ムード,1500
			PRINTL 基礎,理性,1000
			PRINTL 基礎,怒り,1000
			PRINTL 
			PRINTL 
			PRINTFORML 基礎,身長,{身長保存}
			PRINTFORML 基礎,バスト,{バスト保存}
			PRINTL 
			PRINTL {
			PRINTL CSTR,知識素質設定,
			PRINTFORML 性知識Lv{ランダムキャラ素質:6}
			FOR LOCAL, 1, 20
				IF ERDNAME(知識素質, LOCAL) != ""
					PRINTFORML %ERDNAME(知識素質, LOCAL)%Lv{知識素質数値保存:LOCAL}
				ENDIF
			NEXT
			PRINTL }
			PRINTL 
			PRINTL {
			PRINTL CSTR,性癖素質設定,
			FOR LOCAL, 1, 20
				IF ERDNAME(性癖素質, LOCAL) != ""
					PRINTFORML %ERDNAME(性癖素質, LOCAL)%Lv{性癖素質数値保存:LOCAL}
				ENDIF
			NEXT
			PRINTL }
			PRINTL
			PRINTFORML 素質,種族,{ランダムキャラ素質:0}
			PRINTFORML 素質,性別,{ランダムキャラ素質:1}
			IF GETBIT(ランダムキャラ素質:2, 0)
				PRINTL 素質,処女,0
			ELSE
				PRINTL 素質,処女,1
			ENDIF
			IF GETBIT(ランダムキャラ素質:2, 1)
				PRINTL 素質,童貞,1
			ELSE
				PRINTL 素質,童貞,0
			ENDIF
			PRINTFORML 素質,バストサイズ,{ランダムキャラ素質:3}
			SIF 肉体特徴数値保存:0
				PRINTFORML 素質,角あり,1
			SIF 肉体特徴数値保存:1
				PRINTFORML 素質,尻尾あり,1
			SIF 肉体特徴数値保存:2
				PRINTFORML 素質,エルーン耳,1
			SIF 肉体特徴数値保存:3
				PRINTFORML 素質,ハーヴィン耳,1
			FOR LOCAL, 0, 300
				SIF TALENT数値保存:LOCAL == 0
					CONTINUE
				SIF TALENT数値保存:LOCAL == 5
					CONTINUE
				PRINTFORML 素質,%TALENTNAME:(TALENT一時保存:LOCAL)%,{TALENT数値保存:LOCAL - 5}
			NEXT
			PRINTFORML 素質,体格,{ランダムキャラ素質:31}
			PRINTFORML 素質,体型,{体型保存}
			PRINTFORML 素質,男性器サイズ,{ランダムキャラ素質:32}

			FOR LOCAL, 0, 50
				IF ABL数値保存:LOCAL > 0
					PRINTFORML 能力,%ABLNAME:(ABL一時保存:LOCAL)%,{ABL数値保存:LOCAL}
				ENDIF
			NEXT
			FOR LOCAL, 0, 100
				IF EXP数値保存:LOCAL > 0
					PRINTFORML 経験,%ABLNAME:(EXP一時保存:LOCAL)%,{EXP数値保存:LOCAL}
				ENDIF
			NEXT
			PRINTL 
			PRINTL {
			PRINTFORML CSTR,戦闘基礎ステータス設定,
			PRINTFORML 属性_%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%
			PRINTFORML 得意武器_%得意武器数値文字列変換(ランダムキャラ戦闘タイプ:1)%
			PRINTFORML 初期ステータスタイプ_%成長タイプ:(ランダムキャラ戦闘タイプ:2)%
			PRINTL }
			PRINTL 
			
			PRINTFORML CSTR,服名称,%ランダムキャラ服装名%
			FOR LOCAL, 1, 23
				PRINTFORML 装着物,%EQUIPNAME:LOCAL%,{服装数値保存:LOCAL}
			NEXT
			
			OUTPUTLOG @"CSV_EDITFILE/Chara{登録番号} %ランダムキャラネーム%.csv"
			PRINTW CSV_EDITFILEフォルダに出力しました。CSV/キャラデータフォルダに移動して再起動してください
			RETURN 0
		ENDIF
		IF 従業員部屋割り配列:(従業員キャラ上限 - 1) > 0
			PRINTW 従業員部屋が一杯です。
			RETURN 0
		ENDIF
		IF 必要ルピ > MONEY
			PRINTW ルピが足りません。
			RETURN 0
		ENDIF
		MONEY -= 必要ルピ
		PRINTFORMW [従業員]%ランダムキャラネーム%を雇用しました。
		FLAG:従業員数 += 1
		LOCAL:1 = CHARANUM
		ADDCHARA 999
		TALENT:(LOCAL:1):従業員 = 1
		NAME:(LOCAL:1) = [従業員]%ランダムキャラネーム%
		CALLNAME:(LOCAL:1) = %ランダムキャラネーム%
		TALENT:(LOCAL:1):200 = ランダムキャラ素質:0
		TALENT:(LOCAL:1):2 = ランダムキャラ素質:1
		IF GETBIT(ランダムキャラ素質:2, 0)
			TALENT:(LOCAL:1):0 = 0
		ELSE
			TALENT:(LOCAL:1):0 = 1
		ENDIF
		IF GETBIT(ランダムキャラ素質:2, 1)
			TALENT:(LOCAL:1):1 = 1
		ELSE
			TALENT:(LOCAL:1):1 = 0
		ENDIF
		TALENT:(LOCAL:1):205 = ランダムキャラ素質:3
		
		
		BASE:(LOCAL:1):身長 = 身長保存
		MAXBASE:(LOCAL:1):身長 = 身長保存
		BASE:(LOCAL:1):バスト = バスト保存
		MAXBASE:(LOCAL:1):バスト = バスト保存

		SIF 肉体特徴数値保存:0
			TALENT:(LOCAL:1):206 = 1
		SIF 肉体特徴数値保存:1
			TALENT:(LOCAL:1):207 = 1
		SIF 肉体特徴数値保存:2
			TALENT:(LOCAL:1):208 = 1
		SIF 肉体特徴数値保存:3
			TALENT:(LOCAL:1):209 = 1
		FOR LOCAL, 0, 300
			IF TALENT数値保存:LOCAL == 5
				CONTINUE
			ENDIF
			TALENT:(LOCAL:1):(TALENT一時保存:LOCAL) = TALENT数値保存:LOCAL - 5
		NEXT
		TALENT:(LOCAL:1):容姿 = ランダムキャラ素質:30
		TALENT:(LOCAL:1):体格 = ランダムキャラ素質:31
		TALENT:(LOCAL:1):体型 = 体型保存
		TALENT:(LOCAL:1):男性器サイズ = ランダムキャラ素質:32
		FOR LOCAL, 0, 300
			SIF TALENT数値保存:LOCAL == 0
				CONTINUE
			SIF TALENT数値保存:LOCAL == 5
				CONTINUE
			TALENT:(LOCAL:1):(TALENT一時保存:LOCAL) = TALENT数値保存:LOCAL - 5
		NEXT
		LOCAL = 0
		FOR COUNT, 1, 10
			SIF フレーバー素質数値保存:COUNT == 0
				CONTINUE
			フレーバー素質:(LOCAL:1):COUNT = フレーバー素質数値保存:COUNT
			LOCAL = 1
		NEXT
		IF LOCAL
			フレーバー素質:(LOCAL:1):素質表示設定 = 1
		ENDIF
		FOR LOCAL, 0, 50
			IF ABL数値保存:LOCAL > 0
				ABL:(LOCAL:1):(ABL一時保存:LOCAL) = ABL数値保存:LOCAL
			ENDIF
		NEXT
		FOR LOCAL, 0, 100
			IF EXP数値保存:LOCAL > 0
				EXP:(LOCAL:1):(EXP一時保存:LOCAL) = EXP数値保存:LOCAL
			ENDIF
		NEXT

		;戦闘関連設定
		CSTR:(LOCAL:1):戦闘基礎ステータス設定 =  属性_%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)% 得意武器_%得意武器数値文字列変換(ランダムキャラ戦闘タイプ:1)% 初期ステータスタイプ_%成長タイプ:(ランダムキャラ戦闘タイプ:2)%

		CALL コマンド存在チェック
		従業員追加中フラグ = 1
		CALL キャラクター初期化(LOCAL:1)
		
		;性知識は初期化の後
		知識素質:(LOCAL:1):性知識 = ランダムキャラ素質:6
		
		CFLAG:(LOCAL:1):滞在期間 = 999
		CALL ステータス記録(LOCAL:1)

		FOR LOCAL:2, 0, 従業員キャラ上限
			IF 従業員部屋割り配列:(LOCAL:2) < 1
				従業員部屋割り配列:(LOCAL:2) = LOCAL:1
				BREAK
			ENDIF
		NEXT
		CALL 従業員人数チェック()
		RETURN 0
	CASE 0
		表示モード = 0
	CASE 1
		表示モード = 1
	CASE 2
		表示モード = 2
	CASE 999
		RETURN 0
ENDSELECT

GOTO 従業員_LOOP


@ステータス記録(キャラ番号)
;引き継ぎ処理時に使う初期値の記録用
#DIM キャラ番号
#DIM 記録位置

LOCAL:1 = 0
FOR LOCAL, 0, 100
	IF 従業員初期値記録:LOCAL == ""
		記録位置 = LOCAL
		LOCAL:1 = 1
		BREAK
	ENDIF
NEXT
IF LOCAL:1 == 0
	PRINTW ※記録枠がいっぱいのため、このキャラは周回時に引き継がれません
	RETURN 0
ENDIF

従業員初期値記録:記録位置 = 

従業員初期値記録:記録位置 += @"キャラ番号_{キャラ番号}_"
従業員初期値記録:記録位置 += "^^^"
従業員初期値記録:記録位置 += @"名前_%CALLNAME:キャラ番号%_"
従業員初期値記録:記録位置 += "^^^"
FOR LOCAL, 0, 300
	IF TALENT:キャラ番号:LOCAL
		従業員初期値記録:記録位置 += @"素質_{LOCAL}_{TALENT:キャラ番号:LOCAL}_"
	ENDIF
NEXT
従業員初期値記録:記録位置 += "^^^"
FOR LOCAL, 0, 10
	IF フレーバー素質:キャラ番号:LOCAL
		従業員初期値記録:記録位置 += @"フレーバー素質_{LOCAL}_{フレーバー素質:キャラ番号:LOCAL}_"
	ENDIF
NEXT
従業員初期値記録:記録位置 += "^^^"
FOR LOCAL, 0, 50
	IF ABL:キャラ番号:LOCAL
		従業員初期値記録:記録位置 += @"ABL_{LOCAL}_{ABL:キャラ番号:LOCAL}_"
	ENDIF
NEXT
従業員初期値記録:記録位置 += "^^^"
FOR LOCAL, 0, 100
	IF EXP:キャラ番号:LOCAL
		従業員初期値記録:記録位置 += @"EXP_{LOCAL}_{EXP:キャラ番号:LOCAL}_"
	ENDIF
NEXT
従業員初期値記録:記録位置 += "^^^"
従業員初期値記録:記録位置 += @"属性_{基礎BATTLE_STATE:キャラ番号:属性}_"
従業員初期値記録:記録位置 += @"得意武器_{基礎BATTLE_STATE:キャラ番号:得意武器}_"
従業員初期値記録:記録位置 += @"タイプ_{基礎BATTLE_STATE:キャラ番号:ステータスタイプ保存}_"

従業員初期値記録:記録位置 += @"性知識_{知識素質:キャラ番号:性知識}_"



@EDIT用バストサイズ表示(バスト保存, アンダーバスト)
#FUNCTIONS
#DIM バスト保存
#DIM アンダーバスト
#DIMS CONST 表記 = "(-)","(AAA)","(AA)","(A)","(B)","(C)","(D)","(E)","(F)","(G)","(H)","(I)","(J)","(K)","(L)","(M)","(N)","(O)","(P)","(Q)","(R)","(S)","(T)","(U)","(V)","(W)","(X)","(Y)","(Z)","(Z+) "

LOCAL = (バスト保存 - アンダーバスト) / 25

;文字列変数が若干不自由なので無理やり作成
LOCAL = MAX(LOCAL, 1)
LOCAL = MIN(LOCAL, 29)

LOCALS:0 = %表記:(LOCAL)%

; IF !HAS_VAGINA(対象)
; 	RETURNF "(--)"
; ELSE
	RETURNF LOCALS:0
; ENDIF

@EDIT用バストTALENT算出(バスト保存, アンダーバスト)
#FUNCTION
#DIM バスト保存
#DIM アンダーバスト

LOCAL = バスト保存 - アンダーバスト

SELECTCASE LOCAL
	CASE IS < 89
		RETURNF -2
	CASE IS < 139
		RETURNF -1
	CASE IS < 184
		RETURNF 0
	CASE IS < 239
		RETURNF 1
	CASE IS < 314
		RETURNF 2
	CASEELSE
		RETURNF 3
ENDSELECT


@EDIT用バストcm算出(バストサイズ, アンダーバスト)
#FUNCTION
#DIM バストサイズ
#DIM アンダーバスト

;最低サイズ算出
LOCAL = アンダーバスト + 4 + ((バストサイズ + 2) * 50)
SIF バストサイズ == 3
	LOCAL += 25


IF バストサイズ < 2
	;-2~1の時、+4.9cmまで許容
	LOCAL = LOCAL + RAND:50
ELSE
	;それ以上の時、+7.4cmまで許容
	LOCAL = LOCAL + RAND:75
ENDIF

; IF !HAS_VAGINA(対象)
; 	RETURNF "(--)"
; ELSE
	RETURNF LOCAL
; ENDIF

@EDIT用アンダーバスト算出(性別, 種族, 身長, 体格, 体型)
#FUNCTION
#DIM 性別
#DIM 体格
#DIM 体型
#DIM 身長
#DIM 種族
#DIM 基礎値
基礎値 = 身長 * (!GETBIT(性別, 0) ? 50 # 43) / 100

SELECTCASE 体格
	CASE 体格_ハーヴィン並
		TIMES 基礎値, 1.15
	CASE 体格_子供
		TIMES 基礎値, 1.05
	CASE 体格_小柄
		SIF 種族 != 種族_ドラフ
			TIMES 基礎値, 0.95
	CASE 体格_普通
	CASE 体格_大柄
		TIMES 基礎値, 1.05
	CASE 体格_男ドラフ並
		TIMES 基礎値, 1.10
ENDSELECT

SELECTCASE 体型
	CASE 体型_痩せすぎ
		TIMES 基礎値, 0.90
	CASE 体型_細身
		TIMES 基礎値, 0.94
	CASE 体型_スレンダー
		TIMES 基礎値, 0.97
	CASE 体型_グラマー
		TIMES 基礎値, 1.01
	CASE 体型_豊満
		TIMES 基礎値, 1.05
	CASE 体型_肥満
		TIMES 基礎値, 1.10
ENDSELECT

;夢詰め込んだらドラフカップが大きくなりすぎるので補正
SIF 種族 == 種族_ドラフ && 体格 >= 体格_小柄 && GETBIT(性別, 0)
	基礎値 = 基礎値 * 110 / 100

RETURNF 基礎値

@EDIT用身長算出(性別, 種族, 体格)
#FUNCTION
#DIM 性別
#DIM 体格
#DIM 種族
#DIM 基礎値

;今のところ子供は未実装なのでコメントアウト
;IF CFLAG:対象:行動不能状態 == 行動不能_子供
;	基礎値 = MIN(500 + CFLAG:対象:子の成長度 * 20, 1300)
;ELSEIF TALENT:対象:幼児
;	基礎値 = MIN(800 + (CFLAG:対象:子の成長度 - 6) * 50, 1300)
;ELSE
	SELECTCASE 体格
		CASE 体格_ハーヴィン並
			基礎値 = !GETBIT(性別, 0) ? 800 # 700
			基礎値 += RAND:200
		CASE 体格_子供
			基礎値 = !GETBIT(性別, 0) ? 1200 # 1100
			基礎値 += RAND:200
			SIF 種族 == 種族_ドラフ && GETBIT(性別, 0)
				TIMES 基礎値, 0.90
		CASE 体格_小柄
			基礎値 = !GETBIT(性別, 0) ? 1400 # 1300
			基礎値 += RAND:200
		CASE 体格_普通
			基礎値 = !GETBIT(性別, 0) ? 1600 # 1500
			基礎値 += RAND:200
		CASE 体格_大柄
			基礎値 = !GETBIT(性別, 0) ? 1950 + RAND:200 # 1800 + RAND:200
		CASE 体格_男ドラフ並
			基礎値 = !GETBIT(性別, 0) ? 2150 + RAND:300 # 2000 + RAND:200
	ENDSELECT
;ENDIF

RETURNF 基礎値

@EDIT用体格算出(性別, 身長)
#FUNCTION
#DIM 性別
#DIM 身長
#DIM 基礎値

;身長と性別をもとに算出する
SELECTCASE 身長
	CASE IS < 1000 + !GETBIT(性別, 0)*100
		RETURNF 体格_ハーヴィン並
	CASE IS < 1300 + !GETBIT(性別, 0)*100
		RETURNF 体格_子供
	CASE IS < 1500 + !GETBIT(性別, 0)*100
		RETURNF 体格_小柄
	CASE IS < 1700 + !GETBIT(性別, 0)*100
		RETURNF 体格_普通
	CASE IS < 1900 + !GETBIT(性別, 0)*100
		RETURNF 体格_大柄
	CASEELSE
		RETURNF 体格_男ドラフ並
ENDSELECT 


