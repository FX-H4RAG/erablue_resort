@従業員雇用
#DIM ランダムキャラ素質, 70
#DIM ランダムキャラ戦闘タイプ, 10
#DIMS ランダムキャラネーム
#DIMS ランダムキャラ知識素質, 2
#DIM 重複チェック用, 10

$従業員_LOOP
DRAWLINE
PRINTL 従業員（ランダムキャラ）を雇います。
PRINTPLAIN [110] 別の候補を見る　を選んだ場合、前のキャラには戻れないので注意してください。
PRINTL
DRAWLINE
VARSET ランダムキャラ素質:0
VARSET ランダムキャラ戦闘タイプ:0
VARSET ランダムキャラ知識素質:0, "料理人Lv0"
LOCALS = <div rect='81,31,1937,2812' border='31' bcolor='#C0C0C0'></div><div rect='2050,31,3875,2812' border='31' bcolor='#C0C0C0'></div>
LOCALS += "<div rect='156,156,1937,2812'>"
LOCALS += "<button value='100'>[100] このキャラを雇用する</button>"
LOCALS += "<br>"
LOCALS += "<button value='110'>[110] 別の候補を見る</button>"
LOCALS += "<br>"
LOCALS += "<button value='999'>[999] 戻る</button>"
LOCALS += "</div>"

LOCALS += "<div rect='2125,156,3675,2812'>"
;戦闘タイプ決定
ランダムキャラ戦闘タイプ:0 = RAND:6
ランダムキャラ戦闘タイプ:1 = RAND:10
ランダムキャラ戦闘タイプ:2 = RAND:8
;種族決定
ランダムキャラ素質:0 = LIMIT(RAND:5, 1, 4)
ランダムキャラネーム = %ランダムキャラ名前生成(ランダムキャラ素質:0)%
LOCALS += @"■[従業員]%ランダムキャラネーム, 18, LEFT%"
LOCALS:1 = %カラーパレット_HTML(@"%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%属性")%
LOCALS += @"<font color='#%LOCALS:1%'>属性：%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)%</font>"
LOCALS += @"　得意武器：%得意武器数値文字列変換(ランダムキャラ戦闘タイプ:1)%　タイプ：%成長タイプ:(ランダムキャラ戦闘タイプ:2)%"
LOCALS += "<br><br>"
LOCALS += "□ 情報 □---------------------------------------------------------------<br>"
;性別枠
ランダムキャラ素質:1 = (RAND:20 / 19 * 2) + 1
SIF 素質切り替えOPTION記憶:ふたなり_女性
	ランダムキャラ素質:1 = 1
;処女（10%で非処女）
ランダムキャラ素質:2 = MIN(RAND:10, 1)
;バストサイズ
ランダムキャラ素質:3 = RAND:5 - 2
;ランダムキャラ素質:4、5に種族系素質
;年齢
SELECTCASE RAND:100
	CASE 0 TO 4
		ランダムキャラ素質:51 = -2
	CASE 5 TO 24
		ランダムキャラ素質:51 = -1
	CASE 25 TO 68
		ランダムキャラ素質:51 = 0
	CASE 69 TO 88
		ランダムキャラ素質:51 = 1
	CASE 89 TO 98
		ランダムキャラ素質:51 = 2
	CASE 99
		ランダムキャラ素質:51 = 3
ENDSELECT
SIF ランダムキャラ素質:51 == 3
	ランダムキャラ素質:51 += RAND:2
;交際経験(年齢ごとに確率が異なる)
IF ランダムキャラ素質:2 == 0
	;非処女の場合
	SELECTCASE ランダムキャラ素質:51
		CASE -1, 0
			;若い～成人 想い人あり、恋人持ち、セフレありが各20%ずつ
			IF RAND:5
				ランダムキャラ素質:55 = RAND:4 - 2
			ELSE
				ランダムキャラ素質:55 = 0
			ENDIF
		CASE 1
			;大人 想い人あり、恋人持ちが各20%ずつ、セフレあり、人妻が各10%ずつ
			IF RAND:5
				ランダムキャラ素質:55 = RAND:4 - 2
				SIF ランダムキャラ素質:55 == -2 && RAND:2
					ランダムキャラ素質:55 = 2
			ELSE
				ランダムキャラ素質:55 = 0
			ENDIF
		CASE 2
			;中年 50%で人妻、25%で未亡人
			IF RAND:2
				ランダムキャラ素質:55 = 2
			ELSEIF RAND:2
				ランダムキャラ素質:55 = 3
			ELSE
				ランダムキャラ素質:55 = 0
			ENDIF
		CASE 3, 4
			;長命者 50%で未亡人、25%でセフレあり
			IF RAND:2
				ランダムキャラ素質:55 = 3
			ELSEIF RAND:2
				ランダムキャラ素質:55 = -2
			ELSE
				ランダムキャラ素質:55 = 0
			ENDIF
		CASEELSE
			;幼い 50%でセフレあり、25%で恋人持ち
			IF RAND:2
				ランダムキャラ素質:55 = -2
			ELSEIF RAND:2
				ランダムキャラ素質:55 = 1
			ELSE
				ランダムキャラ素質:55 = 0
			ENDIF
	ENDSELECT
	;出産経験
	IF ランダムキャラ素質:55 == 2 || ランダムキャラ素質:55 == 3
		;人妻or未亡人に+0～3
		ランダムキャラ素質:55 += 10000 * (RAND:2 + RAND:2 + RAND:2)
	ELSEIF ランダムキャラ素質:55 == -2 && RAND:50 == 0
		;セフレありに2%で+1
		ランダムキャラ素質:55 -= 10000
	ENDIF
ELSE
	;処女の場合
	SELECTCASE ランダムキャラ素質:51
		CASE -1
			;若い 10%で想い人あり、2.5%で恋人持ち
			IF RAND:8
				ランダムキャラ素質:55 = 0
			ELSEIF RAND:5
				ランダムキャラ素質:55 = -1
			ELSE
				ランダムキャラ素質:55 = 1
			ENDIF
		CASE 0 TO 2
			;成人～中年 10%で恋人持ち、2.5%で想い人あり
			IF RAND:8
				ランダムキャラ素質:55 = 0
			ELSEIF RAND:5
				ランダムキャラ素質:55 = 1
			ELSE
				ランダムキャラ素質:55 = -1
			ENDIF
		CASEELSE
			;幼いor長命者 5%で想い人あり
			IF RAND:20
				ランダムキャラ素質:55 = 0
			ELSE
				ランダムキャラ素質:55 = -1
			ENDIF
	ENDSELECT
ENDIF
;性知識(年齢ごとに確率が異なる)
IF ランダムキャラ素質:2 == 0
	;非処女の場合
	SELECTCASE RAND:20
		CASE 0
			;疎い
			ランダムキャラ素質:6 = 0
		CASE 1 TO 4
			;耳年増
			ランダムキャラ素質:6 = 1
		CASE 5 TO 15
			;必要十分
			ランダムキャラ素質:6 = 2
		CASE 16 TO 19
			;有識者
			ランダムキャラ素質:6 = 3
	ENDSELECT
ELSEIF ランダムキャラ素質:51 >= 0
	;大人処女の場合
	SELECTCASE RAND:20
		CASE 0 TO 9
			;疎い
			ランダムキャラ素質:6 = 0
		CASE 10 TO 14
			;耳年増
			ランダムキャラ素質:6 = 1
		CASE 15 TO 18
			;必要十分
			ランダムキャラ素質:6 = 2
		CASE 19
			;有識者
			ランダムキャラ素質:6 = 3
	ENDSELECT
ELSE
	;子供処女の場合
	SELECTCASE RAND:20
		CASE 0
			;無知
			ランダムキャラ素質:6 = -2
		CASE 1 TO 3
			;獣並み(エルーン)/無知(それ以外)
			ランダムキャラ素質:6 = (ランダムキャラ素質:0 == 2) ? -1 # -2
		CASE 4 TO 15
			;疎い
			ランダムキャラ素質:6 = 0
		CASE 16 TO 19
			;耳年増
			ランダムキャラ素質:6 = 1
	ENDSELECT
ENDIF
LOCALS += "　種族："
SELECTCASE ランダムキャラ素質:0
	CASE 1
		LOCALS += "ヒューマン"
	CASE 2
		LOCALS += "エルーン"
		ランダムキャラ素質:4 = 208
		LOCALS += "（エルーン耳）"
		IF RAND:5 == 0
			ランダムキャラ素質:5 = 207
			LOCALS += "（尻尾）"
		ENDIF
	CASE 3
		LOCALS += "ドラフ"
		IF ランダムキャラ素質:51 == -2 && ランダムキャラ素質:3 < 2
			ランダムキャラ素質:3 = 2
		ELSE
			ランダムキャラ素質:3 = 3
		ENDIF
		ランダムキャラ素質:4 = 206
		LOCALS += "（角あり）"
	CASE 4
		LOCALS += "ハーヴィン"
		ランダムキャラ素質:3 = -2
		ランダムキャラ素質:4 = 209
		LOCALS += "（ハーヴィン耳）"
ENDSELECT
LOCALS += "<br>"
LOCALS += "　性別："
SELECTCASE ランダムキャラ素質:1
	CASE 1
		LOCALS += "女性"
		IF ランダムキャラ素質:2 == 0
			LOCALS += "　　　　　　　　　"
		ELSE
			LOCALS += "（処女）　　　　　"
		ENDIF
	CASE 3
		LOCALS += "ふたなり"
		IF ランダムキャラ素質:2 == 0
			LOCALS += "　　　　　　　　　"
		ELSE
			LOCALS += "（処女）（童貞）　"
		ENDIF
ENDSELECT
LOCALS += @"バストサイズ：%GET_TALENTNAME(205,ランダムキャラ素質:3)%"
LOCALS += "<br>"
LOCALS += @"　性知識レベル："
SELECTCASE ランダムキャラ素質:6
	CASE -2
		LOCALS += "無知"
		ランダムキャラ知識素質:0 '= "性知識Lv-2"
	CASE -1
		LOCALS += "獣並み"
		ランダムキャラ知識素質:0 '= "性知識Lv-1"
	CASE 0
		LOCALS += "疎い"
		ランダムキャラ知識素質:0 '= "性知識Lv0"
	CASE 1
		LOCALS += "耳年増"
		ランダムキャラ素質:18 = 23
		ランダムキャラ知識素質:0 '= "性知識Lv0"
	CASE 2
		LOCALS += "必要十分"
		ランダムキャラ知識素質:0 '= "性知識Lv1"
	CASE 3
		LOCALS += "有識者"
		ランダムキャラ知識素質:0 '= "性知識Lv2"
ENDSELECT
LOCALS += "<br>　容姿："
LOCAL = RAND:100
SELECTCASE LOCAL
	CASE 0 TO 9
		ランダムキャラ素質:50 = -1
	CASE 10 TO 69
		ランダムキャラ素質:50 = 0
	CASE 70 TO 94
		ランダムキャラ素質:50 = 1
	CASE 95 TO 99
		ランダムキャラ素質:50 = 2
ENDSELECT
LOCALS += @"%GET_TALENTNAME(160,ランダムキャラ素質:50), 14, LEFT%"
LOCALS += "年齢："
LOCALS += @"%GET_TALENTNAME(164,ランダムキャラ素質:51), 17, LEFT%"
LOCALS += "<br>　体格："
;体格(種族と年齢ごとに分布が異なる)
SELECTCASE ランダムキャラ素質:0
	CASE 1, 2
		;ヒューマン、エルーン
		SELECTCASE ランダムキャラ素質:51
			CASE 4
				;ロリババア
				ランダムキャラ素質:52 = -2
			CASE -2
				;幼い
				ランダムキャラ素質:52 = RAND:5 - 2
				SIF ランダムキャラ素質:52 == 1 || ランダムキャラ素質:52 == 2
					ランダムキャラ素質:52 = -1
			CASEELSE
				;それ以外
				ランダムキャラ素質:52 = RAND:5 - 2
				SIF ランダムキャラ素質:52 == -2 || ランダムキャラ素質:52 == 2
					ランダムキャラ素質:52 = 0
		ENDSELECT
	CASE 3
		;ドラフ
		SELECTCASE ランダムキャラ素質:51
			CASE 4
				;ロリババア
				ランダムキャラ素質:52 = -2
			CASE -2, -1
				;幼い/若い
				IF ランダムキャラ素質:3 >= 3 && RAND:5
					ランダムキャラ素質:52 = -1
				ELSE
					ランダムキャラ素質:52 = -2
				ENDIF
			CASEELSE
				;それ以外
				ランダムキャラ素質:52 = -1
		ENDSELECT
	CASE 4
		;ハーヴィン
		ランダムキャラ素質:52 = -3 + MAX(RAND:5 - 3, 0)
ENDSELECT
LOCALS += @"%GET_TALENTNAME(161,ランダムキャラ素質:52), 14, LEFT%"
LOCALS += "体型："
ランダムキャラ素質:54 = RAND:2 + RAND:2 - 1
SELECTCASE ランダムキャラ素質:0
	CASE 2
		ランダムキャラ素質:54 -= 1
	CASE 3
		ランダムキャラ素質:54 += 1
ENDSELECT
LOCALS += @"%GET_TALENTNAME(163,ランダムキャラ素質:54)%"
IF ランダムキャラ素質:1 == 3
	LOCALS += "<br>　男性器サイズ："
	LOCAL = RAND:100
	SELECTCASE LOCAL
		CASE 0 TO 4
			ランダムキャラ素質:53 = -2
		CASE 5 TO 19
			ランダムキャラ素質:53 = -1
		CASE 20 TO 74
			ランダムキャラ素質:53 = 0
		CASE 75 TO 89
			ランダムキャラ素質:53 = 1
		CASE 90 TO 99
			ランダムキャラ素質:53 = 2
	ENDSELECT
	IF ランダムキャラ素質:53 == 0
		LOCALS += "平凡"
	ELSE
		LOCALS += @"%GET_TALENTNAME(162,ランダムキャラ素質:53)%"
	ENDIF
ENDIF
LOCALS += "<br>"
LOCALS += "<br>"
$素質リロール_LOOP
;ランダムキャラ素質:10～29にランダムで素質
;6個～8個（うち2つは性格系の素質確定）
LOCAL:1 = RAND(6, 9)
FOR LOCAL, 0, 8
	SELECTCASE LOCAL
		CASE IS < 2
			ランダムキャラ素質:(LOCAL + 10) = ランダム素質候補_性格のみ()
		CASE IS < LOCAL:1
			ランダムキャラ素質:(LOCAL + 10) = ランダム素質候補()
		CASEELSE
			ランダムキャラ素質:(LOCAL + 10) = 0
	ENDSELECT
NEXT
FOR LOCAL, 0, 9
	IF ランダムキャラ素質:(LOCAL + 10) == 0
		重複チェック用:LOCAL = LOCAL * -10000
	ELSE
		重複チェック用:LOCAL = ランダム素質番号(ランダムキャラ素質:(LOCAL + 10))
	ENDIF
NEXT
IF NOSAMES(重複チェック用:0, 重複チェック用:1, 重複チェック用:2, 重複チェック用:3, 重複チェック用:4, 重複チェック用:5, 重複チェック用:6, 重複チェック用:7, 重複チェック用:8) == 0
	GOTO 素質リロール_LOOP
ENDIF
LOCALS += "□ 素質 □---------------------------------------------------------------<br>"
LOCALS:1 = 
FOR LOCAL, 0, 8
	SIF ランダムキャラ素質:(LOCAL + 10) == 0
		BREAK
	LOCALS += @"[%GET_TALENTNAME(ランダム素質番号(ランダムキャラ素質:(LOCAL + 10)), ランダム素質値(ランダムキャラ素質:(LOCAL + 10)))%]"
	LOCALS:1 += @"[%GET_TALENTNAME(ランダム素質番号(ランダムキャラ素質:(LOCAL + 10)), ランダム素質値(ランダムキャラ素質:(LOCAL + 10)))%]"
NEXT
;こっから敏感系
FOR LOCAL, 0, 5
	LOCAL:1 = RAND:10
	IF LOCAL:1 < 2
		;鈍感
		ランダムキャラ素質:(LOCAL + 20) = (LOCAL + 101) * -1
		LOCALS += @"[%GET_TALENTNAME(ABS(ランダムキャラ素質:(LOCAL + 20)), -1)%]"
		LOCALS:1 += @"[%GET_TALENTNAME(ABS(ランダムキャラ素質:(LOCAL + 20)), -1)%]"
	ELSEIF LOCAL:1 < 7
	ELSE
		;敏感
		ランダムキャラ素質:(LOCAL + 20) = LOCAL + 101
		LOCALS += @"[%GET_TALENTNAME(ランダムキャラ素質:(LOCAL + 20), 1)%]"
		LOCALS:1 += @"[%GET_TALENTNAME(ランダムキャラ素質:(LOCAL + 20), 1)%]"
	ENDIF
NEXT
IF RAND:100 == 0
	ランダムキャラ素質:26 = 1086
	LOCALS += "[元男]"
	LOCALS:1 += "[元男]"
ENDIF
IF ランダムキャラ素質:3 > 1 && RAND:10 == 0 && 素質切り替えOPTION記憶:陥没乳首_なし == 0
	ランダムキャラ素質:27 = 111
	LOCALS += "[陥没乳首]"
	LOCALS:1 += "[陥没乳首]"
ENDIF
IF ランダムキャラ素質:3 > 1 && RAND:10 == 0 && 素質切り替えOPTION記憶:母乳体質_なし == 0
	ランダムキャラ素質:28 = 149
	LOCALS += "[母乳体質]"
	LOCALS:1 += "[母乳体質]"
ENDIF
IF ランダムキャラ素質:1 == 3 && RAND:3 == 0
	ランダムキャラ素質:29 = 152
	LOCALS += "[絶倫]"
	LOCALS:1 += "[絶倫]"
ENDIF
IF ABS(ランダムキャラ素質:55) >= 10000
	LOCALS += @"[子持ち]"
	LOCALS:1 += @"[子持ち]"
	LOCAL = SIGN(ランダムキャラ素質:55) * (ABS(ランダムキャラ素質:55) % 10000)
ELSE
	LOCAL = ランダムキャラ素質:55
ENDIF
IF LOCAL != 0
	LOCALS += @"[%GET_TALENTNAME(234, LOCAL)%]"
	LOCALS:1 += @"[%GET_TALENTNAME(234, LOCAL)%]"
ENDIF
IF RAND:10 == 0
	STRDATA ランダムキャラ知識素質:1
		DATA 調合知識Lv1
		DATA 魔法知識Lv1
		DATA 機械知識Lv1
		DATA 錬金知識Lv1
		DATA 魔物知識Lv1
		DATA 絵師Lv1
		DATA 高貴Lv1
		DATA 釣り好きLv1
		DATA 踊り子Lv1
		DATA 歌唱技能Lv1
		DATA 料理人Lv1
	ENDDATA
	LOCALS += @"[%ランダムキャラ知識素質:1%]"
	LOCALS:1 += @"[%ランダムキャラ知識素質:1%]"
ENDIF
LOCALS += "<br>"
LOCALS += "<br>"
LOCALS += "□ 能力 □---------------------------------------------------------------<br>"
LOCAL:2 = 0
FOR LOCAL, 0, 6
	LOCAL:1 = RAND:30
	SELECTCASE LOCAL:1
		CASE 29
			ランダムキャラ素質:(LOCAL + 30) = 2
			LOCALS += @"%ABLNAME:LOCAL, 16%:2   "
			LOCAL:2 += 1
		CASE IS > 23
			ランダムキャラ素質:(LOCAL + 30) = 1
			LOCALS += @"%ABLNAME:LOCAL, 16%:1   "
			LOCAL:2 += 1
	ENDSELECT
	IF LOCAL:2 == 3
		LOCALS += "<br>"
		LOCAL:2 = 0
	ENDIF
NEXT
FOR LOCAL, 0, 4
	LOCAL:1 = RAND:30
	SELECTCASE LOCAL:1
		CASE 29
			ランダムキャラ素質:(LOCAL + 40) = 2
			LOCALS += @"%ABLNAME:(LOCAL + 10), 16%:2   "
			LOCAL:2 += 1
		CASE IS > 23
			ランダムキャラ素質:(LOCAL + 40) = 1
			LOCALS += @"%ABLNAME:(LOCAL + 10), 16%:1   "
			LOCAL:2 += 1
	ENDSELECT
	IF LOCAL:2 == 3
		LOCALS += "<br>"
		LOCAL:2 = 0
	ENDIF
NEXT
SIF LOCAL:2 != 0
	LOCALS += "<br>"
LOCALS += "<br>"
LOCALS += "□ 経験 □---------------------------------------------------------------<br>"
LOCAL:2 = 0
LOCAL:3 = 0
FOR LOCAL, 0, 5
	IF ランダムキャラ素質:(LOCAL + 30) == 2
		LOCALS += @"%EXPNAME:LOCAL, 16%:100  "
		LOCAL:2 += 1
	ELSEIF ランダムキャラ素質:(LOCAL + 30) == 1
		LOCALS += @"%EXPNAME:LOCAL, 16%:30  "
		LOCAL:2 += 1
	ENDIF
	IF LOCAL:2 == 3
		LOCALS += "<br>"
		LOCAL:2 = 0
	ENDIF
	IF ランダムキャラ素質:(LOCAL + 30) == 2
		LOCALS += @"%(EXPNAME:(LOCAL + 5)), 16%:20  "
		LOCAL:2 += 1
		LOCAL:3 += 10
	ELSEIF ランダムキャラ素質:(LOCAL + 30) == 1
		LOCALS += @"%(EXPNAME:(LOCAL + 5)), 16%:5   "
		LOCAL:2 += 1
		LOCAL:3 += 5
	ENDIF
	IF LOCAL:2 == 3
		LOCALS += "<br>"
		LOCAL:2 = 0
	ENDIF
NEXT
IF ABS(ランダムキャラ素質:55) >= 10000
	LOCALS += @"%(EXPNAME:30), 16%:{ABS(ランダムキャラ素質:55) / 10000}  "
	LOCAL:2 += 1
	IF LOCAL:2 == 3
		LOCALS += "<br>"
		LOCAL:2 = 0
	ENDIF
	LOCALS += @"%(EXPNAME:31), 16%:{ABS(ランダムキャラ素質:55) / 10000}  "
	LOCAL:2 += 1
	IF LOCAL:2 == 3
		LOCALS += "<br>"
		LOCAL:2 = 0
	ENDIF
ENDIF
LOCALS += @"%(EXPNAME:10), 16%:{LOCAL:3}"
LOCALS += "</div>"


HTML_PRINT LOCALS
FOR LOCAL, 0, 25
	PRINTL
NEXT
DRAWLINE

$INPUT_LOOP
INPUT

LOCAL = RESULT
SELECTCASE LOCAL
	CASE 100
		LOCAL:10 = 0
		FOR LOCAL, 0, 従業員キャラ上限
			IF 従業員部屋割り配列:LOCAL < 1
				LOCAL:10 = 1
				BREAK
			ENDIF
		NEXT
		IF LOCAL:10 == 0
			PRINTW 従業員部屋が一杯です。
			RETURN 0
		ENDIF
		PRINTFORMW [従業員]%ランダムキャラネーム%を雇用しました。
		FLAG:従業員数 += 1
		LOCAL:1 = CHARANUM
		ADDCHARA 999
		TALENT:(LOCAL:1):従業員 = 1
		NAME:(LOCAL:1) = [従業員]%ランダムキャラネーム%
		CALLNAME:(LOCAL:1) = %ランダムキャラネーム%
		TALENT:(LOCAL:1):200 = ランダムキャラ素質:0
		TALENT:(LOCAL:1):2 = ランダムキャラ素質:1
		IF ランダムキャラ素質:2
			TALENT:(LOCAL:1):0 = 1
			TALENT:(LOCAL:1):1 = 0
		ELSE
			TALENT:(LOCAL:1):0 = 0
			TALENT:(LOCAL:1):1 = 1
		ENDIF
		TALENT:(LOCAL:1):205 = ランダムキャラ素質:3
		SIF ランダムキャラ素質:4
			TALENT:(LOCAL:1):(ランダムキャラ素質:4) = 1
		SIF ランダムキャラ素質:5
			TALENT:(LOCAL:1):(ランダムキャラ素質:5) = 1
		FOR LOCAL, 10, 19
			SIF ランダムキャラ素質:LOCAL == 0
				CONTINUE
			TALENT:(LOCAL:1):(ランダム素質番号(ランダムキャラ素質:LOCAL)) = ランダム素質値(ランダムキャラ素質:LOCAL)
		NEXT
		FOR LOCAL, 20, 25
			SIF ランダムキャラ素質:LOCAL == 0
				CONTINUE
			TALENT:(LOCAL:1):(ランダム素質番号(ランダムキャラ素質:LOCAL)) = ランダム素質値(ランダムキャラ素質:LOCAL)
		NEXT
		IF ランダムキャラ素質:26
			TALENT:(LOCAL:1):86 = 2
		ENDIF
		IF ランダムキャラ素質:27
			TALENT:(LOCAL:1):111 = 1
		ENDIF
		IF ランダムキャラ素質:28
			TALENT:(LOCAL:1):149 = 1
		ENDIF
		IF ランダムキャラ素質:29
			TALENT:(LOCAL:1):152 = 1
		ENDIF
		FOR LOCAL, 30, 36
			ABL:(LOCAL:1):(LOCAL - 30) = ランダムキャラ素質:LOCAL
			IF ランダムキャラ素質:LOCAL == 1
				EXP:(LOCAL:1):(LOCAL - 30) = ランダムキャラ素質:LOCAL * 20
				EXP:(LOCAL:1):(LOCAL - 25) = ランダムキャラ素質:LOCAL * 5
			ELSEIF ランダムキャラ素質:LOCAL == 2
				EXP:(LOCAL:1):(LOCAL - 30) = ランダムキャラ素質:LOCAL * 50
				EXP:(LOCAL:1):(LOCAL - 25) = ランダムキャラ素質:LOCAL * 15
			ENDIF
		NEXT
		FOR LOCAL, 40, 46
			IF ランダムキャラ素質:LOCAL
				ABL:(LOCAL:1):(LOCAL - 30) = ランダムキャラ素質:LOCAL
			ENDIF
		NEXT
		EXP:(LOCAL:1):10 = LOCAL:3
		TALENT:(LOCAL:1):容姿 = ランダムキャラ素質:50
		TALENT:(LOCAL:1):体格 = ランダムキャラ素質:52
		TALENT:(LOCAL:1):男性器サイズ = ランダムキャラ素質:53
		TALENT:(LOCAL:1):体型 = ランダムキャラ素質:54
		TALENT:(LOCAL:1):年齢 = ランダムキャラ素質:51
		TALENT:(LOCAL:1):恋人持ち = SIGN(ランダムキャラ素質:55) * (ABS(ランダムキャラ素質:55) % 10000)
		IF ABS(ランダムキャラ素質:55) >= 10000
			TALENT:(LOCAL:1):子持ち = 1
			EXP:(LOCAL:1):妊娠経験 = ABS(ランダムキャラ素質:55) / 10000
			EXP:(LOCAL:1):出産経験 = ABS(ランダムキャラ素質:55) / 10000
		ENDIF
		;性知識はここに含まれる
		CSTR:(LOCAL:1):知識素質設定 '= @" %STRJOIN(ランダムキャラ知識素質, " ")% "
		STRDATA CSTR:(LOCAL:1):プレゼント好み
			DATA 食べ物
			DATA 宝石
			DATA アクセサリ
			DATA 実用品
			DATA 本
			DATA 花
		ENDDATA
		SIF TALENT:(LOCAL:1):飲兵衛 > 0
			CSTR:(LOCAL:1):プレゼント好み += "_酒"

		;戦闘関連設定
		CSTR:(LOCAL:1):戦闘基礎ステータス設定 =  属性_%属性数値文字列変換(ランダムキャラ戦闘タイプ:0)% 得意武器_%得意武器数値文字列変換(ランダムキャラ戦闘タイプ:1)% 初期ステータスタイプ_%成長タイプ:(ランダムキャラ戦闘タイプ:2)%

		CALL コマンド存在チェック
		CALL キャラクター初期化(LOCAL:1)

		CFLAG:(LOCAL:1):滞在期間 = 999
		CFLAG:(LOCAL:1):招待不可フラグ = 0
		CALL ステータス記録(LOCAL:1)

		FOR LOCAL:2, 0, 従業員キャラ上限
			IF 従業員部屋割り配列:(LOCAL:2) < 1
				従業員部屋割り配列:(LOCAL:2) = LOCAL:1
				BREAK
			ENDIF
		NEXT
		CALL 従業員人数チェック()
		RETURN 0
	CASE 110
		GOTO 従業員_LOOP
	CASE 999
		RETURN 0
	CASEELSE
ENDSELECT



@ランダムキャラ名前生成(ARG, ARG:1 = 0)
#FUNCTIONS
#DIM C_ID
#DIM 重複フラグ

FOR LOCAL, 0, 200
	SELECTCASE ARG
		CASE 2
			LOCALS '= ランダムキャラ名_エルーン()
		CASE 3
			LOCALS '= ランダムキャラ名_ドラフ()
		CASE 4
			LOCALS '= ランダムキャラ名_ハーヴィン()
		CASEELSE
			LOCALS '= ランダムキャラ名()
	ENDSELECT
	;100回重複した場合または男性の場合、完全ランダム作成に切り替える
	SIF LOCAL > 99 || ARG:1
		LOCALS '= ランダムキャラ名ジェネレータ(ARG, ARG:1)
	;200回重複した場合さすがに諦める
	SIF LOCAL == 199
		BREAK

	;重複チェック
	重複フラグ = 0
	FOR C_ID, 0, CHARANUM
		IF CALLNAME:C_ID == LOCALS
			重複フラグ = 1
			BREAK
		ENDIF
	NEXT
	SIF !重複フラグ
		BREAK
NEXT
RETURNF LOCALS


@ランダム素質候補_性格のみ
#FUNCTION
{
	#DIM CONST 素質数字配列 =
		-10,
		10,
		-11,
		11,
		-12,
		12,
		-13,
		13,
		14,
		-17,
		17,
		-10018,
		-18,
		18,
		-19,
		19,
		10019,
		-20,
		20,
		10020,
		21,
		22,
		-23,
		23,
		-24,
		24,
		26,
		-33,
		33,
		62,
		-229,
		229,
		-231,
		231,
		-10232,
		-232,
		232
}
;37種
RETURNF 素質数字配列:(RAND:37)

@ランダム素質候補
#FUNCTION
{
	#DIM CONST 素質数字配列 =
		-10,
		10,
		-11,
		11,
		-12,
		12,
		-13,
		13,
		14,
		-15,
		15,
		-16,
		16,
		-17,
		17,
		-10018,
		-18,
		18,
		-19,
		19,
		10019,
		-20,
		20,
		10020,
		21,
		22,
		-23,
		23,
		-24,
		24,
		25,
		26,
		-27,
		27,
		-30,
		30,
		-33,
		33,
		-50,
		50,
		10050,
		51,
		52,
		60,
		-10061,
		-61,
		61,
		10061,
		62,
		-70,
		70,
		77,
		10077,
		80,
		-81,
		81,
		10081,
		20081,
		-110,
		110,
		-130,
		130,
		135,
		-220,
		220,
		-221,
		221,
		222,
		223,
		-224,
		224,
		-225,
		225,
		-226,
		226,
		10226,
		-229,
		229,
		230,
		-231,
		231,
		-10232,
		-232,
		232
}
;84種

RETURNF 素質数字配列:(RAND:84)

@ランダム素質番号(ARG)
#FUNCTION
RETURNF ABS(ARG) % 10000

@ランダム素質値(ARG)
#FUNCTION
RETURNF SIGN(ARG) * (ABS(ARG) / 10000 + 1)
