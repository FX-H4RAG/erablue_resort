
@性癖開拓処理
#DIM 表示候補, 200
#DIM 現在ページ
#DIM 開拓対象
#DIM 複数名フラグ
#DIM キャラ番号
#DIM 性癖番号
#DIMS 複数登録キャラ
#DIMS 複数登録配列, 100

VARSET 表示候補, -1
現在ページ = 0
複数名フラグ = 0
複数登録キャラ = _

DRAWLINE
PRINTL 図書室の蔵書から該当するエロ本が任意のキャラに渡るよう細工し、キャラの性癖を開拓します
PRINTL 誰の性癖を開拓しますか？
DRAWLINE
PRINTBUTTON "[1000] 複数名一括開拓", 1000
PRINTL

;表示キャラを登録
LOCAL:1 = 0
FOR LOCAL, 1, CHARANUM
	;非滞在は出さない
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	表示候補:(LOCAL:1) = LOCAL
	LOCAL:1 += 1
NEXT

$表示_LOOP
DRAWLINE
LOCAL:1 = 0
;キャラの表示（15人まで）
FOR LOCAL, (現在ページ * 15), ((現在ページ * 15) + 15)
	;登録が終わったら表示終了
	IF 表示候補:LOCAL == -1
		LOCAL:1 = 1
		BREAK
	ENDIF
	PRINTBUTTON @"[{LOCAL}]%NAME:(表示候補:LOCAL)%", LOCAL
	PRINTL 
NEXT

DRAWLINE

;ページめくり表示
IF 現在ページ > 0
	PRINTBUTTONLC "[900]前ページへ", 900
ELSE
	PRINTLC 　
ENDIF
PRINTFORM - {現在ページ + 1} -
IF LOCAL:1 == 0
	PRINTBUTTONC "[901]次ページへ", 901
ENDIF
PRINTL 
PRINTBUTTON "[999]戻る", 999

$INPUT_LOOP
INPUT

;広告対象のINPUT
SELECTCASE RESULT
	CASE 900
		現在ページ = MAX(現在ページ - 1, 0)
		GOTO 表示_LOOP
	CASE 901
		SIF LOCAL:1 == 0
			現在ページ += 1
		GOTO 表示_LOOP
	CASE 999
		RETURN -1
	CASE 1000
		複数名フラグ = 1
	CASE IS >= CHARANUM, IS < 0
		REUSELASTLINE 
		GOTO INPUT_LOOP
	CASEELSE
		IF 表示候補:RESULT < 0
			REUSELASTLINE 
			GOTO INPUT_LOOP
		ENDIF
		開拓対象 = 表示候補:RESULT
ENDSELECT

IF 複数名フラグ
	$性癖表示LOOP
	現在ページ = 0
	DRAWLINE
	PRINTFORML 複数名の一括性癖開拓を行います
	PRINTL どの性癖を開拓しますか？
	DRAWLINE

	FOR LOCAL, 1, 100
		IF ERDNAME(性癖素質, LOCAL) != ""
			PRINTBUTTON @"[{LOCAL}]%ERDNAME(性癖素質, LOCAL)%", LOCAL
			PRINTL
		ENDIF
	NEXT
	PRINTL
	PRINTBUTTON "[999]戻る", 999
	PRINTL
	BINPUT
	性癖番号 = RESULT
	SELECTCASE 性癖番号
		CASE 1 TO 100
			;表示キャラを登録
			LOCAL:1 = 0
			FOR キャラ番号, 1, CHARANUM
				;非滞在は出さない
				SIF CFLAG:キャラ番号:滞在期間 < 0
					CONTINUE
				;すでに性癖持ってるならスキップ
				SIF 性癖素質:キャラ番号:性癖番号
					CONTINUE
				表示候補:(LOCAL:1) = キャラ番号
				LOCAL:1 += 1
			NEXT

			$表示_LOOP2
			DRAWLINE
			PRINTFORML 複数名の一括性癖開拓を行います
			PRINTL どのキャラの性癖を開拓しますか？（100名まで）
			DRAWLINE
			PRINTBUTTON "[1000] これで確定する", 1000
			PRINTL
			DRAWLINE
			LOCAL:1 = 0
			;キャラの表示（15人まで）
			FOR LOCAL, (現在ページ * 15), ((現在ページ * 15) + 15)
				;登録が終わったら表示終了
				IF 表示候補:LOCAL == -1
					LOCAL:1 = 1
					BREAK
				ENDIF
				SIF STRFIND(複数登録キャラ, @"_{表示候補:LOCAL}_") > -1
					SETCOLOR カラーパレット("黄")
				PRINTBUTTON @"[{LOCAL}]%NAME:(表示候補:LOCAL)%", LOCAL
				RESETCOLOR
				PRINTL 
			NEXT

			DRAWLINE

			;ページめくり表示
			IF 現在ページ > 0
				PRINTBUTTONLC "[900]前ページへ", 900
			ELSE
				PRINTLC 　
			ENDIF
			PRINTFORM - {現在ページ + 1} -
			IF LOCAL:1 == 0
				PRINTBUTTONC "[901]次ページへ", 901
			ENDIF
			PRINTL 
			PRINTBUTTON "[999]戻る", 999
			BINPUT

			;広告対象のINPUT
			SELECTCASE RESULT
				CASE 900
					現在ページ = MAX(現在ページ - 1, 0)
					GOTO 表示_LOOP2
				CASE 901
					SIF LOCAL:1 == 0
						現在ページ += 1
					GOTO 表示_LOOP2
				CASE 999
					GOTO 性癖表示LOOP
				CASE 1000
					VARSET 複数登録配列
					SPLIT 複数登録キャラ, "__", 複数登録配列
					FOR LOCAL, 1, 100
						SIF 複数登録配列:LOCAL == ""
							BREAK
						REPLACE 複数登録配列:LOCAL, "_", ""
						開拓対象 = TOINT(RESULTS)
						性癖素質:開拓対象:性癖開発用フラグ変数 = 性癖番号
						PRINTFORML %CALLNAME:開拓対象%に%ERDNAME(性癖素質, 性癖番号)%のエロ本が渡るように細工を行います
					NEXT
					PRINTW ※結果は一日の終了時に表示されます
				CASEELSE
					LOCALS = _{表示候補:RESULT}_
					IF STRFIND(複数登録キャラ, LOCALS) > -1
						REPLACE 複数登録キャラ, LOCALS, ""
						複数登録キャラ = %RESULTS%
					ELSE
						複数登録キャラ += @"%LOCALS%"
					ENDIF
					GOTO 表示_LOOP2
			ENDSELECT
		CASE 999
			GOTO 表示_LOOP
	ENDSELECT
ELSE
	DRAWLINE
	PRINTFORML %CALLNAME:開拓対象%の性癖開拓を行います
	PRINTL どの性癖を開拓しますか？
	DRAWLINE

	FOR LOCAL, 1, 100
		IF ERDNAME(性癖素質, LOCAL) != ""
			;すでに性癖持ってるならスキップ
			SIF 性癖素質:開拓対象:LOCAL
				CONTINUE
			PRINTBUTTON @"[{LOCAL}]%ERDNAME(性癖素質, LOCAL)%", LOCAL
			PRINTL
		ENDIF
	NEXT

	PRINTL
	PRINTBUTTON "[999]戻る", 999
	PRINTL
	BINPUT
	LOCAL = RESULT
	SELECTCASE LOCAL
		CASE 1 TO 100
			PRINTFORML %CALLNAME:開拓対象%に%ERDNAME(性癖素質, LOCAL)%のエロ本が渡るように細工を行います
			PRINTW ※結果は一日の終了時に表示されます
			性癖素質:開拓対象:性癖開発用フラグ変数 = LOCAL
		CASE 999
			GOTO 表示_LOOP
	ENDSELECT
ENDIF

@性癖開拓チャレンジ処理_1(ARG)
;野外オナニー
PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:ARG%にさりげなく「外で自慰をするシチュ」のエロ本が渡るように小細工した…
PRINTL

;エロ本を読むかどうか
IF 知識素質:ARG:性知識 < 0
	IF EXP:ARG:自慰経験 < 1
		;性知識が-1か-2、かつ自慰経験がないと本の内容を理解できない
		PRINTFORMW %CALLNAME:ARG%はいつの間にか近くにあったエロ本をぱらぱらと開いてみたが、よく意味がわからなかったようだ…
		RETURN -1
	ELSEIF CFLAG:ARG:オナニー覚えたて
		;オナニー覚えたてだと確実に読む
		PRINTFORML …%CALLNAME:ARG%はいつの間にか近くにあったエロ本を見つけた。
		PRINTFORML ぱらぱらと本をめくってみると、最近覚えたばかりのオナニーのことが書かれていることに気づいた。
		PRINTFORMW 外で隠れてやると、オナニーはもっと気持ちいいらしい……
		PRINTL
		PRINTFORMW %CALLNAME:ARG%は性癖素質[野外オナニー]を取得した
		性癖素質:ARG:野外オナニー = 1
		RETURN 1
	ENDIF
ENDIF

;基礎確率は2/10
LOCAL = 2
;自制心、性的興味、性欲が影響
LOCAL -= TALENT:ARG:自制心
LOCAL += TALENT:ARG:性的興味 * 2
LOCAL += TALENT:ARG:性欲
SIF BASE:ARG:性欲 > 1500
	LOCAL += 1
;露出癖が影響
LOCAL += ABL:ARG:露出癖

;自慰しやすいが影響
LOCAL += TALENT:ARG:自慰しやすい * 2
;最低確率1割は確保
LOCAL = MAX(LOCAL, 1)
;補正確率より低ければ読む
IF RAND:10 < LOCAL
	;読むパターン
	PRINTFORML …%CALLNAME:ARG%はいつの間にか近くにあったエロ本を見つけた。
	IF 知識素質:ARG:性知識 < 0
		PRINTFORML ぱらぱらと本をめくってみると、この前覚えたオナニーのことが書かれている本であることに気づく。
		PRINTFORMW 外で隠れてやると、オナニーはもっと気持ちいいらしい……
	ELSE
		PRINTFORMW 周りをきょろきょろと見回し自分ひとりであることを確認すると、%CALLNAME:ARG%はこっそりとその本を開いた……
	ENDIF
	PRINTL
	PRINTFORMW %CALLNAME:ARG%は性癖素質[野外オナニー]を取得した
	性癖素質:ARG:野外オナニー = 1
	RETURN 1
ELSE
	;読まないパターン
	PRINTFORMW %CALLNAME:ARG%はいつの間にか近くにあったエロ本に訝しげな顔をして、ゴミ箱へと投げ捨てた
ENDIF

RETURN 0

@性癖開拓チャレンジ処理_2(ARG)
;逆アナル
PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:ARG%にさりげなく「男性の尻穴になにかを挿入するシチュ」のエロ本が渡るように小細工した…
PRINTL

;エロ本を読むかどうか
IF 知識素質:ARG:性知識 < 0
	;性知識が-1か-2だと本の内容を理解できない
	PRINTFORMW %CALLNAME:ARG%はいつの間にか近くにあったエロ本をぱらぱらと開いてみたが、よく意味がわからなかったようだ…
	RETURN -1
ENDIF

;基礎確率は2/10
LOCAL = 2
;自制心、性的興味、性欲が影響
LOCAL -= TALENT:ARG:自制心
LOCAL += TALENT:ARG:性的興味 * 2
LOCAL += TALENT:ARG:性欲
SIF BASE:ARG:性欲 > 1500
	LOCAL += 1
;サド性感が影響
LOCAL += ABL:ARG:サド性感

;ふたなりだと影響大
SIF TALENT:ARG:性別 == 3
	LOCAL += 3
;最低確率1割は確保
LOCAL = MAX(LOCAL, 1)
;補正確率より低ければ読む
IF RAND:10 < LOCAL
	;読むパターン
	PRINTFORML …%CALLNAME:ARG%はいつの間にか近くにあったエロ本を見つけた。
	PRINTFORMW 周りをきょろきょろと見回し自分ひとりであることを確認すると、%CALLNAME:ARG%はこっそりとその本を開いた……
	PRINTL
	PRINTFORMW %CALLNAME:ARG%は性癖素質[逆アナル]を取得した
	性癖素質:ARG:逆アナル = 1
	RETURN 1
ELSE
	;読まないパターン
	PRINTFORMW %CALLNAME:ARG%はいつの間にか近くにあったエロ本に訝しげな顔をして、ゴミ箱へと投げ捨てた
ENDIF

RETURN 0


@性癖開拓チャレンジ処理_3(ARG)
;搾乳プレイ
PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:ARG%にさりげなく「母乳を絞られて絶頂するシチュ」のエロ本が渡るように小細工した…
PRINTL

;エロ本を読むかどうか
IF 知識素質:ARG:性知識 < 0
	;性知識が-1か-2だと本の内容を理解できない
	PRINTFORMW %CALLNAME:ARG%はいつの間にか近くにあったエロ本をぱらぱらと開いてみたが、よく意味がわからなかったようだ…
	RETURN -1
ENDIF

;基礎確率は2/10
LOCAL = 2
;自制心、性的興味、性欲が影響
LOCAL -= TALENT:ARG:自制心
LOCAL += TALENT:ARG:性的興味 * 2
LOCAL += TALENT:ARG:性欲
SIF BASE:ARG:性欲 > 1500
	LOCAL += 1

;バストサイズが影響
IF TALENT:ARG:バストサイズ > 2
	LOCAL += 1
ENDIF
;母乳体質でないならガッツリマイナス
IF TALENT:ARG:母乳体質 == 0
	LOCAL -= 5
ENDIF
;最低確率1割は確保
LOCAL = MAX(LOCAL, 1)
;補正確率より低ければ読む
IF RAND:10 < LOCAL
	;読むパターン
	PRINTFORML …%CALLNAME:ARG%はいつの間にか近くにあったエロ本を見つけた。
	PRINTFORMW 周りをきょろきょろと見回し自分ひとりであることを確認すると、%CALLNAME:ARG%はこっそりとその本を開いた……
	PRINTL
	PRINTFORMW %CALLNAME:ARG%は性癖素質[搾乳プレイ]を取得した
	性癖素質:ARG:搾乳プレイ = 1
	RETURN 1
ELSE
	;読まないパターン
	PRINTFORMW %CALLNAME:ARG%はいつの間にか近くにあったエロ本に訝しげな顔をして、ゴミ箱へと投げ捨てた
ENDIF

RETURN 0

@性癖開拓チャレンジ処理_4(ARG)
;触手プレイ
PRINTFORML %CALLNAME:PLAYER%は%CALLNAME:ARG%にさりげなく「触手に蹂躙されてイキまくるシチュ」のエロ本が渡るように小細工した…
PRINTL

;エロ本を読むかどうか
IF 知識素質:ARG:性知識 < 0
	;性知識が-1か-2だと本の内容を理解できない
	PRINTFORMW %CALLNAME:ARG%はいつの間にか近くにあったエロ本をぱらぱらと開いてみたが、よく意味がわからなかったようだ…
	RETURN -1
ENDIF

;基礎確率は2/10
LOCAL = 2
;自制心、性的興味、性欲が影響
LOCAL -= TALENT:ARG:自制心
LOCAL += TALENT:ARG:性的興味 * 2
LOCAL += TALENT:ARG:性欲
SIF BASE:ARG:性欲 > 1500
	LOCAL += 1

;性知識が低いとマイナス
LOCAL = LOCAL - (3 - 知識素質:ARG:性知識)

;最低確率1割は確保
LOCAL = MAX(LOCAL, 1)
;補正確率より低ければ読む
IF RAND:10 < LOCAL
	;読むパターン
	PRINTFORML …%CALLNAME:ARG%はいつの間にか近くにあったエロ本を見つけた。
	PRINTFORMW 周りをきょろきょろと見回し自分ひとりであることを確認すると、%CALLNAME:ARG%はこっそりとその本を開いた……
	PRINTL
	PRINTFORMW %CALLNAME:ARG%は性癖素質[触手プレイ]を取得した
	性癖素質:ARG:触手プレイ = 1
	RETURN 1
ELSE
	;読まないパターン
	PRINTFORMW %CALLNAME:ARG%はいつの間にか近くにあったエロ本に訝しげな顔をして、ゴミ箱へと投げ捨てた
ENDIF

RETURN 0
