
@性癖開拓処理
#DIM DYNAMIC 表示候補, 200
#DIM 現在ページ
#DIM 開拓対象
#DIM 複数名フラグ
#DIM キャラ番号
#DIM 性癖番号
#DIM スキップフラグ
#DIM 開拓性癖ループ
#DIMS 複数登録キャラ
#DIMS DYNAMIC 複数登録配列, 100
#DIM DYNAMIC 開拓性癖配列, 100

VARSET 表示候補, -1
現在ページ = 0
複数名フラグ = 0
複数登録キャラ = _

;表示キャラを登録
LOCAL:1 = 0
FOR LOCAL, 1, CHARANUM
	;非滞在は出さない
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	表示候補:(LOCAL:1) = LOCAL
	LOCAL:1 += 1
NEXT

$表示_LOOP

DRAWLINE
PRINTL 図書室の蔵書から該当するエロ本が任意のキャラに渡るよう細工し、キャラの性癖を開拓します
PRINTL 誰の性癖を開拓しますか？
DRAWLINE
PRINTBUTTON "[1000] 複数名一括開拓", 1000
PRINTL
DRAWLINE
LOCAL:1 = 0
;キャラの表示（15人まで）
FOR LOCAL, (現在ページ * 15), ((現在ページ * 15) + 15)
	;登録が終わったら表示終了
	IF 表示候補:LOCAL == -1
		LOCAL:1 = 1
		BREAK
	ENDIF
	LOCALS = %ADD_STR_SPACE(NAME:(表示候補:LOCAL), "]"), 40, LEFT%
	IF 性癖素質:(表示候補:LOCAL):性癖開発用フラグ変数 == 0
		LOCALS += "現在の性癖開拓：なし"
	ELSE
		LOCALS += @"現在の性癖開拓：%GET_性癖素質NAME(性癖素質:(表示候補:LOCAL):性癖開発用フラグ変数, 表示候補:LOCAL)%"
	ENDIF
	PRINTBUTTON @"[{LOCAL, 3}] %LOCALS%", LOCAL
	PRINTL 
NEXT

DRAWLINE

;ページめくり表示
IF 現在ページ > 0
	PRINTBUTTONLC "[900]前ページへ", 900
ELSE
	PRINTLC 　
ENDIF
PRINTFORM - {現在ページ + 1} -
IF LOCAL:1 == 0
	PRINTBUTTONC "[901]次ページへ", 901
ENDIF
PRINTL 
PRINTBUTTON "[998]システムの説明", 998
PRINT 　　
PRINTBUTTON "[999]戻る", 999

IF チュートリアルフラグ:性癖開拓 == 0
	RESULT = 998
ELSE
	BINPUT
ENDIF
;広告対象のINPUT
SELECTCASE RESULT
	CASE 900
		現在ページ = MAX(現在ページ - 1, 0)
		GOTO 表示_LOOP
	CASE 901
		SIF LOCAL:1 == 0
			現在ページ += 1
		GOTO 表示_LOOP
	CASE 999
		RETURN -1
	CASE 998
		PRINTL
		DRAWLINE
		PRINTL ［性癖開拓システム：チュートリアル］
		DRAWLINE
		HTML_PRINT "<img src='era_顔' height='900' width='900'>"
		PRINTL 
		PRINTL 
		PRINTL 
		PRINTL 
		PRINTL 
		PRINTL 
		PRINTL 
		CALL 性癖開拓システムチュートリアル
		DRAWLINE
		RESTART
	CASE 1000
		複数名フラグ = 1
	CASEELSE
		開拓対象 = 表示候補:RESULT
ENDSELECT

IF 複数名フラグ
	VARSET 開拓性癖配列
	$性癖表示LOOP
	VARSET 表示候補, -1
	現在ページ = 0
	DRAWLINE
	PRINTFORML 複数名の一括性癖開拓を行います
	PRINTL どの性癖を開拓しますか？
	PRINTL ※優先順位を付けて複数選択可能です。
	PRINTL 　選んだキャラに対して、０の性癖を取得済みなら１を、１の性癖を取得済みなら２を、
	PRINTL 　のように本が配られます。
	DRAWLINE

	FOR LOCAL, 1, 100
		IF ERDNAME(性癖素質, LOCAL) != ""
			IF ENUMFUNCBEGINSWITH(@"性癖開拓チャレンジ処理_%ERDNAME(性癖素質, LOCAL)%")
				IF MATCH(開拓性癖配列, LOCAL)
					SETCOLOR カラーパレット("黄")
					PRINTBUTTON @"[{LOCAL, 2}]%GET_性癖素質NAME(LOCAL)%：%TOFULL(TOSTR(FINDELEMENT(開拓性癖配列, LOCAL)))%", LOCAL
					RESETCOLOR
				ELSE
					PRINTBUTTON @"[{LOCAL, 2}]%GET_性癖素質NAME(LOCAL)%", LOCAL
				ENDIF
				PRINTL
			ENDIF
		ENDIF
	NEXT
	PRINTL
	PRINTBUTTON "[998]この優先順位で開拓する", 998
	PRINTL
	PRINTBUTTON "[999]戻る", 999
	PRINTL
	BINPUT
	性癖番号 = RESULT
	SELECTCASE 性癖番号
		CASE 1 TO 100
			IF MATCH(開拓性癖配列, 性癖番号)
				ARRAYREMOVE 開拓性癖配列, FINDELEMENT(開拓性癖配列, 性癖番号), 1
			ELSE
				開拓性癖配列:(FINDELEMENT(開拓性癖配列, 0)) = 性癖番号
			ENDIF
			GOTO 性癖表示LOOP
		CASE 998
			;表示キャラを登録
			LOCAL:1 = 0
			FOR キャラ番号, 1, CHARANUM
				;非滞在は出さない
				SIF CFLAG:キャラ番号:滞在期間 < 0
					CONTINUE
				スキップフラグ = 1
				FOR 開拓性癖ループ, 0, 100
					SIF 開拓性癖配列:開拓性癖ループ == 0
						BREAK
					;すでに性癖持ってるならスキップ
					SIF 性癖素質:キャラ番号:(開拓性癖配列:開拓性癖ループ)
						CONTINUE
					;名前の設定されていない性癖は取れない
					SIF GET_性癖素質NAME(開拓性癖配列:開拓性癖ループ, キャラ番号) == ""
						CONTINUE
					スキップフラグ = 0
				NEXT
				SIF スキップフラグ
					CONTINUE
				表示候補:(LOCAL:1) = キャラ番号
				LOCAL:1 += 1
			NEXT

			$表示_LOOP2
			DRAWLINE
			PRINTFORML 複数名の一括性癖開拓を行います
			PRINTL どのキャラの性癖を開拓しますか？（100名まで）
			DRAWLINE
			PRINTBUTTON "[1001] 上から100名開拓候補に入れる", 1001
			PRINTL
			DRAWLINE
			LOCAL:1 = 0
			;キャラの表示（15人まで）
			FOR LOCAL, (現在ページ * 15), ((現在ページ * 15) + 15)
				;登録が終わったら表示終了
				IF 表示候補:LOCAL == -1
					LOCAL:1 = 1
					BREAK
				ENDIF
				SIF STRFIND(複数登録キャラ, @"_{表示候補:LOCAL}_") > -1
					SETCOLOR カラーパレット("黄")
				LOCALS = %ADD_STR_SPACE(NAME:(表示候補:LOCAL), "]"), 40, LEFT%
				IF 性癖素質:(表示候補:LOCAL):性癖開発用フラグ変数 == 0
					LOCALS += "現在の性癖開拓：なし"
				ELSE
					LOCALS += @"現在の性癖開拓：%GET_性癖素質NAME(性癖素質:(表示候補:LOCAL):性癖開発用フラグ変数, 表示候補:LOCAL)%"
				ENDIF
				PRINTBUTTON @"[{LOCAL, 3}] %LOCALS%", LOCAL
				RESETCOLOR
				PRINTL 
			NEXT

			DRAWLINE

			;ページめくり表示
			IF 現在ページ > 0
				PRINTBUTTONLC "[900]前ページへ", 900
			ELSE
				PRINTLC 　
			ENDIF
			PRINTFORM - {現在ページ + 1} -
			IF LOCAL:1 == 0
				PRINTBUTTONC "[901]次ページへ", 901
			ENDIF
			PRINTL 
			DRAWLINE
			PRINTBUTTON "[1000] これで確定する", 1000
			PRINTL
			PRINTBUTTON "[999]戻る", 999
			BINPUT

			;広告対象のINPUT
			SELECTCASE RESULT
				CASE 900
					現在ページ = MAX(現在ページ - 1, 0)
					GOTO 表示_LOOP2
				CASE 901
					SIF LOCAL:1 == 0
						現在ページ += 1
					GOTO 表示_LOOP2
				CASE 999
					GOTO 性癖表示LOOP
				CASE 1000
					VARSET 複数登録配列
					SPLIT 複数登録キャラ, "_", 複数登録配列
					FOR LOCAL, 1, 100
						SIF 複数登録配列:LOCAL == ""
							BREAK
						REPLACE 複数登録配列:LOCAL, "_", ""
						開拓対象 = TOINT(RESULTS)
						FOR 開拓性癖ループ, 0, 100
							SIF 開拓性癖配列:開拓性癖ループ == 0
								BREAK
							;すでに性癖持ってるならスキップ
							SIF 性癖素質:開拓対象:(開拓性癖配列:開拓性癖ループ)
								CONTINUE
							;名前の設定されていない性癖は取れない
							SIF GET_性癖素質NAME(開拓性癖配列:開拓性癖ループ, 開拓対象) == ""
								CONTINUE

							性癖素質:開拓対象:性癖開発用フラグ変数 = 開拓性癖配列:開拓性癖ループ
							PRINTFORML %CALLNAME:開拓対象%に%GET_性癖素質NAME(開拓性癖配列:開拓性癖ループ, 開拓対象)%のエロ本が渡るように細工を行います
							BREAK
						NEXT
					NEXT
					PRINTW ※結果は一日の終了時に表示されます
				CASE 1001
					複数登録キャラ = _
					FOR LOCAL, 0, 100
						SIF 表示候補:LOCAL < 0
							BREAK
						複数登録キャラ += @"{表示候補:LOCAL}_"
					NEXT
					GOTO 表示_LOOP2
				CASEELSE
					LOCALS = _{表示候補:RESULT}_
					IF STRFIND(複数登録キャラ, LOCALS) > -1
						REPLACE 複数登録キャラ, LOCALS, "_"
						複数登録キャラ = %RESULTS%
					ELSE
						IF STRCOUNT(複数登録キャラ, "_") < 99
							複数登録キャラ += @"{表示候補:RESULT}_"
						ENDIF
					ENDIF
					GOTO 表示_LOOP2
			ENDSELECT
		CASE 999
			複数登録キャラ = 
			RESTART
	ENDSELECT
ELSE
	DRAWLINE
	PRINTFORML %CALLNAME:開拓対象%の性癖開拓を行います
	PRINTL どの性癖を開拓しますか？
	DRAWLINE

	FOR LOCAL, 1, 100
		IF ERDNAME(性癖素質, LOCAL) != ""
			;すでに性癖持ってるならスキップ
			SIF 性癖素質:開拓対象:LOCAL
				CONTINUE
			;名前の設定されていない性癖は取れない
			SIF GET_性癖素質NAME(LOCAL, 開拓対象) == ""
				CONTINUE
			IF ENUMFUNCBEGINSWITH(@"性癖開拓チャレンジ処理_%ERDNAME(性癖素質, LOCAL)%")
				PRINTBUTTON @"[{LOCAL}]%GET_性癖素質NAME(LOCAL, 開拓対象)%", LOCAL
				PRINTL
			ENDIF
		ENDIF
	NEXT

	PRINTL
	PRINTBUTTON "[999]戻る", 999
	PRINTL
	BINPUT
	LOCAL = RESULT
	SELECTCASE LOCAL
		CASE 1 TO 100
			PRINTFORML %CALLNAME:開拓対象%に%GET_性癖素質NAME(LOCAL, 開拓対象)%のエロ本が渡るように細工を行います
			PRINTW ※結果は一日の終了時に表示されます
			性癖素質:開拓対象:性癖開発用フラグ変数 = LOCAL
		CASE 999
			GOTO 表示_LOOP
	ENDSELECT
ENDIF

@性癖開拓チャレンジ処理_野外オナニー(ARG)
;野外オナニー
CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:PLAYER%は%CALLNAME:ARG%にさりげなく「外で自慰をするシチュ」のエロ本が渡るように小細工した…<br><br>"

;エロ本を読むかどうか
IF 知識素質:ARG:性知識 < 0
	IF EXP:ARG:自慰経験 < 1
		;性知識が-1か-2、かつ自慰経験がないと本の内容を理解できない
		CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%はいつの間にか近くにあったエロ本をぱらぱらと開いてみたが、よく意味がわからなかったようだ…<br>"
		RETURN -1
	ELSEIF CFLAG:ARG:オナニー覚えたて
		;オナニー覚えたてだと確実に読む
		CSTR:ARG:性癖開拓報告_記録文字列 += @"…%CALLNAME:ARG%はいつの間にか近くにあったエロ本を見つけた。<br>"
		CSTR:ARG:性癖開拓報告_記録文字列 += @" ぱらぱらと本をめくってみると、最近覚えたばかりのオナニーのことが書かれていることに気づいた。<br>"
		CSTR:ARG:性癖開拓報告_記録文字列 += @"外で隠れてやると、オナニーはもっと気持ちいいらしい……<br>"
		CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
		CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%は性癖素質[野外オナニー(興味)]を取得した<br>"
		性癖素質:ARG:野外オナニー = 1
		CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[野外オナニー(興味)]を取得した</font>","うふふ")
		RETURN 1
	ENDIF
ENDIF

;基礎確率は2/10
LOCAL = 2
;自制心、性的興味、性欲が影響
LOCAL -= TALENT:ARG:自制心
LOCAL += TALENT:ARG:性的興味 * 2
LOCAL += TALENT:ARG:性欲
SIF BASE:ARG:性欲 > 1500
	LOCAL += 1
;露出癖が影響
LOCAL += ABL:ARG:露出癖
;野外オナニー経験が影響
SIF EXP:ARG:野外オナニー経験 > 0
	LOCAL += MAX(1, EXP:ARG:野外オナニー経験 / 5)

;自慰しやすいが影響
LOCAL += TALENT:ARG:自慰しやすい * 2
;最低確率1割は確保
LOCAL = MAX(LOCAL, 1)
;性欲無しだと読まない
SIF TALENT:ARG:性欲 == -2
	LOCAL = -1
;補正確率より低ければ読む
IF RAND:10 < LOCAL
	;読むパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"…%CALLNAME:ARG%はいつの間にか近くにあったエロ本を見つけた。<br>"
	IF 知識素質:ARG:性知識 < 0
		CSTR:ARG:性癖開拓報告_記録文字列 += @"ぱらぱらと本をめくってみると、この前覚えたオナニーのことが書かれている本であることに気づく。<br>"
		CSTR:ARG:性癖開拓報告_記録文字列 += @"外で隠れてやると、オナニーはもっと気持ちいいらしい……<br>"
	ELSE
		CSTR:ARG:性癖開拓報告_記録文字列 += @"周りをきょろきょろと見回し自分ひとりであることを確認すると、%CALLNAME:ARG%はこっそりとその本を開いた……<br>"
	ENDIF
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%は性癖素質[野外オナニー(興味)]を取得した<br>"
	性癖素質:ARG:野外オナニー = 1
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[野外オナニー(興味)]を取得した</font>","うふふ")
	RETURN 1
ELSE
	;読まないパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%はいつの間にか近くにあったエロ本に訝しげな顔をして、ゴミ箱へと投げ捨てた<br>"
ENDIF

RETURN 0

@性癖段階非同室成長_野外オナニー(キャラ番号)
#DIM キャラ番号

SELECTCASE 性癖素質:キャラ番号:野外オナニー
	CASE 1
		;経験が足りないとダメ
		SIF EXP:キャラ番号:野外オナニー経験 < 30
			RETURN 0
		性癖素質:キャラ番号:野外オナニー = 2
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%の性癖は野外オナニーを繰り返したことでより強くなったようだ…
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%の性癖素質[野外オナニー(興味)]は[野外オナニー(愛好)]に成長した
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[野外オナニー(興味)]が[野外オナニー(愛好)]に成長した</font>","うふふ")
		PRINTL

		RETURN 1
	CASE 2
		;経験が足りないとダメ
		SIF EXP:キャラ番号:野外オナニー経験 < 100
			RETURN 0
		性癖素質:キャラ番号:野外オナニー = 3
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%は、もはや野外オナニーが日常になってしまっている…
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%の性癖素質[野外オナニー(愛好)]は[野外オナニー(中毒)]に成長した
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[野外オナニー(愛好)]が[野外オナニー(中毒)]に成長した</font>","うふふ")
		PRINTL

		RETURN 1
	CASE 3
		;既に中毒ならスキップ
		RETURN 0
ENDSELECT


@性癖開拓チャレンジ処理_逆アナル(ARG)
;逆アナル
CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:PLAYER%は%CALLNAME:ARG%にさりげなく「男性の尻穴になにかを挿入するシチュ」のエロ本が渡るように小細工した…<br><br>"

;エロ本を読むかどうか
IF 知識素質:ARG:性知識 < 0
	;性知識が-1か-2だと本の内容を理解できない
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%はいつの間にか近くにあったエロ本をぱらぱらと開いてみたが、よく意味がわからなかったようだ…<br>"
	RETURN -1
ENDIF

;基礎確率は2/10
LOCAL = 2
;自制心、性的興味、性欲が影響
LOCAL -= TALENT:ARG:自制心
LOCAL += TALENT:ARG:性的興味 * 2
LOCAL += TALENT:ARG:性欲
SIF BASE:ARG:性欲 > 1500
	LOCAL += 1
;サド性感が影響
LOCAL += ABL:ARG:サド性感

;ふたなりだと影響大
SIF TALENT:ARG:性別 == 3
	LOCAL += 3
;最低確率1割は確保
LOCAL = MAX(LOCAL, 1)
;性欲無しだと読まない
SIF TALENT:ARG:性欲 == -2
	LOCAL = -1
;男は読まない
SIF TALENT:ARG:性別 == 2
	LOCAL = -1
;補正確率より低ければ読む
IF RAND:10 < LOCAL
	;読むパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"…%CALLNAME:ARG%はいつの間にか近くにあったエロ本を見つけた。<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"周りをきょろきょろと見回し自分ひとりであることを確認すると、%CALLNAME:ARG%はこっそりとその本を開いた……<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%は性癖素質[逆アナル(興味)]を取得した<br>"
	性癖素質:ARG:逆アナル = 1
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[逆アナル(興味)]を取得した</font>","うふふ")
	RETURN 1
ELSE
	;読まないパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%はいつの間にか近くにあったエロ本に訝しげな顔をして、ゴミ箱へと投げ捨てた<br>"
ENDIF

RETURN 0

@性癖段階成長_逆アナル(キャラ番号)
#DIM キャラ番号

SELECTCASE 性癖素質:キャラ番号:逆アナル
	CASE 1
		;経験が足りないとダメ
		SIF EXP:キャラ番号:Ｃ性交経験_Ａ挿入 < 100
			RETURN 0
		;取得メッセージ
		CALL 性癖段階成長メッセージ_逆アナル(キャラ番号, 性癖素質:キャラ番号:逆アナル)
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[逆アナル(興味)]が[逆アナル(愛好)]に成長した</font>","うふふ")
		性癖素質:キャラ番号:逆アナル = 2

		;元のレベルを返す
		RETURN 1
	CASE 2
		;経験が足りないとダメ
		SIF EXP:キャラ番号:Ｃ性交経験_Ａ挿入 < 300
			RETURN 0
		;取得メッセージ
		CALL 性癖段階成長メッセージ_逆アナル(キャラ番号, 性癖素質:キャラ番号:逆アナル)
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[逆アナル(愛好)]が[逆アナル(中毒)]に成長した</font>","うふふ")
		性癖素質:キャラ番号:逆アナル = 3

		;元のレベルを返す
		RETURN 2
	CASE 3
		;既に中毒ならスキップ
		RETURN 0
ENDSELECT

@性癖段階成長メッセージ_逆アナル(キャラ番号, 表示段階)
#DIM キャラ番号
#DIM 表示段階

SELECTCASE 表示段階
	CASE 1
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%の性癖は逆アナル行為を繰り返したことでより強くなったようだ…
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%の性癖素質[逆アナル(興味)]は[逆アナル(愛好)]に成長した
		PRINTL
	CASE 2
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%は、もはや逆アナル行為の虜になってしまっている…
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%の性癖素質[逆アナル(愛好)]は[逆アナル(中毒)]に成長した
		PRINTL
ENDSELECT


@中毒連れ込み判定_逆アナル(キャラ番号)
#DIM キャラ番号

;中毒じゃないなら戻す
SIF 性癖素質:キャラ番号:逆アナル < 3
	RETURN 0
;MASTERが男じゃないなら戻す
SIF TALENT:MASTER:性別 != 2
	RETURN 0
;MASTERにAがない場合駄目
SIF TALENT:MASTER:Ａ感度 == -2
	RETURN 0
;キャラが男だと別ジャンルなのでダメ
SIF TALENT:キャラ番号:性別 == 2
	RETURN 0
;キャラがふたなり、もしくはペニスバンドがないとダメ
SIF TALENT:キャラ番号:性別 != 3 && (ITEM:PBAND == 0 && NOITEM == 0)
	RETURN 0

RETURN 1


@性癖開拓チャレンジ処理_搾乳プレイ(ARG)
;搾乳プレイ
CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:PLAYER%は%CALLNAME:ARG%にさりげなく「母乳を絞られて絶頂するシチュ」のエロ本が渡るように小細工した…<br>"
CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"

;エロ本を読むかどうか
IF 知識素質:ARG:性知識 < 0
	;性知識が-1か-2だと本の内容を理解できない
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%はいつの間にか近くにあったエロ本をぱらぱらと開いてみたが、よく意味がわからなかったようだ…<br>"
	RETURN -1
ENDIF

;基礎確率は2/10
LOCAL = 2
;自制心、性的興味、性欲が影響
LOCAL -= TALENT:ARG:自制心
LOCAL += TALENT:ARG:性的興味 * 2
LOCAL += TALENT:ARG:性欲
SIF BASE:ARG:性欲 > 1500
	LOCAL += 1

;バストサイズが影響
IF TALENT:ARG:バストサイズ > 2
	LOCAL += 1
ENDIF
;母乳体質でないならガッツリマイナス
IF TALENT:ARG:母乳体質 == 0
	LOCAL -= 5
ENDIF
;最低確率1割は確保
LOCAL = MAX(LOCAL, 1)
;性欲無しだと読まない
SIF TALENT:ARG:性欲 == -2
	LOCAL = -1
;乳が無いなら読まない
SIF GETBIT(TALENT:ARG:性別, 0) == 0
	LOCAL = -1
SIF TALENT:ARG:Ｂ感度 == -2
	LOCAL = -1
;補正確率より低ければ読む
IF RAND:10 < LOCAL
	;読むパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"…%CALLNAME:ARG%はいつの間にか近くにあったエロ本を見つけた。<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"周りをきょろきょろと見回し自分ひとりであることを確認すると、%CALLNAME:ARG%はこっそりとその本を開いた……<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%は性癖素質[搾乳プレイ(興味)]を取得した<br>"
	性癖素質:ARG:搾乳プレイ = 1
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[搾乳プレイ(興味)]を取得した</font>","うふふ")
	RETURN 1
ELSE
	;読まないパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%はいつの間にか近くにあったエロ本に訝しげな顔をして、ゴミ箱へと投げ捨てた<br>"
ENDIF

RETURN 0

@性癖段階成長_搾乳プレイ(キャラ番号)
#DIM キャラ番号

SELECTCASE 性癖素質:キャラ番号:搾乳プレイ
	CASE 1
		;経験が足りないとダメ
		SIF EXP:キャラ番号:搾乳快楽経験 < 100
			RETURN 0
		;取得メッセージ
		CALL 性癖段階成長メッセージ_搾乳プレイ(キャラ番号, 性癖素質:キャラ番号:搾乳プレイ)
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[搾乳プレイ(興味)]が[搾乳プレイ(愛好)]に成長した</font>","うふふ")
		性癖素質:キャラ番号:搾乳プレイ = 2

		RETURN 1
	CASE 2
		;経験が足りないとダメ
		SIF EXP:キャラ番号:搾乳快楽経験 < 300
			RETURN 0
		;取得メッセージ
		CALL 性癖段階成長メッセージ_搾乳プレイ(キャラ番号, 性癖素質:キャラ番号:搾乳プレイ)
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[搾乳プレイ(愛好)]が[搾乳プレイ(中毒)]に成長した</font>","うふふ")
		性癖素質:キャラ番号:搾乳プレイ = 3

		RETURN 2
	CASE 3
		;既に中毒ならスキップ
		RETURN 0
ENDSELECT

@性癖段階成長メッセージ_搾乳プレイ(キャラ番号, 表示段階)
#DIM キャラ番号
#DIM 表示段階

SELECTCASE 表示段階
	CASE 1
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%の性癖は搾乳行為を繰り返したことでより強くなったようだ…
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%の性癖素質[搾乳プレイ(興味)]は[搾乳プレイ(愛好)]に成長した
		PRINTL
	CASE 2
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%は、もはや搾乳行為の虜になってしまっている…
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%の性癖素質[搾乳プレイ(愛好)]は[搾乳プレイ(中毒)]に成長した
		PRINTL
ENDSELECT

@中毒連れ込み判定_搾乳プレイ(キャラ番号)
#DIM キャラ番号

;中毒じゃないなら戻す
SIF 性癖素質:キャラ番号:搾乳プレイ < 3
	RETURN 0
;キャラにBがない場合駄目
SIF TALENT:キャラ番号:Ｂ感度 == -2
	RETURN 0
SIF GETBIT(TALENT:キャラ番号:性別, 0) == 0
	RETURN 0
;搾乳機非所持なら戻す
SIF ITEM:11 == 0 && NOITEM == 0
	RETURN 0
;ベッドルームでないとダメ
SIF !BEDROOM(CFLAG:キャラ番号:現在マップ種別, CFLAG:キャラ番号:現在位置)
	RETURN 0

RETURN 1

@性癖開拓チャレンジ処理_触手プレイ(ARG)
;触手プレイ
CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:PLAYER%は%CALLNAME:ARG%にさりげなく「触手に蹂躙されてイキまくるシチュ」のエロ本が渡るように小細工した…<br>"
CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"

;エロ本を読むかどうか
IF 知識素質:ARG:性知識 < 0
	;性知識が-1か-2だと本の内容を理解できない
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%はいつの間にか近くにあったエロ本をぱらぱらと開いてみたが、よく意味がわからなかったようだ…<br>"
	RETURN -1
ENDIF

;基礎確率は2/10
LOCAL = 2
;自制心、性的興味、性欲が影響
LOCAL -= TALENT:ARG:自制心
LOCAL += TALENT:ARG:性的興味 * 2
LOCAL += TALENT:ARG:性欲
SIF BASE:ARG:性欲 > 1500
	LOCAL += 1

;性知識が低いとマイナス
LOCAL = LOCAL - (3 - 知識素質:ARG:性知識)

;最低確率1割は確保
LOCAL = MAX(LOCAL, 1)
;性欲無しだと読まない
SIF TALENT:ARG:性欲 == -2
	LOCAL = -1
;補正確率より低ければ読む
IF RAND:10 < LOCAL
	;読むパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"…%CALLNAME:ARG%はいつの間にか近くにあったエロ本を見つけた。<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"周りをきょろきょろと見回し自分ひとりであることを確認すると、%CALLNAME:ARG%はこっそりとその本を開いた……<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%は性癖素質[触手プレイ(興味)]を取得した<br>"
	性癖素質:ARG:触手プレイ = 1
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[触手プレイ(興味)]を取得した</font>","うふふ")
	RETURN 1
ELSE
	;読まないパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%はいつの間にか近くにあったエロ本に訝しげな顔をして、ゴミ箱へと投げ捨てた<br>"
ENDIF

RETURN 0

@性癖段階成長_触手プレイ(キャラ番号)
#DIM キャラ番号

SELECTCASE 性癖素質:キャラ番号:触手プレイ
	CASE 1
		;経験が足りないとダメ
		SIF EXP:キャラ番号:触手快楽経験 < 100
			RETURN 0
		CALL 性癖段階成長メッセージ_触手プレイ(キャラ番号, 性癖素質:キャラ番号:触手プレイ)
		性癖素質:キャラ番号:触手プレイ = 2
		;取得メッセージ
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[触手プレイ(興味)]が[触手プレイ(愛好)]に成長した</font>","うふふ")

		RETURN 1
	CASE 2
		;経験が足りないとダメ
		SIF EXP:キャラ番号:触手快楽経験 < 500
			RETURN 0
		CALL 性癖段階成長メッセージ_触手プレイ(キャラ番号, 性癖素質:キャラ番号:触手プレイ)
		性癖素質:キャラ番号:触手プレイ = 3
		;取得メッセージ
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[触手プレイ(愛好)]が[触手プレイ(中毒)]に成長した</font>","うふふ")

		RETURN 2
	CASE 3
		;既に中毒ならスキップ
		RETURN 0
ENDSELECT

@性癖段階成長メッセージ_触手プレイ(キャラ番号, 表示段階)
#DIM キャラ番号
#DIM 表示段階

SELECTCASE 表示段階
	CASE 1
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%の性癖は触手に嬲られる行為を繰り返したことでより強くなったようだ…
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%の性癖素質[触手プレイ(興味)]は[触手プレイ(愛好)]に成長した
		PRINTL
	CASE 2
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%は、もはや触手快楽の虜になってしまっている…
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%の性癖素質[触手プレイ(愛好)]は[触手プレイ(中毒)]に成長した
		PRINTL
ENDSELECT

@中毒連れ込み判定_触手プレイ(キャラ番号)
#DIM キャラ番号

;中毒じゃないなら戻す
SIF 性癖素質:キャラ番号:触手プレイ < 3
	RETURN 0
;触手スキル非所持なら戻す
SIF あなた特殊能力:触手変化術 == 0
	RETURN 0

RETURN 1

@性癖開拓チャレンジ処理_同性愛(ARG)
#DIMS 素質名

;同性愛
CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:PLAYER%は%CALLNAME:ARG%にさりげなく"
素質名 '= GET_性癖素質NAME(GETNUM(性癖素質, "同性愛"), ARG)
IF 素質名 == "異性愛"
	CSTR:ARG:性癖開拓報告_記録文字列 += "「男女で体を重ねるシチュ」のエロ本が渡るように小細工した…<br>"
ELSEIF TALENT:ARG:性別 == 2
	CSTR:ARG:性癖開拓報告_記録文字列 += "「男性同士で体を重ねるシチュ」のエロ本が渡るように小細工した…<br>"
ELSE
	CSTR:ARG:性癖開拓報告_記録文字列 += "「女性同士で体を重ねるシチュ」のエロ本が渡るように小細工した…<br>"
ENDIF
CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"

;エロ本を読むかどうか
IF 知識素質:ARG:性知識 < 0
	;性知識が-1か-2だと本の内容を理解できない
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%はいつの間にか近くにあったエロ本をぱらぱらと開いてみたが、よく意味がわからなかったようだ…<br>"
	RETURN -1
ENDIF

;基礎確率は2/10
LOCAL = 2
;自制心、性的興味、性欲が影響
LOCAL -= TALENT:ARG:自制心
LOCAL += TALENT:ARG:性的興味 * 2
LOCAL += TALENT:ARG:性欲
SIF BASE:ARG:性欲 > 1500
	LOCAL += 1

SELECTCASE TALENT:ARG:性別嗜好
	CASE 1
		;男の男嫌いパターン
		SIF TALENT:ARG:性別 == 2
			LOCAL -= 5
	CASE 2
		;女の女嫌いパターン
		SIF TALENT:ARG:性別 == 1
			LOCAL -= 5
	CASE 3
		;そもそも人間が嫌い
		LOCAL -= 5
ENDSELECT

;最低確率1割は確保
LOCAL = MAX(LOCAL, 1)
;性欲無しだと読まない
SIF TALENT:ARG:性欲 == -2
	LOCAL = -1
;取得対象不適の場合も読まない
SIF 素質名 == ""
	LOCAL = -1
;補正確率より低ければ読む
IF RAND:10 < LOCAL
	;読むパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"…%CALLNAME:ARG%はいつの間にか近くにあったエロ本を見つけた。<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"周りをきょろきょろと見回し自分ひとりであることを確認すると、%CALLNAME:ARG%はこっそりとその本を開いた……<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%は性癖素質[%素質名%(興味)]を取得した<br>"
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[%素質名%(興味)]を取得した</font>","うふふ")
	性癖素質:ARG:同性愛 = 1
	RETURN 1
ELSE
	;読まないパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%はいつの間にか近くにあったエロ本に訝しげな顔をして、ゴミ箱へと投げ捨てた<br>"
ENDIF

RETURN 0

@性癖段階成長_同性愛(キャラ番号)
#DIM キャラ番号
#DIMS 素質名
#DIMS 性愛対象
素質名 '= GET_性癖素質NAME(GETNUM(性癖素質, "同性愛"), キャラ番号)
SIF 素質名 == ""
	RETURN 0

性愛対象 '= SUBSTRINGU(素質名, 0, STRLENSU(素質名) - 1)

SELECTCASE 性癖素質:キャラ番号:同性愛
	CASE 1
		;経験が足りないとダメ
		SIF EXP:キャラ番号:同性愛経験 < 500
			RETURN 0
		CALL 性癖段階成長メッセージ_同性愛(キャラ番号, 性癖素質:キャラ番号:同性愛)
		性癖素質:キャラ番号:同性愛 = 2
		;取得メッセージ
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[%素質名%(興味)]が[%素質名%(愛好)]に成長した</font>","うふふ")

		RETURN 1
	CASE 2
		;経験が足りないとダメ
		SIF EXP:キャラ番号:同性愛経験 < 2000
			RETURN 0
		CALL 性癖段階成長メッセージ_同性愛(キャラ番号, 性癖素質:キャラ番号:同性愛)
		性癖素質:キャラ番号:同性愛 = 3
		;取得メッセージ
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[%素質名%(愛好)]が[%素質名%(中毒)]に成長した</font>","うふふ")

		RETURN 2
	CASE 3
		;既に中毒ならスキップ
		RETURN 0
ENDSELECT

@性癖段階成長メッセージ_同性愛(キャラ番号, 表示段階)
#DIM キャラ番号
#DIM 表示段階
#DIMS 素質名
#DIMS 性愛対象

素質名 '= GET_性癖素質NAME(GETNUM(性癖素質, "同性愛"), キャラ番号)
性愛対象 '= SUBSTRINGU(素質名, 0, STRLENSU(素質名) - 1)

SELECTCASE 表示段階
	CASE 1
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%の性癖は%性愛対象%と肌を重ねる行為を繰り返したことでより強くなったようだ…
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%の性癖素質[%素質名%(興味)]は[%素質名%(愛好)]に成長した
		PRINTL
	CASE 2
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%は、もはや%性愛対象%との情事に魅了されてしまっている…
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%の性癖素質[%素質名%(愛好)]は[%素質名%(中毒)]に成長した
		PRINTL
ENDSELECT

@性癖開拓チャレンジ処理_寝取られ(ARG)
;寝取られ

;気になってどういうものか見に来たあなたに向けて簡単な仕様説明
;
;・性癖開拓システムでリゾート客(仮にＡとする)にエロ本を押し付けてＡの脳を破壊し、性癖素質[寝取られ(興味)]を与えます。
;・縁結びシステムを使って、Ａと他のキャラ(仮にＢとする)をくっつけます。関係性は恋慕でもセフレでもＯＫ。
;・あなたとＡの「信頼度」を、一定以上にします。その際あなたが「遊び人」として極まってきているなら、このボーダーはちょっとだけ下がります。Ａが「従業員」であるなら、このボーダーは劇的に下がります。
;上記のみっつの条件を、すべて満たせば準備完了！(別にＡもＢも両方寝取られ性癖持ってても大丈夫だよ！)
;
;「ＡとＢがどちらも島にいる」状態で、あなたが「自室」で「就寝」する際、「他に同衾するキャラがいなければ(一人寝であるなら)」確率でイベントが発生。
;一日の終わりに、度し難い性癖に脳を破壊されたＡがあなたの部屋を訪ねてきて、「Ｂを抱いてくれ」とお願いされます。性癖名は「寝取られ」だけど、Ａがやる実際のプレイは「寝取らせ」が正しいわけですな。
;いやぁ、頼まれちゃったからなぁ～！しょうがないなぁ～～～！！ほな、いただきま～～～っす！！
;
;毎日来られても困るし特別感もないので、発生確率は低め。「こんなもんかな～」で適当に決めてるので、低すぎる！と思ったら本スレとかDiscordで教えてください。調整します。
;実際問題、発生に一人寝を要求すること自体がプレイに対する一つの縛りになっちゃってるから、もうちょい高くてもいいかもな～とは書いた本人も思ってます。
;抽出処理とタイミングの関係で、条件を満たしたカップルが増えるほどに発生確率は平均化されちゃうし、狙った組み合わせも出づらくなるし。
;あなたの「遊び人」が５とかだと確率がちょっと上がる。理由は確率処理んとこに書いた。
;
;さいごに
;エロ本ばら撒きには要注意。みんながみんな性癖持ちだと、「男あなたにグラン(♂)を差し出してくるジータ(寝取られ性癖持ち)」とかそういう事故が起きます。
;まぁ、意図して除外処理しなかったんですけども。だ、だってそういう需要もあるかもだし……。

CSTR:ARG:性癖開拓報告_記録文字列 += @" %CALLNAME:PLAYER%は%CALLNAME:ARG%にさりげなく「大事な人が他人に抱かれるシチュ」のエロ本が渡るように小細工した…<br>"
CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"

;エロ本を読むかどうか
IF 知識素質:ARG:性知識 < 0
	;性知識が-1か-2だと本の内容を理解できない
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%はいつの間にか近くにあったエロ本をぱらぱらと開いてみたが、よく意味がわからなかったようだ…<br>"
	RETURN -1
ENDIF

;基礎確率は2/10
LOCAL = 2
;自制心、プライド、性的興味、性欲が影響
LOCAL -= TALENT:ARG:自制心
LOCAL -= TALENT:ARG:プライド
LOCAL += TALENT:ARG:性的興味 * 2
LOCAL += TALENT:ARG:性欲
SIF BASE:ARG:性欲 > 1500
	LOCAL += 1

;マゾ性感が影響
LOCAL += ABL:ARG:マゾ性感

;男性器サイズ
;自分の「男としての才能/素養」にコンプレックスがあるほどこういうのに陥りやすいんじゃなかろうか、という発想
IF TALENT:ARG:男性器サイズ == -2
	LOCAL += 2
ELSEIF TALENT:ARG:男性器サイズ == -1
	LOCAL += 1
ENDIF

;最低確率1割は確保
LOCAL = MAX(LOCAL, 1)
;性欲無しだと読まない
SIF TALENT:ARG:性欲 == -2
	LOCAL = -1
;補正確率より低ければ読む
IF RAND:10 < LOCAL
	;読むパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"…%CALLNAME:ARG%はいつの間にか近くにあったエロ本を見つけた。<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"周りをきょろきょろと見回し自分ひとりであることを確認すると、%CALLNAME:ARG%はこっそりとその本を開いた……<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"自分の大事な人が他人に抱かれ、果てには奪われてしまうというこの本の内容に、%CALLNAME:ARG%はなぜか、切なく苦しい心とは裏腹に"
	IF GETBIT(TALENT:ARG:性別, 1)
	;ちんちんあるなら 鬱勃起(七五調)(字余り)
		CSTR:ARG:性癖開拓報告_記録文字列 += "鬱勃起"
	ELSE
		CSTR:ARG:性癖開拓報告_記録文字列 += "興奮"
	ENDIF
	CSTR:ARG:性癖開拓報告_記録文字列 += @"が止まらない！<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"……やがて、ひとつの物語に脳を灼かれた%CALLNAME:ARG%は、性癖素質[寝取られ(興味)]を取得してしまった！<br>"
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[寝取られ(興味)]を取得した</font>","うふふ")
	性癖素質:ARG:寝取られ = 1
	RETURN 1
ELSE
	;読まないパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%はいつの間にか近くにあったエロ本に訝しげな顔をして、ゴミ箱へと投げ捨てた<br>"
ENDIF

RETURN 0

@性癖段階成長_寝取られ(キャラ番号)
#DIM キャラ番号

SELECTCASE 性癖素質:キャラ番号:寝取られ
	CASE 1
		;経験が足りないとダメ
		;いままで興味だったものが、実践を経て確信に変わり、自身の中に深く根付く。故に、愛好になるのは「最初の一歩を踏み出してしまったその直後」だと思う。異論は認める。
		SIF EXP:キャラ番号:寝取られ経験 < 1
			RETURN 0
		CALL 性癖段階成長メッセージ_寝取られ(キャラ番号, 性癖素質:キャラ番号:寝取られ)
		性癖素質:キャラ番号:寝取られ = 2
		;取得メッセージ
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[寝取られ(興味)]が[寝取られ(愛好)]に成長した</font>","うふふ")

		RETURN 1
	CASE 2
		;経験が足りないとダメ
		;事が事だけに、5回もリピれば立派な中毒だと思う
		SIF EXP:キャラ番号:寝取られ経験 < 5
			RETURN 0
		CALL 性癖段階成長メッセージ_寝取られ(キャラ番号, 性癖素質:キャラ番号:寝取られ)
		性癖素質:キャラ番号:寝取られ = 3
		;取得メッセージ
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[寝取られ(愛好)]が[寝取られ(中毒)]に成長した</font>","うふふ")

		RETURN 2
	CASE 3
		;既に中毒ならスキップ
		RETURN 0
ENDSELECT

@性癖段階成長メッセージ_寝取られ(キャラ番号, 表示段階)
#DIM キャラ番号
#DIM 表示段階

SELECTCASE 表示段階
	CASE 1
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%の寝取られへの興味は、実際に抱かせてみたことで確たる性癖として%CALLNAME:キャラ番号%の中に深く根付いてしまったようだ……
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%の性癖素質[寝取られ(興味)]は[寝取られ(愛好)]へと成長してしまった！
		PRINTL
	CASE 2
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%は、もはや%CALLNAME:PLAYER%に自分の女を差し出すことに、破滅的な魅力を感じてしまっている……
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%の性癖素質[寝取られ(愛好)]は[寝取られ(中毒)]に成長してしまった！
		PRINTL
ENDSELECT


@性癖開拓チャレンジ処理_首絞め(ARG)
;首絞め
CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:PLAYER%は%CALLNAME:ARG%にさりげなく「首を絞められながら性行為するシチュ」のエロ本が渡るように小細工した…<br>"
CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"

;エロ本を読むかどうか
IF 知識素質:ARG:性知識 < 0
	;性知識が-1か-2だと本の内容を理解できない
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%はいつの間にか近くにあったエロ本をぱらぱらと開いてみたが、よく意味がわからなかったようだ…<br>"
	RETURN -1
ENDIF

;基礎確率は2/20（見た目怖いので他より低い）
LOCAL = 2
;自制心、性的興味、性欲が影響
LOCAL -= TALENT:ARG:自制心
LOCAL += TALENT:ARG:性的興味 * 2
LOCAL += TALENT:ARG:性欲
SIF BASE:ARG:性欲 > 1500
	LOCAL += 1
;マゾ性感が影響
LOCAL += ABL:ARG:マゾ性感
;メスドラフはなぜかちょっと高い
SIF TALENT:ARG:種族 == 種族_ドラフ && IS_FEMALE(ARG)
	LOCAL += 2

;最低確率1割は確保
LOCAL = MAX(LOCAL, 1)
;性欲無しだと読まない
SIF TALENT:ARG:性欲 == -2
	LOCAL = -1
;補正確率より低ければ読む
IF RAND:20 < LOCAL
	;読むパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"…%CALLNAME:ARG%はいつの間にか近くにあったエロ本を見つけた。<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"周りをきょろきょろと見回し自分ひとりであることを確認すると、%CALLNAME:ARG%はこっそりとその本を開いた……<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += "<br>"
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%は性癖素質[首絞め(興味)]を取得した<br>"
	性癖素質:ARG:首絞め = 1
	CALL 履歴データベース登録(CFLAG:ARG:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[首絞め(興味)]を取得した</font>","うふふ")
	RETURN 1
ELSE
	;読まないパターン
	CSTR:ARG:性癖開拓報告_記録文字列 += @"%CALLNAME:ARG%はいつの間にか近くにあったエロ本に訝しげな顔をして、ゴミ箱へと投げ捨てた<br>"
ENDIF

RETURN 0

@性癖段階成長_首絞め(キャラ番号)
#DIM キャラ番号

SELECTCASE 性癖素質:キャラ番号:首絞め
	CASE 1
		;経験が足りないとダメ
		SIF EXP:キャラ番号:首絞め絶頂経験 < 100
			RETURN 0
		CALL 性癖段階成長メッセージ_首絞め(キャラ番号, 性癖素質:キャラ番号:首絞め)
		性癖素質:キャラ番号:首絞め = 2
		;取得メッセージ
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[首絞め(興味)]が[首絞め(愛好)]に成長した</font>","うふふ")
		PRINTL

		RETURN 1
	CASE 2
		;経験が足りないとダメ
		SIF EXP:キャラ番号:首絞め絶頂経験 < 300
			RETURN 0
		CALL 性癖段階成長メッセージ_首絞め(キャラ番号, 性癖素質:キャラ番号:首絞め)
		性癖素質:キャラ番号:首絞め = 3
		;取得メッセージ
		CALL 履歴データベース登録(CFLAG:キャラ番号:人物番号, @"<font color='#%カラーパレット_HTML("薄ピンク")%'>性癖素質[首絞め(愛好)]が[首絞め(中毒)]に成長した</font>","うふふ")
		PRINTL

		RETURN 2
	CASE 3
		;既に中毒ならスキップ
		RETURN 0
ENDSELECT

@性癖段階成長メッセージ_首絞め(キャラ番号, 表示段階)
#DIM キャラ番号
#DIM 表示段階

SELECTCASE 表示段階
	CASE 1
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%の性癖は首絞め行為を繰り返したことでより強くなったようだ…
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%の性癖素質[首絞め(興味)]は[首絞め(愛好)]に成長した
		PRINTL
	CASE 2
		;取得メッセージ
		DRAWLINE
		PRINTFORML %CALLNAME:キャラ番号%は、もはや首絞めがもたらす快楽の虜になってしまっている…
		PRINTL 
		PRINTFORML %CALLNAME:キャラ番号%の性癖素質[首絞め(愛好)]は[首絞め(中毒)]に成長した
		PRINTL
ENDSELECT

@中毒連れ込み判定_首絞め(キャラ番号)
#DIM キャラ番号

;中毒じゃないなら戻す
SIF 性癖素質:キャラ番号:首絞め < 3
	RETURN 0
RETURN 1
