@特殊料理_2種レシピ_パンケーキ(ARGS)

SELECTCASE ARGS
	CASE "レシピ参照"
		;レシピブック用にレシピ文章をRESULTSへ
		RESULTS = 
		RESULTS += "　メイン　：名前に「小麦」を含む食材<br>"
		RESULTS += "　サブ　　：名前に「はちみつ」を含む食材・なし・なし<br>"
		RESULTS += "　調理器具：フライパン<br>"

		;メインが小麦
		LOCALS:0 = %DT_CELL_GETS("所持素材データベース", 使用食材:0, "素材アイテム名", 1)%
		SIF STRCOUNT(LOCALS:0, "小麦") < 1
			RETURN 0

		;追加ははちみつ
		LOCALS:1 = %DT_CELL_GETS("所持素材データベース", 使用食材:1, "素材アイテム名", 1)%
		LOCALS:1 += @"%DT_CELL_GETS("所持素材データベース", 使用食材:2, "素材アイテム名", 1)%"
		LOCALS:1 += @"%DT_CELL_GETS("所持素材データベース", 使用食材:3, "素材アイテム名", 1)%"
		SIF STRCOUNT(LOCALS:1, "はちみつ") < 1
			RETURN 0
		
		;調理器具はフライパン
		SIF 使用食材:9 != 4
			RETURN 0

		;全部抜けてきたら一致
		;料理名と関数名は一致させること！
		料理名受け渡し = パンケーキ
		RETURN 1
	CASE "料理基礎ステータス"
		;レシピブック用に収入をRESULTSへ
		RESULTS = 
		RESULTS += "　ルピ収入：料理ランク ✕ 料理ランク ✕ ２ ＋ ３<br>"
		RESULTS += "　ZP収入　：料理ランク ✕ 料理ランク ÷ １０ ＋ １（最低２）<br>"
		;-----------------------------------------------------
		;ルピ収入
		;基本は「開発料理ランク保存 * 開発料理ランク保存 * 2」に追加食材の数だけ＋した値
		;(追加食材はランク２以下だと１個につき＋１、ランク３以上だと＋２)
		;なので基本よりちょい上になるように設定すること
		RESULT:1 = 開発料理ランク保存 * 開発料理ランク保存 * 2 + 3
		;-----------------------------------------------------
		;ZP収入
		;基本はMAX(開発料理ランク保存 * 開発料理ランク保存 / 10, 1)
		;追加食材を３つ全部埋めたら＋１
		;基本よりちょい上になるように設定すること
		RETURN MAX(開発料理ランク保存 * 開発料理ランク保存 / 10, 1) + 1
		;-----------------------------------------------------
	CASE "料理特殊ステータス"
		詳細文文字列受け渡し変数 = 【あまくてふわふわパンケーキ】<br>
		詳細文文字列受け渡し変数 += "　元々の属性が風、かつ滞在中キャラの回復力が<br>　５％上昇する。"
		;増加処理は@装備再計算処理に記述
ENDSELECT
