@特殊料理_4種レシピ_寿司盛り合わせ(ARGS)

SELECTCASE ARGS
	CASE "レシピ参照"
		;レシピブック用にレシピ文章をRESULTSへ
		RESULTS = 
		RESULTS += "　メイン　：魚系食材<br>"
		RESULTS += "　サブ　　：魚系食材・名前に「米」を含む食材・名前に「米」を含む食材<br>"
		RESULTS += "　調理器具：包丁<br>"

		;メインが魚
		SIF DT_CELL_GETS("所持素材データベース", 使用食材:0, "食材分類", 1) != "魚"
			RETURN 0

		;追加は米・魚・米
		LOCALS:1 = %DT_CELL_GETS("所持素材データベース", 使用食材:1, "食材分類", 1)%
		LOCALS:2 = %DT_CELL_GETS("所持素材データベース", 使用食材:2, "食材分類", 1)%
		LOCALS:3 = %DT_CELL_GETS("所持素材データベース", 使用食材:3, "食材分類", 1)%
		SIF GROUPMATCH("魚", LOCALS:1, LOCALS:2, LOCALS:3) != 1
			RETURN 0
		LOCALS:1 = %DT_CELL_GETS("所持素材データベース", 使用食材:1, "素材アイテム名", 1)%
		LOCALS:1 += @"%DT_CELL_GETS("所持素材データベース", 使用食材:2, "素材アイテム名", 1)%"
		LOCALS:1 += @"%DT_CELL_GETS("所持素材データベース", 使用食材:3, "素材アイテム名", 1)%"
		SIF STRCOUNT(LOCALS:1, "米") < 2
			RETURN 0
		
		;調理器具は包丁
		SIF 使用食材:9 != 1
			RETURN 0

		;全部抜けてきたら一致
		;料理名と関数名は一致させること！
		料理名受け渡し = 寿司盛り合わせ
		RETURN 1
	CASE "料理基礎ステータス"
		;レシピブック用に収入をRESULTSへ
		RESULTS = 
		RESULTS += "　ルピ収入：料理ランク ✕ 料理ランク ✕ ２ ＋ ８<br>"
		RESULTS += "　ZP収入　：料理ランク ✕ 料理ランク ÷ １０ ＋ ３（最低４）<br>"
		;-----------------------------------------------------
		;ルピ収入
		;基本は「開発料理ランク保存 * 開発料理ランク保存 * 2」に追加食材の数だけ＋した値
		;(追加食材はランク２以下だと１個につき＋１、ランク３以上だと＋２)
		;なので基本よりちょい上になるように設定すること
		RESULT:1 = 開発料理ランク保存 * 開発料理ランク保存 * 2 + 8
		;-----------------------------------------------------
		;ZP収入
		;基本はMAX(開発料理ランク保存 * 開発料理ランク保存 / 10, 1)
		;追加食材を３つ全部埋めたら＋１
		;基本よりちょい上になるように設定すること
		RETURN MAX(開発料理ランク保存 * 開発料理ランク保存 / 10, 1) + 3
		;-----------------------------------------------------
	CASE "料理特殊ステータス"
		詳細文文字列受け渡し変数 = 【寿司！　美味すぎるぜ！！】<br>
		詳細文文字列受け渡し変数 += "　メニュー効果によるZP取得量を１０％増加させる。"
		;増加処理は@EVENTEND0で記述
ENDSELECT
