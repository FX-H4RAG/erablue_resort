@SHOW_SHOP
#DIM コマンド表示数
;奴隷購入ページフラグ
TFLAG:ゲームフェイズ管理 = 0
TOOLTIP_CUSTOM 1
TOOLTIP_SETFONTSIZE 12
TOOLTIP_SETDURATION 100000

CALL リゾートランク上昇チェック()
CALL イベント発生条件チェック()
CALL モブ観光客算出

DRAWLINE
LOCALS = %月算出()%
PRINTFORM {RESULT}年目 %LOCALS% {DAY % 30 + 1}日(%GET_DAY()%)
PRINTFORML （所持金：%NUM_FORMAT(MONEY)%%MONEYLABEL% 所持ZP：%ZP所持量全文字列()%）
PRINTFORML リゾート人気度：%リゾート人気度全文字列()%
PRINTFORML リゾートランク：%リゾートランク文字列:リゾートランク%
IF 欲情フィールド展開フラグ
	SETCOLOR カラーパレット("えっちな色")
	PRINTFORML 展開中フィールド：欲情フィールド　　残り{欲情フィールド展開フラグ, 2}日
	RESETCOLOR
ENDIF
DRAWLINE
LOCAL:1 = 0
FOR LOCAL, 1, CHARANUM
	SIF CFLAG:LOCAL:滞在期間 == 1
		LOCAL:1 += 1
NEXT
PRINTFORML ネームドキャラ滞在人数：%NUM_FORMAT(滞在キャラ数), 3%人 / 最大%NUM_FORMAT(滞在キャラ上限), 3%人　　　本日帰宅予定者：%NUM_FORMAT(LOCAL:1), 3%人
PRINTFORML 今日のモブ観光客人数：%NUM_FORMAT(モブ観光客数), 5%人
LOCAL:1 = 0
FOR LOCAL, 0, 従業員キャラ上限
	IF 従業員部屋割り配列:LOCAL > 0
		LOCAL:1 += 1
	ENDIF
NEXT
PRINTFORML 雇用中従業員キャラ人数：%NUM_FORMAT(LOCAL:1), 3%人
DRAWLINE
LOCALS = <div rect='94, 0, 5937, 1062' border='31' bcolor='#C0C0C0'></div>
LOCALS += "<div rect='156, 94, 5812, 937'>"
起床前メニュー上部分表示文字列 = 
CALL 起床前メニュー上部分表示文章
LOCALS += @"%起床前メニュー上部分表示文字列%"
LOCALS += "</div>"
HTML_PRINT LOCALS, 1
FOR LOCAL, 0, 10
	PRINTL 
NEXT
LOCALS = <font color='#f39800'>-----[ゲーム進行]-------------------------------------------------------------------------------------------</font><br>
LOCALS += "<button value='100'>[100] - 起床</button>　　　　　　　　　　　　　　"
IF TSTR:NEWイベントフラグ != ""
	LOCALS += @"<font color='#%カラーパレット_HTML("黄")%'><button value='120'>[120] - イベントリスト</button></font><font color='#FF5555'> New!</font> 　　　　　　"
ELSE
	LOCALS += "<button value='120'>[120] - イベントリスト</button>　　　　　　　　　"
ENDIF
IF 探索フラグ == 0
	LOCALS += "<button value='101'>[101] - 探索へ出かける</button>　　　　　　　　　"
ELSE
	LOCALS += "<font color='#666666'><nonbutton>[101] - 探索へ出かける</nonbutton></font>　　　　　　　　　"
ENDIF
LOCALS += "<br><br>"
LOCALS += "<font color='#f39800'>-----[リゾート]---------------------------------------------------------------------------------------------</font><br>"
IF 滞在キャラ数 >= 滞在キャラ上限
	IF ツールチップ切り替えOPTION記憶 == 0
		LOCALS += "<font color='#666666'><nonbutton title='<br>滞在人数上限<br>'>[110] - リゾートの広告を出す</nonbutton></font>　　　　　　"
	ELSE
		LOCALS += "<font color='#666666'><nonbutton>[110] - リゾートの広告を出す</nonbutton></font>　　　　　　"
	ENDIF
ELSE
	LOCALS += "<button value='110'>[110] - リゾートの広告を出す</button>　　　　　　"
ENDIF

LOCALS += "<button value='111'>[111] - 経営計画</button>　　　　　　　　　　　　"

IF ダンジョンクリアフラグ_沿岸部の森
	LOCALS += "<button value='113'>[113] - 従業員設定</button>　　　　　　　　　　　"
ELSE
	IF ツールチップ切り替えOPTION記憶 == 0
		LOCALS += "<font color='#666666'><nonbutton title='<br>未開放機能<br>ダンジョン「沿岸部の森」クリアで解放<br>'>[113] - 従業員設定</nonbutton></font>　　　　　　　　　　　"
	ELSE
		LOCALS += "<font color='#666666'><nonbutton>[113] - 従業員設定</nonbutton></font>　　　　　　　　　　　"
	ENDIF
ENDIF

LOCALS += "<br><br>"
LOCALS += "<font color='#f39800'>-----[キャラクター]-----------------------------------------------------------------------------------------</font><br>"
LOCALS += "<button value='121'>[121] - 能力表示</button>　　　　　　　　　　　　"
LOCALS += "<button value='122'>[122] - 探索系情報一覧</button>　　　　　　　　　"
IF 施設改造度:5:6
	LOCALS += "<button value='123'>[123] - 性癖開拓</button>　　　　　　　　　　　　"
ELSE
	IF ツールチップ切り替えOPTION記憶 == 0
		LOCALS += "<font color='#666666'><nonbutton title='<br>未開放機能<br>施設改造「遊戯室系列：図書室」達成で解放<br>'>[123] - 性癖開拓</nonbutton></font>　　　　　　　　　　　　"
	ELSE
		LOCALS += "<font color='#666666'><nonbutton>[123] - 性癖開拓</nonbutton></font>　　　　　　　　　　　　"
	ENDIF
ENDIF
LOCALS += "<br>"
LOCALS += "<button value='124'>[124] - 干渉力強化</button>　　　　　　　　　　　"
LOCALS += "<br><br>"
LOCALS += "<font color='#f39800'>-----[ショップ類]-------------------------------------------------------------------------------------------</font><br>"
LOCALS += "<button value='114'>[114] - 通信販売</button>　　　　　　　　　　　　"
LOCALS += "<button value='115'>[115] - ZPショップ</button>　　　　　　　　　　　"
コマンド表示数 = 0
IF イベント完了フラグ確認("大拡張！空艇港")
	LOCALS += @"<button value='116'>[116] - よろず屋で依頼を出す</button>　　　　　　"
	LOCALS += "<br>"
	LOCALS += @"<button value='117'>[117] - 輸入品店</button>　　　　　　　　　　　　"
	コマンド表示数 += 1
ELSE
	LOCALS += "<br>"
ENDIF
FOR LOCAL, 3, 9
	IF 空艇港店舗状況:LOCAL:0 == 1
		LOCALS += @"<button value='{140 + LOCAL}'>[{140 + LOCAL}] - 道具屋</button>　　　　　　　　　　　　　"
		コマンド表示数 += 1
	ELSEIF 空艇港店舗状況:LOCAL:0 == 2
		LOCALS += @"<button value='{140 + LOCAL}'>[{140 + LOCAL}] - 土産物店</button>　　　　　　　　　　　　"
		コマンド表示数 += 1
	ELSEIF 空艇港店舗状況:LOCAL:0 == 4
		LOCALS += @"<button value='{140 + LOCAL}'>[{140 + LOCAL}] - 装飾品店</button>　　　　　　　　　　　　"
		コマンド表示数 += 1
	ENDIF
	IF コマンド表示数 > 2
		LOCALS += "<br>"
		コマンド表示数 = 0
	ENDIF
NEXT
LOCALS += "<br><br>"
LOCALS += "<font color='#f39800'>-----[アイテム類]-------------------------------------------------------------------------------------------</font><br>"
LOCALS += "<button value='135'>[135] - 写真一覧</button>　　　　　　　　　　　　"
LOCALS += "<br><br>"
LOCALS += "<font color='#f39800'>-----[オプション]-------------------------------------------------------------------------------------------</font><br>"
LOCALS += "<button value='200'>[200] - OPTION</button>　　　　　　　　　　　　　"
LOCALS += "<button value='201'>[201] - SAVE</button>　　　　　　　　　　　　　　"
LOCALS += "<button value='202'>[202] - LOAD</button>　　　　　　　　　　　　　　"
LOCALS += "<br>"
LOCALS += "<button value='203'>[203] - ゲームヘルプ</button>　　　　　　　　　　"
LOCALS += "<br>"
SIF FLAG:エンディング後フラグ
	LOCALS += "<button value='900'>[900] - 強くてNEWGAME</button>　　　　　　　　　　"
LOCALS += "<br><br>"
HTML_PRINT LOCALS


@USERSHOP
SELECTCASE RESULT
	CASE 800 TO 899
		CALL 起床前メニュー上部分文章切り替え(RESULT)
	CASE 100
		DEBUGCLEAR
		CALL TRAIN_MAIN
		RETURN 1
	CASE 101
		IF 探索フラグ
			PRINTW 探索は一日一回までです。
		ELSE
			CALL COM601
			IF RESULT == 1
				CALL ダンジョン内_SOURCE_CHECK(0)
				CALL TURN_RESET
				LOCALS = %月算出()%
				SAVEDATA 99, @"%GETTIMES()% {RESULT}年目 %LOCALS% {DAY % 30 + 1}日(%GET_DAY()%)"
			ENDIF
		ENDIF
	CASE 110
		SIF 滞在キャラ数 < 滞在キャラ上限
			CALL キャラクター招待
	CASE 111
		CALL 経営計画メニュー
	CASE 113
		SIF ダンジョンクリアフラグ_沿岸部の森
			CALL 従業員メインメニュー
	CASE 114
		CALL ITEM_BUY
	CASE 115
		CALL ZP_SHOP
	CASE 116
		CALL よろず屋依頼発行処理
	CASE 117
		CALL よろず屋輸入品店処理
	CASE 120
		CALL 起床前イベントリスト
	CASE 121
		TFLAG:調教モード = -1
		CALL SHOW_CHARADATA
		TFLAG:調教モード = 0
	CASE 122
		表示切り替え文字列 = 探索メンバー
		CALL 探索系集約表示
	CASE 123
		SIF 施設改造度:5:6
			CALL 性癖開拓処理
	CASE 124
		CALL 干渉強化メニュー
	CASE 135
		CALL 所持写真一覧表示
	CASE 143 TO 148
		IF 空艇港店舗状況:(RESULT - 140):0 == 1
			CALL 空艇港店舗用コマンド_COM_1
		ELSEIF 空艇港店舗状況:(RESULT - 140):0 == 2
			CALL 空艇港店舗用コマンド_COM_2
		ELSEIF 空艇港店舗状況:(RESULT - 140):0 == 4
			CALL 空艇港店舗用コマンド_COM_4
		ENDIF
	CASE 200
		CALL OPTION
	CASE 201
		SAVEGAME
	CASE 202
		LOADGAME
	CASE 203
		CALL チュートリアル_MAIN
	CASE 900
		SIF FLAG:エンディング後フラグ
			CALL 周回引き継ぎ処理
	CASE 9999
		;デバッグ用
		; ENUMFUNCBEGINSWITH "素材性能記述_"
		
		; FOR LOCAL, 0, 100
		; 	DT_ROW_ADD "料理メニューデータベース", "メニュー名", @"{LOCAL}", "料理ランク", 1, "料理属性", 0,  "メイン食材ID", 10, "調理器具ID",  0,
		; NEXT
		
		; CALL 汎用搭載兵器取得処理("10mmバルカン砲")
		; CALL 汎用搭載兵器取得処理("メタル・デストロイヤー")
		; CALL 汎用搭載兵器取得処理("近接用ブレード")
		; CALL 乗機取得処理("バトルケッタギア")
		; CALL 乗機取得処理("汎用型走艇")
		; CALL 乗機取得処理("コロッサスレプリカ")

		CALL 輸入品店品揃え変動処理
ENDSELECT




@起床時処理
FOR LOCAL, 0, CHARANUM
	SIF CFLAG:LOCAL:滞在期間 < 0
		CONTINUE
	CALL 移動ルーチンセット決定(LOCAL)
	;業務以外のルーチンでは行動時間に±60分のゆらぎが発生する（大体は±15分に収まるはず）
	SIF STRFIND(移動ルーチン:LOCAL:ルーチンID, "業務") == -1
		TCVAR:LOCAL:行動時間ゆらぎ = (RAND:9 + RAND:9 + RAND:9) * 5 - 60
NEXT
CALL 関係性同行決定()

IF モブ観光客数 > 0
	DRAWLINE
	PRINTFORMW 今日のモブ観光客数は{モブ観光客数, 4}人です。
ENDIF


@起床前メニュー上部分表示文章

IF 起床前メニュー上部分表示フラグ
	CALL TIPSランダム表示
ELSE
	CALL かんたんチュートリアル
ENDIF


@起床前メニュー上部分文章切り替え(入力番号)
#DIM 入力番号

IF 起床前メニュー上部分表示フラグ
	SELECTCASE 入力番号
		CASE 810 
			起床前メニュー上部分表示フラグ = 0
		CASE 811
			RETURN -1
	ENDSELECT
ELSE
	SELECTCASE 入力番号
		CASE 800 TO 802
			チュートリアル番号 = 入力番号
		CASE 809
			チュートリアル番号 = 0
			起床前メニュー上部分表示フラグ = 1
		CASE 810 
			チュートリアル番号 = 0
	ENDSELECT
ENDIF

@かんたんチュートリアル

SELECTCASE チュートリアル番号
	CASE 0
		LOCALS = ■かんたんチュートリアル<br>
		LOCALS += "　簡易的なチュートリアルが表示されます。項目を選んでください。<br>"
		LOCALS += "<br>"
		LOCALS += "　<button value='800'>[800] まず何すりゃいいの？</button><br>"
		LOCALS += "　<button value='801'>[801] さっさとスケベしたいんだけど</button><br>"
		LOCALS += "　<button value='802'>[802] このキャラに口上付いてる？</button><br>"
		LOCALS += "<br>"
		LOCALS += "　<button value='809'>[809] かんたんチュートリアルを表示しない</button><br>"
	CASE 800
		LOCALS = ■まず何すりゃいいの？<br>
		LOCALS += "　最初に[110] - リゾートの広告を出す を選び、[1]通常の広告を打つ を選んでください。<br>"
		LOCALS += "　広告を打つことで初めてキャラがリゾートに訪れるようになります。<br>"
		LOCALS += "　その後は[100] - 起床 を選択することでリゾート内に移行し、キャラたちと触れ合うことが出来ます。<br>"
		LOCALS += "<br>"
		LOCALS += "　<button value='810'>[810] かんたんチュートリアルに戻る</button><br>"
	CASE 801
		LOCALS = ■さっさとスケベしたいんだけど<br>
		LOCALS += "　このバリアントは、セックスまでが少々遠いバリアントになっています。<br>"
		LOCALS += "　最も簡単な方法は[121] - 能力表示 から、あるいはキャラと同室した状態で能力表示を選び、<br>"
		LOCALS += "　[110]恋慕対象に選ぶ を選択。それから行動を共にすることで恋慕レベルを上げ、告白する[352] を成功させることです。<br>"
		LOCALS += "　告白成功後に二人きりになれば、押し倒す[350] でセックスに持ち込むことが出来ます。<br>"
		LOCALS += "<br>"
		LOCALS += "　<button value='810'>[810] かんたんチュートリアルに戻る</button><br>"
	CASE 802
		LOCALS = ■このキャラに口上付いてる？<br>
		LOCALS += "　[121] - 能力表示 から[2000]フィルタ機能 を選び、[口上なし] を非表示にすると口上ファイルがあるキャラのみ一覧で表示されます。<br>"
		LOCALS += "　ただしこれはファイルがあるかどうかしか見ていないので、中身を保証するものではありません。<br>"
		LOCALS += "　口上の充実度に関してはERBファイルを直接閲覧してください。<br>"
		LOCALS += "<br>"
		LOCALS += "　<button value='810'>[810] かんたんチュートリアルに戻る</button><br>"
ENDSELECT

起床前メニュー上部分表示文字列 = %LOCALS%


@TIPSランダム表示

LOCALS = ■TIPS!<br>
SELECTCASE RAND:5
	CASE 0
		LOCALS += "　探索は現在表示されている起床前メニューからと、起床後に玄関まで移動してから行う方法があります。<br>"
		LOCALS += "　内容に違いはありませんが、玄関から出発すると時間を消費します。<br>"
		LOCALS += "　起床前メニューから行う方がお得です。<br>"
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "<br>"
	CASE 1
		LOCALS += "　バリアントの開発は主にDiscordで行われています。<br>"
		LOCALS += "　雑談だけ参加やROM専などでも歓迎です。お気軽に参加ください。<br>"
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "<br>"
	CASE 2
		LOCALS += "　沿岸部の森の奥にいるストーンイーターは、一度討伐した後も繰り返し戦うことが出来ます。<br>"
		LOCALS += "　ランダムな下級の指輪や火の耳飾りを入手出来るので、装備が整うまで毎日殴っておくといいでしょう。<br>"
		LOCALS += "　他のダンジョンボスも、再討伐によってアイテムが手に入ることが多いです。<br>"
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "<br>"
	CASE 3
		LOCALS += "　開発中は[101] - 探索へ出かける から簡易探索を選ぶと、好感度上昇用アイテムと恋慕度上昇用アイテムを入手出来ます。<br>"
		LOCALS += "　お手軽に好感度と恋慕度を上昇させることが出来るため、手早くスケベがしたい場合は利用してみてください。<br>"
		LOCALS += "　開発が進んだら削除予定です。<br>"
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "<br>"
	CASE 4
		LOCALS += "　人気度はリゾートの充実度によって上限が定められています。<br>"
		LOCALS += "　広告出しても人気度上がらないな？　と思ったら施設の開発を進めてみてください。<br>"
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "<br>"
		LOCALS += "<br>"
	CASE 5
		LOCALS += "　射精先の選択が煩わしい時はオート射精先設定をどうぞ。<br>"
		LOCALS += "　・OPTION > 機能切り替え > その他機能切り替え > オート射精設定<br>"
		LOCALS += "　・日中行動画面で[射精先設定]<br>"
		LOCALS += "　のどちらかから設定に入ることが可能です。<br>"
		LOCALS += "<br>"
		LOCALS += "<br>"
ENDSELECT
LOCALS += "　<button value='810'>[810] かんたんチュートリアルを表示する</button>　　<button value='811'>[811] TIPSリロール</button><br>"

起床前メニュー上部分表示文字列 = %LOCALS%
