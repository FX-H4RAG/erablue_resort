@通信販売_BUY
#DIMS 右側表示モード
#DIM 現在ページ
#DIM 表示候補, 500
#DIM 候補人数
#DIM 販売フラグ


DRAWLINE
PRINTL 通信販売にてアイテムを購入します。
PRINTL ※開発中のため、正しく挙動しないことがあります。
DRAWLINE
;上
LOCALS = <div rect='81,31,1937,381' padding='50,25,50' border='31' bcolor='#C0C0C0'>
LOCALS += @"所持ルピ：%NUM_FORMAT(MONEY)%<br>"
LOCALS += @"所持ZP　：%ZP所持量全文字列()%"
LOCALS += "</div>"

;左側、アイテムの種類を羅列
LOCALS += "<div rect='81,444,1937,2812' padding='50,25,50' border='31' bcolor='#C0C0C0'>"
IF 右側表示モード == "コマンド解放アイテム"
	LOCALS += @"<nonbutton><font color='#%カラーパレット_HTML("黄")%'>[-1] コマンド解放アイテム</font></nonbutton><br>"
ELSE
	LOCALS += "<button value='-1'>[-1] コマンド解放アイテム</button><br>"
ENDIF
IF 右側表示モード == "消費アイテム"
	LOCALS += @"<nonbutton><font color='#%カラーパレット_HTML("黄")%'>[-2] 消費アイテム</font></nonbutton><br>"
ELSE
	LOCALS += "<button value='-2'>[-2] 消費アイテム</button><br>"
ENDIF
IF 施設改造度:20:0
	IF 右側表示モード == "自宅用アイテム"
		LOCALS += @"<nonbutton><font color='#%カラーパレット_HTML("黄")%'>[-3] 自宅用アイテム</font></nonbutton><br>"
	ELSE
		LOCALS += "<button value='-3'>[-3] 自宅用アイテム</button><br>"
	ENDIF
ENDIF
LOCALS += "<br>"
LOCALS += "<button value='-99'>[-99] 戻る</button><br>"
LOCALS += "</div>"

;右側
LOCALS += "<div rect='2050,444,3875,2812' padding='50,25,50' border='31' bcolor='#C0C0C0'>"
CALL 通信販売_品揃え登録(LOCALS, 右側表示モード)
LOCALS += "</div>"

HTML_PRINT LOCALS
FOR LOCAL, 0, 30
	PRINTL
NEXT

$INPUT_LOOP
BINPUT
LOCAL = RESULT
SELECTCASE LOCAL
	CASE -10 TO -1
		右側表示モード = %表示モード配列:(ABS(LOCAL) - 1)%
		販売フラグ = 0
		現在ページ = 0
		RESTART
	CASE -98
		INVERTBIT 購入済み非表示フラグ, 0
		RESTART
	CASE -99
		RETURN 0
ENDSELECT


; IF ITEMSTOCK(RESULT) == 1 || ITEMSTOCK(RESULT) == 4 || LOCAL < 0 || LOCAL > 2000
; 	RESTART
; ELSEIF ITEMSTOCK(RESULT) == 2
; 	PRINTW 売切です
; 	RESTART
; ELSEIF ITEMSTOCK(RESULT) == 3
; 	PRINTW 所持金が足りません
; 	RESTART
; ELSEIF ITEMSTOCK(RESULT) == 5
; 	PRINTW 既に十分持っています
; 	RESTART
; ENDIF
LOCAL = RESULT
DRAWLINE

IF LOCAL == 70
	現在ページ = 0
	PRINTL 【元気になる薬】を購入しますか？
	PRINTL ※３日間、使用したキャラの体力最大値が1000増加します。
	PRINTBUTTON "[0] はい", 0
	PRINTL
	PRINTBUTTON "[1] いいえ", 1
	PRINTL
	BINPUT
	SIF RESULT
		RESTART
	PRINTL 誰に使いますか？

	VARSET 表示候補
	候補人数 = 0
	FOR LOCAL, 1, CHARANUM
		SIF CFLAG:LOCAL:滞在期間 < 0
			CONTINUE
		表示候補:候補人数 = LOCAL
		候補人数 += 1
	NEXT

	$INPUT_LOOP_70
	VARSET LOCAL
	LOCALS = 
	;表示処理
	FOR LOCAL, 現在ページ * 30, 候補人数
		IF CFLAG:(表示候補:LOCAL):元気になる薬フラグ > 0
			LOCALS += @"<font color='#%カラーパレット_HTML("グレーアウト")%'><nonbutton>[{表示候補:LOCAL,3}] %ADD_STR_SPACE(NAME表示省略(NAME:(表示候補:LOCAL)), "]", 1),41,LEFT%</nonbutton>"
			LOCALS += @"　使用済み：残{CFLAG:(表示候補:LOCAL):元気になる薬フラグ, 2}日　</font>"
		ELSE
			LOCALS += @"<button value = '{表示候補:LOCAL}'>[{表示候補:LOCAL,3}] %ADD_STR_SPACE(NAME表示省略(NAME:(表示候補:LOCAL)), "]", 1),41,LEFT%</button>"
			IF CFLAG:(表示候補:LOCAL):滞在期間 == 1
				LOCALS += @"　　　　　<font color='#%カラーパレット_HTML("黄")%'>本日帰宅</font>　"
			ELSEIF CFLAG:(表示候補:LOCAL):滞在期間 == 999
				LOCALS += "　　　　　常時滞在　"
			ELSEIF CFLAG:(表示候補:LOCAL):滞在期間 > 1
				LOCALS += @"　　　残滞在：{CFLAG:(表示候補:LOCAL):滞在期間 - 1, 2}日　"
			ENDIF
		ENDIF
		LOCAL:1 += 1
		IF LOCAL:1 % 2 == 0
			HTML_PRINT LOCALS
			LOCALS = 
		ENDIF
		SIF LOCAL:1 > 29
			BREAK
	NEXT
	IF LOCALS != ""
		HTML_PRINT LOCALS
		LOCALS = 
	ENDIF
	PRINTL 
	IF 現在ページ > 0
		PRINTLC  [900]前のページへ
	ELSE
		PRINTLC  
	ENDIF
		PRINTLC  [999]戻る
	IF 現在ページ < 候補人数 / 30
		PRINTLC  [901]次のページへ
	ENDIF
	PRINTL
	DRAWLINE

	BINPUT
	LOCAL = RESULT
	SELECTCASE LOCAL
		CASE 999
			RESTART
		CASE 900
			現在ページ -= 1
			GOTO INPUT_LOOP_70
		CASE 901
			現在ページ += 1
			GOTO INPUT_LOOP_70
		CASEELSE
			PRINTFORMW %NAME:LOCAL%の体力最大値が1000増加しました。
			CFLAG:LOCAL:元気になる薬フラグ = 3
			CALL 体力変動素質適用処理(LOCAL)
			BASE:LOCAL:体力 = MIN(MAXBASE:LOCAL:体力, BASE:LOCAL:体力 + 1000)
	ENDSELECT
	GOTO INPUT_LOOP_70
ENDIF

IF LOCAL == 90
	現在ページ = 0
	PRINTL 【記憶消去＆再生薬】を購入しますか？
	PRINTL ※恋慕やセフレなど後天的な素質の一部、関係、個別に設定のあるもの以外の固有関係などをリセットして童貞と処女を付け直します
	PRINTL ※本来想定された仕様ではないため思わぬバグが起きてしまう可能性があります！セーブデータを分けなるなどしてご利用ください！
	PRINTBUTTON "[0] はい", 0
	PRINTL
	PRINTBUTTON "[1] いいえ", 1
	PRINTL
	BINPUT
	SIF RESULT
		RESTART
	PRINTL 誰に使いますか？

	VARSET 表示候補
	候補人数 = 0
	FOR LOCAL, 1, CHARANUM
		SIF CFLAG:LOCAL:滞在期間 < 0
			CONTINUE
		表示候補:候補人数 = LOCAL
		候補人数 += 1
	NEXT

	$INPUT_LOOP_90
	VARSET LOCAL
	LOCALS = 
	;表示処理
	FOR LOCAL, 現在ページ * 30, 候補人数
		LOCALS += @"<button value = '{表示候補:LOCAL}'>[{表示候補:LOCAL,3}] %ADD_STR_SPACE(NAME表示省略(NAME:(表示候補:LOCAL)), "]", 1),41,LEFT%</button>"
		IF CFLAG:(表示候補:LOCAL):滞在期間 == 1
			LOCALS += @"　　　　　<font color='#%カラーパレット_HTML("黄")%'>本日帰宅</font>　"
		ELSEIF CFLAG:(表示候補:LOCAL):滞在期間 == 999
			LOCALS += "　　　　　常時滞在　"
		ELSEIF CFLAG:(表示候補:LOCAL):滞在期間 > 1
			LOCALS += @"　　　残滞在：{CFLAG:(表示候補:LOCAL):滞在期間 - 1, 2}日　"
		ENDIF
		LOCAL:1 += 1
		IF LOCAL:1 % 2 == 0
			HTML_PRINT LOCALS
			LOCALS = 
		ENDIF
		SIF LOCAL:1 > 29
			BREAK
	NEXT

	IF LOCALS != ""
	HTML_PRINT LOCALS
	LOCALS = 
	ENDIF
	PRINTL 
	IF 現在ページ > 0
		PRINTLC  [900]前のページへ
	ELSE
		PRINTLC  
	ENDIF
		PRINTLC  [999]戻る
	IF 現在ページ < 候補人数 / 30
		PRINTLC  [901]次のページへ
	ENDIF
	PRINTL
	DRAWLINE

	BINPUT
	LOCAL = RESULT
	SELECTCASE LOCAL
		CASE 999
			RESTART
		CASE 900
			現在ページ -= 1
			GOTO INPUT_LOOP_90
		CASE 901
			現在ページ += 1
			GOTO INPUT_LOOP_90
		CASEELSE
			PRINTFORMW %NAME:LOCAL%に記憶消去薬を投与しました
			CALL 記憶消去処理(LOCAL)
	ENDSELECT
	GOTO INPUT_LOOP_90
ENDIF

IF LOCAL == 41
	MONEY -= ITEMPRICE:LOCAL
	ITEM:LOCAL ++
	PRINTW 媚薬を精製しました
	RESTART
ENDIF
;複数所持アイテム
SELECTCASE ITEM種別(LOCAL)
	CASE "消費アイテム"
		PRINTFORM %ITEMNAME:LOCAL%をいくつ購入しますか？
		PRINTFORML 
		;金欠、アイテム数の超過はあとで合わせる
		PRINTL [0] - やめる
		PRINT [1] - 1　
		PRINT [5] - 5　
		PRINT [10] - 10　
		PRINT [20] - 20　
		PRINT [50] - 50　
		PRINT [99] - 買えるだけ
		BINPUT
		SIF RESULT * ITEMPRICE:LOCAL > MONEY
			RESULT = MONEY / ITEMPRICE:LOCAL
		ITEM:LOCAL += RESULT
		IF ITEM:LOCAL > 99
			RESULT = 99 - (ITEM:LOCAL - RESULT)
			ITEM:LOCAL = 99
		ENDIF
		PRINTFORMW %ITEMNAME:LOCAL%を{RESULT}個、%NUM_FORMAT(RESULT * ITEMPRICE:LOCAL)%%MONEYLABEL%で購入しました
		MONEY -= RESULT * ITEMPRICE:LOCAL
		RESTART
	CASE "コマンド解放アイテム"
		SIF ITEM:LOCAL
			RESTART
		PRINTFORML %ITEMNAME:LOCAL%を購入しますか？
		PRINTL [0] - はい  [1] - いいえ
		INPUT
		SIF RESULT
			RESTART
		IF MONEY < ITEMPRICE:LOCAL
			PRINTW ルピが足りません。
			RESTART
		ENDIF
		ITEM:LOCAL ++
		MONEY -= ITEMPRICE:LOCAL
		RESTART
	CASE "自宅用アイテム"
		SIF ITEM:LOCAL
			RESTART
		CALL 通信販売_アイテム説明(ITEMNAME:LOCAL)
		PRINTFORML %ITEMNAME:LOCAL%を購入しますか？
		PRINTL [0] - はい  [1] - いいえ
		INPUT
		SIF RESULT
			RESTART
		IF MONEY < ITEMPRICE:LOCAL
			PRINTW ルピが足りません。
			RESTART
		ENDIF
		ITEM:LOCAL ++
		MONEY -= ITEMPRICE:LOCAL
		RESTART
ENDSELECT


@通信販売_品揃え登録(HTML文字列, 右側表示モード)
#DIMS REF HTML文字列
#DIMS 右側表示モード

SELECTCASE 右側表示モード
	CASE "コマンド解放アイテム"
		HTML文字列 += "・性的なコマンドの使用条件として必要になるアダルトグッズカテゴリです。<br><br>"
		FOR LOCAL, 0, 200
			SIF ITEMNAME:LOCAL == ""
				CONTINUE
			SIF ITEM種別(LOCAL) != "コマンド解放アイテム"
				CONTINUE
			SIF ITEM:LOCAL && 購入済み非表示フラグ
				CONTINUE
			IF ITEM:LOCAL
				HTML文字列 += @"<nonbutton><font color='#%カラーパレット_HTML("グレーアウト")%'>[{LOCAL, 2}] %ITEMNAME:LOCAL, 20, LEFT% （購入済み）　　価格：%NUM_FORMAT(ITEMPRICE:LOCAL), 6%ルピ</font></nonbutton>"
			ELSE
				HTML文字列 += @"<button value='{LOCAL}'>[{LOCAL, 2}] %ITEMNAME:LOCAL, 20, LEFT% を購入する</button>　　　価格：%NUM_FORMAT(ITEMPRICE:LOCAL), 6%ルピ"
			ENDIF
			HTML文字列 += "<br>"
		NEXT
		HTML文字列 += "</div>"
		IF 購入済み非表示フラグ
			HTML文字列 += @"<div rect='2250,3044,3875,2812'><button value='-98'>[-98] 購入済みアイテムを表示 <font color='#%カラーパレット_HTML("グレーアウト")%'>する</font> / しない</button>"
		ELSE
			HTML文字列 += @"<div rect='2250,3044,3875,2812'><button value='-98'>[-98] 購入済みアイテムを表示 する / <font color='#%カラーパレット_HTML("グレーアウト")%'>しない</font></button>"
		ENDIF
	CASE "消費アイテム"
		HTML文字列 += "・一度使うとなくなってしまう、あるいは使用時に対象を選ぶ必要のあるアイテムカテゴリです。<br><br>"
		FOR LOCAL, 0, 200
			SIF ITEMNAME:LOCAL == ""
				CONTINUE
			SIF ITEM種別(LOCAL) != "消費アイテム"
				CONTINUE
			HTML文字列 += @"<button value='{LOCAL}'>[{LOCAL, 2}] %ITEMNAME:LOCAL, 20, LEFT% を購入する</button>　　価格：%NUM_FORMAT(ITEMPRICE:LOCAL), 6%ルピ　　所持数：{ITEM:LOCAL, 2}個"
			HTML文字列 += "<br>"
		NEXT
	CASE "自宅用アイテム"
		HTML文字列 += "・自宅におけるコマンドの使用条件として必要になるアイテムカテゴリです。<br><br>"
		FOR LOCAL, 0, 200
			SIF ITEMNAME:LOCAL == ""
				CONTINUE
			SIF ITEM種別(LOCAL) != "自宅用アイテム"
				CONTINUE
			SIF ITEM:LOCAL && 購入済み非表示フラグ
				CONTINUE
			IF ITEM:LOCAL
				HTML文字列 += @"<nonbutton><font color='#%カラーパレット_HTML("グレーアウト")%'>[{LOCAL, 2}] %ITEMNAME:LOCAL, 20, LEFT% （購入済み）　　価格：%NUM_FORMAT(ITEMPRICE:LOCAL), 6%ルピ</font></nonbutton>"
			ELSE
				HTML文字列 += @"<button value='{LOCAL}'>[{LOCAL, 2}] %ITEMNAME:LOCAL, 20, LEFT% を購入する</button>　　　価格：%NUM_FORMAT(ITEMPRICE:LOCAL), 6%ルピ"
			ENDIF
			HTML文字列 += "<br>"
		NEXT
		HTML文字列 += "</div>"
		IF 購入済み非表示フラグ
			HTML文字列 += @"<div rect='2250,3044,3875,2812'><button value='-98'>[-98] 購入済みアイテムを表示 <font color='#%カラーパレット_HTML("グレーアウト")%'>する</font> / しない</button>"
		ELSE
			HTML文字列 += @"<div rect='2250,3044,3875,2812'><button value='-98'>[-98] 購入済みアイテムを表示 する / <font color='#%カラーパレット_HTML("グレーアウト")%'>しない</font></button>"
		ENDIF
ENDSELECT


@ITEM種別(アイテム番号)
#FUNCTIONS
#DIM アイテム番号

SELECTCASE アイテム番号
	CASE 0 TO 20
		RETURNF "コマンド解放アイテム"
	CASE 40, 70, 90, 91, 92
		RETURNF "消費アイテム"
	CASE 80 TO 82
		RETURNF "自宅用アイテム"
ENDSELECT


@通信販売_アイテム説明(アイテム名)
#DIMS アイテム名

SELECTCASE アイテム名
	CASE "トランプセット"
		PRINTL 一般的なトランプセット。
		PRINTL これ１セットで色んな遊び方が出来る。
		PRINTL ※自宅リビングにて「カードで遊ぶ」コマンドを開放
		PRINTL
	CASE "ドリンクセラー"
		PRINTL 飲み物を多く冷やすための魔道具。
		PRINTL これがあれば大人数での宅飲みだって大丈夫。
		PRINTL ※自宅リビングにて「飲み会を始める」コマンドを開放
		PRINTL
	CASE "マッサージマット"
		PRINTL 自宅用のマッサージ用マット。
		PRINTL やっぱり専用の道具は使い心地が違う。
		PRINTFORML ※自宅%CALLNAME:MASTER%の部屋にて「マッサージを始める」コマンドを開放
		PRINTL
ENDSELECT


