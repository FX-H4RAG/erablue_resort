

;-------------------------------------------------
;下着についてランダム設定する
;一度設定したものは一日を通して有効
;-------------------------------------------------
@今日の下着文字列生成(対象キャラ)
#FUNCTIONS
#DIM 対象キャラ
#DIM ぱんつネーム

;既に今日のぱんつが決定されてるならそれを返す
IF CSTR:対象キャラ:今日の下着 != ""
	RETURNF CSTR:対象キャラ:今日の下着
ENDIF

;まず口上での個別設定を確認
IF EXISTFUNCTION(@"固有ぱんつ設定_{NO:対象キャラ}")
	RESULTS = 
	CALLFORMF 固有ぱんつ設定_{NO:対象キャラ}(対象キャラ)
	IF RESULTS != ""
		CSTR:対象キャラ:今日の下着 = %RESULTS%
		RETURNF CSTR:対象キャラ:今日の下着
	ENDIF
ENDIF

;口上設定がないなら素質ごとに分岐
;色設定
STRDATA LOCALS:0
	DATA 白の
	DATA ピンクの
	DATA 黒の
	DATA 赤い
	DATA パステルカラーの
	DATA 青の
ENDDATA
IF TEQUIP:対象キャラ:下半身下着あり == 3
	CSTR:対象キャラ:今日の下着 = 白いふんどし
	CSTR:対象キャラ:今日の下着の色 = 白の
ELSEIF TEQUIP:対象キャラ:下半身下着あり == 2
	CSTR:対象キャラ:今日の下着 = %LOCALS:0%前張り
	CSTR:対象キャラ:今日の下着の色 = %LOCALS:0%
ELSEIF 知識素質:対象キャラ:性知識 > 1 && BASE:対象キャラ:性欲 > 1500
	STRDATA LOCALS:1
		DATA 穴が空いた下着
		DATA Ｔバック
		DATA 紐パン
		DATA 透けた下着
	ENDDATA
	CSTR:対象キャラ:今日の下着 = %LOCALS:0%%LOCALS:1%
	CSTR:対象キャラ:今日の下着の色 = %LOCALS:0%
ELSEIF TALENT:対象キャラ:精神的余裕 < 0 || TALENT:対象キャラ:年齢層 == 年齢層_幼い
	STRDATA LOCALS:1
		DATA こどもぱんつ
		DATA 可愛い下着
		DATA シンプルな下着
		DATA 縞パン
	ENDDATA
	CSTR:対象キャラ:今日の下着 = %LOCALS:0%%LOCALS:1%
	CSTR:対象キャラ:今日の下着の色 = %LOCALS:0%
ELSE
	STRDATA LOCALS:1
		DATA 清楚な下着
		DATA 可愛い下着
		DATA シンプルな下着
		DATA セクシーな下着
		DATA 際どい下着
	ENDDATA
	CSTR:対象キャラ:今日の下着 = %LOCALS:0%%LOCALS:1%
	CSTR:対象キャラ:今日の下着の色 = %LOCALS:0%
ENDIF

RETURNF CSTR:対象キャラ:今日の下着
